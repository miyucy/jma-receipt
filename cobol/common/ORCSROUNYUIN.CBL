      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION              DIVISION.
       PROGRAM-ID.                 ORCSROUNYUIN.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 入院
      *  コンポーネント名  : 労災入院会計作成サブ
      *  管理者            : 
      *  作成日付    作業者        記述
      *  03/07/11    NACL−小豆沢  新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *  01.00.01    NACL-門脇    04/02/27  未実施減算月途中変更対応
      *  01.00.02    NACL-小豆沢  04/03/11  有床診療所１群入院基本料１
      *                                     （医師配置適合）の対応
      *  01.00.03    NACL-門脇    04/03/17  他医療機関受診の判定用コード
      *                                     （入院基本料加算区分）追加
      *  01.00.04    NACL-門脇    04/03/29  褥瘡患者管理加算の対応
      *  01.00.05    NACL-小豆沢  04/09/10  労災乗数をサブルーチンからの
      *                                     判定に変更
      *  01.00.05    NACL-小豆沢  05/09/16  以下の特定入院料の外泊計算を修正
      *                                     診療所老人医療管理料
      *                                     老人性認知症疾患治療病棟入院料１
      *                                     老人性認知症疾患治療病棟入院料２
      *  01.00.06    NACL-門脇    06/01/20  標欠対応
      *  01.00.07    NACL-小豆沢  06/03/22  特定入院料追加分（脳卒中ケアユニ）
      *                                     外泊置き換え処理
      *  02.09.01    NACL-門脇    06/04/13  ２００６年４月改正対応
      *  02.09.02    NACL-門脇    06/05/17  略称編集(190123250等)
      *  02.09.03    NACL-門脇    06/05/23  外泊時の入院基本料取得修正
      *  02.09.04    NACL-門脇    06/06/02  褥瘡患者管理加算編集対応
      *  02.09.05    NACL-門脇    06/06/05  複数医師及び看護配置適合加算と
      *                                     看護配置適合加算の編集対応
      *****************************************************************
      *
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *
       DATA                        DIVISION.
      *FILE                        SECTION.
      *
       WORKING-STORAGE             SECTION.
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-NYUINACCT       PIC 9(01).
           03  FLG-NYUINACT        PIC 9(01).
           03  FLG-SRYACCT         PIC 9(01).
           03  FLG-SRYACT          PIC 9(01).
           03  FLG-TENSU           PIC 9(01).
           03  FLG-PTINF           PIC 9(01).
           03  FLG-SYSKANRI        PIC 9(01).
           03  FLG-HKNCOMBI        PIC 9(01).
           03  FLG-RJN             PIC 9(01).
           03  FLG-ROUSAI          PIC 9(01).
           03  FLG-ARI             PIC 9(01).
           03  FLG-RYAKU           PIC 9(01).
           03  FLG-NYURYAKUSHO     PIC 9(01).
           03  FLG-OK              PIC 9(01).
           03  FLG-KSN             PIC 9(01).
      *    添字領域
       01  IDX-AREA.
           03  IDX1                PIC 9(04).
           03  IDX2                PIC 9(04).
           03  IDX3                PIC 9(04).
           03  ID1                 PIC 9(04).
           03  KSN-IDX             PIC 9(04).
           03  GENSAN-IDX          PIC 9(04).
           03  GENSAN-IDX2         PIC 9(04).
           03  IDV                 PIC 9(02).
           03  SRYACT-IDX          PIC 9(04).
      *
      *    コンスタント値
       01  CONSTANT-AREA.
      *    入院診療計画未実施減算
           03  CONS-KEIKAKU-GENSAN1        PIC X(09) VALUE "190080470".
           03  CONS-KEIKAKU-GENSAN2        PIC X(09) VALUE "190799670".
      *    院内感染防止対策未実施減算
           03  CONS-INNAI-GENSAN1          PIC X(09) VALUE "190080570".
           03  CONS-INNAI-GENSAN2          PIC X(09) VALUE "190799770".
      *    医療安全管理体制未整備減算
           03  CONS-ANZEN-GENSAN1          PIC X(09) VALUE "190111690".
           03  CONS-ANZEN-GENSAN2          PIC X(09) VALUE "190809090".
      *    褥瘡対策未実施減算
           03  CONS-JYOKU-GENSAN1          PIC X(09) VALUE "190111790".
           03  CONS-JYOKU-GENSAN2          PIC X(09) VALUE "190809190".
      *    日常生活障害加算
           03  CONS-NICHIJOU-KSN1          PIC X(09) VALUE "190109170".
           03  CONS-NICHIJOU-KSN2          PIC X(09) VALUE "190807570".
      *    痴呆加算
           03  CONS-CHIHOU-KSN1            PIC X(09) VALUE "190109270".
           03  CONS-CHIHOU-KSN2            PIC X(09) VALUE "190808670".
      *    褥瘡患者管理加算
           03  CONS-JYOKU-KSN1             PIC X(09) VALUE "190116990".
           03  CONS-JYOKU-KSN2             PIC X(09) VALUE "190813390".
      *    複数医師及び看護配置適合加算
           03  CONS-KANGO-KSN1             PIC X(09) VALUE "190117070".
      *    看護配置適合加算
           03  CONS-KANGO-KSN2             PIC X(09) VALUE "190109670".
      *
      *    労災治療計画加算
           03  CONS-ROSAICHIRYO            PIC X(09) VALUE "101910050".
      *    病衣貸与料
           03  CONS-BYOUI                  PIC X(09) VALUE "101910060".
      *    特別監視料
           03  CONS-TOKUKANSI              PIC X(09) VALUE "101920010".
      *
      *    一時領域
       01  WRK-AREA.
      *
           03  WRK-GAIHAKU-SRYCD           PIC X(09).
           03  WRK-MEISAISU                PIC 9(03).
           03  WRK-RYAKU-SRYCD             PIC X(09).
           03  WRK-RYAKUSHO                PIC X(14).
           03  WRK-SRYKBN                  PIC X(02).
           03  WRK-KSNKBN                  PIC 9(03).
           03  WRK-ZAISKBKBN               PIC X(01).
           03  WRK-SANTEIED-DDX            PIC X(02).
           03  WRK-SANTEIED-DD9            REDEFINES  WRK-SANTEIED-DDX
                                           PIC 9(02).
      *
           03  WRK-SYS5000-COUNT           PIC 9(02).
      *
           03  WRK-GENSAN2-MIJISI          PIC 9(01).
           03  WRK-GENSAN3-MIJISI          PIC 9(01).
           03  WRK-GENSAN4-MIJISI          PIC 9(01).
      *
           03  WRK-SHOKIKSN-TAIHI          PIC X(09).
           03  WRK-HYOKETU-TAIHI           PIC X(09).
           03  WRK-HYOKETU-TEN             PIC 9(09)V99.
           03  WRK-ZAITEN-HYOKETU          PIC 9(08).
      *
      *    労災入院会計ワーク
           03  WRK-ROUSAI-TABLE.
               05  WRK-ROUSAI-TBL          OCCURS  31.
                   07  WRK-NYUINKHN        PIC X(09).
                   07  WRK-SHOKIKSN        PIC X(09).
                   07  WRK-HYOKETU         PIC X(09).
                   07  WRK-GAIHAKU         PIC X(09).
                   07  WRK-NYUKHNKSNKBN    PIC 9(03).
      *
      *    入院料加算退避領域
           03  WRK-NYUKSN-TABLE.
               05  WRK-NYUKSN-TBL          OCCURS  20.
                   07  WRK-NYUKSN          PIC X(09).
                   07  WRK-NYUKSN-ZAITEN   PIC 9(08).
                   07  WRK-NYUKSN-DAY      PIC 9(03)  OCCURS 31.
      *
      *    未実施減算退避用フラグテーブル
           03  WRK-GENSAN-TABLE.
               05  WRK-GENSAN-INF          OCCURS  31.
                   07  WRK-GENSAN-INNAI    PIC X(01).
                   07  WRK-GENSAN-IRYO     PIC X(01).
                   07  WRK-GENSAN-JYOKU    PIC X(01).
      *
      *    院内感染防止対策未実施減算ワーク
           03  WRK-GENSAN2                 PIC X(09).
      *    医療安全管理体制未整備減算ワーク
           03  WRK-GENSAN3                 PIC X(09).
      *    褥瘡対策未実施減算ワーク
           03  WRK-GENSAN4                 PIC X(09).
      *
      *    入院診療計画未実施減算ワーク
           03  WRK-KEIKAKU-GENSAN1         PIC 9(03) OCCURS 31.
           03  WRK-KEIKAKU-GENSAN2         PIC 9(03) OCCURS 31.
      *    院内感染防止対策未実施減算ワーク
           03  WRK-INNAI-GENSAN1           PIC 9(03) OCCURS 31.
           03  WRK-INNAI-GENSAN2           PIC 9(03) OCCURS 31.
      *    医療安全管理体制未整備減算ワーク
           03  WRK-ANZEN-GENSAN1           PIC 9(03) OCCURS 31.
           03  WRK-ANZEN-GENSAN2           PIC 9(03) OCCURS 31.
      *    褥瘡対策未実施減算ワーク
           03  WRK-JYOKU-GENSAN1           PIC 9(03) OCCURS 31.
           03  WRK-JYOKU-GENSAN2           PIC 9(03) OCCURS 31.
      *
      *    日常生活障害加算退避
           03  WRK-NICHIJOU-KSN1           PIC 9(03) OCCURS 31.
           03  WRK-NICHIJOU-KSN2           PIC 9(03) OCCURS 31.
      *
      *    痴呆加算退避
           03  WRK-CHIHOU-KSN1             PIC 9(03) OCCURS 31.
           03  WRK-CHIHOU-KSN2             PIC 9(03) OCCURS 31.
      *
      *    褥瘡患者管理加算
           03  WRK-JYOKU-KSN1              PIC 9(03) OCCURS 31.
           03  WRK-JYOKU-KSN2              PIC 9(03) OCCURS 31.
      *
      *    複数医師及び看護配置適合加算
           03  WRK-KANGO-KSN1              PIC 9(03) OCCURS 31.
      *    看護配置適合加算
           03  WRK-KANGO-KSN2              PIC 9(03) OCCURS 31.
      *
      *    診療コード計集計用
           03  WRK-SRYCD-X                 PIC X(09).
           03  WRK-SRYCD-9    REDEFINES    WRK-SRYCD-X
                                           PIC 9(09).
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    システム管理マスタ
           COPY    "CPSYSKANRI.INC".
      *
           COPY    "CPSK5000.INC".
      *
      *    入院診療会計マスタ−
       01  NYUINACCT-REC.
           COPY    "CPNYUINACCT.INC".
      *
      *    入院診療行為マスター
       01  NYUINACT-REC.
           COPY    "CPNYUINACT.INC".
      *
      *    診療会計
       01  SRYACCT-REC.
           COPY    "CPSRYACCT.INC".
      *
      *    診療行為
       01  SRYACT-REC.
           COPY    "CPSRYACT.INC".
      *
      *    点数マスタ−
           COPY    "CPTENSU.INC".
      *
      *    患者マスタ−
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *    保険組み合わせ
       01  HKNCOMBI-REC.
           COPY    "CPHKNCOMBI.INC".
      *
      *    入院略称
       01  NYURYAKUSHO-REC.
           COPY    "CPNYUINRYAKUSHO.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
           COPY    "MCPAREA".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *    保険算定用年齢・割合計算サブ
           COPY    "CPORCSAGECHK.INC".
      *
      *    通算日数取得サブ（労災保険用）
           COPY    "CPORCSRSITUSAN.INC".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
        LINKAGE                     SECTION.
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *    労災入院会計作成用パラメタ
           COPY    "CPORCSROUNYUIN.INC".
      *
       PROCEDURE                   DIVISION    USING
           SPA-AREA
           ORCSROUNYUIN-AREA
           .
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  IDX-AREA
           INITIALIZE                  WRK-AREA
           INITIALIZE                  LNK-SROUNYUIN-HENKYAKU-AREA
           MOVE    ZERO                TO  LNK-SROUNYUIN-RC
      *
      *    パラメータチェック
           PERFORM 100-PRM-CHK-SEC
           IF    ( LNK-SROUNYUIN-RC       NOT =   ZERO )
               GO  TO  000-PROC-EXT
           END-IF
      *
      *    主処理
           IF      LNK-SROUNYUIN-RC      =    ZERO
               PERFORM 200-MAIN-SEC
           END-IF
      *
           .
       000-PROC-EXT.
      *
           EXIT    PROGRAM
           .
      *
      *****************************************************************
      *    パラメータチェック
      *****************************************************************
       100-PRM-CHK-SEC                     SECTION.
      *
           IF    ( LNK-SROUNYUIN-SANTEIYM    =   SPACE )
               MOVE    1               TO  LNK-SROUNYUIN-RC
               GO  TO  100-PRM-CHK-EXT
           END-IF
      *
      *    患者マスター検索
           INITIALIZE                      PTINF-REC
           MOVE    LNK-SROUNYUIN-HOSPID    TO  PTINF-HOSPID
           MOVE    LNK-SROUNYUIN-PTID      TO  PTINF-PTID
           PERFORM 900-PTINF-READ-SEC
           IF      FLG-PTINF           NOT =   ZERO
               MOVE    2               TO  LNK-SROUNYUIN-RC
               GO  TO  100-PRM-CHK-EXT
           END-IF
      *
      *    労災入院中のチェック（入院会計マスター検索）
           INITIALIZE                      NYUINACCT-REC
           MOVE    LNK-SROUNYUIN-HOSPID    TO  NACCT-HOSPID
           MOVE    LNK-SROUNYUIN-PTID      TO  NACCT-PTID
           MOVE    LNK-SROUNYUIN-SANTEIYM  TO  NACCT-SRYYM
           MOVE    "5"                     TO  NACCT-ZAISKBKBN
           MOVE    "NYUINACCT-KEY17"       TO  ORC-DBPATH
           PERFORM 910-NYUINACCT-SELECT-SEC
           IF      FLG-NYUINACCT           NOT =   ZERO
               MOVE    3               TO  LNK-SROUNYUIN-RC
               GO  TO  100-PRM-CHK-EXT
           ELSE
               MOVE    "NYUINACCT-KEY17"   TO  ORC-DBPATH
               PERFORM 910-NYUINACCT-READ-SEC
               IF   FLG-NYUINACCT          NOT =   ZERO
                    MOVE    3               TO  LNK-SROUNYUIN-RC
                    GO  TO  100-PRM-CHK-EXT
               ELSE
      *            労災保険の判定
                   MOVE      ZERO              TO  FLG-ROUSAI
                   PERFORM   VARYING   IDX1   FROM  1   BY  1
                             UNTIL    IDX1   >   31
                        IF   LNK-SROUNYUIN-HKNCOMBI  =
                                               NACCT-DAY(IDX1)
                             MOVE    1         TO  FLG-ROUSAI
                             MOVE   "1"        TO  
                                          LNK-SROUNYUIN-NYUIN-FLG(IDX1)
                        END-IF
                   END-PERFORM
                   IF   FLG-ROUSAI             =   ZERO
                        MOVE    3              TO  LNK-SROUNYUIN-RC
                   END-IF
               END-IF
           END-IF
      *
      *    入院会計クローズ
           MOVE    "NYUINACCT-KEY17"           TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
           .
       100-PRM-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    主取得
      *****************************************************************
       200-MAIN-SEC            SECTION.
      *
      *労災乗数の２週間以内の最終日を取得
           INITIALIZE                      ORCSRSITUSANAREA
           MOVE    LNK-SROUNYUIN-HOSPID
                                       TO  RSITUSAN-HOSPID
           MOVE    LNK-SROUNYUIN-PTID
                                       TO  RSITUSAN-PTID
           MOVE    LNK-SROUNYUIN-HKNCOMBI
                                       TO  RSITUSAN-HKNCOMBI
           MOVE    LNK-SROUNYUIN-SANTEIYM
                                       TO  RSITUSAN-SRYYM
           CALL    "ORCSRSITUSAN"      USING
                                           ORCSRSITUSANAREA
           IF    ( RSITUSAN-RC          =   ZERO )
               CONTINUE
           ELSE
               INITIALIZE  ORCSRSITUSANAREA
           END-IF
           PERFORM   VARYING   IDX1   FROM 1  BY  1
                     UNTIL     IDX1   >    31
               IF    RSITUSAN-SANTEIEDYMD(1:6) < LNK-SROUNYUIN-SANTEIYM
                   MOVE   "2"       TO    LNK-SROUNYUIN-JOUSU-KBN(IDX1)
               ELSE
                   MOVE   RSITUSAN-SANTEIEDYMD(7:2)
                                    TO    WRK-SANTEIED-DDX
                   IF     IDX1        <=    WRK-SANTEIED-DD9
                       MOVE   "1"   TO    LNK-SROUNYUIN-JOUSU-KBN(IDX1)
                   ELSE
                       MOVE   "2"   TO    LNK-SROUNYUIN-JOUSU-KBN(IDX1)
                   END-IF
               END-IF
           END-PERFORM
      *労災入院料の取得
           MOVE    ZERO                    TO  FLG-OK
           INITIALIZE                          NYUINACCT-REC
           MOVE    LNK-SROUNYUIN-HOSPID    TO  NACCT-HOSPID
           MOVE    LNK-SROUNYUIN-PTID      TO  NACCT-PTID
           MOVE    LNK-SROUNYUIN-SANTEIYM  TO  NACCT-SRYYM
           MOVE    "1"                     TO  NACCT-ZAISKBKBN
           MOVE    "NYUINACCT-KEY17"       TO  ORC-DBPATH
           PERFORM 910-NYUINACCT-SELECT-SEC
           IF      FLG-NYUINACCT           =   ZERO
               MOVE     "NYUINACCT-KEY17"  TO  ORC-DBPATH
               PERFORM   910-NYUINACCT-READ-SEC
               PERFORM   UNTIL   (FLG-NYUINACCT    =   1)
                   MOVE      ZERO     TO      FLG-RYAKU
                                              WRK-KSNKBN
                   MOVE      SPACE    TO      WRK-RYAKUSHO
                                              WRK-SRYKBN
                                              WRK-SHOKIKSN-TAIHI
                                              WRK-HYOKETU-TAIHI
                   IF    NACCT-SRYCDTOTAL     =    99999916
                       CONTINUE
                   ELSE
      *                該当入院行為テーブルの読み込み
                       PERFORM    210-NYUINACT-GET-SEC
      *                入院基本料と初期加算（長期減算）の退避
                       PERFORM    VARYING   IDX1   FROM   1   BY   1
                                  UNTIL     IDX1   >   31
                           IF    (LNK-SROUNYUIN-NYUIN-FLG(IDX1) = "1")
                           AND   (NACCT-DAY(IDX1)    NOT =  ZERO) 
                               MOVE    NSRY-SRYCD(1)   TO  
                                                   WRK-NYUINKHN(IDX1)
      *
                               IF   FLG-RYAKU   =   ZERO
      *
                                  PERFORM  VARYING  IDX3  FROM  2  BY  1
                                            UNTIL   IDX3   >    3
                                    IF   NSRY-SRYCD(IDX3)  NOT =   SPACE
                                      INITIALIZE         TNS-TENSU-REC
                                      MOVE NSRY-SRYCD(IDX3)
                                                     TO  TNS-SRYCD
                                      MOVE LNK-SROUNYUIN-SANTEIYM
                                                     TO  ORC-DBYMD(1:6)
                                      MOVE    "01"   TO  ORC-DBYMD(7:2)
                                      PERFORM 910-TENSU-READ-SEC
                                      EVALUATE  TNS-TENSIKIBETU
                                         WHEN     3
                                         WHEN     8
                                             MOVE   NSRY-SRYCD(IDX3)  TO
                                                      WRK-SHOKIKSN-TAIHI
                                         WHEN     6
                                             MOVE   NSRY-SRYCD(IDX3)  TO
                                                      WRK-HYOKETU-TAIHI
                                      END-EVALUATE
                                    END-IF
                                  END-PERFORM
      *
                                  MOVE    NACCT-SRYKBN
                                                TO  WRK-SRYKBN
      *                           外泊コード編集用に入院基本料加算区分
      *                           の退避
                                  PERFORM   210-GAIHAKU-MAE-SEC
      *
                                  MOVE   NSRY-SRYCD(1)
                                                TO  WRK-RYAKU-SRYCD
                                  MOVE   NACCT-ZAISKBKBN
                                                TO  WRK-ZAISKBKBN
                                  PERFORM   210-RYAKUSYO-HENSYU-SEC
      *
                                  MOVE    1     TO    FLG-RYAKU
                               END-IF
      *
                               IF    WRK-SHOKIKSN-TAIHI  NOT = SPACE
                                  MOVE  WRK-SHOKIKSN-TAIHI
                                               TO  WRK-SHOKIKSN(IDX1)
                               END-IF
                               IF    WRK-HYOKETU-TAIHI   NOT = SPACE
                                  MOVE  WRK-HYOKETU-TAIHI
                                               TO  WRK-HYOKETU(IDX1)
                               END-IF
      *
                               MOVE  WRK-SRYKBN
                                         TO  LNK-SROUNYUIN-SRYKBN(IDX1)
                               MOVE  WRK-KSNKBN
                                         TO  WRK-NYUKHNKSNKBN(IDX1)
      *
                               IF    WRK-RYAKUSHO   NOT =  SPACE
                                  MOVE  WRK-RYAKUSHO
                                      TO  LNK-SROUNYUIN-RYAKUSYO(IDX1)
                               END-IF
      *
                               MOVE    1    TO   FLG-OK
                           END-IF
                       END-PERFORM
                   END-IF
                   MOVE    "NYUINACCT-KEY17"       TO  ORC-DBPATH
                   PERFORM 910-NYUINACCT-READ-SEC
               END-PERFORM
               IF   FLG-OK     =   ZERO
                   MOVE     3      TO      LNK-SROUNYUIN-RC
                   GO   TO    200-MAIN-EXT
               END-IF
           ELSE
               MOVE     3      TO      LNK-SROUNYUIN-RC
               GO   TO    200-MAIN-EXT
           END-IF
      *    入院会計クローズ
           MOVE    "NYUINACCT-KEY17"           TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
      *労災入院時の外泊取得
           INITIALIZE                          NYUINACCT-REC
           MOVE    LNK-SROUNYUIN-HOSPID    TO  NACCT-HOSPID
           MOVE    LNK-SROUNYUIN-PTID      TO  NACCT-PTID
           MOVE    LNK-SROUNYUIN-SANTEIYM  TO  NACCT-SRYYM
           MOVE    "2"                     TO  NACCT-ZAISKBKBN
           MOVE    "NYUINACCT-KEY17"       TO  ORC-DBPATH
           PERFORM 910-NYUINACCT-SELECT-SEC
           IF      FLG-NYUINACCT           =   ZERO
               MOVE     "NYUINACCT-KEY17"  TO  ORC-DBPATH
               PERFORM   910-NYUINACCT-READ-SEC
               PERFORM   UNTIL   (FLG-NYUINACCT    =   1)
      *            外泊区分の退避
                   PERFORM    VARYING   IDX1   FROM   1   BY   1
                              UNTIL     IDX1   >   31
                       IF    (WRK-NYUINKHN(IDX1) NOT = SPACE) 
                       AND   (NACCT-DAY(IDX1)    NOT =  ZERO) 
                              PERFORM     210-GAIHAKU-SET-SEC
                       END-IF
                   END-PERFORM
                   MOVE    "NYUINACCT-KEY17"       TO  ORC-DBPATH
                   PERFORM 910-NYUINACCT-READ-SEC
               END-PERFORM
           END-IF
      *    入院会計クローズ
           MOVE    "NYUINACCT-KEY17"           TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
      *老人対象患者チェック
           PERFORM   210-RJN-TAISYO-CHK-SEC
      *
      *月内に複数の入院基本情報（５０００）がないか検索
           PERFORM   210-SYS-5000-CHK-SEC
      *
      *医療機関減算情報の取得（システム管理テーブルより）
           MOVE    ZERO                TO  FLG-SYSKANRI
           INITIALIZE                      SYSKANRI-REC
           MOVE    "5000"              TO  SYS-KANRICD
           MOVE    "*"                 TO  SYS-KBNCD
           MOVE    LNK-SROUNYUIN-SANTEIYM
                                       TO  ORC-DBYMD(1:6)
           MOVE    "01"                TO  ORC-DBYMD(7:2)
           MOVE    SYSKANRI-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SYSKANRI-KEY"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"             USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SYSKANRI-KEY"  TO  ORC-DBPATH
               PERFORM  910-SYSKANRI-READ-SEC
               PERFORM  VARYING   GENSAN-IDX  FROM  1  BY  1
                        UNTIL     GENSAN-IDX   >   4
                    IF    SYS-5000-GENKBN(GENSAN-IDX)   =   "2"
                        EVALUATE  GENSAN-IDX
                            WHEN  2
                                MOVE  "190080570"  TO  WRK-GENSAN2
      **                          IF   FLG-RJN    =    ZERO
      **                              MOVE  "190080570"  TO  WRK-GENSAN2
      **                          ELSE
      **                              MOVE  "190799770"  TO  WRK-GENSAN2
      **                          END-IF
                                PERFORM VARYING GENSAN-IDX2 FROM 1 BY 1
                                    UNTIL     GENSAN-IDX2   >   31
                                  MOVE   "1"  TO
                                          WRK-GENSAN-INNAI(GENSAN-IDX2)
                                END-PERFORM
                            WHEN  3
                                MOVE  "190111690"  TO  WRK-GENSAN3
      **                          IF   FLG-RJN    =    ZERO
      **                              MOVE  "190111690"  TO  WRK-GENSAN3
      **                          ELSE
      **                              MOVE  "190809090"  TO  WRK-GENSAN3
      **                          END-IF
                                PERFORM VARYING GENSAN-IDX2 FROM 1 BY 1
                                    UNTIL     GENSAN-IDX2   >   31
                                  MOVE   "1"  TO
                                          WRK-GENSAN-IRYO(GENSAN-IDX2)
                                END-PERFORM
                            WHEN  4
                                MOVE  "190111790"  TO  WRK-GENSAN4
      **                          IF   FLG-RJN    =    ZERO
      **                              MOVE  "190111790"  TO  WRK-GENSAN4
      **                          ELSE
      **                              MOVE  "190809190"  TO  WRK-GENSAN4
      **                          END-IF
                                PERFORM VARYING GENSAN-IDX2 FROM 1 BY 1
                                    UNTIL     GENSAN-IDX2   >   31
                                  MOVE   "1"  TO
                                          WRK-GENSAN-JYOKU(GENSAN-IDX2)
                                END-PERFORM
                        END-EVALUATE
                    END-IF
               END-PERFORM
           END-IF
      *    システム管理クローズ
           MOVE    "SYSKANRI-KEY"          TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
           IF        WRK-SYS5000-COUNT    =     ZERO
                 CONTINUE
           ELSE
                 PERFORM  210-SYS-5000-GET-SEC
           END-IF
      *
      *
      *労災入院時の入院料加算取得（入院会計より）
           MOVE    ZERO                    TO  KSN-IDX
           INITIALIZE                          NYUINACCT-REC
           MOVE    LNK-SROUNYUIN-HOSPID    TO  NACCT-HOSPID
           MOVE    LNK-SROUNYUIN-PTID      TO  NACCT-PTID
           MOVE    LNK-SROUNYUIN-SANTEIYM  TO  NACCT-SRYYM
           MOVE    "6"                     TO  NACCT-ZAISKBKBN
           MOVE    "NYUINACCT-KEY17"       TO  ORC-DBPATH
           PERFORM 910-NYUINACCT-SELECT-SEC
           IF      FLG-NYUINACCT           =   ZERO
               MOVE     "NYUINACCT-KEY17"  TO  ORC-DBPATH
               PERFORM   910-NYUINACCT-READ-SEC
               PERFORM   UNTIL   (FLG-NYUINACCT    =   1)
                   MOVE      ZERO     TO      FLG-RYAKU
                   MOVE      ZERO     TO      FLG-KSN
      *            入院料加算の退避
                   PERFORM    VARYING   IDX1   FROM   1   BY   1
                              UNTIL     IDX1   >   31
                       IF    (WRK-NYUINKHN(IDX1) NOT = SPACE)
                       AND   (LNK-SROUNYUIN-GAIHAKU(IDX1)  
                                             =  ZERO  OR  4)
                       AND   (NACCT-DAY(IDX1)    NOT =  ZERO)
      *
                         IF  (NACCT-SRYCDTOTAL(6:9) = CONS-KANGO-KSN1
                                                   OR CONS-KANGO-KSN2)
                         AND (LNK-SROUNYUIN-SANTEIYM  >= "200604"    )
                           EVALUATE   NACCT-SRYCDTOTAL(6:9)
                             WHEN  CONS-KANGO-KSN1
                                  MOVE   NACCT-DAY(IDX1)
                                        TO  WRK-KANGO-KSN1(IDX1)
                             WHEN  CONS-KANGO-KSN2
                                  MOVE   NACCT-DAY(IDX1)
                                        TO  WRK-KANGO-KSN2(IDX1)
                           END-EVALUATE
      *
                           IF   FLG-RYAKU   =   ZERO
                              MOVE   NACCT-SRYCDTOTAL(6:9)
                                            TO  WRK-RYAKU-SRYCD
                              MOVE   NACCT-ZAISKBKBN
                                            TO  WRK-ZAISKBKBN
                              PERFORM   210-RYAKUSYO-HENSYU-SEC
      *
                              MOVE    1     TO    FLG-RYAKU
                           END-IF
                         ELSE
                           IF   FLG-KSN   =  ZERO
                               ADD     1    TO    KSN-IDX
                               MOVE    1    TO    FLG-KSN
                           END-IF
                           MOVE   NACCT-SRYCDTOTAL(6:9)
                                        TO  WRK-NYUKSN(KSN-IDX)
                           MOVE   NACCT-ZAITEN  
                                        TO  WRK-NYUKSN-ZAITEN(KSN-IDX)
                           MOVE   NACCT-DAY(IDX1)
                                        TO  WRK-NYUKSN-DAY(KSN-IDX IDX1)
      *
                           IF   FLG-RYAKU   =   ZERO
                              MOVE   NACCT-SRYCDTOTAL(6:9)
                                            TO  WRK-RYAKU-SRYCD
                              MOVE   NACCT-ZAISKBKBN
                                            TO  WRK-ZAISKBKBN
                              PERFORM   210-RYAKUSYO-HENSYU-SEC
      *
                              MOVE    1     TO    FLG-RYAKU
                           END-IF
                         END-IF
      *
                       END-IF
                   END-PERFORM
                   MOVE    "NYUINACCT-KEY17"       TO  ORC-DBPATH
                   PERFORM 910-NYUINACCT-READ-SEC
               END-PERFORM
           END-IF
      *    入院会計クローズ
           MOVE    "NYUINACCT-KEY17"           TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
      *
      *労災入院時の入院料加算取得（診療会計より）
           INITIALIZE                           SRYACCT-REC
           MOVE    LNK-SROUNYUIN-HOSPID    TO   ACCT-HOSPID
           MOVE    "1"                     TO   ACCT-NYUGAIKBN
           MOVE    LNK-SROUNYUIN-PTID      TO   ACCT-PTID
           MOVE    LNK-SROUNYUIN-SANTEIYM  TO   ACCT-SRYYM
           MOVE    LNK-SROUNYUIN-HKNCOMBI  TO   ACCT-HKNCOMBI
           MOVE    "SRYACCT-KEY22"         TO   ORC-DBPATH
           PERFORM 910-SRYACCT-SELECT-SEC
           IF      FLG-SRYACCT           =   ZERO
               MOVE     "SRYACCT-KEY22"    TO   ORC-DBPATH
               PERFORM   910-SRYACCT-READ-SEC
               PERFORM   UNTIL   (FLG-SRYACCT    =   1)
                 MOVE      ZERO     TO      FLG-RYAKU
                 IF   (ACCT-ZAITEN    NOT =    ZERO)    AND
                      (ACCT-ZAIKAISU  NOT =    ZERO)
                   IF   ACCT-SRYKBN  =  "90" OR "92"   
                     IF ( ACCT-SRYCDTOTAL(6:9)  =
                                            CONS-KEIKAKU-GENSAN1
                                        OR  CONS-KEIKAKU-GENSAN2
                                        OR  CONS-INNAI-GENSAN1
                                        OR  CONS-INNAI-GENSAN2
                                        OR  CONS-ANZEN-GENSAN1
                                        OR  CONS-ANZEN-GENSAN2
                                        OR  CONS-JYOKU-GENSAN1
                                        OR  CONS-JYOKU-GENSAN2
                                        OR  CONS-NICHIJOU-KSN1
                                        OR  CONS-NICHIJOU-KSN2
                                        OR  CONS-CHIHOU-KSN1
                                        OR  CONS-CHIHOU-KSN2       )
                     OR ((ACCT-SRYCDTOTAL(6:9)  =  CONS-JYOKU-KSN1
                                               OR  CONS-JYOKU-KSN2) AND
                         (LNK-SROUNYUIN-SANTEIYM  < "200604"      ))
                     OR ((ACCT-SRYCDTOTAL(6:9)  =  CONS-KANGO-KSN1
                                               OR  CONS-KANGO-KSN2) AND
                         (LNK-SROUNYUIN-SANTEIYM >= "200604"      ))
      *                入院料加算の退避
                       PERFORM    VARYING   IDX1   FROM   1   BY   1
                                  UNTIL     IDX1   >   31
                           IF    (WRK-NYUINKHN(IDX1) NOT = SPACE)
                           AND   (LNK-SROUNYUIN-GAIHAKU(IDX1)  
                                                        =  ZERO)
                           AND   (ACCT-DAY(1 IDX1)   NOT =  ZERO)
                              EVALUATE   ACCT-SRYCDTOTAL(6:9)
      *                         入院診療計画未実施減算
                                WHEN  CONS-KEIKAKU-GENSAN1
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-KEIKAKU-GENSAN1(IDX1)
                                WHEN  CONS-KEIKAKU-GENSAN2
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-KEIKAKU-GENSAN2(IDX1)
      *                         院内感染防止対策未実施減算
                                WHEN  CONS-INNAI-GENSAN1
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-INNAI-GENSAN1(IDX1)
                                WHEN  CONS-INNAI-GENSAN2
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-INNAI-GENSAN2(IDX1)
      *                         医療安全管理体制未整備減算
                                WHEN  CONS-ANZEN-GENSAN1
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-ANZEN-GENSAN1(IDX1)
                                WHEN  CONS-ANZEN-GENSAN2
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-ANZEN-GENSAN2(IDX1)
      *                         褥瘡対策未実施減算
                                WHEN  CONS-JYOKU-GENSAN1
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-JYOKU-GENSAN1(IDX1)
                                WHEN  CONS-JYOKU-GENSAN2
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-JYOKU-GENSAN2(IDX1)
      *                         日常生活障害加算
                                WHEN  CONS-NICHIJOU-KSN1
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-NICHIJOU-KSN1(IDX1)
                                WHEN  CONS-NICHIJOU-KSN2
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-NICHIJOU-KSN2(IDX1)
      *                         痴呆加算
                                WHEN  CONS-CHIHOU-KSN1
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-CHIHOU-KSN1(IDX1)
                                WHEN  CONS-CHIHOU-KSN2
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-CHIHOU-KSN2(IDX1)
      *                         褥瘡患者管理加算
                                WHEN  CONS-JYOKU-KSN1
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-JYOKU-KSN1(IDX1)
                                WHEN  CONS-JYOKU-KSN2
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-JYOKU-KSN2(IDX1)
      *                         複数医師及び看護配置適合加算
                                WHEN  CONS-KANGO-KSN1
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-KANGO-KSN1(IDX1)
      *                         看護配置適合加算
                                WHEN  CONS-KANGO-KSN2
                                  MOVE   ACCT-DAY(1 IDX1)
                                        TO  WRK-KANGO-KSN2(IDX1)
                              END-EVALUATE
      *
                              IF   FLG-RYAKU   =   ZERO
                                 MOVE   ACCT-SRYCDTOTAL(6:9)
                                               TO  WRK-RYAKU-SRYCD
                                 MOVE   "6"    TO  WRK-ZAISKBKBN
                                 PERFORM   210-RYAKUSYO-HENSYU-SEC
      *
                                 MOVE    1     TO    FLG-RYAKU
                              END-IF
      *
                           END-IF
                       END-PERFORM
                     ELSE
                       IF   ACCT-SRYCDTOTAL(6:9)  =  CONS-ROSAICHIRYO
                                                 OR  CONS-BYOUI
                                                 OR  CONS-TOKUKANSI
      *                  入院料加算の退避
                         PERFORM    VARYING   IDX1   FROM   1   BY   1
                                    UNTIL     IDX1   >   31
                             IF    (WRK-NYUINKHN(IDX1) NOT = SPACE)
                             AND   (ACCT-DAY(1 IDX1)   NOT =  ZERO)
                               COMPUTE LNK-SROUNYUIN-ZAITEN-ACCTETC
                                                             (IDX1)
                                    =  LNK-SROUNYUIN-ZAITEN-ACCTETC
                                                             (IDX1)
                                    + (ACCT-ZAITEN * ACCT-DAY(1 IDX1))
      *
                               IF   FLG-RYAKU   =   ZERO
                                  MOVE   ACCT-SRYCDTOTAL(6:9)
                                                TO  WRK-RYAKU-SRYCD
                                  MOVE   "6"    TO  WRK-ZAISKBKBN
                                  PERFORM   210-RYAKUSYO-HENSYU-SEC
      *
                                  MOVE    1     TO    FLG-RYAKU
                               END-IF
      *
                             END-IF
                         END-PERFORM
                       ELSE
      *                  入院料加算の退避
                         PERFORM    VARYING   IDX1   FROM   1   BY   1
                                    UNTIL     IDX1   >   31
                             IF    (WRK-NYUINKHN(IDX1) NOT = SPACE)
                             AND   (ACCT-DAY(1 IDX1)   NOT =  ZERO)
                               COMPUTE LNK-SROUNYUIN-ZAITEN-ACCT(IDX1)
                                    =  LNK-SROUNYUIN-ZAITEN-ACCT(IDX1)
                                    + (ACCT-ZAITEN * ACCT-DAY(1 IDX1))
      *
                               IF   FLG-RYAKU   =   ZERO
                                  PERFORM   210-SRYACT-HENSYU-SEC
      *
                                  MOVE    1     TO    FLG-RYAKU
                               END-IF
      *
                             END-IF
                         END-PERFORM
                       END-IF
                     END-IF
                   END-IF
                 END-IF
                 MOVE     "SRYACCT-KEY22"    TO   ORC-DBPATH
                 PERFORM   910-SRYACCT-READ-SEC
               END-PERFORM
           END-IF
      *    診療会計クローズ
           MOVE    "SRYACCT-KEY22"      TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
      *
      *    取得した労災入院情報から日単位の入院会計情報を再構築
           PERFORM      VARYING    IDX1   FROM   1  BY  1
                        UNTIL      IDX1   >   31
               MOVE     ZERO                TO  WRK-MEISAISU
      *        入院基本料
               IF         WRK-NYUINKHN(IDX1)  NOT =        SPACE
                   ADD      1               TO  WRK-MEISAISU
                   MOVE   WRK-NYUINKHN(IDX1) TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "1"           TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        外泊
               IF         WRK-GAIHAKU(IDX1)   NOT =        SPACE
                   ADD      1               TO  WRK-MEISAISU
                   MOVE   WRK-GAIHAKU(IDX1)  TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "2"           TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        日常生活障害加算
               IF         WRK-NICHIJOU-KSN1(IDX1)   NOT =   ZERO
                   ADD      1                 TO  WRK-MEISAISU
                   MOVE   CONS-NICHIJOU-KSN1  TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"            TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
               IF         WRK-NICHIJOU-KSN2(IDX1)   NOT =   ZERO
                   ADD      1                 TO  WRK-MEISAISU
                   MOVE   CONS-NICHIJOU-KSN2  TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"            TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        痴呆加算
               IF         WRK-CHIHOU-KSN1(IDX1)     NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-CHIHOU-KSN1     TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
               IF         WRK-CHIHOU-KSN2(IDX1)     NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-CHIHOU-KSN2     TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        複数医師及び看護配置適合加算
               IF         WRK-KANGO-KSN1(IDX1)      NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-KANGO-KSN1      TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        看護配置適合加算
               IF         WRK-KANGO-KSN2(IDX1)      NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-KANGO-KSN2      TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        褥瘡患者管理加算
               IF         WRK-JYOKU-KSN1(IDX1)      NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-JYOKU-KSN1      TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
               IF         WRK-JYOKU-KSN2(IDX1)      NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-JYOKU-KSN2      TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        入院診療計画未実施減算
               IF         WRK-KEIKAKU-GENSAN1(IDX1) NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-KEIKAKU-GENSAN1 TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
               IF         WRK-KEIKAKU-GENSAN2(IDX1) NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-KEIKAKU-GENSAN2 TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        院内感染防止対策未実施減算
               IF         WRK-INNAI-GENSAN1(IDX1) NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-INNAI-GENSAN1   TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
               IF         WRK-INNAI-GENSAN2(IDX1) NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-INNAI-GENSAN2   TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        医療安全管理体制未整備減算
               IF         WRK-ANZEN-GENSAN1(IDX1) NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-ANZEN-GENSAN1   TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
               IF         WRK-ANZEN-GENSAN2(IDX1) NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-ANZEN-GENSAN2   TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        褥瘡対策未実施減算
               IF         WRK-JYOKU-GENSAN1(IDX1) NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-JYOKU-GENSAN1   TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
               IF         WRK-JYOKU-GENSAN2(IDX1) NOT =   ZERO
                   ADD      1                  TO  WRK-MEISAISU
                   MOVE   CONS-JYOKU-GENSAN2   TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"             TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        減算情報（システム管理）
               IF      (WRK-NYUINKHN(IDX1)   NOT =         SPACE)  AND
                       (WRK-GAIHAKU(IDX1)        =         SPACE)  AND
                       (WRK-GENSAN-INNAI(IDX1)   NOT =     SPACE)
                   ADD      1               TO  WRK-MEISAISU
                   MOVE   WRK-GENSAN2       TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"          TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
                   MOVE     1               TO  WRK-GENSAN2-MIJISI
               END-IF
               IF      (WRK-NYUINKHN(IDX1)   NOT =         SPACE)  AND
                       (WRK-GAIHAKU(IDX1)        =         SPACE)  AND
                       (WRK-GENSAN-IRYO(IDX1)   NOT =      SPACE)
                   ADD      1               TO  WRK-MEISAISU
                   MOVE   WRK-GENSAN3       TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"          TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
                   MOVE     1               TO  WRK-GENSAN3-MIJISI
               END-IF
               IF      (WRK-NYUINKHN(IDX1)   NOT =         SPACE)  AND
                       (WRK-GAIHAKU(IDX1)        =         SPACE)  AND
                       (WRK-GENSAN-JYOKU(IDX1)   NOT =     SPACE)
                   ADD      1               TO  WRK-MEISAISU
                   MOVE   WRK-GENSAN4       TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "3"          TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
                   MOVE     1               TO  WRK-GENSAN4-MIJISI
               END-IF
      *        労災乗数（特定入院料の場合は労災乗数なし）
               IF       LNK-SROUNYUIN-SRYKBN(IDX1)    =    "90"
                   EVALUATE  LNK-SROUNYUIN-JOUSU-KBN(IDX1)
                       WHEN  "1"
                           ADD      1           TO  WRK-MEISAISU
                           MOVE   "101900010"   TO  LNK-SROUNYUIN-SRYCD
                                               (IDX1 WRK-MEISAISU)
                           MOVE    "4"      TO  LNK-SROUNYUIN-SRYCD-SK
                                               (IDX1 WRK-MEISAISU)
                       WHEN  "2"
                           ADD      1           TO  WRK-MEISAISU
                           MOVE   "101900020"   TO  LNK-SROUNYUIN-SRYCD
                                               (IDX1 WRK-MEISAISU)
                           MOVE    "4"      TO  LNK-SROUNYUIN-SRYCD-SK
                                               (IDX1 WRK-MEISAISU)
                   END-EVALUATE
               END-IF
      *        初期加算（長期減算）
               IF      (WRK-GAIHAKU(IDX1)        =         SPACE)  AND
                       (WRK-SHOKIKSN(IDX1)   NOT =         SPACE)
                   ADD      1               TO  WRK-MEISAISU
                   MOVE   WRK-SHOKIKSN(IDX1) TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "5"           TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        標欠
               IF       WRK-HYOKETU(IDX1)    NOT =         SPACE
                   ADD      1               TO  WRK-MEISAISU
                   MOVE   WRK-HYOKETU(IDX1)  TO  LNK-SROUNYUIN-SRYCD
                                           (IDX1 WRK-MEISAISU)
                   MOVE    "6"           TO  LNK-SROUNYUIN-SRYCD-SK
                                           (IDX1 WRK-MEISAISU)
               END-IF
      *        入院料加算（入院会計）
      *         PERFORM   VARYING   KSN-IDX   FROM  1  BY  1
      *                   UNTIL    (WRK-NYUKSN(KSN-IDX)  =  SPACE)  OR
      *                            (KSN-IDX             >  20)
      *            IF    WRK-NYUKSN-DAY(KSN-IDX IDX1)  =  ZERO
      *                CONTINUE
      *            ELSE
      *                ADD     1           TO  WRK-MEISAISU
      *                MOVE WRK-NYUKSN(KSN-IDX)
      *                                    TO  LNK-SROUNYUIN-SRYCD
      *                                   (IDX1 WRK-MEISAISU)
      *            END-IF
      *         END-PERFORM
           END-PERFORM
      *
      *    再構築した労災入院会計情報から点数の算定
           PERFORM      VARYING    IDX1   FROM   1  BY  1
                        UNTIL      IDX1   >   31
      *
               MOVE     ZERO       TO       WRK-HYOKETU-TEN
                                            WRK-ZAITEN-HYOKETU
               IF   WRK-HYOKETU(IDX1)   =   SPACE
      *          点数マスタの読み込み
                 PERFORM   VARYING   IDX2    FROM   1  BY   1
                      UNTIL (LNK-SROUNYUIN-SRYCD(IDX1 IDX2) = SPACE)
                         OR (IDX2    >      20)
                     PERFORM    210-TENSU-KEISAN-SEC
                 END-PERFORM
               ELSE
      *          点数マスタの読み込み
                 INITIALIZE                          TNS-TENSU-REC
                 MOVE    WRK-HYOKETU(IDX1)       TO  TNS-SRYCD
                 MOVE    LNK-SROUNYUIN-SANTEIYM  TO  ORC-DBYMD(1:6)
                 MOVE    "01"                    TO  ORC-DBYMD(7:2)
                 PERFORM 910-TENSU-READ-SEC
                 MOVE    TNS-TEN                 TO  WRK-HYOKETU-TEN
      *
                 PERFORM   VARYING   IDX2    FROM   1  BY   1
                      UNTIL (LNK-SROUNYUIN-SRYCD(IDX1 IDX2) = SPACE)
                         OR (IDX2    >      20)
                     PERFORM    210-TENSU-KEISAN-HYOKETU-SEC
                 END-PERFORM
               END-IF
      *
               PERFORM   VARYING   KSN-IDX   FROM   1  BY   1
                         UNTIL (WRK-NYUKSN(KSN-IDX)  =  SPACE)  OR
                               (KSN-IDX    >      20)
                  IF    WRK-NYUKSN-DAY(KSN-IDX IDX1)  =  ZERO
                      CONTINUE
                  ELSE
                      ADD  WRK-NYUKSN-ZAITEN(KSN-IDX)
                                 TO  LNK-SROUNYUIN-ZAITEN-NACCT(IDX1)
                  END-IF
               END-PERFORM
      *
               IF    LNK-SROUNYUIN-NYUIN-FLG(IDX1)      =   "1"
               AND   LNK-SROUNYUIN-ZAITEN(IDX1)         =  ZERO
                  MOVE     SPACE   TO   LNK-SROUNYUIN-NYUIN-FLG(IDX1)
               END-IF
           END-PERFORM
      *
      *
           IF  WRK-GENSAN2-MIJISI  =  1
                 MOVE    WRK-GENSAN2       TO  WRK-RYAKU-SRYCD
                 MOVE    "6"               TO  WRK-ZAISKBKBN
                 PERFORM   210-RYAKUSYO-HENSYU-SEC
           END-IF
      *
           IF  WRK-GENSAN3-MIJISI  =  1
                 MOVE    WRK-GENSAN3       TO  WRK-RYAKU-SRYCD
                 MOVE    "6"               TO  WRK-ZAISKBKBN
                 PERFORM   210-RYAKUSYO-HENSYU-SEC
           END-IF
      *
           IF  WRK-GENSAN4-MIJISI  =  1
                 MOVE    WRK-GENSAN4       TO  WRK-RYAKU-SRYCD
                 MOVE    "6"               TO  WRK-ZAISKBKBN
                 PERFORM   210-RYAKUSYO-HENSYU-SEC
           END-IF
      *
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院診療行為取得
      *****************************************************************
       210-NYUINACT-GET-SEC            SECTION.
      *
           INITIALIZE                      NYUINACT-REC
           MOVE    NACCT-HOSPID        TO  NSRY-HOSPID
           MOVE    NACCT-NYUGAIKBN     TO  NSRY-NYUGAIKBN
           MOVE    NACCT-PTID          TO  NSRY-PTID
           MOVE    NACCT-SRYYM         TO  NSRY-SRYYM
           MOVE    NACCT-ZAINUM        TO  NSRY-ZAINUM
           MOVE    "NYUINACT-KEY9"     TO  ORC-DBPATH
           PERFORM 910-NYUINACT-SELECT-SEC
           IF      FLG-NYUINACT        =   ZERO
               MOVE     "NYUINACT-KEY9"    TO  ORC-DBPATH
               PERFORM   910-NYUINACT-READ-SEC
           END-IF
      *
      *    入院行為クローズ
           MOVE    "NYUINACT-KEY9"     TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
           .
       210-NYUINACT-GET-EXT.
           EXIT.
      *
      *****************************************************************
      *    外泊前処理
      *****************************************************************
       210-GAIHAKU-MAE-SEC            SECTION.
      *
      *    外泊コード編集用に入院基本料加算区分と診療区分の退避
           INITIALIZE                      TNS-TENSU-REC
           MOVE    NSRY-SRYCD(1)       TO  TNS-SRYCD
           MOVE    LNK-SROUNYUIN-SANTEIYM
                                       TO  ORC-DBYMD(1:6)
           MOVE    "01"                TO  ORC-DBYMD(7:2)
           PERFORM 910-TENSU-READ-SEC
           MOVE    TNS-NYUKHNKSNKBN    TO  WRK-KSNKBN
      *
           .
       210-GAIHAKU-MAE-EXT.
           EXIT.
      *
      *****************************************************************
      *    外泊診療コード取得
      *****************************************************************
       210-GAIHAKU-SET-SEC            SECTION.
      *
           MOVE   NACCT-DAY(IDX1)     TO    LNK-SROUNYUIN-GAIHAKU(IDX1)
      *
           EVALUATE   LNK-SROUNYUIN-SRYKBN(IDX1)
              WHEN     "90"
                 EVALUATE   NACCT-DAY(IDX1)
                    WHEN     1
                      MOVE   "190107290"   TO  WRK-GAIHAKU(IDX1)
                    WHEN     2
                      MOVE   "190107390"   TO  WRK-GAIHAKU(IDX1)
                    WHEN     4
                      PERFORM   2101-GAIHAKU-TAKIKAN-SEC
                 END-EVALUATE
              WHEN     "92"
                 EVALUATE   NACCT-DAY(IDX1)
                    WHEN     1
                      MOVE   "190107490"   TO  WRK-GAIHAKU(IDX1)
                    WHEN     2
                      MOVE   "190107590"   TO  WRK-GAIHAKU(IDX1)
                    WHEN     4
                      PERFORM   2101-GAIHAKU-TAKIKAN-SEC
                 END-EVALUATE
           END-EVALUATE
      *
           .
       210-GAIHAKU-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    外泊診療コード取得
      *****************************************************************
       2101-GAIHAKU-TAKIKAN-SEC           SECTION.
      *
           EVALUATE   WRK-NYUKHNKSNKBN(IDX1)
      *
               WHEN   099  THRU  100 
               WHEN   201  THRU  219
               WHEN   221  THRU  222
               WHEN   224
               WHEN   227  THRU  228
               WHEN   230  THRU  233
                  MOVE   "190108790"    TO   WRK-GAIHAKU(IDX1)
               WHEN   013  THRU  021
               WHEN   101  THRU  109
                  MOVE   "190108890"    TO   WRK-GAIHAKU(IDX1)
               WHEN   075  THRU  080
                  MOVE   "190108990"    TO   WRK-GAIHAKU(IDX1)
               WHEN   085  THRU  086
               WHEN   132  THRU  133
                  MOVE   "190109090"    TO   WRK-GAIHAKU(IDX1)
      *
           END-EVALUATE
      *
           .
       2101-GAIHAKU-TAKIKAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    老人対象患者チェック
      *****************************************************************
       210-RJN-TAISYO-CHK-SEC         SECTION.
      *
           MOVE    ZERO                TO  FLG-RJN
      *
      *    入院患者の老人保険判定
           MOVE    ZERO                TO   FLG-HKNCOMBI
           INITIALIZE                       HKNCOMBI-REC
           MOVE    LNK-SROUNYUIN-HOSPID TO  COMB-HOSPID
           MOVE    LNK-SROUNYUIN-PTID  TO   COMB-PTID
           MOVE    LNK-SROUNYUIN-HKNCOMBI
                                       TO   COMB-HKNCOMBINUM
           MOVE    "HKNCOMBI-KEY"      TO   ORC-DBPATH
           PERFORM 910-HKNCOMBI-SELECT-SEC
           IF      FLG-HKNCOMBI        =    ZERO
               MOVE     "HKNCOMBI-KEY" TO   ORC-DBPATH
               PERFORM   910-HKNCOMBI-READ-SEC
           END-IF
      *    保険組合せクローズ
           MOVE    "HKNCOMBI-KEY"      TO   ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
      *    入院患者の年齢計算
           INITIALIZE                      ORCSAGECHKAREA
           MOVE    LNK-SROUNYUIN-HOSPID
                                       TO  AGECHK-HOSPID
           MOVE    LNK-SROUNYUIN-PTID
                                       TO  AGECHK-PTID
           MOVE    LNK-SROUNYUIN-SANTEIYM
                                       TO  AGECHK-SRYYMD(1:6)
           MOVE    "01"                TO  AGECHK-SRYYMD(7:2)
           MOVE    PTINF-BIRTHDAY      TO  AGECHK-BIRTHDAY
           CALL    "ORCSAGECHK"        USING
                                           ORCSAGECHKAREA
           IF    ( AGECHK-RC           =   ZERO )
               CONTINUE
           ELSE
               INITIALIZE  ORCSAGECHKAREA
           END-IF
      *
      *    老人対象患者判定
           IF    ((FLG-HKNCOMBI        =   ZERO)    AND
                  (COMB-KOH1HKNNUM     =   "027"))   OR
                 ( AGECHK-NENREI      >=    75)      OR
                 ((AGECHK-NENREI      >=    70)     AND
                  (PTINF-BIRTHDAY     <=   "19320930"))
               MOVE    1               TO  FLG-RJN
           END-IF
      *
           .
       210-RJN-TAISYO-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数計算処理
      *****************************************************************
       210-TENSU-KEISAN-SEC            SECTION.
      *
           INITIALIZE                      TNS-TENSU-REC
           IF     (IDX2                   =     1  )
           AND    (WRK-GAIHAKU(IDX1)   NOT =   SPACE)
               PERFORM    2101-GAIHAKU-NYUINKHN-SET-SEC
               IF    WRK-GAIHAKU-SRYCD    NOT =   SPACE
                  MOVE    WRK-GAIHAKU-SRYCD   TO  TNS-SRYCD
               ELSE
                  MOVE    LNK-SROUNYUIN-SRYCD(IDX1 IDX2)
                                              TO  TNS-SRYCD
               END-IF
           ELSE
                  MOVE    LNK-SROUNYUIN-SRYCD(IDX1 IDX2)
                                              TO  TNS-SRYCD
           END-IF
           MOVE    LNK-SROUNYUIN-SANTEIYM  TO  ORC-DBYMD(1:6)
           MOVE    "01"                    TO  ORC-DBYMD(7:2)
           PERFORM 910-TENSU-READ-SEC
      *
      *    点数算定
           EVALUATE    TNS-TENSIKIBETU
      *        点数（プラス）
               WHEN    3
                   ADD   TNS-TEN     TO  LNK-SROUNYUIN-ZAITEN(IDX1)
      *        ％加算
               WHEN    5
                   COMPUTE    LNK-SROUNYUIN-ZAITEN(IDX1)  =
                              LNK-SROUNYUIN-ZAITEN(IDX1)  +
                            ((LNK-SROUNYUIN-ZAITEN(IDX1)  *
                              (TNS-TEN   /  100))  + 0.5)
      *        ％減算
               WHEN    6
                   COMPUTE    LNK-SROUNYUIN-ZAITEN(IDX1)  =
                            ( LNK-SROUNYUIN-ZAITEN(IDX1)  -
                             (LNK-SROUNYUIN-ZAITEN(IDX1)  *
                             (TNS-TEN   /  100)))        +  0.5
      *        点数（マイナス）
               WHEN    8
                   COMPUTE  LNK-SROUNYUIN-ZAITEN(IDX1)  =
                            LNK-SROUNYUIN-ZAITEN(IDX1)  -  TNS-TEN
      *        WHEN  OTHER
      *            ADD   TNS-TEN     TO  LNK-SROUNYUIN-ZAITEN(IDX1)
           END-EVALUATE
      *
      *    診療コード計
           MOVE    TNS-SRYCD          TO  WRK-SRYCD-X
           COMPUTE LNK-SROUNYUIN-SRYCDTOTAL(IDX1)  =
                   LNK-SROUNYUIN-SRYCDTOTAL(IDX1)  +   WRK-SRYCD-9
           ADD      1          TO  LNK-SROUNYUIN-MEISAISU(IDX1)
      *
           .
       210-TENSU-KEISAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数計算処理（標欠）
      *****************************************************************
       210-TENSU-KEISAN-HYOKETU-SEC            SECTION.
      *
           IF   LNK-SROUNYUIN-SRYCD-SK(IDX1 IDX2)       =   "5"
              MOVE  LNK-SROUNYUIN-ZAITEN(IDX1)  TO  WRK-ZAITEN-HYOKETU
              MOVE       ZERO      TO     LNK-SROUNYUIN-ZAITEN(IDX1)
           END-IF
      *
           IF   LNK-SROUNYUIN-SRYCD-SK(IDX1 IDX2)   NOT =   "6"
              PERFORM    210-TENSU-KEISAN-SEC
           END-IF
      *
           IF      IDX2   =   1
      *      標欠：％減算
             COMPUTE    LNK-SROUNYUIN-ZAITEN(IDX1)  =
                      ( LNK-SROUNYUIN-ZAITEN(IDX1)  -
                       (LNK-SROUNYUIN-ZAITEN(IDX1)  *
                       (WRK-HYOKETU-TEN   /  100))) +  0.5
      *      診療コード計
             MOVE    WRK-HYOKETU(IDX1)   TO  WRK-SRYCD-X
             COMPUTE LNK-SROUNYUIN-SRYCDTOTAL(IDX1)  =
                     LNK-SROUNYUIN-SRYCDTOTAL(IDX1)  +   WRK-SRYCD-9
             ADD      1          TO  LNK-SROUNYUIN-MEISAISU(IDX1)
           END-IF
      *
           IF      LNK-SROUNYUIN-SRYCD-SK(IDX1 IDX2)  =   "5"
             IF    LNK-SROUNYUIN-ZAITEN(IDX1)   >=   ZERO
      *        標欠：％減算
               COMPUTE    LNK-SROUNYUIN-ZAITEN(IDX1)  =
                        ( LNK-SROUNYUIN-ZAITEN(IDX1)  -
                         (LNK-SROUNYUIN-ZAITEN(IDX1)  *
                         (WRK-HYOKETU-TEN   /  100))) +  0.5
             ELSE
               COMPUTE    LNK-SROUNYUIN-ZAITEN(IDX1)  =
                          LNK-SROUNYUIN-ZAITEN(IDX1)  *
                          -1
               COMPUTE    LNK-SROUNYUIN-ZAITEN(IDX1)  =
                        ( LNK-SROUNYUIN-ZAITEN(IDX1)  -
                         (LNK-SROUNYUIN-ZAITEN(IDX1)  *
                         (WRK-HYOKETU-TEN   /  100))) +  0.5
               COMPUTE    LNK-SROUNYUIN-ZAITEN(IDX1)  =
                          LNK-SROUNYUIN-ZAITEN(IDX1)  *
                          -1
             END-IF
      *
             COMPUTE LNK-SROUNYUIN-ZAITEN(IDX1)  =
                     LNK-SROUNYUIN-ZAITEN(IDX1)  +  WRK-ZAITEN-HYOKETU
           END-IF
      *
           .
       210-TENSU-KEISAN-HYOKETU-EXT.
           EXIT.
      *
      **************************************************************
      *    外泊時の入院基本料取得
      **************************************************************
       2101-GAIHAKU-NYUINKHN-SET-SEC           SECTION.
      *
           MOVE    SPACE    TO   WRK-GAIHAKU-SRYCD
      *
           EVALUATE  LNK-SROUNYUIN-SRYCD(IDX1 IDX2)
               WHEN  "190078210"
                     MOVE  "190077410"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190078310"
                     MOVE  "190077510"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190078410"
                     MOVE  "190077610"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190078510"
                     MOVE  "190077710"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190078610"
                     MOVE  "190077810"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190078710"
                     MOVE  "190077910"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190078810"
                     MOVE  "190078010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190078910"
                     MOVE  "190078110"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190082410"
                     MOVE  "190081710"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190082510"
                     MOVE  "190081810"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190082610"
                     MOVE  "190081910"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190082710"
                     MOVE  "190082010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190082810"
                     MOVE  "190082110"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190082910"
                     MOVE  "190082210"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190083010"
                     MOVE  "190082310"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190084310"
                     MOVE  "190083610"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190084410"
                     MOVE  "190083710"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190084510"
                     MOVE  "190083810"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190084610"
                     MOVE  "190083910"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190084710"
                     MOVE  "190084010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190084810"
                     MOVE  "190084110"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190084910"
                     MOVE  "190084210"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190087410"
                     MOVE  "190085910"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190087510"
                     MOVE  "190086010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190087610"
                     MOVE  "190086110"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190087710"
                     MOVE  "190086210"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190087810"
                     MOVE  "190086310"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190087910"
                     MOVE  "190086410"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190088010"
                     MOVE  "190086510"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190088110"
                     MOVE  "190086610"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190088210"
                     MOVE  "190086710"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190088310"
                     MOVE  "190086810"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190088410"
                     MOVE  "190086910"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190088510"
                     MOVE  "190087010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190088610"
                     MOVE  "190087110"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190088710"
                     MOVE  "190087210"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190088810"
                     MOVE  "190087310"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190094710"
                     MOVE  "190093710"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190094810"
                     MOVE  "190093810"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190094910"
                     MOVE  "190093910"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190095010"
                     MOVE  "190094010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190095110"
                     MOVE  "190094110"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190797410"
                     MOVE  "190796610"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190797510"
                     MOVE  "190796710"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190797610"
                     MOVE  "190796810"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190797710"
                     MOVE  "190796910"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190797810"
                     MOVE  "190797010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190797910"
                     MOVE  "190797110"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190798010"
                     MOVE  "190797210"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190798110"
                     MOVE  "190797310"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190803510"
                     MOVE  "190802810"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190803610"
                     MOVE  "190802910"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190803710"
                     MOVE  "190803010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190803810"
                     MOVE  "190803110"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190803910"
                     MOVE  "190803210"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190804010"
                     MOVE  "190803310"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190804110"
                     MOVE  "190803410"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190091210"
                     MOVE  "190090210"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190091310"
                     MOVE  "190090310"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190109670"
                     MOVE  "190097010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190808870"
                     MOVE  "190805710"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190117070"
                     MOVE  "190097010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190813270"
                     MOVE  "190805710"      TO   WRK-GAIHAKU-SRYCD
      *
               WHEN  "190117670"
                     MOVE  "190097010"      TO   WRK-GAIHAKU-SRYCD
               WHEN  "190813870"
                     MOVE  "190805710"      TO   WRK-GAIHAKU-SRYCD
      *        診療所老人医療管理料
               WHEN  "190774210"
                     MOVE  "190774110"      TO   WRK-GAIHAKU-SRYCD
      *        老人性認知症疾患治療病棟入院料１
               WHEN  "190740010"
                     MOVE  "190739910"      TO   WRK-GAIHAKU-SRYCD
      *        老人性認知症疾患治療病棟入院料２
               WHEN  "190813510"
                     MOVE  "190813410"      TO   WRK-GAIHAKU-SRYCD
           END-EVALUATE
      *
           IF   (LNK-SROUNYUIN-SANTEIYM  >= "200604"   )
           AND  (WRK-GAIHAKU-SRYCD        = "190774110" OR
                                            "190739910" OR
                                            "190813410")
               MOVE    SPACE    TO    WRK-GAIHAKU-SRYCD
           END-IF
      *
           .
       2101-GAIHAKU-NYUINKHN-SET-EXT.
           EXIT.
      *
      **************************************************************
      *    略称の編集
      **************************************************************
       210-RYAKUSYO-HENSYU-SEC           SECTION.
      *
           IF   LNK-SROUNYUIN-RYAKUKBN   =   "1"
      *
              MOVE    SPACE         TO   NYURYAKUSHO-REC
              INITIALIZE                 NYURYAKUSHO-REC
      *
              MOVE    WRK-RYAKU-SRYCD          TO   NYURYAKUSHO-SRYCD
              MOVE    LNK-SROUNYUIN-SANTEIYM   TO   ORC-DBYMD(1:6)
              MOVE    "01"                     TO   ORC-DBYMD(7:2)
              PERFORM  910-NYURYAKUSHO-READ-SEC
      *
              IF       FLG-NYURYAKUSHO    =    ZERO
      *
                EVALUATE   WRK-ZAISKBKBN
      *
                 WHEN     1
      *
                  IF   WRK-SRYKBN  =  "90"
      *
                    MOVE     ZERO     TO    FLG-ARI
                    PERFORM  VARYING  ID1    FROM    1   BY  1
                          UNTIL   ID1                          > 20
                            OR    LNK-SROUNYUIN-RYAKUSYO2(ID1) = SPACE
                            OR    FLG-ARI                      = 1 
                       IF  LNK-SROUNYUIN-RYAKUSYO2(ID1)
                                           =  NYURYAKUSHO-RYAKUSHO
                           MOVE  1   TO   FLG-ARI
                       END-IF
                    END-PERFORM  
                    IF  FLG-ARI  =  ZERO  
                           MOVE   NYURYAKUSHO-RYAKUSHO
                                     TO   LNK-SROUNYUIN-RYAKUSYO2(ID1)
                    END-IF
      *
                    IF  LNK-SROUNYUIN-SANTEIYM  >= "200604"
                     CONTINUE
                    ELSE
                     IF  WRK-RYAKU-SRYCD  =  "190109670" OR "190808870"
      *
                       MOVE     ZERO     TO    FLG-ARI
                       PERFORM  VARYING  ID1    FROM    1   BY  1
                           UNTIL   ID1                          > 20
                             OR    LNK-SROUNYUIN-RYAKUSYO2(ID1) = SPACE
                             OR    FLG-ARI                      = 1 
                         IF LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:4) = "有看"
                              MOVE  1   TO   FLG-ARI
                         END-IF
                       END-PERFORM  
                       IF  FLG-ARI  =  ZERO  
                            MOVE  "有看"
                                TO   LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:4)
                       END-IF
      *
                     END-IF
                     IF  WRK-RYAKU-SRYCD  =  "190117070" OR "190813270"
      *
                       MOVE     ZERO     TO    FLG-ARI
                       PERFORM  VARYING  ID1    FROM    1   BY  1
                           UNTIL   ID1                          > 20
                             OR    LNK-SROUNYUIN-RYAKUSYO2(ID1) = SPACE
                             OR    FLG-ARI                      = 1 
                         IF LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:4) = "有医"
                              MOVE  1   TO   FLG-ARI
                         END-IF
                       END-PERFORM  
                       IF  FLG-ARI  =  ZERO  
                            MOVE  "有医"
                                TO   LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:4)
                       END-IF
      *
                     END-IF
                     IF  WRK-RYAKU-SRYCD  =  "190117670" OR "190813870"
      *
                       MOVE     ZERO     TO    FLG-ARI
                       PERFORM  VARYING  ID1    FROM    1   BY  1
                           UNTIL   ID1                          > 20
                             OR    LNK-SROUNYUIN-RYAKUSYO2(ID1) = SPACE
                             OR    FLG-ARI                      = 1 
                         IF LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:6) = "有看医"
                              MOVE  1   TO   FLG-ARI
                         END-IF
                       END-PERFORM  
                       IF  FLG-ARI  =  ZERO  
                            MOVE  "有看医"
                                TO   LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:6)
                       END-IF
      *
                     END-IF
                    END-IF
      *
                  ELSE
      *
                    MOVE    NYURYAKUSHO-RYAKUSHO   TO  WRK-RYAKUSHO
      *
                  END-IF
      *
                 WHEN   6
      *
                    MOVE     ZERO     TO    FLG-ARI
                    PERFORM  VARYING  ID1    FROM    1   BY  1
                          UNTIL   ID1                          > 20
                            OR    LNK-SROUNYUIN-RYAKUSYO2(ID1) = SPACE
                            OR    FLG-ARI                      = 1 
                       IF  LNK-SROUNYUIN-RYAKUSYO2(ID1)
                                           =  NYURYAKUSHO-RYAKUSHO
                           MOVE  1   TO   FLG-ARI
                       END-IF
                    END-PERFORM  
                    IF  FLG-ARI  =  ZERO  
                           MOVE   NYURYAKUSHO-RYAKUSHO
                                     TO   LNK-SROUNYUIN-RYAKUSYO2(ID1)
                    END-IF
      *
                END-EVALUATE
              ELSE
                IF   ( LNK-SROUNYUIN-SANTEIYM  >= "200604"    )
                AND  ( WRK-RYAKU-SRYCD          = "190123250" OR
                                                  "190123350" OR
                                                  "190123450" OR
                                                  "190123550" )
      *
                    MOVE     ZERO     TO    FLG-ARI
                    PERFORM  VARYING  ID1    FROM    1   BY  1
                          UNTIL   ID1                          > 20
                            OR    LNK-SROUNYUIN-RYAKUSYO2(ID1) = SPACE
                            OR    FLG-ARI                      = 1
                      EVALUATE   WRK-RYAKU-SRYCD
                        WHEN     "190123250"
                         IF  LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:8)
                                                         = "一般経過"
                             MOVE  1   TO   FLG-ARI
                         END-IF
                        WHEN     "190123350"
                         IF  LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:8)
                                                         = "結核経過"
                             MOVE  1   TO   FLG-ARI
                         END-IF
                        WHEN     "190123450"
                         IF  LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:8)
                                                         = "精神経過"
                             MOVE  1   TO   FLG-ARI
                         END-IF
                        WHEN     "190123550"
                         IF  LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:8)
                                                         = "障害経過"
                             MOVE  1   TO   FLG-ARI
                         END-IF
                      END-EVALUATE
                    END-PERFORM
                    IF  FLG-ARI  =  ZERO
                      EVALUATE   WRK-RYAKU-SRYCD
                        WHEN     "190123250"
                           MOVE   "一般経過"
                                 TO   LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:8)
                        WHEN     "190123350"
                           MOVE   "結核経過"
                                 TO   LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:8)
                        WHEN     "190123450"
                           MOVE   "精神経過"
                                 TO   LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:8)
                        WHEN     "190123550"
                           MOVE   "障害経過"
                                 TO   LNK-SROUNYUIN-RYAKUSYO2(ID1)(1:8)
                      END-EVALUATE
                    END-IF
      *
                END-IF
              END-IF
      *
           END-IF
      *
           .
       210-RYAKUSYO-HENSYU-EXT.
           EXIT.
      *
      **************************************************************
      *    診療行為の読み込み処理
      **************************************************************
       210-SRYACT-HENSYU-SEC           SECTION.
      *
           IF   LNK-SROUNYUIN-RYAKUKBN   =   "1"
      *
              INITIALIZE                           SRYACT-REC
              MOVE    ACCT-HOSPID          TO      SRY-HOSPID
              MOVE    ACCT-NYUGAIKBN       TO      SRY-NYUGAIKBN
              MOVE    ACCT-PTID            TO      SRY-PTID
              MOVE    ACCT-SRYYM           TO      SRY-SRYYM
              MOVE    ACCT-ZAINUM          TO      SRY-ZAINUM
              MOVE    SRYACT-REC           TO      MCPDATA-REC
              MOVE    "DBSELECT"           TO      MCP-FUNC
              MOVE    "SRYACT-KEY9"        TO      ORC-DBPATH
              CALL    "ORCMCPSUB"          USING   MCPAREA
                                                   ORCMCPAREA
                                                   MCPDATA-REC
              IF      MCP-RC               =       ZERO
                  MOVE    "SRYACT-KEY9"    TO      ORC-DBPATH
                  PERFORM  910-SRYACT-READ-SEC
                  PERFORM  UNTIL  FLG-SRYACT    =    1
                      PERFORM   VARYING   SRYACT-IDX  FROM  1   BY   1
                                UNTIL    (SRYACT-IDX    >   5)  OR
                                         (SRY-SRYCD(SRYACT-IDX)
                                                            =   SPACE)
      *
                          MOVE    SRY-SRYCD(SRYACT-IDX)
                                                  TO  WRK-RYAKU-SRYCD
                          MOVE    "6"             TO  WRK-ZAISKBKBN
      *
                          PERFORM   210-RYAKUSYO-HENSYU-SEC
      *
                      END-PERFORM
                      MOVE     "SRYACT-KEY9"    TO   ORC-DBPATH
                      PERFORM   910-SRYACT-READ-SEC
                  END-PERFORM
              END-IF
      *
              MOVE     "SRYACT-KEY9"       TO  ORC-DBPATH
              PERFORM   910-DB-CLOSE-SEC
      *
           END-IF
      *
           .
       210-SRYACT-HENSYU-EXT.
           EXIT.
      *
      ******************************************************************
      *    月内に複数の医療期間情報がないか検索（医療機関減算の確認）  *
      ******************************************************************
       210-SYS-5000-CHK-SEC                 SECTION.
      *
           MOVE    ZERO                TO  WRK-SYS5000-COUNT
      *
           MOVE    ZERO                TO  FLG-SYSKANRI
           INITIALIZE                      SYSKANRI-REC
           MOVE    "5000"              TO  SYS-KANRICD
           MOVE    "*"                 TO  SYS-KBNCD
           MOVE    LNK-SROUNYUIN-SANTEIYM
                                       TO  ORC-DBYMD(1:6)
           MOVE    "__"                TO  ORC-DBYMD(7:2)
           MOVE    SYSKANRI-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SYSKANRI-KEY11"    TO  ORC-DBPATH
           CALL    "ORCMCPSUB"             USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SYSKANRI-KEY11"  TO  ORC-DBPATH
               PERFORM  910-SYSKANRI-READ-SEC
               PERFORM  UNTIL    FLG-SYSKANRI   =   1
                   ADD     1     TO        WRK-SYS5000-COUNT
                   MOVE    "SYSKANRI-KEY11"  TO  ORC-DBPATH
                   PERFORM  910-SYSKANRI-READ-SEC
               END-PERFORM
           END-IF
      *
           MOVE    "SYSKANRI-KEY11"    TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
           .
      *
       210-SYS-5000-CHK-EXT.
           EXIT.
      *
      ******************************************************************
      *    月内の医療期間情報取得（医療機関減算情報）                  *
      ******************************************************************
       210-SYS-5000-GET-SEC                 SECTION.
      *
           MOVE    ZERO                TO  FLG-SYSKANRI
           INITIALIZE                      SYSKANRI-REC
           MOVE    "5000"              TO  SYS-KANRICD
           MOVE    "*"                 TO  SYS-KBNCD
           MOVE    LNK-SROUNYUIN-SANTEIYM
                                       TO  ORC-DBYMD(1:6)
           MOVE    "__"                TO  ORC-DBYMD(7:2)
           MOVE    SYSKANRI-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SYSKANRI-KEY11"    TO  ORC-DBPATH
           CALL    "ORCMCPSUB"             USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SYSKANRI-KEY11"  TO  ORC-DBPATH
               PERFORM  910-SYSKANRI-READ-SEC
               PERFORM  UNTIL    FLG-SYSKANRI   =   1
                 MOVE   SYS-5000-STYUKYMD(7:2)  TO  IDV
                 PERFORM  VARYING   GENSAN-IDX  FROM  1  BY  1
                            UNTIL     GENSAN-IDX   >   4
                    IF    SYS-5000-GENKBN(GENSAN-IDX)   =   "2"
                        EVALUATE  GENSAN-IDX
                            WHEN  2
                              MOVE  "190080570"  TO  WRK-GENSAN2
      **                        IF   FLG-RJN    =    ZERO
      **                            MOVE  "190080570"  TO  WRK-GENSAN2
      **                        ELSE
      **                            MOVE  "190799770"  TO  WRK-GENSAN2
      **                        END-IF
                              PERFORM VARYING GENSAN-IDX2 FROM IDV BY 1
                                  UNTIL     GENSAN-IDX2   >   31
                                MOVE   "1"  TO
                                        WRK-GENSAN-INNAI(GENSAN-IDX2)
                              END-PERFORM
                            WHEN  3
                              MOVE  "190111690"  TO  WRK-GENSAN3
      **                        IF   FLG-RJN    =    ZERO
      **                            MOVE  "190111690"  TO  WRK-GENSAN3
      **                        ELSE
      **                            MOVE  "190809090"  TO  WRK-GENSAN3
      **                        END-IF
                              PERFORM VARYING GENSAN-IDX2 FROM IDV BY 1
                                  UNTIL     GENSAN-IDX2   >   31
                                MOVE   "1"  TO
                                        WRK-GENSAN-IRYO(GENSAN-IDX2)
                              END-PERFORM
                            WHEN  4
                              MOVE  "190111790"  TO  WRK-GENSAN4
      **                        IF   FLG-RJN    =    ZERO
      **                            MOVE  "190111790"  TO  WRK-GENSAN4
      **                        ELSE
      **                            MOVE  "190809190"  TO  WRK-GENSAN4
      **                        END-IF
                              PERFORM VARYING GENSAN-IDX2 FROM IDV BY 1
                                  UNTIL     GENSAN-IDX2   >   31
                                MOVE   "1"  TO
                                        WRK-GENSAN-JYOKU(GENSAN-IDX2)
                              END-PERFORM
                        END-EVALUATE
                    ELSE
                        EVALUATE  GENSAN-IDX
                            WHEN  2
                              PERFORM VARYING GENSAN-IDX2 FROM IDV BY 1
                                  UNTIL     GENSAN-IDX2   >   31
                                MOVE   SPACE  TO
                                        WRK-GENSAN-INNAI(GENSAN-IDX2)
                              END-PERFORM
                            WHEN  3
                              PERFORM VARYING GENSAN-IDX2 FROM IDV BY 1
                                  UNTIL     GENSAN-IDX2   >   31
                                MOVE   SPACE  TO
                                        WRK-GENSAN-IRYO(GENSAN-IDX2)
                              END-PERFORM
                            WHEN  4
                              PERFORM VARYING GENSAN-IDX2 FROM IDV BY 1
                                  UNTIL     GENSAN-IDX2   >   31
                                MOVE   SPACE  TO
                                        WRK-GENSAN-JYOKU(GENSAN-IDX2)
                              END-PERFORM
                        END-EVALUATE
                    END-IF
                 END-PERFORM
                 MOVE    "SYSKANRI-KEY11"  TO  ORC-DBPATH
                 PERFORM  910-SYSKANRI-READ-SEC
               END-PERFORM
           END-IF
      *
           MOVE    "SYSKANRI-KEY11"    TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
           .
      *
       210-SYS-5000-GET-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理マスター読込
      *****************************************************************
       910-SYSKANRI-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-5000-REC
               MOVE    ZERO                TO  FLG-SYSKANRI
           ELSE
               INITIALIZE                      SYS-5000-REC
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
      *
           .
       910-SYSKANRI-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者マスター読込
      *****************************************************************
       900-PTINF-READ-SEC         SECTION.
      *
           MOVE    PTINF-REC           TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "PTINF-KEY"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "PTINF-KEY"         TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC        TO  PTINF-REC
                   MOVE    ZERO               TO  FLG-PTINF
               ELSE
                   INITIALIZE                     PTINF-REC
                   MOVE    1                  TO  FLG-PTINF
               END-IF
           ELSE
               INITIALIZE                 PTINF-REC
               MOVE    1              TO  FLG-PTINF
           END-IF
      *
           MOVE    "PTINF-KEY"        TO  ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
           .
       900-PTINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院会計マスター検索
      *****************************************************************
       910-NYUINACCT-SELECT-SEC        SECTION.
      *
           MOVE    NYUINACCT-REC       TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO               TO  FLG-NYUINACCT
           ELSE
               MOVE    1                  TO  FLG-NYUINACCT
           END-IF
      *
           .
       910-NYUINACCT-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院会計マスター読込
      *****************************************************************
       910-NYUINACCT-READ-SEC        SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC        TO  NYUINACCT-REC
               MOVE    ZERO               TO  FLG-NYUINACCT
           ELSE
               INITIALIZE                     NYUINACCT-REC
               MOVE    1                  TO  FLG-NYUINACCT
           END-IF
      *
           .
       910-NYUINACCT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院診療行為マスター検索
      *****************************************************************
       910-NYUINACT-SELECT-SEC        SECTION.
      *
           MOVE    NYUINACT-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO               TO  FLG-NYUINACT
           ELSE
               MOVE    1                  TO  FLG-NYUINACT
           END-IF
      *
           .
       910-NYUINACT-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院診療行為マスター読込
      *****************************************************************
       910-NYUINACT-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  NYUINACT-REC
               MOVE    ZERO                TO  FLG-NYUINACT
           ELSE
               INITIALIZE                      NYUINACT-REC
               MOVE    1                   TO  FLG-NYUINACT
           END-IF
      *
           .
       910-NYUINACT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療会計マスター検索
      *****************************************************************
       910-SRYACCT-SELECT-SEC        SECTION.
      *
           MOVE    SRYACCT-REC         TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO               TO  FLG-SRYACCT
           ELSE
               MOVE    1                  TO  FLG-SRYACCT
           END-IF
      *
           .
       910-SRYACCT-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療会計マスター読込
      *****************************************************************
       910-SRYACCT-READ-SEC        SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC        TO  SRYACCT-REC
               MOVE    ZERO               TO  FLG-SRYACCT
           ELSE
               INITIALIZE                     SRYACCT-REC
               MOVE    1                  TO  FLG-SRYACCT
           END-IF
      *
           .
       910-SRYACCT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療行為読込
      *****************************************************************
       910-SRYACT-READ-SEC           SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  SRYACT-REC
               MOVE    ZERO            TO  FLG-SRYACT
           ELSE
               INITIALIZE                  SRYACT-REC
               MOVE    1               TO  FLG-SRYACT
           END-IF
      *
           .
       910-SRYACT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険組合せマスター検索
      *****************************************************************
       910-HKNCOMBI-SELECT-SEC        SECTION.
      *
           MOVE    HKNCOMBI-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO               TO  FLG-HKNCOMBI
           ELSE
               MOVE    1                  TO  FLG-HKNCOMBI
           END-IF
      *
           .
       910-HKNCOMBI-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険組合せマスター読込
      *****************************************************************
       910-HKNCOMBI-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  HKNCOMBI-REC
               MOVE    ZERO                TO  FLG-HKNCOMBI
           ELSE
               INITIALIZE                      HKNCOMBI-REC
               MOVE    1                   TO  FLG-HKNCOMBI
           END-IF
      *
           .
       910-HKNCOMBI-READ-EXT.
           EXIT.
      *****************************************************************
      *    点数マスター読込（主キー）
      *****************************************************************
       910-TENSU-READ-SEC         SECTION.
      *
           MOVE    TNS-TENSU-REC       TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "TENSU-KEY"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "TENSU-KEY"         TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  TNS-TENSU-REC
                   MOVE    ZERO                TO  FLG-TENSU
               ELSE
                   MOVE    1                   TO  FLG-TENSU
                   INITIALIZE                  TNS-TENSU-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-TENSU
               INITIALIZE                  TNS-TENSU-REC
           END-IF
      *
           MOVE    "TENSU-KEY"     TO   ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
           .
       910-TENSU-READ-EXT.
           EXIT.
      *     
      *****************************************************************
      *    入院略称マスター読込（主キー）
      *****************************************************************
       910-NYURYAKUSHO-READ-SEC        SECTION.
      *
           MOVE    NYURYAKUSHO-REC     TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "NYUINRYAKUSHO-KEY" TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "NYUINRYAKUSHO-KEY" TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  NYURYAKUSHO-REC
                   MOVE    ZERO                TO  FLG-NYURYAKUSHO
               ELSE
                   MOVE    1                   TO  FLG-NYURYAKUSHO
                   INITIALIZE                  NYURYAKUSHO-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-NYURYAKUSHO
               INITIALIZE                  NYURYAKUSHO-REC
           END-IF
      *
           MOVE    "NYUINRYAKUSHO-KEY"     TO   ORC-DBPATH
           PERFORM   910-DB-CLOSE-SEC
      *
           .
       910-NYURYAKUSHO-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢクローズ処理
      *****************************************************************
       910-DB-CLOSE-SEC                 SECTION.
      *
           MOVE    "DBCLOSECURSOR"      TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
      *
       910-DB-CLOSE-EXT.
           EXIT.
