      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION              DIVISION.
       PROGRAM-ID.                 ORCSNYUKSNGET.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 入院
      *  コンポーネント名  : 入院料加算取得サブ
      *  管理者            : 
      *  作成日付    作業者        記述
      *  13/10/31    NACL−小豆沢  新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *****************************************************************
      *
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *
       DATA                        DIVISION.
      *FILE                        SECTION.
      *
       WORKING-STORAGE             SECTION.
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-SYSKANRI                PIC 9(01).
           03  FLG-PTINF                   PIC 9(01).
           03  FLG-TENSU                   PIC 9(01).
           03  FLG-NYUKSNCHK               PIC 9(01).
           03  FLG-NYUKSN                  PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                         PIC 9(05).
           03  IDX7                        PIC 9(05).
           03  IDX8                        PIC 9(05).
           03  IDX9                        PIC 9(05).
           03  IDX-KSN                     PIC 9(05).
      *
      *    一時領域
       01  WRK-AREA.
      *
      *    医療機関設定の入院料加算チェック用
           03  WRK-NYUKHNKSN-SRYCD         PIC X(09)  OCCURS 20.
           03  WRK-KHN-NYUKSNKBN           PIC X(03).
           03  WRK-NYUKSNKBN               PIC X(03).
           03  WRK-BTU-NYUKSNKBN           PIC X(03).
           03  WRK-BTU-CDKBN-KBNNUM        PIC 9(03).
           03  WRK-BTU-CDKBN-KBNNUM-EDA    PIC 9(02).
           03  WRK-BTU-CDKBN-KOUBAN        PIC 9(02).
           03  WRK-KSN-NYUKSNKBN           PIC X(03).
           03  WRK-KSN-CDKBN-KBNNUM        PIC 9(03).
           03  WRK-KSN-CDKBN-KBNNUM-EDA    PIC 9(02).
      *
           03  WRK-TOKUBETSU-NYUIN         PIC X(01).
      *
      *    プレイルーム、保育士加算加算退避領域
           03  WRK-BTU-HIKSISUKBN          PIC X(01).
           03  WRK-BTU-KNGTEK-KBN          PIC X(01).
           03  WRK-BTU-DRTEK-KBN           PIC X(01).
      *
      *    患者年齢算出用エリア
           03  WRK-NENREI-AREA.
              05  WRK-NENREI               PIC 9(03).
              05  WRK-YMDS1.
                  07  WRK-YYS1             PIC 9(04).
                  07  WRK-MMS1             PIC 9(02).
                  07  WRK-DDS1             PIC 9(02).
              05  WRK-YMDS2.
                  07  WRK-YYS2             PIC 9(04).
                  07  WRK-MMS2             PIC 9(02).
                  07  WRK-DDS2             PIC 9(02).
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    システム管理マスタ
           COPY    "CPSYSKANRI.INC".
      *    （医療機関情報）
           COPY    "CPSK5000.INC".
      *    （病棟情報）
           COPY    "CPSK5001.INC".
      *    （病室情報）
           COPY    "CPSK5002.INC".
      *
      *    患者マスタ
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *    点数マスタ−
      *01  TENSU-REC.
           COPY    "CPTENSU.INC".
      *
      *    入院料加算チェック
       01  NYUKSNCHK-REC.
           COPY    "CPNYUKSNCHK.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
      *
           COPY    "MCPAREA".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *    日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
           COPY    "CPORCSGDAY.INC".
      *
      *    夜勤時間超過減算入院料コード取得サブ
           COPY "CPORCSYAKINGEN.INC".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
        LINKAGE                     SECTION.
      *
      *    入院料加算取得パラメタ
           COPY    "CPORCSNYUKSNGET.INC".
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *
       PROCEDURE                   DIVISION    USING
           ORCSNYUKSNGET-AREA
           SPA-AREA
           .
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                    SECTION.
      *
           INITIALIZE                      FLG-AREA
           INITIALIZE                      IDX-AREA
           INITIALIZE                      WRK-AREA
           INITIALIZE                      LNK-NYUKSNGET-HENKYAKU-AREA
           MOVE    ZERO                TO  LNK-NYUKSNGET-RC
      *
           PERFORM    100-NYUKSN-GET-SEC
           .
      *
       000-PROC-EXT.
           EXIT   PROGRAM
           .
      *
      *****************************************************************
      *    入院料加算の取り込み処理
      *****************************************************************
       100-NYUKSN-GET-SEC            SECTION.
      *
           MOVE   ZERO             TO   IDX8
      *
      *医療機関加算情報
           INITIALIZE                  SYSKANRI-REC
           MOVE    SPA-HOSPNUM     TO  SYS-HOSPNUM
           MOVE    "5000"          TO  SYS-KANRICD
           MOVE    LNK-NYUKSNGET-SANTEIYM
                                   TO  SYS-STYUKYMD(1:6)
           MOVE   "01"             TO  SYS-STYUKYMD(7:2)
           MOVE    SYS-STYUKYMD    TO  SYS-EDYUKYMD
           MOVE    SYSKANRI-REC    TO  MCPDATA-REC
      *
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBFETCH-SEC
           IF   MCP-RC             =    ZERO
               MOVE    MCPDATA-REC TO   SYS-5000-REC
      *        入院料加算
               PERFORM   VARYING   IDX7   FROM   1  BY  1
                 UNTIL  (SYS-5000-KSNCD(IDX7)    =   SPACE)  OR
                        (IDX7                    >   20)
                    IF   SYS-5000-KSNKBN(IDX7)   =   "2"
                             ADD    1                TO  IDX8
                             MOVE   SYS-5000-KSNCD(IDX7)  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                    END-IF
               END-PERFORM
      *        地域加算
               IF   SYS-5000-CHIIKIKSN    NOT =   SPACE
                    ADD    1        TO   IDX8
                    MOVE   SYS-5000-CHIIKIKSN   TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
               END-IF
           END-IF
      *
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
      *病棟加算情報
           PERFORM     4801-BTUSEL-SEC
      *
           IF   FLG-SYSKANRI           =    ZERO
      *
      *        看護補助加算
               IF    SYS-5001-BTU-KNGHJOKSN   NOT =   SPACE
                   ADD      1          TO     IDX8
                   EVALUATE     SYS-5001-BTU-KNGHJOKSN
                       WHEN     "01"
                           MOVE  "190103070"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "02"
                           MOVE  "190103470"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "03"
                           MOVE  "190103770"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "04"
                           MOVE  "190103970"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "05"
                           MOVE  "190104070"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
      *                平成１８年４月改正対応
                       WHEN     "06"
      *                    看護補助加算１
                           MOVE  "190103770"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "07"
      *                    看護補助加算１（１３対１入院基本料）
                           MOVE  "190103870"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "08"
      *                    看護補助加算２
                           MOVE  "190103970"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "09"
      *                    看護補助加算３
                           MOVE  "190104070"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                   END-EVALUATE
               END-IF
      *
      *        夜間勤務等看護加算
               IF    SYS-5001-BTU-YAKANKNGKSN   NOT =   SPACE
                   ADD      1          TO     IDX8
                   EVALUATE     SYS-5001-BTU-YAKANKNGKSN
                       WHEN     "01"
                           MOVE  "190109870"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "02"
                           MOVE  "190076070"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "03"
                           MOVE  "190076170"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "04"
                           MOVE  "190076370"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                       WHEN     "05"
                           MOVE  "190076470"   TO
                                              WRK-NYUKHNKSN-SRYCD(IDX8)
                   END-EVALUATE
               END-IF
      *
      *        小児入院医療管理料加算（保育士１人以上）
               MOVE  SPACE       TO   WRK-BTU-HIKSISUKBN
               IF    LNK-NYUKSNGET-NYUIN-SRYCD =   "190129310"  OR
                                                   "190111110"  OR
                                                   "190111210"  OR
                                                   "190075810"  OR
                                                   "190140710"
                   IF    SYS-5001-BTU-HIKSISUKBN   =   "2"
                       MOVE   "2"    TO   WRK-BTU-HIKSISUKBN
                   END-IF
               END-IF
      *
      *      病棟入院料加算
      *
      *      平成２０年３月までの加算セット処理
             IF     LNK-NYUKSNGET-SANTEIYM     <=     "200803"
               PERFORM   VARYING   IDX7   FROM   1  BY  1
                 UNTIL  (IDX7                    >   14)
                   EVALUATE   IDX7
                       WHEN   3
                       WHEN   4
                       WHEN   5
                           CONTINUE
      *                児童思春期精神科入院医療管理加算（２０才未満）
                       WHEN   6
                           IF      SYS-5001-KSNKBN(IDX7)   =   "2"
                               PERFORM   4801-PT-AGE-SEC
                               IF      WRK-NENREI          <      20
                                   ADD    1                TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                               WRK-NYUKHNKSN-SRYCD(IDX8)
                               END-IF
                           END-IF
      *                看護配置適合加算
                       WHEN   13
                           MOVE  SPACE       TO   WRK-BTU-KNGTEK-KBN
                           IF    SYS-5001-KNGTEK-KBN   =   "2"
                               MOVE   "2"    TO   WRK-BTU-KNGTEK-KBN
                           END-IF
      *                複数医師及び看護配置適合加算
                       WHEN   14
                           MOVE  SPACE       TO   WRK-BTU-DRTEK-KBN
                           IF    SYS-5001-DRTEK-KBN   =   "2"
                               MOVE   "2"    TO   WRK-BTU-DRTEK-KBN
                           END-IF
                       WHEN   OTHER
                           IF     SYS-5001-KSNKBN(IDX7)    =   "2"
                               ADD    1                TO  IDX8
                               MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                           END-IF
                   END-EVALUATE
               END-PERFORM
             END-IF
      *
      *      平成２０年４月から平成２２年３月までの加算セット処理
             IF    (LNK-NYUKSNGET-SANTEIYM  >=   "200804")    AND
                   (LNK-NYUKSNGET-SANTEIYM  <=   "201003")
                 PERFORM   VARYING   IDX7   FROM   1  BY  1
                   UNTIL  (IDX7      >   14)
                     IF   SYS-5001-KSNKBN(IDX7)   =   "2"
                         EVALUATE    SYS-5001-KSNCD(IDX7)
      *                      児童思春期精神科入院医療管理加算（２０才未満）
                             WHEN    "190110170"
                                PERFORM   4801-PT-AGE-SEC
                                IF      WRK-NENREI   <      20
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      重症患者回復病棟加算（回復期リハビリテーション入院料１）
                             WHEN    "190129870"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                     = "190129410"  OR
                                                       "190129510"  
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      医師配置適合加算（有床診療所入院基本料１）
      *                      看護配置加算１（有床診療所入院基本料１）
      *                      看護配置加算２（有床診療所入院基本料１）
      *                      夜間看護配置加算１（有床診療所入院基本料１）
      *                      夜間看護配置加算２（有床診療所入院基本料１）
                             WHEN    "190117070"
                             WHEN    "190126570"
                             WHEN    "190109670"
                             WHEN    "190126670"
                             WHEN    "190126770"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190097010"
                                                        OR "190097110"
                                                        OR "190119510"
                                                        OR "190119610"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      夜間緊急体制確保加算（有床診療所入院基本料１と２）
                             WHEN    "190126470"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190097010"
                                                        OR "190097110"
                                                        OR "190119510"
                                                        OR "190119610"
                                                        OR "190097210"
                                                        OR "190097310"
                                                        OR "190119710"
                                                        OR "190119810"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
                             WHEN   OTHER
                                ADD    1                TO  IDX8
                                MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                         END-EVALUATE
                     END-IF
                 END-PERFORM
             END-IF
      *
      *      平成２２年４月から平成２４年３月までの加算セット処理
             IF    (LNK-NYUKSNGET-SANTEIYM    >=     "201004")    AND
                   (LNK-NYUKSNGET-SANTEIYM    <=     "201203")
      *         （システム管理の病棟設定見直しによるもの）
               PERFORM   VARYING   IDX7   FROM   1  BY  1
                 UNTIL  (IDX7      >   14)
                     IF   SYS-5001-KSNKBN(IDX7)   =   "2"
                         EVALUATE    SYS-5001-KSNCD(IDX7)
      *                      児童思春期精神科入院医療管理加算（２０才未満）
                             WHEN    "190110170"
                                PERFORM   4801-PT-AGE-SEC
                                IF      WRK-NENREI   <      20
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      重症患者回復病棟加算（回復期リハビリテーション入院料１）
                             WHEN    "190129870"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       = "190129410" OR
                                                         "190129510" OR
                                                         "190141610" OR
                                                         "190141710"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
      *                            基準未適合の場合は重症患者回復病棟加算（基準未適合）
      *                            に置き換える
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       = "190129410" OR
                                                         "190129510"
                                     MOVE   "190142370"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
                                END-IF
      *                      医師配置加算１（有床診療所入院基本料１と２）
      *                      医師配置加算２（有床診療所入院基本料１と２）
      *                      看護配置加算１（有床診療所入院基本料１と２）
      *                      看護配置加算２（有床診療所入院基本料１と２）
      *                      夜間看護配置加算１（有床診療所入院基本料１と２）
      *                      夜間看護配置加算２（有床診療所入院基本料１と２）
                             WHEN    "190135570"
                             WHEN    "190117070"
                             WHEN    "190109670"
                             WHEN    "190126570"
                             WHEN    "190126770"
                             WHEN    "190126670"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190097010"
                                                        OR "190119510"
                                                        OR "190119610"
                                                        OR "190097210"
                                                        OR "190119710"
                                                        OR "190119810"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      夜間緊急体制確保加算（有床診療所入院基本料１と２と３）
                             WHEN    "190126470"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190097010"
                                                        OR "190119510"
                                                        OR "190119610"
                                                        OR "190097210"
                                                        OR "190119710"
                                                        OR "190119810"
                                                        OR "190135110"
                                                        OR "190135210"
                                                        OR "190135310"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      一般病棟看護必要度評価加算（１０対１入院基本料）
                             WHEN    "190131570"
      *                            一般病棟看護必要度評価加算
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190077410"
                                     ADD    1            TO  IDX8
                                     MOVE   "190131570"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                            一般病棟看護必要度評価加算（特定機能病院）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190085910"
                                     ADD    1            TO  IDX8
                                     MOVE   "190134870"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                            一般病棟看護必要度評価加算（専門病院）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190090210"
                                     ADD    1            TO  IDX8
                                     MOVE   "190134970"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                      休日リハビリテーション提供体制加算（回復期リハビリテーション病棟入院料）
                             WHEN    "190140870"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       = "190141610" OR
                                                         "190141710" OR
                                                         "190141810" OR
                                                         "190141910"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
                             WHEN   OTHER
                                ADD    1                TO  IDX8
                                MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                         END-EVALUATE
                     END-IF
               END-PERFORM
             END-IF
      *
      *      平成２４年４月からの加算セット処理
             IF    (LNK-NYUKSNGET-SANTEIYM    >=     "201204")  AND
                   (LNK-NYUKSNGET-SANTEIYM    <=     "201403")
      *         （システム管理の病棟設定見直しによるもの）
               PERFORM   VARYING   IDX7   FROM   1  BY  1
                 UNTIL  (IDX7      >   14)
                     IF   SYS-5001-KSNKBN(IDX7)   =   "2"
                         EVALUATE    SYS-5001-KSNCD(IDX7)
      *                      医師配置加算１（有床診療所入院基本料１と２）
      *                      医師配置加算２（有床診療所入院基本料１と２）
      *                      看護配置加算１（有床診療所入院基本料１と２）
      *                      看護配置加算２（有床診療所入院基本料１と２）
      *                      夜間看護配置加算１（有床診療所入院基本料１と２）
      *                      夜間看護配置加算２（有床診療所入院基本料１と２）
                             WHEN    "190135570"
                             WHEN    "190117070"
                             WHEN    "190109670"
                             WHEN    "190126570"
                             WHEN    "190126770"
                             WHEN    "190126670"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190097010"
                                                        OR "190119510"
                                                        OR "190119610"
                                                        OR "190097210"
                                                        OR "190119710"
                                                        OR "190119810"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      夜間緊急体制確保加算（有床診療所入院基本料１と２と３）
                             WHEN    "190126470"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190097010"
                                                        OR "190119510"
                                                        OR "190119610"
                                                        OR "190097210"
                                                        OR "190119710"
                                                        OR "190119810"
                                                        OR "190135110"
                                                        OR "190135210"
                                                        OR "190135310"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      一般病棟看護必要度評価加算（１３対１入院基本料）
                             WHEN    "190131570"
      *                            一般病棟看護必要度評価加算
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190077510"
                                     ADD    1            TO  IDX8
                                     MOVE   "190131570"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                            一般病棟看護必要度評価加算（専門病院）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190090310"
                                     ADD    1            TO  IDX8
                                     MOVE   "190134970"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                            一般病棟看護必要度評価加算（特定一般病棟入院料１）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190151910"
                                     ADD    1            TO  IDX8
                                     MOVE   "190152570"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                      休日リハビリテーション提供体制加算（回復期リハビリテーション病棟入院料）
                             WHEN    "190140870"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       = "190141610" OR
                                                         "190141710" OR
                                                         "190141810" OR
                                                         "190141910" OR
                                                         "190150110" OR
                                                         "190150210"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      看護必要度加算１（１０対１入院基本料）
                             WHEN    "190143070"
      *                         看護必要度加算１（一般病棟入院基本料）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190077410"
                                   ADD    1            TO  IDX8
                                   MOVE   "190143070"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                         看護必要度加算１（特定機能病院）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190085910"
                                   ADD    1            TO  IDX8
                                   MOVE   "190143970"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                         看護必要度加算１（専門病院）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190090210"
                                   ADD    1            TO  IDX8
                                   MOVE   "190144570"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      看護必要度加算２（１０対１入院基本料）
                             WHEN    "190143170"
      *                         看護必要度加算２（一般病棟入院基本料）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190077410"
                                   ADD    1            TO  IDX8
                                   MOVE   "190143170"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                         看護必要度加算２（特定機能病院）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190085910"
                                   ADD    1            TO  IDX8
                                   MOVE   "190144070"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                         看護必要度加算２（専門病院）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =   "190090210"
                                   ADD    1            TO  IDX8
                                   MOVE   "190144670"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
                             WHEN   OTHER
                                ADD    1                TO  IDX8
                                MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                         END-EVALUATE
                     END-IF
               END-PERFORM
             END-IF
      *
      *      平成２６年４月からの加算セット処理
             IF     LNK-NYUKSNGET-SANTEIYM   >=     "201404"
               PERFORM   VARYING   IDX7   FROM   1  BY  1
                 UNTIL  (IDX7      >   14)
                     IF   SYS-5001-KSNKBN(IDX7)   =   "2"
                         EVALUATE    SYS-5001-KSNCD(IDX7)
      *                      医師配置加算１（有床診療所入院基本料１から６）
      *                      医師配置加算２（有床診療所入院基本料１から６）
      *                      看護配置加算１（有床診療所入院基本料１から６）
      *                      看護配置加算２（有床診療所入院基本料１から６）
      *                      夜間看護配置加算１（有床診療所入院基本料１から６）
      *                      夜間看護配置加算２（有床診療所入院基本料１から６）
      *                      看護補助配置加算１（有床診療所入院基本料１から６）
      *                      看護補助配置加算２（有床診療所入院基本料１から６）
                             WHEN    "190135570"
                             WHEN    "190117070"
                             WHEN    "190109670"
                             WHEN    "190126570"
                             WHEN    "190126770"
                             WHEN    "190126670"
                             WHEN    "190170570"
                             WHEN    "190170670"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD  =
      *                                                 有床診療所入院基本料１
                                                           "190169610"
                                                        OR "190169710"
                                                        OR "190169810"
      *                                                 有床診療所入院基本料２
                                                        OR "190169910"
                                                        OR "190170010"
                                                        OR "190170110"
      *                                                 有床診療所入院基本料３
                                                        OR "190170210"
                                                        OR "190170310"
                                                        OR "190170410"
      *                                                 有床診療所入院基本料４
                                                        OR "190097010"
                                                        OR "190119510"
                                                        OR "190119610"
      *                                                 有床診療所入院基本料５
                                                        OR "190097210"
                                                        OR "190119710"
                                                        OR "190119810"
      *                                                 有床診療所入院基本料６
                                                        OR "190135110"
                                                        OR "190135210"
                                                        OR "190135310"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      夜間緊急体制確保加算（有床診療所入院基本料１から６）
                             WHEN    "190126470"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD  =
      *                                                 有床診療所入院基本料１
                                                           "190169610"
                                                        OR "190169710"
                                                        OR "190169810"
      *                                                 有床診療所入院基本料２
                                                        OR "190169910"
                                                        OR "190170010"
                                                        OR "190170110"
      *                                                 有床診療所入院基本料３
                                                        OR "190170210"
                                                        OR "190170310"
                                                        OR "190170410"
      *                                                 有床診療所入院基本料４
                                                        OR "190097010"
                                                        OR "190119510"
                                                        OR "190119610"
      *                                                 有床診療所入院基本料５
                                                        OR "190097210"
                                                        OR "190119710"
                                                        OR "190119810"
      *                                                 有床診療所入院基本料６
                                                        OR "190135110"
                                                        OR "190135210"
                                                        OR "190135310"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      栄養管理実施加算
                             WHEN    "190170770"
      *                         栄養管理実施加算（有床診療所入院基本料）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD  =
      *                                                 有床診療所入院基本料１
                                                           "190169610"
                                                        OR "190169710"
                                                        OR "190169810"
      *                                                 有床診療所入院基本料２
                                                        OR "190169910"
                                                        OR "190170010"
                                                        OR "190170110"
      *                                                 有床診療所入院基本料３
                                                        OR "190170210"
                                                        OR "190170310"
                                                        OR "190170410"
      *                                                 有床診療所入院基本料４
                                                        OR "190097010"
                                                        OR "190119510"
                                                        OR "190119610"
      *                                                 有床診療所入院基本料５
                                                        OR "190097210"
                                                        OR "190119710"
                                                        OR "190119810"
      *                                                 有床診療所入院基本料６
                                                        OR "190135110"
                                                        OR "190135210"
                                                        OR "190135310"
                                   ADD    1            TO  IDX8
                                   MOVE   "190170770"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                         栄養管理実施加算（有床診療所療養病床入院基本料）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD  =
      *                                         有床診療所療養病床入院基本料Ａ
                                                          "190122010"
      *                                         有床診療所療養病床入院基本料Ａ（生活療養）
                                                       OR "190124310"
      *                                         有床診療所療養病床入院基本料Ｂ
                                                       OR "190122110"
      *                                         有床診療所療養病床入院基本料Ｂ（生活療養）
                                                       OR "190124410"
      *                                         有床診療所療養病床入院基本料Ｃ
                                                       OR "190122210"
      *                                         有床診療所療養病床入院基本料Ｃ（生活療養）
                                                       OR "190124510"
      *                                         有床診療所療養病床入院基本料Ｄ
                                                       OR "190122310"
      *                                         有床診療所療養病床入院基本料Ｄ（生活療養）
                                                       OR "190124610"
      *                                         有床診療所療養病床入院基本料Ｅ
                                                       OR "190122410"
      *                                         有床診療所療養病床入院基本料Ｅ（生活療養）
                                                       OR "190124710"
      *                                         療養病床以外・有床診療所療養病床入院Ａ
                                                       OR "190155210"
      *                                         療養病床以外・有床診療所療養病床入院Ａ（生活療養）
                                                       OR "190155410"
      *                                         療養病床以外・有床診療所療養病床入院Ｂ
                                                       OR "190155610"
      *                                         療養病床以外・有床診療所療養病床入院Ｂ（生活療養）
                                                       OR "190155810"
      *                                         療養病床以外・有床診療所療養病床入院Ｃ
                                                       OR "190156010"
      *                                         療養病床以外・有床診療所療養病床入院Ｃ（生活療養）
                                                       OR "190156210"
      *                                         療養病床以外・有床診療所療養病床入院Ｄ
                                                       OR "190156410"
      *                                         療養病床以外・有床診療所療養病床入院Ｄ（生活療養）
                                                       OR "190156610"
      *                                         療養病床以外・有床診療所療養病床入院Ｅ
                                                       OR "190156810"
      *                                         療養病床以外・有床診療所療養病床入院Ｅ（生活療養）
                                                       OR "190157010"
                                   ADD    1            TO  IDX8
                                   MOVE   "190171770"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      一般病棟看護必要度評価加算（１３対１入院基本料）
                             WHEN    "190131570"
      *                            一般病棟看護必要度評価加算（一般病棟入院基本料）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         =  "190077510"
                                                         OR "190165810"
                                     ADD    1            TO  IDX8
                                     MOVE   "190131570"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                            一般病棟看護必要度評価加算（専門病院）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190090310"
                                     ADD    1            TO  IDX8
                                     MOVE   "190134970"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                            一般病棟看護必要度評価加算（特定一般病棟入院料１）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                         = "190151910"
                                     ADD    1            TO  IDX8
                                     MOVE   "190152570"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                      ＡＤＬ維持向上等体制加算
                             WHEN    "190166070"
      *                            ＡＤＬ維持向上等体制加算（一般病棟入院基本料）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
      *                                                  一般病棟７対１入院基本料
                                                         =  "190117710"
                                                         OR "190131310"
      *                                                  一般病棟１０対１入院基本料
                                                         OR "190077410"
                                                         OR "190131410"
                                     ADD    1            TO  IDX8
                                     MOVE   "190166070"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                            ＡＤＬ維持向上等体制加算（特定機能病院入院基本料）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
      *                                                  特定機能病院一般病棟７対１入院基本料
                                                         =  "190118610"
      *                                                  特定機能病院一般病棟１０対１入院基本料
                                                         OR "190085910"
                                     ADD    1            TO  IDX8
                                     MOVE   "190169070"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                            ＡＤＬ維持向上等体制加算（専門病院入院基本料）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
      *                                                  専門病院７対１入院基本料
                                                         =  "190119310"
      *                                                  専門病院１０対１入院基本料
                                                         OR "190090210"
                                     ADD    1            TO  IDX8
                                     MOVE   "190169170"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                      休日リハビリテーション提供体制加算
      *                      （回復期リハビリテーション病棟入院料２と３のみ）
                             WHEN    "190140870"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       = "190141610" OR
                                                         "190141710" OR
                                                         "190141810" OR
                                                         "190141910"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      体制強化加算（回復期リハビリテーション病棟入院料１）
                             WHEN    "190175270"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       = "190150110" OR
                                                         "190150210"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      精神科急性期医師配置加算（精神科急性期治療病棟入院料１）
                             WHEN    "190182270"
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       = "190121110" OR
                                                         "190066910"
                                   ADD    1            TO  IDX8
                                   MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      看護職員配置加算（地域包括ケア病棟入院料）
                             WHEN    "190176970"
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
      *                                                  地域包括ケア病棟入院料１
                                                         =  "190175310"
      *                                                  地域包括ケア病棟入院料１（生活療養）
                                                         OR "190175410"
      *                                                  地域包括ケア入院医療管理料１
                                                         OR "190175510"
      *                                                  地域包括ケア入院医療管理料１（生活療養）
                                                         OR "190175610"
      *                                                  地域包括ケア病棟入院料２
                                                         OR "190175710"
      *                                                  地域包括ケア病棟入院料２（生活療養）
                                                         OR "190175810"
      *                                                  地域包括ケア入院医療管理料２
                                                         OR "190175910"
      *                                                  地域包括ケア入院医療管理料２（生活療養）
                                                         OR "190176010"
                                     ADD    1            TO  IDX8
                                     MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                      看護補助者配置加算（地域包括ケア病棟入院料）
                             WHEN    "190177070"
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
      *                                                  地域包括ケア病棟入院料１
                                                         =  "190175310"
      *                                                  地域包括ケア病棟入院料１（生活療養）
                                                         OR "190175410"
      *                                                  地域包括ケア入院医療管理料１
                                                         OR "190175510"
      *                                                  地域包括ケア入院医療管理料１（生活療養）
                                                         OR "190175610"
      *                                                  地域包括ケア病棟入院料２
                                                         OR "190175710"
      *                                                  地域包括ケア病棟入院料２（生活療養）
                                                         OR "190175810"
      *                                                  地域包括ケア入院医療管理料２
                                                         OR "190175910"
      *                                                  地域包括ケア入院医療管理料２（生活療養）
                                                         OR "190176010"
                                     ADD    1            TO  IDX8
                                     MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                      精神保健福祉士配置加算
                             WHEN    "190168970"
      *                            精神保健福祉士配置加算（精神病棟入院基本料）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
      *                                                  精神病棟１０対１入院基本料
                                                         =  "190083610"
                                                         OR "190134610"
      *                                                  精神病棟１３対１入院基本料
                                                         OR "190134510"
                                                         OR "190168510"
      *                                                  精神病棟１５対１入院基本料
                                                         OR "190083810"
                                                         OR "190168610"
      *                                                  精神病棟１８対１入院基本料
                                                         OR "190083910"
                                                         OR "190168710"
      *                                                  精神病棟２０対１入院基本料
                                                         OR "190084010"
                                                         OR "190168810"
                                     ADD    1            TO  IDX8
                                     MOVE   "190168970"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                            精神保健福祉士配置加算（精神科療養病棟入院料）
                                   IF  LNK-NYUKSNGET-NYUIN-SRYCD
      *                                                  精神療養病棟入院料
                                                         =  "190055010"
                                     ADD    1            TO  IDX8
                                     MOVE   "190177570"  TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                                   END-IF
      *                      看護必要度加算１（１０対１入院基本料）
                             WHEN    "190143070"
      *                         看護必要度加算１（一般病棟入院基本料）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190077410"
                                                       OR "190131410"
                                   ADD    1            TO  IDX8
                                   MOVE   "190143070"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                         看護必要度加算１（特定機能病院）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190085910"
                                   ADD    1            TO  IDX8
                                   MOVE   "190143970"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                         看護必要度加算１（専門病院）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190090210"
                                   ADD    1            TO  IDX8
                                   MOVE   "190144570"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                      看護必要度加算２（１０対１入院基本料）
                             WHEN    "190143170"
      *                         看護必要度加算２（一般病棟入院基本料）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190077410"
                                                       OR "190131410"
                                   ADD    1            TO  IDX8
                                   MOVE   "190143170"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                         看護必要度加算２（特定機能病院）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =  "190085910"
                                   ADD    1            TO  IDX8
                                   MOVE   "190144070"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
      *                         看護必要度加算２（専門病院）
                                IF  LNK-NYUKSNGET-NYUIN-SRYCD
                                                       =   "190090210"
                                   ADD    1            TO  IDX8
                                   MOVE   "190144670"  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                                END-IF
                             WHEN   OTHER
                                ADD    1                TO  IDX8
                                MOVE   SYS-5001-KSNCD(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                         END-EVALUATE
                     END-IF
               END-PERFORM
             END-IF
      *
      *      入院料加算（ユーザー設定）
             PERFORM   VARYING   IDX7   FROM   1  BY  1
               UNTIL  (IDX7      >   10)
                   IF   SYS-5001-KSNCD2(IDX7)  NOT =   SPACE
                       ADD   1     TO    IDX8
                       MOVE   SYS-5001-KSNCD2(IDX7) TO
                                            WRK-NYUKHNKSN-SRYCD(IDX8)
                   END-IF
             END-PERFORM
      *
           END-IF
      *
      *    病室入院料加算
           PERFORM     4801-BRMSEL-SEC
      *
           IF   FLG-SYSKANRI       =    ZERO
      *
               PERFORM   VARYING   IDX7   FROM   1  BY  1
                 UNTIL    (IDX7                    >   12)
                     IF   SYS-5002-KSNKBN(IDX7)   =   "2"
                          ADD    1                TO  IDX8
                          MOVE   SYS-5002-KSNCD(IDX7)  TO
                                             WRK-NYUKHNKSN-SRYCD(IDX8)
                     END-IF
               END-PERFORM
      *
           END-IF
      *
           PERFORM VARYING IDX-KSN FROM    1   BY  1
                   UNTIL ( IDX-KSN >   3 )
               IF    ( LNK-NYUKSNGET-I01-KSN-SRYCD (IDX-KSN)
                                       NOT =   SPACE )
                    ADD     1       TO     IDX8
                    MOVE    LNK-NYUKSNGET-I01-KSN-SRYCD (IDX-KSN)
                                    TO     WRK-NYUKHNKSN-SRYCD(IDX8)
               END-IF
           END-PERFORM
      *
      *    入院料と入院料加算の併算定チェック
           MOVE    ZERO            TO     IDX9
           PERFORM    VARYING    IDX8   FROM   1   BY   1
                      UNTIL      (WRK-NYUKHNKSN-SRYCD(IDX8)  =  SPACE)
                         OR      (IDX8   >     20)
                         PERFORM   4801-NYUKSN-CHK-SEC
                         IF     FLG-NYUKSN       =   ZERO   
                             ADD    1                TO  IDX9
                             MOVE   WRK-NYUKHNKSN-SRYCD(IDX8)  TO
                                           LNK-NYUKSNGET-KSNSRYCD(IDX9)
      *                      褥瘡患者管理加算のフラグセット
                             IF   WRK-NYUKHNKSN-SRYCD(IDX8)  =
                                                         "190116990"
                                  MOVE   "1"    TO
                                       LNK-NYUKSNGET-NYUKSNKBN(IDX9)
                             END-IF
      *                      救急・在宅等支援療養病床初期加算（病院）のフラグセット
                             IF   WRK-NYUKHNKSN-SRYCD(IDX8)  =
                                             "190134270"
                                  IF    LNK-NYUKSNGET-FLG-IPPAN2  =  1
                                     MOVE   "2"    TO
                                         LNK-NYUKSNGET-NYUKSNKBN(IDX9)
                                  END-IF
                                  IF    LNK-NYUKSNGET-FLG-IPPAN2  =  2
                                     MOVE   "3"    TO
                                         LNK-NYUKSNGET-NYUKSNKBN(IDX9)
                                  END-IF
                             END-IF
      *                      救急・在宅等支援療養病床初期加算（診療所）のフラグセット
                             IF   WRK-NYUKHNKSN-SRYCD(IDX8)  =
                                             "190135670"
                                  IF    LNK-NYUKSNGET-FLG-IPPAN2  =  3
                                     MOVE   "4"    TO
                                         LNK-NYUKSNGET-NYUKSNKBN(IDX9)
                                  END-IF
                             END-IF
                         END-IF
           END-PERFORM
      *
      *    小児入院医療管理料加算（保育士１人以上）
           IF    (WRK-BTU-HIKSISUKBN   =   "2")  AND
                 (IDX9    <   20 )
               ADD    1            TO  IDX9
               MOVE  "190111370"   TO
                                   LNK-NYUKSNGET-KSNSRYCD(IDX9)
           END-IF
      *
           IF     LNK-NYUKSNGET-SANTEIYM  <=   "200803"
      *      看護配置適合加算
             IF    (WRK-BTU-KNGTEK-KBN   =   "2")  AND
                   (IDX9    <   20 )
                 ADD    1            TO  IDX9
                 MOVE  "190109670"   TO
                                   LNK-NYUKSNGET-KSNSRYCD(IDX9)
             END-IF          
      *
      *      複数医師及び看護配置適合加算
             IF    (WRK-BTU-DRTEK-KBN   =   "2")  AND
                   (IDX9    <   20 )
                 ADD    1            TO  IDX9
                 MOVE  "190117070"   TO
                                   LNK-NYUKSNGET-KSNSRYCD(IDX9)
             END-IF
           END-IF 
      *
      *    返却領域へのセット
           MOVE     WRK-TOKUBETSU-NYUIN
                                     TO   LNK-NYUKSNGET-TOKUBETSU-NYUIN
      *
      *    復帰コード
           IF    WRK-NYUKHNKSN-SRYCD(1)   =   SPACE
                MOVE    1            TO   LNK-NYUKSNGET-RC
           ELSE
                MOVE    ZERO         TO   LNK-NYUKSNGET-RC
           END-IF
      *
      *    返却情報の表示（テスト用）
      **     DISPLAY    "===========   ORCSNYUKSNGET HENKYAKU ====="
      **     DISPLAY    "WRK-NENREI = " WRK-NENREI
      **     DISPLAY    "LNK-NYUKSNGET-TOKUBETSU-NYUIN = "
      **                 LNK-NYUKSNGET-TOKUBETSU-NYUIN
      **     PERFORM     VARYING    IDX    FROM   1   BY   1
      **       UNTIL    (IDX    >   20)   OR
      **                (LNK-NYUKSNGET-KSNSRYCD(IDX)  =  SPACE)
      **         DISPLAY "IDX = " IDX
      **         MOVE    LNK-NYUKSNGET-HOSPNUM
      **                                 TO  TNS-HOSPNUM
      **         MOVE    WRK-NYUKHNKSN-SRYCD(IDX)
      **                                 TO  TNS-SRYCD
      **         MOVE    LNK-NYUKSNGET-SANTEIYM
      **                                 TO  TNS-YUKOSTYMD(1:6)
      **         MOVE    "01"            TO  TNS-YUKOSTYMD(7:2)
      **         MOVE    TNS-YUKOSTYMD   TO  TNS-YUKOEDYMD
      **         MOVE    TNS-TENSU-REC   TO  MCPDATA-REC
      **         MOVE    "tbl_tensu"     TO  MCP-TABLE
      **         MOVE    "key"           TO  MCP-PATHNAME
      **         PERFORM 910-DBSELECT-SEC
      **         MOVE    "tbl_tensu"     TO  MCP-TABLE
      **         MOVE    "key"           TO  MCP-PATHNAME
      **         PERFORM 910-DBFETCH-SEC
      **         IF    ( MCP-RC          =   ZERO )
      **            MOVE    MCPDATA-REC TO  TNS-TENSU-REC
      **            MOVE    ZERO        TO  FLG-TENSU
      **         ELSE
      **            MOVE    1           TO  FLG-TENSU
      **         END-IF
      ***
      **         MOVE    "tbl_tensu"     TO  MCP-TABLE
      **         MOVE    "key"           TO  MCP-PATHNAME
      **         PERFORM 990-DBCLOSE-SEC
      **         DISPLAY "KSN-SRYCD = " WRK-NYUKHNKSN-SRYCD(IDX) 
      **                 " NAME = " TNS-NAME
      **     END-PERFORM
      *
      **     DISPLAY  "LNK-NYUKSNGET-RC = " LNK-NYUKSNGET-RC
           .
      *
       100-NYUKSN-GET-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院料加算の算定可・不可チェック
      *****************************************************************
       4801-NYUKSN-CHK-SEC         SECTION.
      *
           MOVE    ZERO            TO  FLG-NYUKSN
      *
      **     EVALUATE   WRK-NYUKHNKSN-SRYCD(IDX8)
      *        診療録管理体制加算
      **         WHEN   "190100370"
      **             IF   SPA-GMN-I01-SYORIKBN-CD   =  "07" OR "08"
      **                  MOVE   1        TO     FLG-NYUKSN
      **             END-IF
      *            自院での継続入院チェック
      **     END-EVALUATE
      *
      *    入院料加算チェックテーブルによる判定
           INITIALIZE  NYUKSNCHK-REC
      *
           PERFORM   48011-TENSU-SEL-SEC
           MOVE    WRK-BTU-NYUKSNKBN
                                   TO  NYUKSNCHK-NYUINKBN
           PERFORM   48012-TENSU-SEL-SEC
      *
      *    救急・在宅等支援病床初期加算（地域包括ケア病棟入院料）は
      *    入院料加算チェックができない為、チェックしない
           IF      TNS-SRYCD  =   "190177170"
               GO  TO   4801-NYUKSN-CHK-EXT
           END-IF
      *
           IF      TNS-NYUKHNKSNKBN    NOT =   ZERO
               MOVE    WRK-KSN-NYUKSNKBN
                                       TO  NYUKSNCHK-KSNKBN
               MOVE    LNK-NYUKSNGET-SANTEIYM
                                       TO  NYUKSNCHK-YUKOSTYMD(1:6)
               MOVE    "01"            TO  NYUKSNCHK-YUKOSTYMD(7:2)
               MOVE    NYUKSNCHK-YUKOSTYMD
                                       TO  NYUKSNCHK-YUKOEDYMD
      *
               MOVE    "tbl_nyuksnchk" TO  MCP-TABLE
               MOVE    "key"           TO  MCP-PATHNAME
               MOVE    NYUKSNCHK-REC   TO  MCPDATA-REC
               PERFORM 910-DBSELECT-SEC
               MOVE    "tbl_nyuksnchk" TO  MCP-TABLE
               MOVE    "key"           TO  MCP-PATHNAME
               MOVE    NYUKSNCHK-REC   TO  MCPDATA-REC
               PERFORM 910-DBFETCH-SEC
               IF    ( MCP-RC          =   ZERO )
                   MOVE    MCPDATA-REC TO  NYUKSNCHK-REC
                   IF    ( NYUKSNCHK-CHKKBN    =   "1" OR "2" )
                       CONTINUE
                   ELSE
                       MOVE   1        TO     FLG-NYUKSN
                   END-IF
               ELSE
                   MOVE   1        TO     FLG-NYUKSN
               END-IF
      *
      *
               MOVE    "tbl_nyuksnchk" TO  MCP-TABLE
               MOVE    "key"           TO  MCP-PATHNAME
               PERFORM 990-DBCLOSE-SEC
      *
           END-IF
      *
      *    注加算の表用番号が同一であることの判定
           IF   WRK-KSN-CDKBN-KBNNUM    NOT =    ZERO
              IF  (WRK-BTU-CDKBN-KBNNUM  = WRK-KSN-CDKBN-KBNNUM)  AND
                  (WRK-BTU-CDKBN-KBNNUM-EDA = WRK-KSN-CDKBN-KBNNUM-EDA)
                  CONTINUE
              ELSE
                  MOVE    1     TO    FLG-NYUKSN
              END-IF
           END-IF
      *
      *    平成２４年改正で新設された入院料加算のチェック追加
      *    以下の入院料加算については点数マスタに入院基本料加算区分がセット
      *    されていない為、入院料加算チェックテーブルによる判定ができない
      *    ことから、特定入院料算定時は算定不可とする暫定対処を追加する
      *    こととする。
      *    ・夜間２５対１急性期看護補助体制加算（平成２６年改正で追加）
      *    ・夜間５０対１急性期看護補助体制加算
      *    ・夜間１００対１急性期看護補助体制加算
      *    ・看護職員夜間配置加算
           IF    WRK-NYUKHNKSN-SRYCD(IDX8)   =    "190173010"  OR
                                             =    "190145970"  OR
                                                  "190146070"  OR
                                                  "190146170"
               IF   ((WRK-BTU-CDKBN-KBNNUM   >=   300)     AND
                     (WRK-BTU-CDKBN-KBNNUM   <=   399))    OR
                    ( WRK-BTU-CDKBN-KBNNUM   =    400 )
                  MOVE    1     TO    FLG-NYUKSN
               END-IF
           END-IF
      *
      *    平成２６年改正で新設された入院料加算のチェック追加
      *    以下の入院料加算については点数マスタに入院基本料加算区分がセット
      *    されていない為、入院料加算チェックテーブルによる判定ができない
      *    ことから、療養病棟入院基本料１の場合のみ算定可とする暫定対処を
      *    追加する
      *    こととする。
      *    ・在宅復帰機能強化加算（療養病棟入院基本料）（平成２６年改正で追加）
           IF    WRK-NYUKHNKSN-SRYCD(IDX8)   =    "190168070"
               IF   ((WRK-BTU-CDKBN-KBNNUM   =    101)     AND
                     (WRK-BTU-CDKBN-KOUBAN   =    1))      OR
                    ((WRK-BTU-CDKBN-KBNNUM   =    100)     AND
                     (WRK-BTU-CDKBN-KOUBAN   =    17))     OR
                    ((WRK-BTU-CDKBN-KBNNUM   =    317)     AND
                     (WRK-BTU-CDKBN-KOUBAN   =    11))
                  CONTINUE
               ELSE
                  MOVE    1     TO    FLG-NYUKSN
               END-IF
           END-IF
      *
      *    特定入院基本料（障害者施設等入院基本料）の、看護配置加算について
      *    強制的に算定不可とする
      *    （入院料加算チェックマスタでも対応するが、マスタ更新未実施時を考慮）
           IF      LNK-NYUKSNGET-NYUIN-SRYCD   = "190161210"  OR
                                                 "190161310"
               IF    WRK-NYUKHNKSN-SRYCD(IDX8)   =   "190102070"
                  MOVE    1     TO    FLG-NYUKSN
               END-IF
           END-IF
      *
           .
      *
       4801-NYUKSN-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数マスタ検索処理（入院料）
      *****************************************************************
       48011-TENSU-SEL-SEC              SECTION.
      *
           MOVE    SPACE           TO  WRK-NYUKSNKBN
           MOVE    ZERO            TO  WRK-BTU-CDKBN-KBNNUM
           MOVE    ZERO            TO  WRK-BTU-CDKBN-KBNNUM-EDA
           MOVE    ZERO            TO  WRK-BTU-CDKBN-KOUBAN
           MOVE    SPACE           TO  WRK-TOKUBETSU-NYUIN
      *
      *    平成２６年改正　夜勤時間超過減算の特別入院基本料判定用に
      *    入院料コードを置き換え
      *   （医療観察入院時はこの処理をスキップ）
           IF      LNK-NYUKSNGET-NYUIN-SRYCD(1:3) = "188"
               CONTINUE
           ELSE
      *        特定入院基本料のコード細分化置き換え
               IF     LNK-NYUKSNGET-SANTEIYM   >=   "20120401"
                   IF  LNK-NYUKSNGET-NYUIN-SRYCD  =  "190799410"
                       EVALUATE   SYS-5001-BTU-SBT
      *                    専門病院
                           WHEN    "06"
                                 MOVE "190161010" TO
                                        LNK-NYUKSNGET-NYUIN-SRYCD
      *                    障害者施設等
                           WHEN    "05"
                                 MOVE "190161210" TO
                                        LNK-NYUKSNGET-NYUIN-SRYCD
                       END-EVALUATE
      *
      *                特定機能病院は病棟設定された入院コードから判定
                       IF   SYS-5001-BTU-KHNSRYCD =  "190118610" OR
                                                     "190705610" OR
                                                     "190085910"
                         MOVE "190160810" TO  LNK-NYUKSNGET-NYUIN-SRYCD
                       END-IF
                   END-IF
               END-IF
      *
               INITIALIZE    ORCSYAKINGEN-AREA
               MOVE   LNK-NYUKSNGET-SANTEIYM
                                   TO    LNK-YAKINGEN-SANTEIYM
               MOVE   SYS-5001-BTU-NUM
                                   TO    LNK-YAKINGEN-BTU-NUM
               MOVE   LNK-NYUKSNGET-NYUIN-SRYCD
                                   TO    LNK-YAKINGEN-SRYCD
               CALL   "ORCSYAKINGEN"    USING   ORCSYAKINGEN-AREA
                                                SPA-AREA
               IF   LNK-YAKINGEN-RC     =       ZERO
                    MOVE LNK-YAKINGEN-CHGSRYCD
                                   TO    LNK-NYUKSNGET-NYUIN-SRYCD
               END-IF
           END-IF
      *
           MOVE    ZERO            TO  FLG-TENSU
           INITIALIZE                  TNS-TENSU-REC
           MOVE    LNK-NYUKSNGET-HOSPNUM
                                   TO  TNS-HOSPNUM
           MOVE    LNK-NYUKSNGET-NYUIN-SRYCD
                                   TO  TNS-SRYCD
           MOVE    LNK-NYUKSNGET-SANTEIYM
                                   TO  TNS-YUKOSTYMD(1:6)
           MOVE    "01"            TO  TNS-YUKOSTYMD(7:2)
           MOVE    TNS-YUKOSTYMD   TO  TNS-YUKOEDYMD
           MOVE    TNS-TENSU-REC   TO  MCPDATA-REC
           MOVE    "tbl_tensu"     TO  MCP-TABLE
           MOVE    "key"           TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           MOVE    "tbl_tensu"     TO  MCP-TABLE
           MOVE    "key"           TO  MCP-PATHNAME
           PERFORM 910-DBFETCH-SEC
           IF    ( MCP-RC          =   ZERO )
               MOVE    MCPDATA-REC TO  TNS-TENSU-REC
               MOVE    ZERO        TO  FLG-TENSU
           ELSE
               MOVE    1           TO  FLG-TENSU
           END-IF
      *
           MOVE    "tbl_tensu"     TO  MCP-TABLE
           MOVE    "key"           TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           IF    ( FLG-TENSU       NOT =   ZERO )
               GO  TO  48011-TENSU-SEL-EXT
           END-IF
      *
           MOVE    TNS-NYUKHNKSNKBN
                                   TO  WRK-BTU-NYUKSNKBN
           MOVE    TNS-CDKBN-KBNNUM
                                   TO WRK-BTU-CDKBN-KBNNUM
           MOVE    TNS-CDKBN-KBNNUM-EDA
                                   TO WRK-BTU-CDKBN-KBNNUM-EDA
           MOVE    TNS-CDKBN-KOUBAN
                                   TO WRK-BTU-CDKBN-KOUBAN
      *    療養病床以外・有床診療所療養病床入院基本料のとき、
      *    表用番号の置き換え（平成２４年改正）
           IF     (TNS-CDKBN-KBNNUM  = 108)   AND
                  (TNS-CDKBN-KOUBAN  =  10)   AND
                 ((TNS-YUKOSTYMD    >=  "20120401")   AND
                  (TNS-YUKOSTYMD    <=  "20140331"))
               MOVE   109           TO WRK-BTU-CDKBN-KBNNUM
               MOVE   ZERO          TO WRK-BTU-CDKBN-KBNNUM-EDA
           END-IF
      *    療養病床以外・有床診療所療養病床入院基本料のとき、
      *    表用番号の置き換え（平成２６年改正）
           IF     (TNS-CDKBN-KBNNUM  = 108)   AND
                  (TNS-CDKBN-KOUBAN  =  13)   AND
                  (TNS-YUKOSTYMD    >=  "20040401")
               MOVE   109           TO WRK-BTU-CDKBN-KBNNUM
               MOVE   ZERO          TO WRK-BTU-CDKBN-KBNNUM-EDA
           END-IF
      *
      *    特別入院料の判定
           EVALUATE   TNS-NYUKHNKBN
               WHEN   73
               WHEN   74
               WHEN   75
               WHEN   76
               WHEN   77
               WHEN   78
               WHEN   79
               WHEN   83
               WHEN   84
      *        平成２６年改正で月平均夜勤時間超過減算の入院基本料区分追加
               WHEN   88
                   MOVE    "1"     TO  WRK-TOKUBETSU-NYUIN
           END-EVALUATE
           IF  TNS-SRYCD      =    "190121810"
                   MOVE    "1"     TO  WRK-TOKUBETSU-NYUIN
           END-IF
           IF  TNS-SRYCD      =    "190799510"
                   MOVE    "1"     TO  WRK-TOKUBETSU-NYUIN
           END-IF
           .
      *
       48011-TENSU-SEL-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数マスタ検索処理（入院料加算）
      *****************************************************************
       48012-TENSU-SEL-SEC              SECTION.
      *
           MOVE    SPACE           TO  WRK-NYUKSNKBN
           MOVE    ZERO            TO  WRK-KSN-CDKBN-KBNNUM
           MOVE    ZERO            TO  WRK-KSN-CDKBN-KBNNUM-EDA
      *
           MOVE    ZERO            TO  FLG-TENSU
           INITIALIZE                  TNS-TENSU-REC
      *
           MOVE    LNK-NYUKSNGET-HOSPNUM
                                   TO  TNS-HOSPNUM
      *
      *    特別入院料算定時に幼児加算と乳幼児加算の加算コード置き換え
           IF      WRK-TOKUBETSU-NYUIN   =   "1"
               EVALUATE   WRK-NYUKHNKSN-SRYCD(IDX8)
                   WHEN   "190100470"
                       MOVE  "190100570"  TO  WRK-NYUKHNKSN-SRYCD(IDX8)
                   WHEN   "190100770"
                       MOVE  "190100870"  TO  WRK-NYUKHNKSN-SRYCD(IDX8)
               END-EVALUATE
           END-IF
      *
           MOVE    WRK-NYUKHNKSN-SRYCD(IDX8)
                                   TO  TNS-SRYCD
           MOVE    LNK-NYUKSNGET-SANTEIYM
                                   TO  TNS-YUKOSTYMD(1:6)
           MOVE    "01"            TO  TNS-YUKOSTYMD(7:2)
           MOVE    TNS-YUKOSTYMD   TO  TNS-YUKOEDYMD
           MOVE    TNS-TENSU-REC   TO  MCPDATA-REC
      *
           MOVE    "tbl_tensu"     TO  MCP-TABLE
           MOVE    "key"           TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           MOVE    "tbl_tensu"     TO  MCP-TABLE
           MOVE    "key"           TO  MCP-PATHNAME
           PERFORM 910-DBFETCH-SEC
           IF    ( MCP-RC          =   ZERO )
               MOVE    MCPDATA-REC TO  TNS-TENSU-REC
               MOVE    ZERO        TO  FLG-TENSU
           ELSE
               MOVE    1           TO  FLG-TENSU
           END-IF
      *
           MOVE    "tbl_tensu"     TO  MCP-TABLE
           MOVE    "key"           TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           IF    ( FLG-TENSU       NOT =   ZERO )
               GO  TO  48012-TENSU-SEL-EXT
           END-IF
      *
           MOVE    TNS-NYUKHNKSNKBN
                                   TO  WRK-KSN-NYUKSNKBN
      *
      *    入院料（Ａ１００番台）、特定入院料（Ａ３００番台）に含まれる
      *    注加算のみが判定対象となる
           IF      (TNS-CDKBN-KBN       =   "A")     AND
                   (TNS-CDKBN-KBNNUM   >=   100)     AND
                   (TNS-CDKBN-KBNNUM   <=   199)
               MOVE    TNS-CDKBN-KBNNUM TO  WRK-KSN-CDKBN-KBNNUM
               MOVE    TNS-CDKBN-KBNNUM-EDA
                                        TO  WRK-KSN-CDKBN-KBNNUM-EDA
           END-IF
      *
           IF      (TNS-CDKBN-KBN       =   "A")     AND
                   (TNS-CDKBN-KBNNUM   >=   300)     AND
                   (TNS-CDKBN-KBNNUM   <=   399)
               MOVE    TNS-CDKBN-KBNNUM TO  WRK-KSN-CDKBN-KBNNUM
               MOVE    TNS-CDKBN-KBNNUM-EDA
                                        TO  WRK-KSN-CDKBN-KBNNUM-EDA
           END-IF
           .
      *
       48012-TENSU-SEL-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院患者の年齢算出
      *****************************************************************
       4801-PT-AGE-SEC         SECTION.
      *
           INITIALIZE                      PTINF-REC
           MOVE    LNK-NYUKSNGET-HOSPNUM
                                       TO  PTINF-HOSPNUM
           MOVE    LNK-NYUKSNGET-PTID  TO  PTINF-PTID
           MOVE    PTINF-REC           TO  MCPDATA-REC
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 910-DBFETCH-SEC
           IF    ( MCP-RC              =   ZERO  )
               MOVE    MCPDATA-REC     TO  PTINF-REC
           ELSE
               MOVE    1               TO  FLG-PTINF
           END-IF
      *
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           IF      FLG-PTINF           =   1
               MOVE    ZERO            TO  WRK-NENREI
           ELSE
               MOVE   PTINF-BIRTHDAY   TO  WRK-YMDS1
               MOVE   LNK-NYUKSNGET-SANTEIYM
                                       TO  WRK-YMDS2(1:6)
               MOVE   "01"             TO  WRK-YMDS2(7:2)
               MOVE   ZERO             TO  WRK-NENREI
               COMPUTE  WRK-NENREI     =   WRK-YYS2
                                       -   WRK-YYS1
               IF   WRK-YMDS1(5:4)     >   WRK-YMDS2(5:4)
                    ADD   -1           TO  WRK-NENREI
               END-IF
           END-IF
      *
           .
      *
       4801-PT-AGE-EXT.
           EXIT.
      *
      *****************************************************************
      *    病棟情報検索処理
      *****************************************************************
       4801-BTUSEL-SEC             SECTION.
      *
           INITIALIZE                      SYSKANRI-REC
           MOVE    ZERO                TO  FLG-SYSKANRI
      *
           MOVE    LNK-NYUKSNGET-HOSPNUM
                                       TO  SYS-HOSPNUM
           MOVE    "5001"              TO  SYS-KANRICD
           MOVE    LNK-NYUKSNGET-BTUNUM
                                       TO  SYS-KBNCD
           MOVE    LNK-NYUKSNGET-SANTEIYM
                                       TO  SYS-STYUKYMD(1:6)
           MOVE    "01"                TO  SYS-STYUKYMD(7:2)
           MOVE    SYS-STYUKYMD        TO  SYS-EDYUKYMD
           MOVE    SYSKANRI-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 910-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  SYS-5001-REC
               MOVE    ZERO            TO  FLG-SYSKANRI
           ELSE
               INITIALIZE                  SYS-5001-REC
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       4801-BTUSEL-EXT.
           EXIT.
      *****************************************************************
      *    病室情報検索処理
      *****************************************************************
       4801-BRMSEL-SEC             SECTION.
      *
           INITIALIZE                      SYSKANRI-REC
           MOVE    ZERO                TO  FLG-SYSKANRI
      *
           MOVE    LNK-NYUKSNGET-HOSPNUM
                                       TO  SYS-HOSPNUM
           MOVE    "5002"              TO  SYS-KANRICD
           MOVE    LNK-NYUKSNGET-BRMNUM
                                       TO  SYS-KBNCD
           MOVE    LNK-NYUKSNGET-SANTEIYM
                                       TO  SYS-STYUKYMD(1:6)
           MOVE    "01"                TO  SYS-STYUKYMD(7:2)
           MOVE    SYS-STYUKYMD        TO  SYS-EDYUKYMD
           MOVE    SYSKANRI-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 910-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  SYS-5002-REC
               MOVE    ZERO            TO  FLG-SYSKANRI
           ELSE
               INITIALIZE                  SYS-5002-REC
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       4801-BRMSEL-EXT.
           EXIT.
      *
      *****************************************************************
      *    テーブル検索処理
      *****************************************************************
       910-DBSELECT-SEC                SECTION.
      *
           MOVE    "DBSELECT"      TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       910-DBSELECT-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ読み込み処理
      *****************************************************************
       910-DBFETCH-SEC                 SECTION.
      *
           MOVE    "DBFETCH"       TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       920-DBFETCH-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢクロース処理
      *****************************************************************
       990-DBCLOSE-SEC         SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       990-DBCLOSE-EXT.
           EXIT.
      *
