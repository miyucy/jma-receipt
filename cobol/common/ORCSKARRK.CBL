      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCSKARRK.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 
      *  コンポーネント名  : 診療科履歴　初診算定日更新処理
      *  返却エラーコード  : 00:正常　10:パラメータ不正・対象データなし
      *  管理者            : 
      *  作成日付    作業者        記述
      *  01.00.01    MCC-多々納   02/05/15  労災用追加
      *  01.00.02    NACL-多々納  02/12/15  初診算定日判定修正
      *  01.00.03    NACL-多々納  03/02/13  訂正初診算定日判定修正
      *  01.00.05    NACL-多々納  06/01/12  削除処理変更
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *
       DATA                    DIVISION.
      *FILE                        SECTION.
      *
      *
       WORKING-STORAGE             SECTION.
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-SRYKARRK        PIC 9(01).
           03  FLG-TENSU           PIC 9(01).
           03  FLG-SANTEI          PIC 9(01).
           03  FLG-JYURRK          PIC 9(01).
           03  FLG-SRYACCT         PIC 9(01).
           03  FLG-SRYACT          PIC 9(01).
           03  FLG-OK              PIC 9(01).
      *
           03  FLG-JYURRK-ARI      PIC 9(01).
           03  FLG-JYURRK-CHK      PIC 9(01).
           03  FLG-SRYACCT-CHK     PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                 PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDY2                PIC 9(04).
           03  IDX2                PIC 9(04).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-SRYCD               PIC X(09). 
           03  WRK-SYOSINYMD1          PIC X(08). 
           03  WRK-SYOSINYMD2          PIC X(08). 
           03  WRK-LST-SRYYMD          PIC X(08). 
           03  WRK-LST-SRYYMD2         PIC X(08). 
      *    入院履歴
           03  WRK-NYULST-SRYYMD          PIC X(08). 
           03  WRK-NYULST-SRYYMD2         PIC X(08). 
      *
           03  WRK-SANTEIYMD           PIC X(08). 
           03  WRK-DAY                 PIC 9(02).
           03  WRK-DD                  PIC 9(02).
      *
           03  WRK-SRYKBN              PIC X(02).
           03  WRK-SYOSINYMD-RRK       PIC X(08).
           03  WRK-SYOSINYMD-RRK2      PIC X(08).
      *
      *    マシン日付取得サブ
           COPY    "CPORCSMCNDATE.INC".
      *
      *    保険別算定履歴テーブル
           COPY   "CMSANTEITBL.INC".
      *
      *
      *    共通領域
           COPY    "MCPAREA".
      *
      *    点数マスタ−
           COPY    "CPTENSU.INC".
      *
      *    診療科履歴
       01  SRYKARRK-REC.
           COPY    "CPSRYKARRK.INC".
      *    診療科履歴
       01  HZN-SRYKARRK-REC.
           COPY    "CPSRYKARRK.INC"  REPLACING
                                     //KARRK-// BY //HZN-KARRK-//.
      *
      *    算定履歴
       01  SANTEI-REC.
           COPY    "CPSANTEI.INC".
      *
      *    受診履歴
       01  JYURRK-REC.
           COPY    "CPJYURRK.INC".
      *
      *    診療行為マスター
       01  SRYACT-REC.
           COPY    "CPSRYACT.INC".
      *
      *    診療会計マスタ−
       01  SRYACCT-REC.
           COPY    "CPSRYACCT.INC".
      *
      *01  MCPDATA-REC                 PIC X(5000).
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
       LINKAGE                     SECTION.
      *
           COPY    "CPORCSKARRK.INC".
      *    スパ領域
           COPY    "COMMON-SPA".
      *
      *
       PROCEDURE                    DIVISION    USING
           ORCSKARRKAREA
           SPA-AREA.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                 SECTION.
      *
           MOVE    ZERO                TO  SKARRK-RC
           MOVE    SPACE               TO  SKARRK-ERRMSG
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  IDX-AREA
           INITIALIZE                  WRK-AREA
      *
      *    更新日取得
           INITIALIZE                  ORCSMCNDATEAREA
           CALL    "ORCSMCNDATE"       USING
                                       ORCSMCNDATEAREA
      *
           IF      SKARRK-SRYYM        =   SPACE
               MOVE    SPA-SRYYMD(1:6)     TO  SKARRK-SRYYM
           END-IF
      *
      *    診療科履歴初期処理
           PERFORM 100-SRYKA-SYOKI-SEC
      *    算定履歴チェック処理
           PERFORM 200-SANTEIRRK-CHK-SEC
      *    診療科履歴更新処理
           PERFORM 300-SRYKARRK-HEN-SEC
      *
           IF      SPA-ERRCD           NOT =   SPACE
               MOVE    "診療科履歴更新エラー"      TO  SKARRK-ERRMSG
               MOVE    SPACE               TO  SPA-ERRCD
               MOVE    10                  TO  SKARRK-RC
           END-IF
           .
      *
           EXIT PROGRAM
           .
      *
      *****************************************************************
      *    診療科履歴初期処理
      *****************************************************************
       100-SRYKA-SYOKI-SEC                SECTION.
      *
      *    診療科履歴読込
           MOVE    SPACE               TO  SRYKARRK-REC
           INITIALIZE                      SRYKARRK-REC
           MOVE    SPA-HOSPID          TO  KARRK-HOSPID
           MOVE    SPA-PTID            TO  KARRK-PTID
           MOVE    SPA-SRYKA           TO  KARRK-SRYKA
      *
           MOVE    SRYKARRK-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SRYKARRK-KEY"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SRYKARRK-KEY"      TO  ORC-DBPATH
               PERFORM 975-SRYKARRK-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SRYKARRK
           END-IF
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "SRYKARRK-KEY"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *    診療科新規作成
           IF      FLG-SRYKARRK           =   1
               MOVE    SPACE               TO  SRYKARRK-REC
               INITIALIZE                      SRYKARRK-REC
               MOVE    SPA-HOSPID          TO  KARRK-HOSPID
               MOVE    SPA-PTID            TO  KARRK-PTID
               MOVE    SPA-SRYKA           TO  KARRK-SRYKA
           END-IF
           .
      *
       100-SRYKA-SYOKI-EXT.
           EXIT.
      *
      *****************************************************************
      *    算定履歴チェック処理
      *****************************************************************
       200-SANTEIRRK-CHK-SEC                SECTION.
      *
      *    初診料の算定履歴のチェック
           MOVE    SPACE               TO  WRK-SYOSINYMD1
           MOVE    SPACE               TO  WRK-SYOSINYMD2
      *
      **** EVALUATE    SPA-HOSPSBT
      *        病院
      *******  WHEN    1
      *            老人
                   MOVE    "111700110"         TO  WRK-SRYCD
                   PERFORM 2001-SANTEI-CHK-SEC
      *            老人外
                   MOVE    "111000110"         TO  WRK-SRYCD
                   PERFORM 2001-SANTEI-CHK-SEC
      *        診療所
      ******   WHEN    2
      *            老人
                   MOVE    "111700210"         TO  WRK-SRYCD
                   PERFORM 2001-SANTEI-CHK-SEC
      *            老人外
                   MOVE    "111003610"         TO  WRK-SRYCD
                   PERFORM 2001-SANTEI-CHK-SEC
      *****END-EVALUATE
      *    小児科診察料
      *    　　院外処方   初診
           MOVE    "113003510"         TO  WRK-SRYCD
           PERFORM 2001-SANTEI-CHK-SEC
      *    　　院内処方   初診
           MOVE    "113003710"         TO  WRK-SRYCD
           PERFORM 2001-SANTEI-CHK-SEC
      *
      *    仮の初診料
           MOVE    "099110001"         TO  WRK-SRYCD
           PERFORM 2001-SANTEI-CHK-SEC
      *
      *    労災初診料
           MOVE    "101110010"         TO  WRK-SRYCD
           PERFORM 2001-SANTEI-CHK-SEC
      *
           .
      *
       200-SANTEIRRK-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    算定履歴チェック処理
      *****************************************************************
       2001-SANTEI-CHK-SEC                SECTION.
      *
      *
           MOVE    WRK-SRYCD           TO  TNS-SRYCD
           PERFORM 910-TENSU-READ-SEC
      *
      *    算定履歴検索
           INITIALIZE                  SANTEI-REC
           MOVE    SPA-HOSPID          TO  SANTEI-HOSPID
           MOVE    SPA-PTID            TO  SANTEI-PTID
      *****MOVE    SKARRK-SRYYM        TO  SANTEI-SRYYM
           MOVE    WRK-SRYCD           TO  SANTEI-SRYCD
           EVALUATE    TNS-SANTEIRRKKBN
               WHEN    1
                   MOVE    ZERO                TO  SANTEI-NYUGAIKBN
                   MOVE    ZERO                TO  SANTEI-SRYKA
               WHEN    2
                   MOVE    SPA-NYUGAIKBN       TO  SANTEI-NYUGAIKBN
                   MOVE    ZERO                TO  SANTEI-SRYKA
               WHEN    3
                   MOVE    ZERO                TO  SANTEI-NYUGAIKBN
                   MOVE    SPA-SRYKA           TO  SANTEI-SRYKA
               WHEN    4
                   MOVE    SPA-NYUGAIKBN       TO  SANTEI-NYUGAIKBN
                   MOVE    SPA-SRYKA           TO  SANTEI-SRYKA
           END-EVALUATE
      *
           MOVE    SANTEI-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SANTEI-KEY3"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SANTEI-KEY3"       TO  ORC-DBPATH
               PERFORM 920-SANTEI-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SANTEI
           END-IF
      *
           PERFORM UNTIL       FLG-SANTEI          =   1
      *
               IF      SANTEI-MSANTEICNT   >   ZERO
                   MOVE    ZERO            TO  WRK-DAY
                   PERFORM VARYING     IDY     FROM    1   BY  1
                           UNTIL       IDY     >   31
                       IF      SANTEI-DAY (IDY)    NOT =   ZERO
                           MOVE    IDY             TO  WRK-DAY
                       END-IF
                   END-PERFORM
                   MOVE    SANTEI-SRYYM            TO  WRK-SANTEIYMD
                                                               (1:6)
                   MOVE    WRK-DAY                 TO  WRK-SANTEIYMD
                                                               (7:2)
                   IF     (WRK-SYOSINYMD1          =   SPACE  )  OR
                          (WRK-SANTEIYMD           <   WRK-SYOSINYMD1)
                       MOVE    WRK-SANTEIYMD       TO  WRK-SYOSINYMD1
                   END-IF
                   IF      WRK-SANTEIYMD           >   WRK-SYOSINYMD2
                       MOVE    WRK-SANTEIYMD       TO  WRK-SYOSINYMD2
                   END-IF
               END-IF
      *
               MOVE    "SANTEI-KEY3"       TO  ORC-DBPATH
               PERFORM 920-SANTEI-READ-SEC
           END-PERFORM
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "SANTEI-KEY3"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
      *
       2001-SANTEI-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療科履歴更新処理
      *****************************************************************
       300-SRYKARRK-HEN-SEC                SECTION.
      *
      *    最終受診履歴
           MOVE    SPACE               TO  WRK-LST-SRYYMD
           MOVE    SPACE               TO  WRK-LST-SRYYMD2
           MOVE    SPACE               TO  WRK-NYULST-SRYYMD
           MOVE    SPACE               TO  WRK-NYULST-SRYYMD2
           MOVE    SPACE               TO  WRK-SYOSINYMD-RRK
           MOVE    SPACE               TO  WRK-SYOSINYMD-RRK2
      *
           MOVE    SPACE               TO  JYURRK-REC
           INITIALIZE                      JYURRK-REC
           MOVE    SPA-HOSPID          TO  JYURRK-HOSPID
           MOVE    SPA-PTID            TO  JYURRK-PTID
      *****MOVE    SPA-SRYKA           TO  JYURRK-SRYKA
      *
           MOVE    JYURRK-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "JYURRK-KEY8"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "JYURRK-KEY8"       TO  ORC-DBPATH
               PERFORM 970-JYURRK-NEXT-SEC
           ELSE
               MOVE    1                   TO  FLG-JYURRK
           END-IF
           MOVE    ZERO                TO  FLG-JYURRK-ARI
           MOVE    ZERO                TO  FLG-JYURRK-CHK
           PERFORM
               UNTIL      (FLG-JYURRK      =   1   )  OR
                         ((WRK-LST-SRYYMD  NOT =   SPACE  ) AND
                          (WRK-LST-SRYYMD2 NOT =   SPACE  ) AND
                          (FLG-JYURRK-CHK  NOT =   ZERO   ))
      *      包括は対象外
             IF      JYURRK-HKNCOMBINUM    NOT =   "9999"
      *
      *        診療科履歴は外来のみで作成する
               IF      JYURRK-NYUGAIKBN    =   "2"
                   IF      SPA-SRYKA       =   JYURRK-SRYKA
                       IF      WRK-LST-SRYYMD      =   SPACE
                           MOVE    JYURRK-SRYYMD   TO  WRK-LST-SRYYMD
                       END-IF
                       IF     (JYURRK-SRYKBN1      NOT =   SPACE ) AND
                              (WRK-SYOSINYMD-RRK       =   SPACE )
                           PERFORM 3001-SYOSIN-SENTEI-SEC
                           MOVE    1               TO  FLG-JYURRK-CHK
                       END-IF
                   ELSE
                       IF      WRK-LST-SRYYMD2     =   SPACE
                           MOVE    JYURRK-SRYYMD   TO  WRK-LST-SRYYMD2
                       END-IF
                       IF     (JYURRK-SRYKBN1      NOT =   SPACE ) AND
                              (WRK-SYOSINYMD-RRK2      =   SPACE )
                           PERFORM 3001-SYOSIN-SENTEI-SEC
                       END-IF
                   END-IF
      *            入院履歴
               ELSE
                   IF      SPA-SRYKA       =   JYURRK-SRYKA
                       IF      WRK-NYULST-SRYYMD   =   SPACE
                           MOVE    JYURRK-SRYYMD   TO  WRK-NYULST-SRYYMD
                       END-IF
                       IF     (JYURRK-SRYKBN1      NOT =   SPACE ) AND
                              (WRK-SYOSINYMD-RRK       =   SPACE )
                           PERFORM 3001-SYOSIN-SENTEI-SEC
                           MOVE    1               TO  FLG-JYURRK-CHK
                       END-IF
                   ELSE
                       IF      WRK-NYULST-SRYYMD2  =   SPACE
                           MOVE    JYURRK-SRYYMD   TO
                                                   WRK-NYULST-SRYYMD2
                       END-IF
                       IF     (JYURRK-SRYKBN1      NOT =   SPACE ) AND
                              (WRK-SYOSINYMD-RRK2      =   SPACE )
                           PERFORM 3001-SYOSIN-SENTEI-SEC
                       END-IF
                   END-IF
               END-IF
               MOVE    1                   TO  FLG-JYURRK-ARI
      *
               END-IF
      *
               MOVE    "JYURRK-KEY8"       TO  ORC-DBPATH
               PERFORM 970-JYURRK-NEXT-SEC
           END-PERFORM
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "JYURRK-KEY8"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
      *    外来がなく、入院ののみの時、入院の最終履歴日を編集する
           IF     (WRK-LST-SRYYMD      =   SPACE )  AND
                  (WRK-LST-SRYYMD2     =   SPACE )
               IF     (WRK-NYULST-SRYYMD       =   SPACE )  AND
                      (WRK-NYULST-SRYYMD2      =   SPACE )
                   CONTINUE
               ELSE
                   MOVE    WRK-NYULST-SRYYMD   TO  WRK-LST-SRYYMD
                   MOVE    WRK-NYULST-SRYYMD2  TO  WRK-LST-SRYYMD2
               END-IF
           ELSE
      *        入院の最終履歴日を編集する
               IF     (WRK-NYULST-SRYYMD   NOT =   SPACE ) AND
                      (WRK-LST-SRYYMD          <   WRK-LST-SRYYMD)
                   MOVE    WRK-NYULST-SRYYMD   TO  WRK-LST-SRYYMD
               END-IF
           END-IF
      *
      *    最終受診日
           MOVE    WRK-LST-SRYYMD      TO  KARRK-LASTYMD
      *
           IF      WRK-SYOSINYMD-RRK   NOT =   SPACE
      *        診療科の初診料あり
               IF      WRK-SYOSINYMD-RRK       <   WRK-SYOSINYMD2
      *            算定履歴のみの場合
                   MOVE    WRK-SYOSINYMD2      TO  KARRK-SYOSINYMD2
               ELSE
                   MOVE    WRK-SYOSINYMD-RRK   TO  KARRK-SYOSINYMD2
               END-IF
               IF      WRK-SYOSINYMD-RRK       <   KARRK-SYOSINYMD1
                   MOVE    WRK-SYOSINYMD-RRK   TO  KARRK-SYOSINYMD1
               END-IF
           ELSE
      *        診療科の初診料なし
               IF      WRK-LST-SRYYMD  NOT =   SPACE
                   IF      WRK-SYOSINYMD-RRK2  =   SPACE
                       MOVE    WRK-SYOSINYMD2      TO  KARRK-SYOSINYMD2
                   ELSE
                       MOVE    SPACE           TO  KARRK-SYOSINYMD2
                   END-IF
               END-IF
      *        他科受診あり
               IF     (WRK-LST-SRYYMD      =   SPACE )   AND
                      (WRK-LST-SRYYMD2 NOT =   SPACE )
                   MOVE    SPACE           TO  KARRK-SYOSINYMD1
                   MOVE    SPACE           TO  KARRK-SYOSINYMD2
                   MOVE    SPACE           TO  KARRK-LASTYMD
               END-IF
      *        算定履歴のみ
               IF     (WRK-LST-SRYYMD      =   SPACE )   AND
                      (WRK-LST-SRYYMD2     =   SPACE )
                   MOVE    WRK-SYOSINYMD1      TO  KARRK-SYOSINYMD1
                   MOVE    WRK-SYOSINYMD2      TO  KARRK-SYOSINYMD2
                   MOVE    WRK-SYOSINYMD2      TO  KARRK-LASTYMD
               END-IF
           END-IF
      *    初診料１
      *    IF    ( KARRK-SYOSINYMD1    =   SPACE    )  OR
      *          ( SKARRK-SRYYM        =   KARRK-SYOSINYMD1(1:6))
      *        IF      WRK-SYOSINYMD1      =   SPACE
      *            IF      WRK-LST-SRYYMD      =   SPACE  AND
      *                    WRK-LST-SRYYMD2     =   SPACE
      *                 すべての履歴なし
      *                MOVE    SPACE           TO  KARRK-SYOSINYMD1
      *            END-IF
      *        ELSE
      *            MOVE   WRK-SYOSINYMD1   TO  KARRK-SYOSINYMD1
      *        END-IF
      *    END-IF
      *    初診料２
      *    IF    ( KARRK-SYOSINYMD2    =   SPACE    )  OR
      *          ( SKARRK-SRYYM        =   KARRK-SYOSINYMD2(1:6))
      *        IF      WRK-SYOSINYMD2      =   SPACE
      *            IF      WRK-LST-SRYYMD      =   SPACE
      *                MOVE    SPACE           TO  KARRK-SYOSINYMD2
      *            END-IF
      *        ELSE
      *            MOVE   WRK-SYOSINYMD2   TO  KARRK-SYOSINYMD2
      *        END-IF
      **** END-IF
      *    IF      KARRK-LASTYMD       =   SPACE
      *        最終受診日
      *        MOVE    KARRK-SYOSINYMD2    TO  KARRK-LASTYMD
      *    END-IF
      *
      *    すべての履歴がない時、他の診療科履歴も削除する
           IF     (FLG-JYURRK-ARI      =   ZERO  )  AND
                  (WRK-SYOSINYMD1      =   SPACE )  AND
                  (WRK-SYOSINYMD2      =   SPACE )
               PERFORM 3009-SRYKARRK-DEL-SEC
               GO      TO      300-SRYKARRK-HEN-EXT
           END-IF
      *
      *    すべての日付が空白の時、削除する
           IF     (KARRK-SYOSINYMD1        =   SPACE )  AND
                  (KARRK-SYOSINYMD2        =   SPACE )  AND
                  (KARRK-LASTYMD           =   SPACE )
               IF      FLG-SRYKARRK           =   ZERO
                   MOVE    SRYKARRK-REC        TO  MCPDATA-REC
                   MOVE    "DBDELETE"          TO  MCP-FUNC
                   MOVE    "SRYKARRK-KEY"      TO  ORC-DBPATH
                   CALL    "ORCMCPSUB"          USING
                                                MCPAREA
                                                ORCMCPAREA
                                                MCPDATA-REC
                   IF      MCP-RC          NOT =   ZERO
                       DISPLAY "ORCSCKARRK SRYKARRK DEL ERR:"
                                                    MCP-RC  "."
                       MOVE    "3000"          TO  SPA-ERRCD
                   END-IF
      *            他の診療科の内容確認
                   PERFORM 3005-DEL-SYORI-SEC
              END-IF
              GO      TO      300-SRYKARRK-HEN-EXT
           END-IF
      *
      *
           IF      FLG-SRYKARRK           =   ZERO
      *        更新
               MOVE    SPA-HOSPID          TO  KARRK-HOSPID
               MOVE    SPA-PTID            TO  KARRK-PTID
               MOVE    SPA-SRYKA           TO  KARRK-SRYKA
      *
               MOVE    SMCNDATE-YMD        TO  KARRK-UPYMD
               MOVE    SMCNDATE-HMS        TO  KARRK-UPHMS
      *
               MOVE    SRYKARRK-REC        TO  MCPDATA-REC
               MOVE    "DBUPDATE"          TO  MCP-FUNC
               MOVE    "SRYKARRK-KEY"      TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC          NOT =   ZERO
                   DISPLAY "ORCSCKARRK SRYKARRK UPD ERR:"  MCP-RC  "."
                   MOVE    "3000"          TO  SPA-ERRCD
               END-IF
           ELSE
      *        新規
               MOVE    SPA-HOSPID          TO  KARRK-HOSPID
               MOVE    SPA-PTID            TO  KARRK-PTID
               MOVE    SPA-SRYKA           TO  KARRK-SRYKA
      *
      *
               MOVE    SMCNDATE-YMD        TO  KARRK-CREYMD
               MOVE    SMCNDATE-YMD        TO  KARRK-UPYMD
               MOVE    SMCNDATE-HMS        TO  KARRK-UPHMS
      *
               MOVE    SRYKARRK-REC        TO  MCPDATA-REC
               MOVE    "DBINSERT"          TO  MCP-FUNC
               MOVE    "SRYKARRK-KEY"      TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC          NOT =   ZERO
                   DISPLAY "ORCSCKARRK SRYKARRK INS ERR:"  MCP-RC  "."
                   MOVE    "3000"          TO  SPA-ERRCD
               END-IF
           END-IF
      *
           .
       300-SRYKARRK-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    初診算定日を履歴より処理
      *****************************************************************
       3001-SYOSIN-SENTEI-SEC         SECTION.
      *
           MOVE    "11"                TO  WRK-SRYKBN
           PERFORM 30011-SRYACCT-CHK-SEC
           IF      FLG-SRYACCT-CHK     =   ZERO
               MOVE    "13"                TO  WRK-SRYKBN
               PERFORM 30011-SRYACCT-CHK-SEC
           END-IF
      *
           .
       3001-SYOSIN-SENTEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療科履歴削除処理
      *****************************************************************
       30011-SRYACCT-CHK-SEC         SECTION.
      *
           MOVE    ZERO                TO  FLG-SRYACCT-CHK
      *
      *    診療年月の診療会計マスタ読み込み
           MOVE    SPACE               TO  SRYACCT-REC
           INITIALIZE                      SRYACCT-REC
           MOVE    JYURRK-HOSPID       TO  ACCT-HOSPID
           MOVE    JYURRK-NYUGAIKBN    TO  ACCT-NYUGAIKBN
           MOVE    JYURRK-PTID         TO  ACCT-PTID
           MOVE    JYURRK-SRYKA        TO  ACCT-SRYKA
           MOVE    JYURRK-SRYYMD(1:6)  TO  ACCT-SRYYM
           MOVE    WRK-SRYKBN          TO  ACCT-SRYKBN
      *
           MOVE    SRYACCT-REC         TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SRYACCT-KEY3"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SRYACCT-KEY3"      TO  ORC-DBPATH
               PERFORM 930-SRYACCT-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SRYACCT
           END-IF
           PERFORM
               UNTIL       (FLG-SRYACCT         =   1    )  OR
                           (FLG-SRYACCT-CHK     =   1    )
               IF     (ACCT-ZAIKAISU       >   ZERO  )  AND
                      (ACCT-HKNCOMBI   NOT =   "9999")
      *            診療行為より
                   PERFORM 300112-SRYACT-HEN-SEC
                   IF      FLG-SRYACCT-CHK     =   1
      *            最後の日を保存
                       PERFORM VARYING     IDY2    FROM    1   BY  1
                               UNTIL       IDY2    >   31
                           IF      ACCT-DAY (1 IDY2)   >   ZERO
                               MOVE    IDY2                TO  WRK-DD
                           END-IF
                       END-PERFORM
                       IF      SPA-SRYKA       =   JYURRK-SRYKA
                           MOVE    ACCT-SRYYM      TO  WRK-SYOSINYMD-RRK
                                                                 (1:6)
                           MOVE    WRK-DD          TO  WRK-SYOSINYMD-RRK
                                                                 (7:2)
                       ELSE
                           MOVE    ACCT-SRYYM  TO  WRK-SYOSINYMD-RRK2
                                                                 (1:6)
                           MOVE    WRK-DD      TO  WRK-SYOSINYMD-RRK2
                                                                 (7:2)
                       END-IF
                   END-IF
               END-IF
      *
               MOVE    "SRYACCT-KEY3"      TO  ORC-DBPATH
               PERFORM 930-SRYACCT-READ-SEC
           END-PERFORM
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "SRYACCT-KEY3"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
      *
           .
       30011-SRYACCT-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療行為判定処理
      *****************************************************************
       300112-SRYACT-HEN-SEC         SECTION.
      *
      *    診療会計マスターから診療行為マスターを検索する
           MOVE    SPACE               TO  SRYACT-REC
           INITIALIZE                      SRYACT-REC
           MOVE    ACCT-HOSPID         TO  SRY-HOSPID
           MOVE    ACCT-NYUGAIKBN      TO  SRY-NYUGAIKBN
           MOVE    ACCT-PTID           TO  SRY-PTID
           MOVE    ACCT-SRYKA          TO  SRY-SRYKA
           MOVE    ACCT-SRYYM          TO  SRY-SRYYM
           MOVE    ACCT-ZAINUM         TO  SRY-ZAINUM
      *
           MOVE    SRYACT-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
               PERFORM 920-SRYACT-NEXT-SEC
           ELSE
               MOVE    1                   TO  FLG-SRYACT
           END-IF
           PERFORM UNTIL     (FLG-SRYACT       =   1 )  OR
                             (FLG-SRYACCT-CHK  =   1 )
               PERFORM VARYING     IDX2    FROM    1   BY  1
                       UNTIL      (IDX2            >   5   )  OR
                                  (SRY-SRYCD(IDX2) =   SPACE )
      *            診察料であること
                   EVALUATE    SRY-SRYCD (IDX2)
                       WHEN    "111700110"
                       WHEN    "111000110"
                       WHEN    "111700210"
                       WHEN    "111003610"
                       WHEN    "113003510"
                       WHEN    "113003710"
                       WHEN    "099110001"
                       WHEN    "101110010"
                           MOVE    1               TO  FLG-SRYACCT-CHK
                           MOVE    5               TO  IDX2
                   END-EVALUATE
               END-PERFORM
      *
               MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
               PERFORM 920-SRYACT-NEXT-SEC
           END-PERFORM
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
           .
       300112-SRYACT-HEN-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    他の診療科の内容確認処理
      *****************************************************************
       3005-DEL-SYORI-SEC         SECTION.
      *
      *    診療科履歴読込
           MOVE    SPACE               TO  SRYKARRK-REC
           INITIALIZE                      SRYKARRK-REC
           MOVE    SPA-HOSPID          TO  KARRK-HOSPID
           MOVE    SPA-PTID            TO  KARRK-PTID
      *
           MOVE    SRYKARRK-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SRYKARRK-KEY2"     TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SRYKARRK-KEY2"     TO  ORC-DBPATH
               PERFORM 975-SRYKARRK-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SRYKARRK
           END-IF
           MOVE    ZERO                TO  FLG-OK
           MOVE    SPACE               TO  HZN-SRYKARRK-REC
           PERFORM UNTIL    FLG-SRYKARRK   =   1
               IF      KARRK-SYOSINYMD2    NOT =   SPACE
                   MOVE    1                   TO  FLG-OK
                   MOVE    1                   TO  FLG-SRYKARRK
               ELSE
                   IF     (HZN-SRYKARRK-REC    =   SPACE )  OR
                          (KARRK-SYOSINYMD1    =   WRK-SYOSINYMD2)
                       MOVE    SRYKARRK-REC        TO  HZN-SRYKARRK-REC
                   END-IF
                   MOVE    "SRYKARRK-KEY2"     TO  ORC-DBPATH
                   PERFORM 975-SRYKARRK-READ-SEC
               END-IF
           END-PERFORM
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "SRYKARRK-KEY2"     TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           IF     (HZN-SRYKARRK-REC    NOT =   SPACE )  AND
                  (FLG-OK              =   ZERO      )
               MOVE    HZN-SRYKARRK-REC    TO  SRYKARRK-REC
               MOVE    WRK-SYOSINYMD2      TO  KARRK-SYOSINYMD2
      *
               MOVE    SMCNDATE-YMD        TO  KARRK-UPYMD
               MOVE    SMCNDATE-HMS        TO  KARRK-UPHMS
      *
               MOVE    SRYKARRK-REC        TO  MCPDATA-REC
               MOVE    "DBUPDATE"          TO  MCP-FUNC
               MOVE    "SRYKARRK-KEY"      TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC          NOT =   ZERO
                   DISPLAY "ORCSCKARRK SRYKARRK UPD ERR:"  MCP-RC  "."
                   MOVE    "3000"          TO  SPA-ERRCD
               END-IF
           END-IF
           .
       3005-DEL-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療科履歴削除処理
      *****************************************************************
       3009-SRYKARRK-DEL-SEC         SECTION.
      *
      *    診療科履歴
           INITIALIZE                      SRYKARRK-REC
           MOVE    SPA-HOSPID          TO  KARRK-HOSPID
           MOVE    SPA-PTID            TO  KARRK-PTID
      *
           MOVE    SRYKARRK-REC        TO  MCPDATA-REC
           MOVE    "DBDELETE"          TO  MCP-FUNC
           MOVE    "SRYKARRK-DEL1"     TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
      *
           .
       3009-SRYKARRK-DEL-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数マスター読込
      *****************************************************************
       910-TENSU-READ-SEC         SECTION.
      *
           MOVE    ZERO                TO  TNS-YUKOSTYMD
           MOVE    ALL "9"             TO  TNS-YUKOEDYMD
      *
           MOVE    TNS-TENSU-REC       TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
      **** MOVE    "TENSU-KEY"         TO  ORC-DBPATH
           MOVE    "TENSU-KEY22"       TO  ORC-DBPATH
           MOVE    SKARRK-SRYYM        TO  ORC-DBYMD(1:6)
           MOVE    "01"                TO  ORC-DBYMD(7:2)
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
      *******  MOVE    "TENSU-KEY"         TO  ORC-DBPATH
               MOVE    "TENSU-KEY22"       TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  TNS-TENSU-REC
                   MOVE    ZERO                TO  FLG-TENSU
               ELSE
                   INITIALIZE                  TNS-TENSU-REC
                   MOVE    1                   TO  FLG-TENSU
               END-IF
           ELSE
               INITIALIZE                  TNS-TENSU-REC
               MOVE    1                   TO  FLG-TENSU
           END-IF
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
      **** MOVE    "TENSU-KEY"         TO  ORC-DBPATH
           MOVE    "TENSU-KEY22"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
       910-TENSU-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    算定歴マスタ読み込み
      *****************************************************************
       920-SANTEI-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  SANTEI-REC
               MOVE    ZERO                TO  FLG-SANTEI
           ELSE
               INITIALIZE                      SANTEI-REC
               MOVE    1                   TO  FLG-SANTEI
           END-IF
      *
           .
       920-SANTEI-READ-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    診療科歴データ主キー検索
      *****************************************************************
       975-SRYKARRK-READ-SEC           SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  SRYKARRK-REC
               MOVE    ZERO                TO  FLG-SRYKARRK
           ELSE
               MOVE    1                   TO  FLG-SRYKARRK
               INITIALIZE                  SRYKARRK-REC
           END-IF
      *
           .
       975-SRYKARRK-READ-EXT.
           EXIT.
      *****************************************************************
      *    受診履歴順読み
      *****************************************************************
       970-JYURRK-NEXT-SEC           SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  JYURRK-REC 
               MOVE    ZERO                TO  FLG-JYURRK
           ELSE
               MOVE    1                   TO  FLG-JYURRK
           END-IF
      *
           .
       970-JYURRK-NEXT-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療行為マスター読込
      *****************************************************************
       920-SRYACT-NEXT-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  SRYACT-REC
               MOVE    ZERO                TO  FLG-SRYACT
           ELSE
               INITIALIZE                      SRYACT-REC
               MOVE    1                   TO  FLG-SRYACT
           END-IF
      *
           .
       920-SRYACT-NEXT-EXT.
           EXIT.
      *****************************************************************
      *    診療会計マスター読込
      *****************************************************************
       930-SRYACCT-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  SRYACCT-REC
               MOVE    ZERO                TO  FLG-SRYACCT
           ELSE
               INITIALIZE                      SRYACCT-REC
               MOVE    1                   TO  FLG-SRYACCT
           END-IF
      *
           .
       930-SRYACCT-READ-EXT.
           EXIT.
      *
      *
