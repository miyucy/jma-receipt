      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCR0035.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : レセプト
      *  コンポーネント名  : 公費分点数判定
      *  管理者            : 
      *  作成日付    作業者        記述
      *  00/12/01    MCC−小豆沢   新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *  01.00.01    MCC-藤原     01.06.29  保険者番号・
      *                                     適用開始年月日の追加
      *  01.01.02    MCC-藤原     02.02.13  保険組合せを３から１０に変更
      *  01.00.03    NACL-藤原    03.03.12  補助区分の追加
      *  01.00.04    NACL-門脇    03.03.03  剤番号桁修正対応
      *  01.00.05    NACL-門脇    04.03.17  ＜愛媛県地方公費対応＞
      *                                     合計点数情報（ぶん点・公費分離
      *                                     区分編集用）を使用するよう修正
      *  01.00.06    NACL-門脇    04.08.04  同一保険番号対応
      *  中止処理追加
      *  01.01.01    NACL-藤原    04/09/28  中止処理の追加
      *
      *  02.06.01    NACL-門脇    05/04/20  内服薬剤逓減編集対応
      *  02.06.02    NACL-門脇    05/06/02  公費分離区分編集修正
      *  02.06.03    NACL-竹田    05/05/30  realtime preview 処理追加
      *
      *  03.05.01    NACL-藤原    07/04/19  グループ診療対応
      *  04.02.01    NACL-門脇    07/05/12  第３公費以降の負担区分編集
      *  04.04.01    NACL-門脇    09/09/10  月途中に医保と生保の併用から
      *                                     生保単独になったときの対応
      *  04.04.02    NACL-門脇    10/01/04  月途中に医保と更生等の併用から
      *                                     更生等単独になったときの対応
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION           SECTION.
       INPUT-OUTPUT            SECTION.
       FILE-CONTROL.
           SELECT  RECE02-FILE     ASSIGN  RECE01PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  RECE02-KEY
                                   FILE    STATUS  IS  STS-RECE02.
           SELECT  RECE04-FILE     ASSIGN  RECE04PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  RECE04-KEY
                                   FILE    STATUS  IS  STS-RECE04.
           SELECT  RECE05-FILE     ASSIGN  RECE05PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  RECE05-KEY
                                   FILE    STATUS  IS  STS-RECE05.
      *    エラーファイル
           SELECT  RECEERR-FILE    ASSIGN  RECEERR
                                   FILE    STATUS  IS  STS-RECEERR.
      *
       DATA                    DIVISION.
       FILE                    SECTION.
      *
       FD  RECE02-FILE.
       01  RECE02-REC. 
           COPY    "CPRCF002.INC".
      *
       FD  RECE04-FILE.
       01  RECE04-REC. 
           COPY    "CPRCF004.INC".
      *
       FD  RECE05-FILE.
       01  RECE05-REC. 
           COPY    "CPRCF005.INC".
      * 
      *    エラーファイル
       FD  RECEERR-FILE.
       01  RECEERR-REC             PIC X(200). 
      *
       WORKING-STORAGE             SECTION.
grpsys*
grpsys     COPY    "COMMON-SPA".
      *
      *     レセプト明細ファイル 名称領域 
           COPY    "CPRECEDAT1.INC"
                                   REPLACING  //RECEDAT1//
                                   BY         //RECE01//.
      *
           COPY    "CPRECEDAT1.INC"
                                   REPLACING  //RECEDAT1//
                                   BY         //RECE04//.
      *
           COPY    "CPRECEDAT1.INC"
                                   REPLACING  //RECEDAT1//
                                   BY         //RECE05//.
      *
      *    エラーファイル 名称領域 
           COPY    "CPERRFL.INC"   REPLACING  //ERRFLPARA//
                                   BY         //RECEERR//.
      *
      *    フラグ領域
       01  STS-AREA.
           03  STS-RECE02                              PIC X(02).
           03  STS-RECE04                              PIC X(02).
           03  STS-RECE05                              PIC X(02).
           03  STS-RECEERR                             PIC X(02).
      *
       01  FLG-AREA.
           03  FLG-END                                 PIC 9(01).
           03  FLG-RECE04                              PIC 9(01).
           03  FLG-RECE05                              PIC 9(01).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-IN                                  PIC 9(06).
           03  CNT-IN-CANCEL                           PIC 9(06).
           03  CNT-OUT1                                PIC 9(06).
           03  CNT-OUT2                                PIC 9(06).
           03  CNT-OUT3                                PIC 9(06).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                                     PIC 9(03).
           03  IDX2                                    PIC 9(03).
           03  HC-IDX                                  PIC 9(03).
           03  KH-IDX                                  PIC 9(03).
           03  BUN-IDX                                 PIC 9(03).
           03  IDY                                     PIC 9(01).
           03  IDY2                                    PIC 9(01).
           03  IDY3                                    PIC 9(01).
      *
      *    剤番号の退避
       01  WK-ZAINUM                                   PIC 9(08).
      *    一時領域
       01  WRK-AREA.
           03  WRK-IN                                  PIC X(01).
           03  WRK-PARA.
               05  WRK-PARA-SRYYM                      PIC  X(06).
               05  WRK-PARA-TERMID                     PIC  X(16).
               05  WRK-PARA-SYSYMD                     PIC  X(08).
               05  WRK-PARA-JOBID                      PIC  9(07).
               05  WRK-PARA-SHELLID                    PIC  X(08).
           03  WRK-PARA-HOSPNUM                        PIC 9(02).
      *
           03  WRK-RECEERR             PIC X(200). 
      *
           03  WRK-HIKAKU-TABLE.
               05  WRK-HIKAKU-TBL      OCCURS   4.
                 07  WRK-HIKAKU-TOTALTEN               PIC 9(08).
                 07  WRK-HIKAKU-KOJYUN                 PIC 9(01).
      *
           03  WRK-HIKAKU-DATA                         PIC X(09).
      *
           03  WRK-3SYU-KBN.
               05  WRK-3SYU                            PIC X(03).
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *****************************************************************
      *    サブプロ用領域
      *****************************************************************
      *
      *   ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
      *
           COPY    "MCPAREA".
      *
      ****************************************************************
       LINKAGE                 SECTION.
       01  COMMAND-PARAM.
           02  FILLER      PIC X(256).
      ****************************************************************
       PROCEDURE           DIVISION
               USING
           COMMAND-PARAM.
      *
      *****************************************************************
      *    主処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC    UNTIL   FLG-END =   1 
      *
           PERFORM 300-TERM-SEC
      *
           IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
               STOP    RUN
           ELSE
               EXIT    PROGRAM
           END-IF 
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  CNT-AREA
                                       WRK-AREA
                                       SPA-AREA
      *
           MOVE    ZERO                TO  FLG-END
      *
           UNSTRING    COMMAND-PARAM   DELIMITED  BY  ","
                                       INTO    WRK-PARA
                                               WRK-PARA-HOSPNUM
                                               RECEERR
           END-UNSTRING
           MOVE    WRK-PARA-HOSPNUM    TO  SPA-HOSPNUM
      *
           MOVE    "RECE020"           TO  RECE01PARA-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECE01PARA-TERMID
grpsys     MOVE    WRK-PARA-HOSPNUM    TO  RECE01PARA-HOSPNUM
      *
           MOVE    "RECE040"           TO  RECE04PARA-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECE04PARA-TERMID
grpsys     MOVE    WRK-PARA-HOSPNUM    TO  RECE04PARA-HOSPNUM
      *
           MOVE    "RECE050"           TO  RECE05PARA-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECE05PARA-TERMID
grpsys     MOVE    WRK-PARA-HOSPNUM    TO  RECE05PARA-HOSPNUM
      *
           IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
      *        ＤＢオープン処理
               PERFORM 100-DBOPEN-SEC
      *
      *        ステップ管理開始処理
               MOVE    "STS"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    "ORCR0035"      TO  JOB-PGID
               MOVE    "公費分点数判定"
                                       TO  JOB-SHELLMSG
grpsys         PERFORM 900-CALL-ORCSJOB-SEC
      *
      *        処理中止設定処理
               PERFORM 500-CANCEL-HENSYU-SEC
           END-IF
      *
           OPEN    INPUT               RECE02-FILE
           OPEN    I-O                 RECE04-FILE
                                       RECE05-FILE
      *
           PERFORM    900-RECE02-READ-SEC
      *
          .
       100-INIT-EXT.
           EXIT.
      * 
      *****************************************************************
      *    主処理
      *****************************************************************
       200-MAIN-SEC                SECTION.
      *
           PERFORM     2001-HENSYU-SEC
      *
           PERFORM     900-RECE02-READ-SEC
      *
      *    処理中止設定処理
           IF      CNT-IN-CANCEL   =   50
               MOVE    ZERO            TO  CNT-IN-CANCEL
               PERFORM 500-CANCEL-HENSYU-SEC
           END-IF
           .
       200-MAIN-EXT.
           EXIT.
      * 
      *****************************************************************
      *    編集処理
      *****************************************************************
       2001-HENSYU-SEC                   SECTION.
      *
           IF (RECE02-HENTOTALTEN(2)       =      ZERO)    AND
              (RECE02-HENTOTALTEN(3)       =      ZERO)    AND
              (RECE02-HENTOTALTEN(4)       =      ZERO)    AND
              (RECE02-HENTOTALTEN(5)       =      ZERO)
               CONTINUE
           ELSE
             IF    RECE02-HENTOTALTEN(1)   NOT =  ZERO
               IF (RECE02-HENTOTALTEN(2)   NOT =  ZERO)              AND
                  (RECE02-HENTOTALTEN(1)   NOT =  RECE02-HENTOTALTEN(2))
                   MOVE      1          TO     BUN-IDX
                   PERFORM   20011-RECE04-HENSYU-SEC
               END-IF
               IF (RECE02-HENTOTALTEN(3)   NOT =  ZERO)              AND
                  (RECE02-HENTOTALTEN(1)   NOT =  RECE02-HENTOTALTEN(3))
                   MOVE      2          TO     BUN-IDX
                   PERFORM   20011-RECE04-HENSYU-SEC
               END-IF
               IF (RECE02-HENTOTALTEN(4)   NOT =  ZERO)              AND
                  (RECE02-HENTOTALTEN(1)   NOT =  RECE02-HENTOTALTEN(4))
                   MOVE      3          TO     BUN-IDX
                   PERFORM   20011-RECE04-HENSYU-SEC
               END-IF
               IF (RECE02-HENTOTALTEN(5)   NOT =  ZERO)              AND
                  (RECE02-HENTOTALTEN(1)   NOT =  RECE02-HENTOTALTEN(5))
                   MOVE      4          TO     BUN-IDX
                   PERFORM   20011-RECE04-HENSYU-SEC
               END-IF
             ELSE
                   PERFORM   20011-TENSU-HIKAU-SEC
             END-IF
           END-IF
      *
           .
       2001-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求点数比較
      *****************************************************************
       20011-TENSU-HIKAU-SEC                SECTION.
      *
           INITIALIZE               WRK-HIKAKU-TABLE
           MOVE     SPACE    TO     WRK-3SYU-KBN
      *
           PERFORM    VARYING    IDY    FROM   1   BY   1
                       UNTIL   ( IDY      >    4                )
                         OR    ( RECE02-KOHNUM(IDY)  =  SPACE   )
                MOVE  RECE02-HENTOTALTEN(IDY + 1)
                                    TO WRK-HIKAKU-TOTALTEN(IDY)
                MOVE  IDY           TO WRK-HIKAKU-KOJYUN(IDY)
      *
      *         生保
                IF    RECE02-KOHNUM(IDY)  =   "012"
                      MOVE   "1"    TO WRK-3SYU(1:1)
                END-IF
      *         結核
                IF    RECE02-KOHNUM(IDY)  =   "010"  OR  "011"
                      MOVE   "1"    TO WRK-3SYU(2:1)
                END-IF
                IF    RECE02-SRYYM   <   "200604"
      *           精神
                  IF  RECE02-KOHNUM(IDY)  =   "020"  OR  "021"
                      MOVE   "1"    TO WRK-3SYU(3:1)
                  END-IF
                ELSE
      *           自立支援法
                  IF  RECE02-KOHNUM(IDY)  =   "021"  OR  "015"
                                          OR  "016"  OR  "024"
                      MOVE   "1"    TO WRK-3SYU(3:1)
                  END-IF
                END-IF
           END-PERFORM
      *
           PERFORM    VARYING    IDY    FROM   1   BY   1
                       UNTIL   ( IDY      >    4                )
                         OR    ( WRK-HIKAKU-KOJYUN(IDY) = ZERO  )
      *
              COMPUTE    IDY2  =  IDY  +  1
      *
              PERFORM VARYING    IDY3   FROM  IDY2 BY   1
                        UNTIL  ( IDY3     >    4                )
                          OR   ( WRK-HIKAKU-KOJYUN(IDY3) = ZERO )
      *
                IF       WRK-HIKAKU-TBL(IDY)   <   WRK-HIKAKU-TBL(IDY3)
                   MOVE  WRK-HIKAKU-TBL(IDY)   TO  WRK-HIKAKU-DATA
                   MOVE  WRK-HIKAKU-TBL(IDY3)  TO  WRK-HIKAKU-TBL(IDY)
                   MOVE  WRK-HIKAKU-DATA       TO  WRK-HIKAKU-TBL(IDY3)
                END-IF
      *
              END-PERFORM
      *
           END-PERFORM
      *
           PERFORM    VARYING    IDY    FROM   1   BY   1
                       UNTIL   ( IDY      >    4                )
                         OR    ( WRK-HIKAKU-KOJYUN(IDY)  = ZERO )
      *
              MOVE  WRK-HIKAKU-KOJYUN(IDY)  TO   BUN-IDX
              IF   (WRK-3SYU-KBN            =   ALL "1")
              AND  (RECE02-KOHNUM(BUN-IDX)  =   "012"  )
                   CONTINUE
              ELSE
                   PERFORM   20011-RECE04-HENSYU-SEC
              END-IF
      *
           END-PERFORM
      *
           .
       20011-TENSU-HIKAU-EXT.
           EXIT.
      *
      *****************************************************************
      *    レセプト中間ファイルの作成（Ｆ００４）
      *****************************************************************
       20011-RECE04-HENSYU-SEC                SECTION.
      *
           MOVE    SPACE                TO      RECE04-REC
           INITIALIZE                           RECE04-REC
grpsys     MOVE    RECE02-HOSPNUM       TO      RECE04-HOSPNUM
           MOVE    RECE02-SRYYM         TO      RECE04-SRYYM
           MOVE    RECE02-NYUGAIKBN     TO      RECE04-NYUGAIKBN
           MOVE    RECE02-PTID          TO      RECE04-PTID
           MOVE    RECE02-RECEKA        TO      RECE04-RECEKA
           MOVE    RECE02-TEISYUTUSAKI  TO      RECE04-TEISYUTUSAKI
           MOVE    RECE02-RECESYUBETU   TO      RECE04-RECESYUBETU
           MOVE    RECE02-HKNJANUM-KEY  TO      RECE04-HKNJANUM
           MOVE    RECE02-HOJOKBN-KEY   TO      RECE04-HOJOKBN
           MOVE    RECE02-TEKSTYMD      TO      RECE04-TEKSTYMD
           START   RECE04-FILE   KEY  IS  >=    RECE04-KEY
                   INVALID
                       MOVE   1         TO      FLG-RECE04
                   NOT  INVALID
                       MOVE   ZERO      TO      FLG-RECE04
                       PERFORM    900-RECE04-NEXT-SEC
           END-START
           PERFORM   UNTIL
grpsys              (RECE02-HOSPNUM   NOT = RECE04-HOSPNUM)    OR
                    (RECE02-NYUGAIKBN NOT = RECE04-NYUGAIKBN) OR
                    (RECE02-PTID      NOT = RECE04-PTID)      OR
                    (RECE02-RECEKA    NOT = RECE04-RECEKA)    OR
                    (RECE02-TEISYUTUSAKI NOT = RECE04-TEISYUTUSAKI) OR
                    (RECE02-RECESYUBETU  NOT = RECE04-RECESYUBETU)  OR
                    (RECE02-HKNJANUM-KEY   NOT = RECE04-HKNJANUM)  OR
                    (RECE02-HOJOKBN-KEY    NOT = RECE04-HOJOKBN )  OR
                    (RECE02-TEKSTYMD  NOT = RECE04-TEKSTYMD)  OR
                    (FLG-RECE04       =    1)
                     MOVE   RECE04-ZAINUM    TO   WK-ZAINUM
                     PERFORM      200111-KOHBUNRI-SET-SEC
                     PERFORM      200112-RECE04-REWRITE-SEC
                     PERFORM      200113-RECE05-HENSYU-SEC
                     PERFORM       900-RECE04-NEXT-SEC
           END-PERFORM
      *
           .
       20011-RECE04-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費分離区分の算出
      *****************************************************************
       200111-KOHBUNRI-SET-SEC               SECTION.
      *
      *****EVALUATE  TRUE
      *        WHEN  (RECE04-HKNCOMBI   =    RECE02-HKNCOMBI(1))
      *              MOVE     1         TO   HC-IDX
      *        WHEN  (RECE04-HKNCOMBI   =    RECE02-HKNCOMBI(2))
      *              MOVE     2         TO   HC-IDX
      *        WHEN  (RECE04-HKNCOMBI   =    RECE02-HKNCOMBI(3))
      *              MOVE     3         TO   HC-IDX
      *        WHEN  OTHER
      *              MOVE     ZERO      TO   HC-IDX
      *****END-EVALUATE
           MOVE    ZERO                TO  HC-IDX
           PERFORM VARYING IDX FROM    1   BY  1
                   UNTIL   IDX >       10
                   OR      RECE02-HKNCOMBI (IDX)   =   ZERO 
               IF      RECE04-HKNCOMBI     =   RECE02-HKNCOMBI (IDX)
                     MOVE     IDX              TO  HC-IDX
                     MOVE    10                TO  IDX
               END-IF
           END-PERFORM          
      *
           MOVE       ZERO              TO   KH-IDX
           IF ((RECE02-KOHNUMHC(HC-IDX 1) = RECE02-KOHNUM(BUN-IDX)) AND
               (RECE02-KOHIDHC(HC-IDX 1)  = RECE02-KOHID(BUN-IDX) ))
           OR ((RECE02-KOHNUMHC(HC-IDX 2) = RECE02-KOHNUM(BUN-IDX)) AND
               (RECE02-KOHIDHC(HC-IDX 2)  = RECE02-KOHID(BUN-IDX) ))
           OR ((RECE02-KOHNUMHC(HC-IDX 3) = RECE02-KOHNUM(BUN-IDX)) AND
               (RECE02-KOHIDHC(HC-IDX 3)  = RECE02-KOHID(BUN-IDX) ))
           OR ((RECE02-KOHNUMHC(HC-IDX 4) = RECE02-KOHNUM(BUN-IDX)) AND
               (RECE02-KOHIDHC(HC-IDX 4)  = RECE02-KOHID(BUN-IDX) ))
                MOVE    BUN-IDX     TO        KH-IDX
           END-IF
      **     DISPLAY   "RECE04-PTID     : " RECE04-PTID
      **     DISPLAY   "RECE04-HKNCOMNI : " RECE04-HKNCOMBI
      **     DISPLAY   "KH-IDX          : " KH-IDX
      *
           IF   (RECE02-SEIHOFLG                     =  "1" )
           AND  (KH-IDX                          NOT =  ZERO)
           AND  (RECE02-HENTOTALTEN(1)           NOT =  ZERO)
           AND  (RECE02-HENTOTALTEN(KH-IDX + 1)  NOT =  ZERO)
           AND  (RECE02-HENTOTALTEN(1) <
                              RECE02-HENTOTALTEN(KH-IDX + 1))
              IF   RECE02-HKNNUMHC(HC-IDX)  NOT = SPACE
                MOVE    ZERO        TO        KH-IDX
              END-IF
           END-IF
      *
           IF    RECE02-HENTOTALTEN(1)   NOT  =  ZERO
              IF    KH-IDX      NOT =   ZERO
                     MOVE    KH-IDX      TO     RECE04-KOHBUNRI
              END-IF
           ELSE
              IF    KH-IDX      NOT =   ZERO
      ***       IF    RECE04-KOHBUNRI     =      ZERO
                     MOVE    KH-IDX      TO     RECE04-KOHBUNRI
      ***       ELSE
      ***         IF  RECE02-HENTOTALTEN(2)  >  RECE02-HENTOTALTEN(3)
      ***            MOVE    KH-IDX      TO     RECE04-KOHBUNRI
      ***         END-IF
      ***       END-IF
              END-IF
           END-IF
           .
       200111-KOHBUNRI-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    データ出力処理（Ｆ００４）
      *****************************************************************
       200112-RECE04-REWRITE-SEC                SECTION.
      *
           REWRITE   RECE04-REC
      *
           ADD     1                   TO  CNT-OUT1
      *
           .
       200112-RECE04-REWRITE-EXT.
           EXIT.
      *
      *****************************************************************
      *    レセプト中間ファイルの作成（Ｆ００５）
      *****************************************************************
       200113-RECE05-HENSYU-SEC               SECTION.
      *
           MOVE    SPACE                TO      RECE05-REC
           INITIALIZE                           RECE05-REC
grpsys     MOVE    RECE02-HOSPNUM       TO      RECE05-HOSPNUM
           MOVE    RECE02-SRYYM         TO      RECE05-SRYYM
           MOVE    RECE02-NYUGAIKBN     TO      RECE05-NYUGAIKBN
           MOVE    RECE02-PTID          TO      RECE05-PTID
           MOVE    RECE02-RECEKA        TO      RECE05-RECEKA
           MOVE    RECE02-TEISYUTUSAKI  TO      RECE05-TEISYUTUSAKI
           MOVE    RECE02-RECESYUBETU   TO      RECE05-RECESYUBETU
           MOVE    RECE02-HKNJANUM-KEY  TO      RECE05-HKNJANUM
           MOVE    RECE02-HOJOKBN-KEY   TO      RECE05-HOJOKBN
           MOVE    RECE02-TEKSTYMD      TO      RECE05-TEKSTYMD
           MOVE    WK-ZAINUM            TO      RECE05-ZAINUM
           START   RECE05-FILE   KEY  IS  >=    RECE05-KEY
                   INVALID
                       MOVE    1        TO      FLG-RECE05
                   NOT  INVALID
                       MOVE    ZERO     TO      FLG-RECE05
                       PERFORM    900-RECE05-NEXT-SEC
           END-START
           PERFORM   UNTIL
grpsys            (RECE02-HOSPNUM       NOT =  RECE05-HOSPNUM)    OR
                  (RECE02-NYUGAIKBN     NOT =  RECE05-NYUGAIKBN) OR
                  (RECE02-PTID          NOT =  RECE05-PTID)      OR
                  (RECE02-RECEKA        NOT =  RECE05-RECEKA)    OR
                  (RECE02-TEISYUTUSAKI  NOT =  RECE05-TEISYUTUSAKI) OR
                  (RECE02-RECESYUBETU   NOT =  RECE05-RECESYUBETU)  OR
                  (RECE02-HKNJANUM-KEY  NOT =  RECE05-HKNJANUM)  OR
                  (RECE02-HOJOKBN-KEY   NOT =  RECE05-HOJOKBN )  OR
                  (RECE02-TEKSTYMD      NOT =  RECE05-TEKSTYMD)  OR
                  (WK-ZAINUM            NOT =  RECE05-ZAINUM)    OR
                  (FLG-RECE05           =      1)
      *
                   IF    RECE02-HENTOTALTEN(1)   NOT  =  ZERO
                     IF    KH-IDX      NOT =   ZERO
                            MOVE   KH-IDX        TO     RECE05-KOHBUNRI
                            PERFORM      2001131-RECE05-REWRITE-SEC
                     END-IF
                   ELSE
                     IF    KH-IDX      NOT =   ZERO
      ***              IF    RECE05-KOHBUNRI     =      ZERO
                            MOVE   KH-IDX        TO     RECE05-KOHBUNRI
                            PERFORM      2001131-RECE05-REWRITE-SEC
      ***              ELSE
      ***                IF  RECE02-HENTOTALTEN(2)  >
      ***                                         RECE02-HENTOTALTEN(3)
      ***                   MOVE   KH-IDX        TO     RECE05-KOHBUNRI
      ***                   PERFORM      2001131-RECE05-REWRITE-SEC
      ***                END-IF
      ***              END-IF
                     END-IF
                   END-IF
      *
                   PERFORM       900-RECE05-NEXT-SEC
           END-PERFORM
      *
           .
       200113-RECE05-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    データ出力処理（Ｆ００５）
      *****************************************************************
       2001131-RECE05-REWRITE-SEC                SECTION.
      *
           REWRITE   RECE05-REC
      *
           ADD     1                   TO  CNT-OUT2
      *
           .
       2001131-RECE05-REWRITE-EXT.
           EXIT.
      *
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC                SECTION.
      *
           OPEN    INPUT   RECEERR-FILE
           IF      STS-RECEERR         =   ZERO
               CLOSE   RECEERR-FILE
           ELSE
               OPEN    OUTPUT              RECEERR-FILE
      *
               MOVE    WRK-RECEERR         TO  RECEERR-REC
               WRITE   RECEERR-REC
               CLOSE   RECEERR-FILE
      *         
               IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
      *            ジョブ管理終了処理
                   MOVE    "JBE"           TO  SJOBKANRI-MODE
                   INITIALIZE                  JOBKANRI-REC
                   MOVE    WRK-RECEERR     TO  JOB-YOBI
                   MOVE    "9999"          TO  JOB-ERRCD
grpsys             PERFORM 900-CALL-ORCSJOB-SEC
               END-IF
           END-IF
      *                             
           MOVE    1                   TO  FLG-END
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    処理中止処理
      *****************************************************************
       500-CANCEL-HENSYU-SEC                SECTION.
      *
      *
           IF      WRK-PARA-SHELLID    =   "RECEPTX"
               CONTINUE
           ELSE
               MOVE    "CHK"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
grpsys         PERFORM 900-CALL-ORCSJOB-SEC
               IF    ( SJOBKANRI-RETURN
                                       =   ZERO )
               AND   ( JOB-STOPFLG     =   1    )
                   MOVE    "処理が中止されました"
                                           TO  WRK-RECEERR
      *
                   OPEN    INPUT   RECEERR-FILE
                   IF      STS-RECEERR         =   ZERO
                       CLOSE   RECEERR-FILE
                   ELSE
                       OPEN    OUTPUT              RECEERR-FILE
      *    
                       MOVE    WRK-RECEERR         TO  RECEERR-REC
                       WRITE   RECEERR-REC
                       CLOSE   RECEERR-FILE
      *         
      *                ジョブ管理終了処理
                       MOVE    "CAN"           TO  SJOBKANRI-MODE
                       INITIALIZE                  JOBKANRI-REC
                       MOVE    WRK-RECEERR     TO  JOB-YOBI
                       MOVE    "8888"          TO  JOB-ERRCD
                       MOVE    2               TO  JOB-STOPFLG
grpsys                 PERFORM 900-CALL-ORCSJOB-SEC
                   END-IF
      *                             
                   MOVE    1                   TO  FLG-END
               END-IF
           END-IF
           .
       500-CANCEL-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了処理
      *****************************************************************
       300-TERM-SEC                SECTION.
      *
           IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
      *        ステップ管理終了処理
               MOVE    "STE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
grpsys         PERFORM 900-CALL-ORCSJOB-SEC
      *
               PERFORM 900-DBDISCONNECT-SEC
           END-IF
      *
           CLOSE   RECE02-FILE
                   RECE04-FILE
                   RECE05-FILE
      *
           DISPLAY "RECE02  CNT-IN  :" CNT-IN 
           DISPLAY "RECE04  CNT-MOD :" CNT-OUT1  
           DISPLAY "RECE05  CNT-MOD :" CNT-OUT2 
           DISPLAY "*** ORCR0035 END ***"
           .
       300-TERM-EXT.
           EXIT.
      *
      *****************************************************************
      *    中間ファイルＲＥＡＤ処理（Ｆ００２）
      *****************************************************************
       900-RECE02-READ-SEC            SECTION.
      *
           READ    RECE02-FILE   NEXT
               AT  END
                   MOVE    1     TO    FLG-END
               NOT  AT  END
                   ADD     1     TO    CNT-IN
                                       CNT-IN-CANCEL
      *
                   DISPLAY "RECE02  PTNUM=" RECE02-PTNUM
           END-READ
           .
       900-RECE02-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    中間ファイルＲＥＡＤ処理（Ｆ００４）
      *****************************************************************
        900-RECE04-NEXT-SEC            SECTION.
      *
           MOVE    ZERO          TO    FLG-RECE04
           READ    RECE04-FILE   NEXT
               AT  END
                   MOVE    1     TO    FLG-RECE04
           END-READ
           .
        900-RECE04-NEXT-EXT.
           EXIT.
      *
      *****************************************************************
      *    中間ファイルＲＥＡＤ処理（Ｆ００５）
      *****************************************************************
        900-RECE05-NEXT-SEC            SECTION.
      *
           MOVE    ZERO          TO    FLG-RECE05
           READ    RECE05-FILE   NEXT
               AT  END
                   MOVE    1     TO    FLG-RECE05
           END-READ
           .
       900-RECE05-NEXT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ジョブ管理ＤＢ制御処理
      *****************************************************************
       900-CALL-ORCSJOB-SEC            SECTION.
      *
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
grpsys     MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA 
                                   JOBKANRI-REC
                                   SPA-AREA
           .
       900-CALL-ORCSJOB-EXT.
           EXIT.
      *
      *****************************************************************
      *    テーブルアクセス処理
      *****************************************************************
       900-ORCDBMAIN-SEC               SECTION.
      *
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           .
      *
       900-ORCDBMAIN-EXT.
           EXIT.
      *      
      *****************************************************************
      *    ＤＢオープン処理
      *****************************************************************
       100-DBOPEN-SEC                SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBOPEN"            TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBSTART"           TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
           .
       100-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢクローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC            SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBCOMMIT"          TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBDISCONNECT"      TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
           .
       900-DBDISCONNECT-EXT.
           EXIT.
      *
