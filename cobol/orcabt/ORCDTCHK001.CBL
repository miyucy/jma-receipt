      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION                  DIVISION.
       PROGRAM-ID.                     ORCDTCHK001.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : データチェック
      *  コンポーネント名  : 対象患者抽出
      *  管理者            :
      *  作成日付    作業者        記述
      *  03/11/21    NACL−太田    新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *****************************************************************
      *
       ENVIRONMENT                     DIVISION.
       CONFIGURATION                   SECTION.
       INPUT-OUTPUT                    SECTION.
       FILE-CONTROL.
      *
      *    データチェック診療会計
           SELECT  DCACCT-FILE     ASSIGN  DTCHKACCTPARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  DCACCT-KEY
                                   FILE    STATUS  IS  STS-DCACCT.
      *
      *    データチェック中間ファイル（対象患者情報）
           SELECT  DTCHK003-FILE   ASSIGN  DTCHK003PARA
                                   FILE    STATUS  IS  STS-DTCHK003
                                   ORGANIZATION    IS  LINE SEQUENTIAL.
      *
      *    エラーファイル
           SELECT  DTCHKERR-FILE   ASSIGN  DTCHKERRPARA
                                   FILE    STATUS  IS  STS-DTCHKERR.
      *
       DATA                            DIVISION.
       FILE                            SECTION.
      *
      *    データチェック診療会計
       FD  DCACCT-FILE.
       01  DCACCT-REC.
           COPY    "CPDCACCT.INC".
      *
      *    データチェック中間ファイル（患者情報）
       FD  DTCHK003-FILE.
           COPY    "CPDTCHK001.INC"
                                   REPLACING  //DTCHK001//
                                   BY         //DTCHK003//.
      *
      *    エラーファイル
       FD  DTCHKERR-FILE.
       01  DTCHKERR-REC                PIC X(200). 
      *
       WORKING-STORAGE                 SECTION.
      *
      *    エラーファイルファイル名称領域 
           COPY    "CPCOMMONDAT6.INC"
                                   REPLACING  //DTCHK//
                                   BY         //DTCHKERR//.
           COPY    "CPCOMMONDAT6.INC"
                                   REPLACING  //DTCHK//
                                   BY         //DTCHK003//.
           COPY    "CPCOMMONDAT6.INC"
                                   REPLACING  //DTCHK//
                                   BY         //DTCHKACCT//.
      *
      *    フラグ領域
       01  STS-AREA.
           03  STS-DTCHK003                            PIC X(02).
           03  STS-DTCHKERR                            PIC X(02).
           03  STS-DCACCT                              PIC X(02).
      *
       01  FLG-AREA.
           03  FLG-END                                 PIC 9(01).
           03  FLG-ERR                                 PIC 9(01).
           03  FLG-DCACCT                              PIC 9(01).
           03  FLG-LOOP-END                            PIC 9(01).
           03  FLG-TAISYO-HKN                          PIC 9(01).
           03  FLG-STATUS-G.
               05  FLG-STATUS-VAL.
                   07  FLG-STS-NYUKHN                  PIC 9(01).
                   07  FLG-STS-GAIHAKU                 PIC 9(01).
                   07  FLG-STS-HKNCOMBI                PIC 9(01).
                   07  FLG-STS-NYUKSN                  PIC 9(01).
                   07  FLG-STS-NYUGEN                  PIC 9(01).
                   07  FLG-STS-JITDAY                  PIC 9(01).
                   07  FLG-STS-SNTDAY                  PIC 9(01).
                   07  FLG-STS-08                      PIC 9(01).
                   07  FLG-STS-09                      PIC 9(01).
                   07  FLG-STS-10                      PIC 9(01).
                   07  FLG-STS-11                      PIC 9(01).
                   07  FLG-STS-12                      PIC 9(01).
                   07  FLG-STS-13                      PIC 9(01).
                   07  FLG-STS-14                      PIC 9(01).
                   07  FLG-STS-15                      PIC 9(01).
                   07  FLG-STS-16                      PIC 9(01).
               05  FLG-STATUS-R    REDEFINES   FLG-STATUS-VAL.
                   07  FLG-STATUS                      PIC 9(01)
                                                       OCCURS  16.
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-DTCHK003                            PIC 9(06).
           03  CNT-HKNKBN-G.
               05  CNT-HKNKBN-VAL.
                   07  CNT-SYAHO                       PIC 9(03).
                   07  CNT-KOKUHO                      PIC 9(03).
                   07  CNT-ROSAI                       PIC 9(03).
                   07  CNT-JIBAI                       PIC 9(03).
                   07  CNT-JIHI                        PIC 9(03).
               05  CNT-HKNKBN-R    REDEFINES   CNT-HKNKBN-VAL.
                   07  CNT-HKNKBN                      PIC 9(03)
                                                       OCCURS   5.
           03  CNT-HKNKBN-TTL                          PIC 9(03).
      *
       01  IDX-AREA.
           03  IDX0                                    PIC 9(05). 
      *
       01  WRK-AREA.
           03  WRK-DTCHKERR                            PIC X(200).
           03  WRK-PTID                                PIC 9(10).
           03  WRK-ZAIHKNNUM                           PIC X(03).
           03  WRK-STATUS                              PIC 9(05).
           03  WRK-HKNKBN-G.
               05  WRK-HKNKBN-VAL.
                   07  WRK-SYAHOPROCKBN                PIC X(01).
                   07  WRK-KOKUHOPROCKBN               PIC X(01).
                   07  WRK-RSIPROCKBN                  PIC X(01).
                   07  WRK-JBIPROCKBN                  PIC X(01).
                   07  WRK-JIHIPROCKBN                 PIC X(01).
               05  WRK-HKNKBN-R    REDEFINES           WRK-HKNKBN-VAL.
                   07  WRK-HKNKBN                      PIC X(01)
                                                       OCCURS  5.
      *
      *    パラメタ領域
       01  WRK-PARA.
           COPY    "CPORCSPRTLNK.INC".
           03  WRK-PARA-JOBID      PIC 9(07).
           03  WRK-PARA-SHELLID    PIC X(08).
           03  WRK-PARA-HOSPID     PIC X(24).
      *
      *    データチェック一括処理パラメータ
           COPY    "CPORCBSD01PARA.INC".
      *
       01  CONST-AREA.
           03  CONST-HKNKBN-G.
               05  CONST-HKNKBN-VAL.
                   07  CONST-SYAHO             PIC X(01)   VALUE   "1".
                   07  CONST-KOKUHO            PIC X(01)   VALUE   "2".
                   07  CONST-ROSAI             PIC X(01)   VALUE   "3".
                   07  CONST-JIBAI             PIC X(01)   VALUE   "4".
                   07  CONST-JIHI              PIC X(01)   VALUE   "5".
               05  CONST-HKNKBN-R      REDEFINES   CONST-HKNKBN-VAL.
                   07  CONST-HKNKBN            PIC X(01)   OCCURS  5.
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *    データチェック機能情報２
           COPY    "CPSK1101.INC".
      *
      *****************************************************************
      *    サブプロ用領域
      *****************************************************************
      *
      *    ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    マシン日付取得サブ
           COPY    "CPORCSMCNDATE.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
           COPY    "MCPAREA".
           COPY    "ORCA-DBPATH".
      *
      ****************************************************************
       LINKAGE                         SECTION.
       01  DTCHK-RC        PIC S9(09).
       01  COMMAND-PARAM.
           02  FILLER      PIC X(2000).
      ****************************************************************
       PROCEDURE                       DIVISION
               USING
           DTCHK-RC
           COMMAND-PARAM.
      *
      *****************************************************************
      *    主処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC
      *
           PERFORM 300-END-SEC
      *
           MOVE    FLG-ERR             TO  DTCHK-RC
      *
           EXIT    PROGRAM
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  STS-AREA
                                       IDX-AREA
                                       CNT-AREA
                                       FLG-AREA
                                       WRK-AREA
                                       WRK-PARA
                                       ORCBSD01PARA
      *
           UNSTRING   COMMAND-PARAM    DELIMITED  BY  ","
                                       INTO    LNK-PRTKANRI-RENNUM
                                               LNK-PRTKANRI-TBL-KEY
                                               LNK-PRTKANRI-TBL-GROUP
                                               LNK-PRTKANRI-SHORI-RENNUM
                                               LNK-PRTKANRI-SRYYM
                                               LNK-PRTKANRI-SKYYMD
                                               LNK-PRTKANRI-SHELLID
                                               LNK-PRTKANRI-PRIORITY
                                               LNK-PRTKANRI-TERMID
                                               LNK-PRTKANRI-OPID
                                               LNK-PRTKANRI-PRTNM
                                               WRK-PARA-JOBID
                                               WRK-PARA-SHELLID
                                               WRK-PARA-HOSPID
                                               DTCHKERRPARA
                                               ORCBSD01-IKTKBT
                                               ORCBSD01-SRYYM
                                               ORCBSD01-NYUGAIKBN
                                               ORCBSD01-CHKKBN-G
                                               ORCBSD01-SYAHOPROCKBN
                                               ORCBSD01-KOKUHOPROCKBN
                                               ORCBSD01-RSIPROCKBN
                                               ORCBSD01-JBIPROCKBN
                                               ORCBSD01-JIHIPROCKBN
                                               ORCBSD01-INGAIPROCKBN
                                               ORCBSD01-PRTSRTKBN
                                               ORCBSD01-PRTPTLSTKBN
                                               ORCBSD01-SRYYMDKBN
                                               ORCBSD01-STSRYYMD
                                               ORCBSD01-EDSRYYMD
      *
           PERFORM 900-DBSTART-SEC
      *
      *    ステップ管理開始処理
           MOVE    "STS"               TO  SJOBKANRI-MODE
           INITIALIZE                      JOBKANRI-REC
           MOVE    WRK-PARA-JOBID      TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID    TO  JOB-SHELLID
           MOVE    "ORCDTCHK001"          TO  JOB-PGID
           MOVE    "対象患者抽出"      TO  JOB-SHELLMSG
           CALL    "ORCSJOB"           USING
                                       ORCSJOBKANRIAREA
                                       JOBKANRI-REC
      *
           MOVE    ZERO                TO  FLG-END
                                           FLG-ERR
      *
      *
      *    データチェック中間ファイル名
           MOVE    "DCACCT00"          TO  DTCHKACCT-FILE-ID
           MOVE    LNK-PRTKANRI-TERMID TO  DTCHKACCT-TERMID
      *
           MOVE    "DTCHK003"          TO  DTCHK003-FILE-ID
           MOVE    LNK-PRTKANRI-TERMID TO  DTCHK003-TERMID
      *
      *    データチェック２機能情報
           PERFORM 1001-SYS1101-GET-SEC
      *
      *    初期値設定処理
           PERFORM 1001-INIT-VAL-SET-SEC
      *
           .
       100-INIT-EXT.
           EXIT.
      *****************************************************************
      *    データチェック２機能情報
      *****************************************************************
       1001-SYS1101-GET-SEC            SECTION.
      *
           INITIALIZE                      SYS-1101-REC
           MOVE    "1101"              TO  SYS-1101-KANRICD
           MOVE    "*%"                TO  SYS-1101-KBNCD
           MOVE    ORCBSD01-SRYYM      TO  SYS-1101-STYUKYMD
           MOVE    "01"                TO  SYS-1101-STYUKYMD (7:2)
           MOVE    SYS-1101-STYUKYMD   TO  SYS-1101-EDYUKYMD
           MOVE    SYS-1101-REC        TO  MCPDATA-REC
           MOVE    PATH-TBL-SYSKANRI-KEY   TO  MCP-PATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  SYS-1101-REC
           ELSE
               INITIALIZE                  SYS-1101-REC
           END-IF
      *
           MOVE    PATH-TBL-SYSKANRI-KEY   TO  MCP-PATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       1001-SYS1101-GET-EXT.
           EXIT.
      *****************************************************************
      *    初期値設定処理
      *****************************************************************
       1001-INIT-VAL-SET-SEC           SECTION.
      *
      *
           .
       1001-INIT-VAL-SET-EXT.
           EXIT.
      *****************************************************************
      *    主処理
      *****************************************************************
       200-MAIN-SEC                    SECTION.
      *
      *    対象患者情報編集処理
           PERFORM 2001-DTCHK003-HEN-SEC
      *
           MOVE    1                   TO  FLG-END
           .
       200-MAIN-EXT.
           EXIT.
      *****************************************************************
      *    対象患者編集処理
      *****************************************************************
       2001-DTCHK003-HEN-SEC           SECTION.
      *
           OPEN    INPUT                   DCACCT-FILE
           OPEN    OUTPUT                  DTCHK003-FILE
      *
           MOVE    ZERO                TO  CNT-DTCHK003
           MOVE    ZERO                TO  FLG-DCACCT
      *
      *    データチェック診療会計読込処理
           PERFORM 900-DCACCT-READ-SEC
      *
           PERFORM UNTIL ( FLG-DCACCT      NOT =   ZERO )
      *
               MOVE    DCACCT-PTID     TO  WRK-PTID
               INITIALIZE                  WRK-HKNKBN-G
                                           CNT-HKNKBN-G
      *
               PERFORM UNTIL ( WRK-PTID    NOT =   DCACCT-PTID )
                        OR   ( FLG-DCACCT  NOT =   ZERO )
      *
                   INITIALIZE                  FLG-STATUS-G
                   MOVE    DCACCT-STATUS   TO  WRK-STATUS
      *
                   PERFORM VARYING IDX0    FROM    1   BY  1
                           UNTIL ( IDX0    >   16 )
                       DIVIDE  WRK-STATUS  BY  2   GIVING  WRK-STATUS
                               REMAINDER   FLG-STATUS (IDX0)
                   END-PERFORM
      *
                   IF    ( FLG-STS-HKNCOMBI    =   ZERO )
                       MOVE    DCACCT-HKNNUM   TO  WRK-ZAIHKNNUM
                       PERFORM 220-TAISYO-HKN-CHK-SEC
                   END-IF
      *
                   PERFORM 900-DCACCT-READ-SEC
      *
               END-PERFORM
      *
               MOVE    ZERO                TO  CNT-HKNKBN-TTL
      *
               PERFORM VARYING IDX0    FROM    1   BY  1
                       UNTIL ( IDX0    >   5 )
      *
                   IF    ( CNT-HKNKBN (IDX0)   =   1 )
                       COMPUTE CNT-HKNKBN-TTL
                           =   CNT-HKNKBN-TTL
                           +   1
                   END-IF
      *
               END-PERFORM
      *
               PERFORM VARYING IDX0    FROM    1   BY  1
                       UNTIL ( IDX0    >   5 )
      *
                   IF    ( WRK-HKNKBN (IDX0)   =   "1" )
                       INITIALIZE          DTCHK003-REC
                       MOVE    CONST-HKNKBN (IDX0)
                                       TO  DTCHK003-HKNKBN
                       PERFORM 210-DTCHK003-WRITE-SEC
                   END-IF
      *
               END-PERFORM
      *
           END-PERFORM
      *
           CLOSE                           DCACCT-FILE
                                           DTCHK003-FILE
      *
           .
       2001-DTCHK003-HEN-EXT.
           EXIT.
      *****************************************************************
      *    患者情報ファイル出力処理
      *****************************************************************
       210-DTCHK003-WRITE-SEC        SECTION.
      *
           MOVE    WRK-PARA-HOSPID TO  DTCHK003-HOSPID
           MOVE    WRK-PTID        TO  DTCHK003-PTID
      *
           IF    ( CNT-HKNKBN-TTL  >   1 )
               MOVE    "1"         TO  DTCHK003-MULHKNKBN
           END-IF
      *
           WRITE                       DTCHK003-REC
      *
           COMPUTE CNT-DTCHK003    =   CNT-DTCHK003    +   1
      *
           .
       210-DTCHK003-WRITE-CHK-EXT.
           EXIT.
      *****************************************************************
      *    対象保険チェック処理
      *****************************************************************
       220-TAISYO-HKN-CHK-SEC          SECTION.
      *
           EVALUATE    TRUE
           WHEN      ( WRK-ZAIHKNNUM   =   "060" )
           WHEN      ( WRK-ZAIHKNNUM   =   "067" )
           WHEN      ( WRK-ZAIHKNNUM   =   "068" )
           WHEN      ( WRK-ZAIHKNNUM   =   "069" )
      *
               MOVE    1               TO  CNT-KOKUHO
      *
               IF    ( ORCBSD01-KOKUHOPROCKBN  =   "1" )
                   MOVE    "1"         TO  WRK-KOKUHOPROCKBN
               END-IF
           WHEN      ( WRK-ZAIHKNNUM   =   "971" )
      *
               MOVE    1               TO  CNT-ROSAI
      *
               IF    ( ORCBSD01-RSIPROCKBN  =   "1" )
                   MOVE    "1"         TO  WRK-RSIPROCKBN
               END-IF
           WHEN      ( WRK-ZAIHKNNUM   =   "973" )
      *
               MOVE    1               TO  CNT-JIBAI
      *
               IF    ( ORCBSD01-JBIPROCKBN  =   "1" )
                   MOVE    "1"         TO  WRK-JBIPROCKBN
               END-IF
           WHEN      ( WRK-ZAIHKNNUM (1:2) =   "98" )
      *
               MOVE    1               TO  CNT-JIHI
      *
               IF    ( ORCBSD01-JIHIPROCKBN    =   "1" )
                   MOVE    "1"         TO  WRK-JIHIPROCKBN
               END-IF
           WHEN      OTHER
      *
               MOVE    1               TO  CNT-SYAHO
      *
               IF    ( ORCBSD01-SYAHOPROCKBN   =   "1" )
                   MOVE    "1"         TO  WRK-SYAHOPROCKBN
               END-IF
           END-EVALUATE
      *
           .
       220-TAISYO-HKN-CHK-EXT.
           EXIT.
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC              SECTION.
      *
           OPEN    INPUT   DTCHKERR-FILE
           IF    ( STS-DTCHKERR        =   ZERO )
               CLOSE   DTCHKERR-FILE
           ELSE
               OPEN    OUTPUT              DTCHKERR-FILE
      *
               MOVE    WRK-DTCHKERR    TO  DTCHKERR-REC
               WRITE   DTCHKERR-REC
               CLOSE   DTCHKERR-FILE
      *
      *        ジョブ管理開始処理
               MOVE    "JBE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               MOVE    WRK-DTCHKERR    TO  JOB-YOBI
               MOVE    "9999"          TO  JOB-ERRCD
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA
                                       JOBKANRI-REC
           END-IF
      *
           MOVE    1                   TO  FLG-ERR
           MOVE    1                   TO  FLG-END
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了処理
      *****************************************************************
       300-END-SEC                     SECTION.
      *
      *    ステップ管理終了処理
           MOVE    "STE"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA  
                                   JOBKANRI-REC
      *
           PERFORM 900-DBCOMMIT-SEC
      *     
           DISPLAY "*** ORCDTCHK001 END ***"
           DISPLAY "DTCHK003 CNT [" CNT-DTCHK003 "]"
           DISPLAY "***********************"
           .
       300-END-EXT.
           EXIT.
      *
      *****************************************************************
      *    マシン日付取得サブ
      *****************************************************************
       800-MCNDATE-SEC         SECTION.
      *
           CALL    "ORCSMCNDATE"   USING   ORCSMCNDATEAREA
      *
           .
      *
       800-MCNDATE-EXT.
           EXIT.
      *****************************************************************
      *    データチェック診療会計読込処理
      *****************************************************************
       900-DCACCT-READ-SEC         SECTION.
      *
           MOVE    ZERO            TO  FLG-LOOP-END
      *
           PERFORM UNTIL ( FLG-DCACCT      NOT =   ZERO )
                    OR   ( FLG-LOOP-END    NOT =   ZERO )
      *
               READ    DCACCT-FILE         NEXT
               AT  END
                   MOVE    1       TO  FLG-DCACCT
               NOT AT  END
      *
                   MOVE    1   TO  FLG-LOOP-END
      *
                   IF    ( ORCBSD01-NYUGAIKBN
                                       NOT =   DCACCT-NYUGAIKBN )
                       MOVE    ZERO    TO  FLG-LOOP-END
                   END-IF
      *
               END-READ
           END-PERFORM
      *
           .
       900-DCACCT-READ-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ検索処理
      *****************************************************************
       910-DBSELECT-SEC                SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "MCPSUB"            USING
                                       MCPAREA
                                       MCPDATA-REC
           IF    ( MCP-RC          =   ZERO )
               PERFORM 910-DBFETCH-SEC
           END-IF
      *
           .
      *
       910-DB-SELECT-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ読み込み処理
      *****************************************************************
       910-DBFETCH-SEC                 SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "MCPSUB"            USING
                                       MCPAREA
                                       MCPDATA-REC
      *
           .
      *
       910-DBFETCH-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢクローズ処理
      *****************************************************************
       910-DBCLOSE-SEC                 SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           CALL    "MCPSUB"            USING
                                       MCPAREA
                                       MCPDATA-REC
      *
           .
      *
       910-DBCLOSE-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢＳＴＡＲＴ処理
      *****************************************************************
       900-DBSTART-SEC                 SECTION.
      *
           MOVE    LOW-VALUE            TO  MCP-PATH
           MOVE    "DBSTART"            TO  MCP-FUNC
           CALL    "MCPSUB"             USING
                                        MCPAREA
                                        MCPDATA-REC.
           .
       900-DBSTART-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢＣＯＭＭＩＴ処理
      *****************************************************************
       900-DBCOMMIT-SEC                 SECTION.
      *
           MOVE    LOW-VALUE            TO  MCP-PATH
           MOVE    "DBCOMMIT"           TO  MCP-FUNC
           CALL    "MCPSUB"             USING
                                        MCPAREA
                                        MCPDATA-REC.
      *
           .
       900-DBCOMMIT-EXT.
           EXIT.
