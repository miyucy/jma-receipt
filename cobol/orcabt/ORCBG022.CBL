      *******************************************************************
      * Project code name "ORCA"
      * Æü°åÉ¸½à¥ì¥»¥×¥È¥½¥Õ¥È¡ÊJMA standard receipt software¡Ë
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCBG022.
      *****************************************************************
      *  ¥·¥¹¥Æ¥àÌ¾        :   £Ï£Ò£Ã£Á
      *  ¥µ¥Ö¥·¥¹¥Æ¥àÌ¾    :   ·î¼¡Ä¢É¼
      *  ¥³¥ó¥Ý¡¼¥Í¥ó¥ÈÌ¾  :   ¸þÀº¿ÀÌôÅêÍ¿´µ¼Ô¥ê¥¹¥È
      *  ´ÉÍý¼Ô            : 
      *  ºîÀ®ÆüÉÕ    ºî¶È¼Ô        µ­½Ò
      *  14/06/XX    NACL-Â¿¡¹Ç¼   ¿·µ¬ºîÀ®
      *****************************************************************
      *  ¥×¥í¥°¥é¥à½¤ÀµÍúÎò
      * Maj/Min/Rev  ½¤Àµ¼Ô        ÆüÉÕ      ÆâÍÆ
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *    ¥¨¥é¡¼¥Õ¥¡¥¤¥ë
           SELECT  RECEERR-FILE    ASSIGN  RECEERR
                                   FILE    STATUS  IS  STS-RECEERR.
      *
       DATA                    DIVISION.
       FILE                        SECTION.
      *
      *    ¥¨¥é¡¼¥Õ¥¡¥¤¥ë
       FD  RECEERR-FILE.
       01  RECEERR-REC                 PIC X(200).
      *
       WORKING-STORAGE             SECTION.
      *
      *
      *    °ìÍ÷¡Ê½Ä¡Ë
           COPY    "HCMSL80.INC".
      *    °ìÍ÷¡Ê²£¡Ë
      **   COPY    "HCMSL55.INC".
      *
      *
      *    £Ó£Ô£ÓÎÎ°è
       01  STS-AREA.
           03  STS-RECEERR             PIC X(02).
      *
      *    ¥Õ¥é¥°ÎÎ°è
       01  FLG-AREA.
           03  FLG-END                 PIC 9(01).
           03  FLG-ERR                 PIC 9(01).
           03  FLG-PTINF               PIC 9(01).
           03  FLG-JYURRK              PIC 9(01).
           03  FLG-SRYACT              PIC 9(01).
           03  FLG-SRYACCT             PIC 9(01).
           03  FLG-TENSU               PIC 9(01).
           03  FLG-SYSKANRI            PIC 9(01).
      *
           03  FLG-ARI                 PIC 9(01).
      *    ÂÐ¾ÝÍ­
           03  FLG-CNT-ARI             PIC 9(01).
           03  FLG-CNT-TAZAI           PIC 9(01).
           03  FLG-CNT-K1              PIC 9(01).
           03  FLG-CNT-K2              PIC 9(01).
           03  FLG-CNT-K3              PIC 9(01).
           03  FLG-CNT-K4              PIC 9(01).
      *
      *    Åº»úÎÎ°è
       01  IDX-AREA.
           03  IDX                     PIC 9(05).
           03  IDY                     PIC 9(05).
           03  IDZ                     PIC 9(05).
           03  IDS                     PIC 9(05).
           03  IDX-TBL                 PIC 9(05).
           03  IDS2                    PIC 9(05).
           03  IDX-DAY                 PIC 9(02).
           03  IDX-AC                  PIC 9(02).
      *
           03  IDW                     PIC 9(05).
      *
           03  SRT-IDX                 PIC 9(03).
      *
      *    ¥Ñ¥é¥á¥¿ÎÎ°è
       01  WRK-PARA.
           COPY    "CPORCSPRTLNK.INC".
           03  WRK-PARA-JOBID          PIC 9(07).
           03  WRK-PARA-SHELLID        PIC X(08).
           03  WRK-PARA-HOSPNUM        PIC 9(02).
           03  RECEERR                 PIC X(100).
           03  WRK-PARA-SRYYM          PIC X(06).
           03  WRK-PARA-SYORIKBN       PIC X(01).
      *
      *    ¥«¥¦¥ó¥ÈÎÎ°è
       01  CNT-AREA.
           03  CNT-NUM                 PIC 9(05).
           03  CNT-PAGE                PIC 9(05).
           03  CNT-LINE                PIC 9(02).
           03  CNT-CSV                 PIC 9(05).
      *
           03  CNT-OUT-1               PIC 9(05).
           03  CNT-OUT-2               PIC 9(05).
           03  CNT-OUT-3-1             PIC 9(05).
           03  CNT-OUT-3-2             PIC 9(05).
           03  CNT-OUT-3-3             PIC 9(05).
           03  CNT-OUT-3-4             PIC 9(05).
      *
      *    ¥·¥¹¥Æ¥àÎÎ°è
       01  SYS-AREA.
           03  SYS-YMD.
               05  SYS-YY              PIC 9(02).
               05  SYS-MM              PIC 9(02).
               05  SYS-DD              PIC 9(02).
           03  SYS-TIME.
               05  SYS-THH             PIC 9(02).
               05  SYS-TMM             PIC 9(02).
               05  SYS-TSS             PIC 9(02).
      *
      *    °ì»þÎÎ°è
       01  WRK-AREA.
      *
      *    ¥¨¥é¡¼¾ðÊó
           03  WRK-RECEERR             PIC X(200).
      *
           03  WRK-SYMD.
               05  WRK-SYY             PIC 9(04).
               05  WRK-SMM             PIC 9(02).
               05  WRK-SDD             PIC 9(02).
           03  WRK-SYSYMD.
               05  WRK-SYSYY           PIC 9(04).
               05  WRK-SYSMM           PIC 9(02).
               05  WRK-SYSDD           PIC 9(02).
           03  WRK-SYSYMDG             PIC X(22).
      *    ½èÍýÇ¯·î
           03  WRK-SRY-YYMM.
               05  WRK-SRY-YY          PIC 9(04).
               05  WRK-SRY-MM          PIC 9(02).
      *
           03  WRK-KEY-AREA.
               05  WRK-KEY-PTID            PIC 9(10).
               05  WRK-KEY-SRYYMD          PIC X(08).
               05  WRK-KEY-DENPNUM         PIC 9(07).
           03  WRK-ZAIKAISU            PIC 9(03).
      *
           03  WRK-MAE-PTID            PIC 9(10).
           03  SORT-KEY-GROUP          PIC X(07).
           03  SORT-KEY-FFECT          PIC X(01).
      *
      *
       01  TBL-SRY-AREA.
           03  TBL-PTID                PIC 9(10).
           03  TBL-SRYYMD              PIC X(08).
           03  TBL-SRYKA               PIC X(02).
           03  TBL-SRYKAMEI            PIC X(10).
           03  TBL-HKNCOMBI            PIC X(04).
           03  TBL-JYURRK-RENNUM       PIC 9(01).
           03  TBL-MEISAI-G.
               05  TBL-SRY-OCCS            OCCURS   50.
                   07  TBL-SRY-SRYSYUKBN       PIC X(03).
                   07  TBL-ZAIKAISU            PIC 9(03).
                   07  TBL-SRY-SRYCD           PIC X(09).
                   07  TBL-YAKKAKJNCD          PIC X(12).
                   07  TBL-NAME                PIC X(80).
      *            ÌôºÞ¤Î¸úÇ½¶èÊ¬
                   07  TBL-FFECT-KBN           PIC 9(01).
      *            ¼ïÎà¶èÊ¬
                   07  TBL-GROUP-KBN           PIC X(07).
                   07  TBL-RINJI-KBN           PIC X(01).
      *
      * ÌôºÞSORT
       01  SORT-TBL-AREA.
           03  SORT-SRY-OCCS            OCCURS   50.
               05  SORT-SRY-SRYSYUKBN       PIC X(03).
               05  SORT-ZAIKAISU            PIC 9(03).
               05  SORT-SRY-SRYCD           PIC X(09).
               05  SORT-YAKKAKJNCD          PIC X(12).
               05  SORT-NAME                PIC X(80).
               05  SORT-FFECT-KBN           PIC 9(01).
               05  SORT-GROUP-KBN           PIC X(07).
               05  SORT-RINJI-KBN           PIC X(01).
      *
       01  TBL-SRYKA-AREA.
           03  TBL-SRYKA-OCC           OCCURS   40.
               05  TBL-SRYKA-CD            PIC X(02).
               05  TBL-SRYKA-MEI           PIC X(10).
      *
      *
      * ³ÎÇ§¥ê¥¹¥È
       01  LST-PRT-AREA.
           03  LST-PRT-HEAD1.
               05  FILLER              PIC X(02)   VALUE   SPACE.
               05  FILLER              PIC X(40)   VALUE
                   "¡ô¡ô¡¡¸þÀº¿ÀÌôÅêÍ¿´µ¼Ô°ìÍ÷¡¡¡ô¡ô".
               05  FILLER              PIC X(16)   VALUE   SPACE.
               05  PRT-SRYYM           PIC X(16).
               05  FILLER              PIC X(04)   VALUE
                   "Ê¬".
               05  FILLER              PIC X(09)   VALUE   SPACE.
               05  PRT-SYSYMD          PIC X(10)   VALUE   SPACE.
               05  PRT-TIME            PIC X(07)   VALUE   SPACE.
               05  PRT-PAGE            PIC ZZZ9.
               05  FILLER              PIC X(02)   VALUE   "ÊÇ".
           03  LST-PRT-HEAD2.
               05  FILLER              PIC X(20)   VALUE
                   "´µ¼ÔÈÖ¹æ".
               05  FILLER              PIC X(01)   VALUE   SPACE.
               05  FILLER              PIC X(50)   VALUE
                   "»áÌ¾".
           03  LST-PRT-HEAD3.
               05  FILLER              PIC X(15)   VALUE   SPACE.
               05  FILLER              PIC X(12)   VALUE
                   "¿ÇÎÅÆü".
               05  FILLER              PIC X(12)   VALUE
                   "²Ê".
               05  FILLER              PIC X(10)   VALUE
                   "ÊÝ¸±ÁÈ¹ç¤»".
      *
           03  LST-PRT-BODY-01.
               05  PRT-PTNUM           PIC X(20).
               05  PRT-FIL1            PIC X(01).
               05  PRT-NAME            PIC X(50).
               05  PRT-FIL2            PIC X(01).
      *
           03  LST-PRT-BODY-02.
               05  PRT-FIL20           PIC X(15).
               05  PRT-SRYYMD          PIC X(09).
               05  PRT-FIL21           PIC X(02).
               05  PRT-SRYKA           PIC X(10).
               05  PRT-FIL22           PIC X(01).
               05  PRT-HKNCOMBI        PIC X(04).
               05  PRT-FIL23           PIC X(01).
               05  PRT-SRYKBN          PIC X(06).
               05  PRT-FIL24           PIC X(01).
               05  PRT-KBN             PIC X(06).
               05  PRT-FIL25           PIC X(01).
               05  PRT-YAKKCD          PIC X(09).
               05  PRT-FIL26           PIC X(01).
               05  PRT-TNS-NAME        PIC X(44).
      *
           03  LST-PRT-BODY-03.
               05  PRT-FIL31           PIC X(10)   VALUE   SPACE.
               05  PRT-ERRMSG          PIC X(12).
               05  PRT-FIL32           PIC X(08)   VALUE   SPACE.
               05  FILLER              PIC X(10)   VALUE   "¼ïÎà¿ô".
               05  FILLER              PIC X(10)   VALUE   "¹³ÉÔ°ÂÌô¡§".
               05  PRT-CNT1            PIC Z9.
               05  FILLER              PIC X(04)   VALUE   SPACE.
               05  FILLER              PIC X(08)   VALUE   "¿çÌ²Ìô¡§".
               05  PRT-CNT2            PIC Z9.
               05  FILLER              PIC X(04)   VALUE   SPACE.
               05  FILLER              PIC X(10)   VALUE   "¹³¤¦¤ÄÌô¡§".
               05  PRT-CNT3            PIC Z9.
               05  FILLER              PIC X(04)   VALUE   SPACE.
               05  FILLER              PIC X(12)   VALUE
                                                   "¹³Àº¿ÀÉÂÌô¡§".
               05  PRT-CNT4            PIC Z9.
               05  FILLER              PIC X(02)   VALUE   SPACE.
      *
           03  LST-PRT-FOOT-01.
               05  FILLER              PIC X(02)   VALUE   SPACE.
               05  FILLER              PIC X(12)   VALUE
                                                   "ÂÐ¾Ý´µ¼Ô¿ô¡§".
               05  PRT-F-CNT1          PIC ZZZ9.
               05  FILLER              PIC X(20)   VALUE
                                           "Ì¾¡¤Â¿ºÞÅêÍ¿´µ¼Ô¿ô¡§".
               05  PRT-F-CNT2          PIC ZZZ9.
               05  FILLER              PIC X(02)   VALUE   "Ì¾".
               05  FILLER              PIC X(12)   VALUE
                                           "¡Ê¹³ÉÔ°ÂÌô¡§".
               05  PRT-F-CNT31         PIC ZZZ9.
               05  FILLER              PIC X(02)   VALUE   ",".
               05  FILLER              PIC X(08)   VALUE   "¿çÌ²Ìô¡§".
               05  PRT-F-CNT32         PIC ZZZ9.
               05  FILLER              PIC X(02)   VALUE   ",".
               05  FILLER              PIC X(10)   VALUE   "¹³¤¦¤ÄÌô¡§".
               05  PRT-F-CNT33         PIC ZZZ9.
               05  FILLER              PIC X(02)   VALUE   ",".
               05  FILLER              PIC X(12)   VALUE
                                                   "¹³Àº¿ÀÉÂÌô¡§".
               05  PRT-F-CNT34         PIC ZZZ9.
               05  FILLER              PIC X(01)   VALUE   ")".
      *
       01  WRK-HEN-AREA.
           03  WRK-HENYMDG         PIC X(09).
           03  WRK-HENYMD.
               05  WRK-HGO         PIC X(01).
               05  WRK-HYY         PIC 9(02).
               05  FILLER          PIC X(01)   VALUE   ".".
               05  WRK-HMM         PIC 9(02).
               05  FILLER          PIC X(01)   VALUE   ".".
               05  WRK-HDD         PIC 9(02).
           03  WRK-HENTIME.
               05  FILLER          PIC X(01)   VALUE   "(".
               05  WRK-THH         PIC 9(02).
               05  FILLER          PIC X(01)   VALUE   ":".
               05  WRK-TMM         PIC 9(02).
               05  FILLER          PIC X(01)   VALUE   ")".
      *    £Ã£Ó£ÖÍÑ
           03  WRK-CSVHENYMD.
               05  WRK-CYY         PIC 9(04).
               05  FILLER          PIC X(01)   VALUE   "/".
               05  WRK-CMM         PIC 9(02).
               05  FILLER          PIC X(01)   VALUE   "/".
               05  WRK-CDD         PIC 9(02).
      *
      *
       01  MAX-LINE           PIC 9(03)   VALUE   80.
      *
      *
      *    £Ã£Ó£Ö¥Ç¡¼¥¿ÊÔ½¸ÍÑ
       01  WRK-CSV-AREA.
           03  WRK-IN-DATA             PIC X(2000).
           03  WRK-OUT-REC             PIC X(2000).
           03  WRK-CSV-REC             PIC X(2000).
           03  WRK-REC-LENGTH          PIC 9(05).
           03  WRK-IN-MAX              PIC 9(04).
           03  WRK-END                 PIC 9(01).
      *
           03  WRK-TOUKEIERR           PIC X(200).
      *
      *    £Ã£Ó£Ö½ÐÎÏÀè
       01  WRK-TO-DATA-AREA.
           03  WRK-TO-DATA.
               05  WRK-TO-DATA-HOSPNUM PIC 9(02).
               05  FILLER              PIC X(20)   VALUE
                                       "kouseisinyaku_touyo_".
               05  WRK-TO-DATA-SRYYM   PIC X(06).
               05  FILLER              PIC X(04)   VALUE   ".csv".
      *
       01  WRK-CONS-AREA.
      *    ¥³¥ó¥Þ
           03  WRK-KUGIRI          PIC X(01)   VALUE   ",".
      *    ²þ¹Ô¥³¡¼¥É
           03  WRK-KAIGYO          PIC X(01)   VALUE   X"0D".
      *
       01  CSV-HEAD-01.
           03  FILLER              PIC X(08)   VALUE   "´µ¼ÔÈÖ¹æ".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(08)   VALUE   "´µ¼Ô»áÌ¾".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(06)   VALUE   "¿ÇÎÅÆü".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(08)   VALUE   "¿ÇÎÅ²ÊÌ¾".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(14)   VALUE   "ÊÝ¸±ÁÈ¹ç¤»ÈÖ¹æ".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(12)   VALUE   "¼õ¿ÇÍúÎòÏ¢ÈÖ".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(12)   VALUE   "¸þÀº¿ÀÌô¼ïÎà".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(14)   VALUE   "°ìÈÌÌ¾¥°¥ë¡¼¥×".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(08)   VALUE   "Î×»þÅêÍ¿".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(12)   VALUE   "°åÌôÉÊ¥³¡¼¥É".
           03  FILLER              PIC X(01)   VALUE   ",".
           03  FILLER              PIC X(06)   VALUE   "ÌôºÞÌ¾".
      *
      *****************************************************************
      *    ¥Õ¥¡¥¤¥ë¥ì¥¤¥¢¥¦¥È
      *****************************************************************
      *
      *    ¥·¥¹¥Æ¥à´ÉÍý¥Þ¥¹¥¿
           COPY    "CPSYSKANRI.INC".
      *    ¿ÇÎÅ²ÊÌÜ¾ðÊó
           COPY    "CPSK1005.INC".
      *
      *    ´µ¼Ô
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *    ¿ÇÎÅ¹Ô°Ù¥Þ¥¹¥¿¡Ý
       01  SRYACT-REC.
           COPY    "CPSRYACT.INC".
      *
      *    ¼õ¿ÇÍúÎò
       01  JYURRK-REC.
           COPY    "CPJYURRK.INC".
      *
      *    ¿ÇÎÅ²ñ·×¥Þ¥¹¥¿¡Ý
       01  SRYACCT-REC.
           COPY    "CPSRYACCT.INC".
      *
      *    ÅÀ¿ô¥Þ¥¹¥¿¡Ý
      *01  TENSU-REC.
           COPY    "CPTENSU.INC".
      *
      *
      *    ¥¸¥ç¥Ö´ÉÍý¥Þ¥¹¥¿
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *    °õºþ´ÉÍý
       01  PRTKANRI-REC.
           COPY    "CPPRTKANRI.INC".
      *
      *    °õºþÍÑ¥Ç¡¼¥¿
       01  PRTDATA-REC.
           COPY    "CPPRTDATA.INC".
      *
      *****************************************************************
      *    ¥µ¥Ö¥×¥íÍÑ¡¡ÎÎ°è
      *****************************************************************
      *
      *    ¸þÀº¿ÀÌôÂ¿ºÞÅêÍ¿¥Á¥§¥Ã¥¯¥Ñ¥é¥á¥¿
           COPY    "CPORCSPSYCHO.INC".
      *
      *    ´µ¼ÔÈÖ¹æÊÑ´¹¥µ¥Ö
           COPY    "CPORCSPTID.INC".
      *
      *    ¥¸¥ç¥Ö´ÉÍý£Ä£ÂÀ©¸æ¥µ¥Ö
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    °õºþ£Ä£Â¹¹¿·¥µ¥Ö
           COPY    "CPORCSPRT.INC".
      *
      *    ÆüÉÕÊÑ´¹¥µ¥Ö
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *    ²èÌÌÆüÉÕÊÑ´¹¥µ¥Ö
           COPY    "CPORCSGDAY.INC".
      *
      *    ¥Õ¥¡¥¤¥ëºï½ü¥Ñ¥é¥á¥¿
           COPY    "CPORCSFDEL.INC".
      *
      *    Åý·×£Ã£Ó£ÖÀ©¸æ¥µ¥Ö
           COPY    "CPORCSTOUKEICSV.INC".
      *
      *    £Ä£Â¸¡º÷
           COPY    "MCPAREA".
      *
           COPY    "MCPDATA.INC".
      *
           COPY    "COMMON-SPA".
      *
      ****************************************************************
       LINKAGE                     SECTION.
       01  COMMAND-PARAM.
           02  FILLER              PIC X(256).
      ****************************************************************
       PROCEDURE               DIVISION
               USING
           COMMAND-PARAM.
      *
      *****************************************************************
      *    ¼ç¡¡¡¡½èÍý
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC
      *
           PERFORM 300-END-SEC
      *
           STOP    RUN
           .
      *
      *****************************************************************
      *    ½é´ü¡¡½èÍý
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  STS-AREA
           INITIALIZE                  WRK-AREA
           INITIALIZE                  CNT-AREA
           INITIALIZE                  WRK-PARA
           INITIALIZE                  SPA-AREA
      *
      *    £Ä£Â¥ª¡¼¥×¥ó½èÍý
           PERFORM 100-DBOPEN-SEC
      *
      *    ¥Ñ¥é¥á¥¿¥»¥Ã¥È
           UNSTRING   COMMAND-PARAM    DELIMITED  BY  ","
                                       INTO    LNK-PRTKANRI-RENNUM
                                               LNK-PRTKANRI-TBL-KEY
                                               LNK-PRTKANRI-TBL-GROUP
                                               LNK-PRTKANRI-SHORI-RENNUM
                                               LNK-PRTKANRI-SRYYM
                                               LNK-PRTKANRI-SKYYMD
                                               LNK-PRTKANRI-SHELLID
                                               LNK-PRTKANRI-PRIORITY
                                               LNK-PRTKANRI-TERMID
                                               LNK-PRTKANRI-OPID
                                               LNK-PRTKANRI-PRTNM
                                               WRK-PARA-JOBID
                                               WRK-PARA-SHELLID
                                               WRK-PARA-HOSPNUM
                                               RECEERR
                                               WRK-PARA-SRYYM
                                               WRK-PARA-SYORIKBN
           END-UNSTRING
           MOVE    WRK-PARA-HOSPNUM    TO  SPA-HOSPNUM
      *
      *    ¥¹¥Æ¥Ã¥×´ÉÍý³«»Ï½èÍý
           MOVE    "STS"               TO  SJOBKANRI-MODE
           INITIALIZE                      JOBKANRI-REC
           MOVE    WRK-PARA-JOBID      TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID    TO  JOB-SHELLID
           MOVE    SPA-HOSPNUM         TO  JOB-HOSPNUM
           MOVE    "ORCBG022"          TO  JOB-PGID
           MOVE    "¸þÀº¿ÀÌôÅêÍ¿´µ¼Ô°ìÍ÷"
                                       TO  JOB-SHELLMSG
           CALL    "ORCSJOB"               USING
                                           ORCSJOBKANRIAREA
                                           JOBKANRI-REC
                                           SPA-AREA
      *
      *    ¥Ñ¥é¥á¥¿ÊÔ½¸½èÍý
           PERFORM 110-PARA-HENSYU-SEC
      *
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¥Ñ¥é¥á¥¿ÊÔ½¸½èÍý
      *****************************************************************
       110-PARA-HENSYU-SEC         SECTION.
      *
      *    ¥·¥¹¥Æ¥àÆüÉÕ¥»¥Ã¥È
           MOVE    LNK-PRTKANRI-SKYYMD
                                   TO  WRK-SYSYMD
           INITIALIZE                  STS-AREA-DAY
           INITIALIZE                  LNK-DAY1-AREA
           MOVE    "11"            TO  LNK-DAY1-IRAI
           MOVE    WRK-SYSYMD      TO  LNK-DAY1-YMD
           CALL    "ORCSDAY"           USING
                                       STS-AREA-DAY
                                       LNK-DAY1-AREA
           IF      STS-DAY-RC1         =   ZERO
               MOVE    LNK-DAY1-YMD (2:7)  TO  SPA-SYSYMDW
               MOVE    WRK-SYSYMD          TO  SPA-SYSYMD
           END-IF
      *
      *    ½èÍýÆü
           MOVE    SPA-SYSYMD          TO  WRK-SYMD
           PERFORM 800-SEIWA-HEN-SEC
           MOVE    WRK-HENYMDG         TO  PRT-SYSYMD
      *
      *    ½èÍý»þ´Ö
           ACCEPT  SYS-TIME            FROM    TIME
           MOVE    SYS-THH             TO  WRK-THH
           MOVE    SYS-TMM             TO  WRK-TMM
           MOVE    WRK-HENTIME         TO  PRT-TIME
      *
      *    ¿ÇÎÅÆü
           IF      WRK-PARA-SRYYM      =   SPACE
      *        Ì¤ÀßÄê¤Ï¥·¥¹¥Æ¥àÇ¯¤Î£¶·î
               MOVE    WRK-SYSYMD(1:4)     TO  WRK-SRY-YY
               MOVE    06                  TO  WRK-SRY-MM
               IF      WRK-SRY-YYMM        >   WRK-SYSYMD(1:6)
      *            Ì¤ÍèÆü¤Ê¤éÁ°Ç¯¤Î£¶·î
                    COMPUTE WRK-SRY-YY     =   WRK-SRY-YY   -   1
               END-IF
           ELSE
               MOVE    WRK-PARA-SRYYM      TO  WRK-SRY-YYMM
           END-IF
      *
           MOVE    WRK-SRY-YYMM        TO  SPA-SRYYMD(1:6)
           MOVE    "01"                TO  SPA-SRYYMD(7:2)
      *    ½èÍýÆü
           INITIALIZE                  STS-AREA-DAY
           INITIALIZE                  LNK-DAY2-AREA
           MOVE    "21"            TO  LNK-DAY2-IRAI
           MOVE    SPA-SRYYMD      TO  LNK-DAY2-YMD
           CALL    "ORCSDAY"           USING
                                       STS-AREA-DAY
                                       LNK-DAY2-AREA
           MOVE    LNK-DAY2-EDTYMD3(1:16)  TO  PRT-SRYYM
      *
      *    ¿ÇÎÅ²Ê¤ò£Ô£Â£Ì¤Ë¥»¥Ã¥È
           PERFORM 1101-SRYKA-TBLSET-SEC
      *    £Ã£Ó£Ö
           MOVE    SPA-HOSPNUM         TO  WRK-TO-DATA-HOSPNUM
           MOVE    WRK-SRY-YYMM        TO  WRK-TO-DATA-SRYYM
      *
           .
       110-PARA-HENSYU-EXT.
           EXIT.
      *****************************************************************
      *    ¿ÇÎÅ²Ê  ¥Æ¡¼¥Ö¥ëÊÔ½¸½èÍý
      *****************************************************************
       1101-SRYKA-TBLSET-SEC              SECTION.
      *
           INITIALIZE                  TBL-SRYKA-AREA
           MOVE    ZERO                TO  IDX
      *
           MOVE    SPACE               TO  SYS-1005-REC
           MOVE    "1005"              TO  SYS-1005-KANRICD
           MOVE    SPA-SRYYMD          TO  SYS-1005-STYUKYMD
           MOVE    SPA-SRYYMD          TO  SYS-1005-EDYUKYMD
           MOVE    SPA-HOSPNUM         TO  SYS-1005-HOSPNUM
           MOVE    SYS-1005-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 900-SYSKANRI-READ-SEC
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
           PERFORM VARYING IDX
                   FROM    1       BY  1
                   UNTIL      (IDX             >   40    )   OR
                              (FLG-SYSKANRI    =   1     )
               MOVE    SYSKANRI-REC            TO  SYS-1005-REC
               MOVE    SYS-1005-KBNCD (1:2)    TO  TBL-SRYKA-CD (IDX)
               MOVE    SYS-1005-SRYKANAME1     TO  TBL-SRYKA-MEI (IDX)
      *
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 900-SYSKANRI-READ-SEC
           END-PERFORM
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       1101-SRYKA-TBLSET-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    ¼ç¡¡¡¡½èÍý
      *****************************************************************
       200-MAIN-SEC                SECTION.
      *
           INITIALIZE                  JYURRK-REC
           MOVE    SPA-HOSPNUM         TO  JYURRK-HOSPNUM
           MOVE    "2"                 TO  JYURRK-NYUGAIKBN
           STRING  WRK-SRY-YYMM
                   "%"                 DELIMITED  BY  SIZE
                       INTO                JYURRK-SRYYMD
           END-STRING
      *
           MOVE    JYURRK-REC          TO  MCPDATA-REC
           MOVE    "tbl_jyurrk"        TO  MCP-TABLE
           MOVE    "key57"             TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "key57"             TO  MCP-PATHNAME
               PERFORM 970-JYURRK-READ-SEC
           ELSE
               INITIALIZE                  JYURRK-REC
               MOVE    1               TO  FLG-JYURRK
           END-IF
           INITIALIZE                      WRK-KEY-AREA
           INITIALIZE                      TBL-SRY-AREA
           MOVE    ZERO                TO  IDX-TBL
           MOVE    ZERO                TO  WRK-MAE-PTID
      *
      *
           PERFORM
               UNTIL   (FLG-JYURRK         =   1   )
      *
               IF     (WRK-KEY-PTID        =   JYURRK-PTID   )
                 AND  (WRK-KEY-SRYYMD      =   JYURRK-SRYYMD )
                 AND  (WRK-KEY-DENPNUM     =   JYURRK-DENPNUM)
                   CONTINUE
               ELSE
                   IF    IDX-TBL           >   ZERO
      *                ÅÁÉ¼ÈÖ¹æËè½èÍý
                       PERFORM 2002-HENSYU-SYORI-SEC
                   END-IF
                   INITIALIZE                      TBL-SRY-AREA
                   MOVE    ZERO                TO  IDX-TBL
                   MOVE    JYURRK-PTID         TO  WRK-KEY-PTID
                   MOVE    JYURRK-SRYYMD       TO  WRK-KEY-SRYYMD
                   MOVE    JYURRK-DENPNUM      TO  WRK-KEY-DENPNUM
               END-IF
      *
               PERFORM 2001-JYURRK-HEN-SEC
      *
               MOVE    "tbl_jyurrk"        TO  MCP-TABLE
               MOVE    "key57"             TO  MCP-PATHNAME
               PERFORM 970-JYURRK-READ-SEC
           END-PERFORM
           MOVE    "tbl_jyurrk"        TO  MCP-TABLE
           MOVE    "key57"             TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           IF    IDX-TBL           >   ZERO
      *        ÅÁÉ¼ÈÖ¹æËè½èÍý
               PERFORM 2002-HENSYU-SYORI-SEC
      *        ·ï¿ô½¸·×
               PERFORM 20029-CNT-SYORI-SEC
           END-IF
      *    ¹ç·×
           IF      CNT-OUT-1       >   ZERO
               PERFORM 2004-GOUKEI-HENSYU-SEC
           END-IF
      *
           IF      CNT-LINE        >   ZERO
               PERFORM 400-PRINT-SEC
           END-IF
      *
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¼õ¿ÇÍúÎò½èÍý
      *****************************************************************
       2001-JYURRK-HEN-SEC        SECTION.
      *
           MOVE    JYURRK-SRYYMD(7:2)  TO  IDX-DAY
      *
           PERFORM VARYING     IDX     FROM    1   BY  1
                   UNTIL      (IDX     >   15  )
                          AND (JYURRK-ZAINUM (IDX) =   ZERO )
               MOVE    SPACE               TO  SRYACT-REC
               INITIALIZE                  SRYACT-REC
               MOVE    JYURRK-HOSPNUM      TO  SRY-HOSPNUM
               MOVE    JYURRK-NYUGAIKBN    TO  SRY-NYUGAIKBN
               MOVE    JYURRK-PTID         TO  SRY-PTID
               MOVE    JYURRK-SRYKA        TO  SRY-SRYKA
               MOVE    JYURRK-SRYYMD(1:6)  TO  SRY-SRYYM
               MOVE    JYURRK-ZAINUM (IDX) TO  SRY-ZAINUM
      *
               MOVE    SRYACT-REC          TO  MCPDATA-REC
               MOVE    "tbl_sryact"        TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 910-DBSELECT-SEC
               IF      MCP-RC              =   ZERO
                   MOVE    "tbl_sryact"        TO  MCP-TABLE
                   MOVE    "key2"              TO  MCP-PATHNAME
                   PERFORM 930-SRYACT-READ-SEC
               ELSE
                   INITIALIZE                  SRYACT-REC
                   MOVE    1               TO  FLG-SRYACT
               END-IF
      *
      *        ÅêÌô°Ê³°ÂÐ¾Ý³°
               IF      (SRY-SRYKBN         =   "21"
                                           OR  "22"
                                           OR  "23" )
                   CONTINUE
               ELSE
                   MOVE    1               TO  FLG-SRYACT
               END-IF
      *
               PERFORM
                       UNTIL   FLG-SRYACT      =   1
      *
                   PERFORM 20011-SRYACCT-CHK-SEC
      *            ÌôÉ¾¡¦¸ºÅÀÂÐ¾Ý³°
                   IF     (ACCT-ZAIKBN         =   ZERO )
                     AND  (WRK-ZAIKAISU        >   ZERO )
                       PERFORM 20012-SRYACT-SYORI-SEC
                   END-IF
      *
                   MOVE    "tbl_sryact"        TO  MCP-TABLE
                   MOVE    "key2"              TO  MCP-PATHNAME
                   PERFORM 930-SRYACT-READ-SEC
               END-PERFORM
      *
               MOVE    "tbl_sryact"        TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 990-DBCLOSE-SEC
           END-PERFORM
      *
           .
       2001-JYURRK-HEN-EXT.
           EXIT.
      *****************************************************************
      *    ¿ÇÎÅ²ñ·×¥Á¥§¥Ã¥¯½èÍý
      *****************************************************************
       20011-SRYACCT-CHK-SEC           SECTION.
      *
           MOVE    ZERO                TO  WRK-ZAIKAISU
      *    ºÞ²ó¿ô¼èÆÀ
           MOVE    SPACE               TO  SRYACCT-REC
           INITIALIZE                      SRYACCT-REC
           MOVE    SRY-HOSPNUM         TO  ACCT-HOSPNUM
           MOVE    SRY-NYUGAIKBN       TO  ACCT-NYUGAIKBN
           MOVE    SRY-PTID            TO  ACCT-PTID
           MOVE    SRY-SRYKA           TO  ACCT-SRYKA
           MOVE    SRY-SRYYM           TO  ACCT-SRYYM
           MOVE    SRY-SRYKBN          TO  ACCT-SRYKBN
           MOVE    SRY-ZAINUM          TO  ACCT-ZAINUM
      *    ¼ç¥­¡¼ÆÉ¤ß
           PERFORM 940-SRYACCT-READ-SEC
           IF      FLG-SRYACCT         =   ZERO
      *        Ï¢ÈÖ¡Ü£±
               COMPUTE IDX-AC          =   JYURRK-RENNUM   +  1
      *ver.4.7
               IF      IDX-AC          >   4
                   MOVE    4               TO  IDX-AC
               END-IF
               MOVE    ACCT-DAY(IDX-AC IDX-DAY)
                                           TO  WRK-ZAIKAISU
           END-IF
           .
       20011-SRYACCT-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¿ÇÎÅ¹Ô°ÙÌÀºÙÊÔ½¸½èÍý
      *****************************************************************
       20012-SRYACT-SYORI-SEC           SECTION.
      *
           IF      IDX-TBL             =   ZERO
               MOVE    JYURRK-SRYYMD       TO  TBL-SRYYMD
               MOVE    JYURRK-SRYKA        TO  TBL-SRYKA
               MOVE    JYURRK-PTID         TO  TBL-PTID
               MOVE    JYURRK-HKNCOMBINUM  TO  TBL-HKNCOMBI
               MOVE    JYURRK-RENNUM       TO  TBL-JYURRK-RENNUM
           END-IF
      *
           PERFORM VARYING     IDS     FROM    1   BY  1
                   UNTIL      (IDS         >   5   )
                         OR   (SRY-SRYCD (IDS) =   SPACE )
                         OR   (IDX-TBL         >=  50    )
               IF      SRY-SRYCD (IDS)(1:1)    =   "6"
                   ADD     1                   TO  IDX-TBL
                   MOVE    SRY-SRYCD (IDS)     TO  TBL-SRY-SRYCD
                                                         (IDX-TBL)
                   MOVE    SRY-SRYSYUKBN       TO  TBL-SRY-SRYSYUKBN
                                                         (IDX-TBL)
                   MOVE    WRK-ZAIKAISU        TO  TBL-ZAIKAISU
                                                         (IDX-TBL)
      *            ³°ÍÑÌô
                   IF      ACCT-SRYKBN          =   "23"
                       MOVE    SRY-SRYKAISU (IDS)  TO  TBL-ZAIKAISU
                                                         (IDX-TBL)
                   END-IF
               END-IF
           END-PERFORM
      *
           .
       20012-SRYACT-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    ÅêÌôÆâÍÆ¥Á¥§¥Ã¥¯½èÍý
      *****************************************************************
       2002-HENSYU-SYORI-SEC        SECTION.
      *
      *
      *    ¸þÀº¿ÀÌôÂ¿ºÞÅêÍ¿¥Á¥§¥Ã¥¯
           INITIALIZE                  ORCSPSYCHOAREA
           PERFORM VARYING     IDS     FROM    1   BY  1
                   UNTIL      (IDS         >   50  )   OR
                              (TBL-SRY-SRYCD (IDS) =   SPACE )
               MOVE    TBL-SRY-SRYCD (IDS) TO  TNS-SRYCD
               PERFORM 910-TENSU-READ-SEC
               MOVE    TBL-SRY-SRYCD (IDS) TO  ORCSPSYCHO-SRYCD (IDS)
               MOVE    TNS-YAKKAKJNCD      TO  ORCSPSYCHO-YAKKAKJNCD
                                                                 (IDS)
      *
               MOVE    TBL-SRY-SRYSYUKBN(IDS)
                                           TO  ORCSPSYCHO-SRYSYUKBN
                                                                 (IDS)
               MOVE    TBL-ZAIKAISU (IDS)
                                           TO  ORCSPSYCHO-KAISU
                                                                 (IDS)
      *
               MOVE    TNS-NAME            TO  TBL-NAME      (IDS)
               MOVE    TNS-YAKKAKJNCD      TO  TBL-YAKKAKJNCD(IDS)
           END-PERFORM
      *
           MOVE    WRK-KEY-SRYYMD      TO  ORCSPSYCHO-SRYYMD
           CALL    "ORCSPSYCHO"    USING   ORCSPSYCHOAREA
                                           SPA-AREA
      *
      *    ¸þÀº¿ÀÌôÅêÍ¿ÂÐ¾Ý
           IF      ORCSPSYCHO-FFECT-FLG    =   1
      *
      *        ¼ïÎàËè¤Ë£Ó£Ï£Ò£Ô
               PERFORM 20021-TBL-SORT-SEC
      *
               PERFORM 20020-PRITN-SYORI-SEC
           END-IF
      *
           .
       2002-HENSYU-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¼ïÎàËè¤Î£Ó£Ï£Ò£Ô½èÍý
      *****************************************************************
       20021-TBL-SORT-SEC        SECTION.
      *
           PERFORM VARYING     IDS     FROM    1   BY  1
                   UNTIL      (IDS         >   50  )   OR
                              (TBL-SRY-SRYCD (IDS) =   SPACE )
      *        ÌôºÞ¤Î¸úÇ½¶èÊ¬
               MOVE    ORCSPSYCHO-FFECT-KBN (IDS)
                                           TO  TBL-FFECT-KBN (IDS)
      *        ¼ïÎà¶èÊ¬
               MOVE    ORCSPSYCHO-GROUP-KBN (IDS)
                                           TO  TBL-GROUP-KBN (IDS)
      *        Î×»þÅêÌô¶èÊ¬
               MOVE    ORCSPSYCHO-RINJI-KBN (IDS)
                                           TO  TBL-RINJI-KBN (IDS)
           END-PERFORM
      *
           MOVE    TBL-MEISAI-G        TO  SORT-TBL-AREA
           MOVE    SPACE               TO  TBL-MEISAI-G
           INITIALIZE                      TBL-MEISAI-G
           PERFORM VARYING     IDS     FROM    1   BY  1
                   UNTIL      (IDS         >   50  )
               MOVE    HIGH-VALUE          TO  SORT-KEY-GROUP
               MOVE    ZERO                TO  SORT-KEY-FFECT
               MOVE    ZERO                TO  SRT-IDX
               PERFORM VARYING     IDS2    FROM    1   BY  1
                       UNTIL      (IDS2        >   50  )
                   IF      SORT-SRY-SRYCD (IDS2)    NOT =   SPACE
                       IF     SORT-KEY-GROUP       =   HIGH-VALUE
                           MOVE    SORT-GROUP-KBN (IDS2)
                                                   TO  SORT-KEY-GROUP
                           MOVE    SORT-FFECT-KBN (IDS2)
                                                   TO  SORT-KEY-FFECT
                           MOVE    IDS2            TO  SRT-IDX
                       END-IF
                       IF      SORT-FFECT-KBN (IDS2)
                                                   <  SORT-KEY-FFECT
                           MOVE    SORT-GROUP-KBN (IDS2)
                                                   TO  SORT-KEY-GROUP
                           MOVE    SORT-FFECT-KBN (IDS2)
                                                   TO  SORT-KEY-FFECT
                           MOVE    IDS2            TO  SRT-IDX
                       END-IF
                       IF     (SORT-FFECT-KBN(IDS2)   =  SORT-KEY-FFECT)
                       AND    (SORT-GROUP-KBN(IDS2)   <  SORT-KEY-GROUP)
                           MOVE    SORT-GROUP-KBN (IDS2)
                                                   TO  SORT-KEY-GROUP
                           MOVE    IDS2            TO  SRT-IDX
                       END-IF
                   END-IF
               END-PERFORM
               IF      SRT-IDX            >   ZERO
                   MOVE    SORT-SRY-OCCS (SRT-IDX)
                                              TO  TBL-SRY-OCCS (IDS)
                   INITIALIZE                 SORT-SRY-OCCS (SRT-IDX)
               ELSE
                   MOVE    50                 TO  IDS
               END-IF
           END-PERFORM
      *
           .
       20021-TBL-SORT-EXT.
           EXIT.
      *
      *****************************************************************
      *    °õºþÊÔ½¸½èÍý
      *****************************************************************
       20020-PRITN-SYORI-SEC        SECTION.
      *
      *    ÌÀºÙÊÔ½¸
           MOVE    SPACE               TO  LST-PRT-BODY-01
      *    ´µ¼ÔÊÑ¹¹¤Ê¤·
           IF      WRK-MAE-PTID    NOT =   TBL-PTID
      *        ·ï¿ô½¸·×
               PERFORM 20029-CNT-SYORI-SEC
      *        ´µ¼Ô¾ðÊó¼èÆÀ
               PERFORM 20020-PTNUM-HEN-SEC
           END-IF
      *
           IF      CNT-LINE            >=  MAX-LINE
               PERFORM 400-PRINT-SEC
               MOVE    ZERO            TO  CNT-LINE
               PERFORM 280-HEAD-HENSYU-SEC
           END-IF
           IF      CNT-LINE            =   ZERO
               PERFORM 280-HEAD-HENSYU-SEC
           END-IF
      *    ´µ¼ÔÊÑ¹¹
           IF     (WRK-MAE-PTID     NOT =   TBL-PTID )
              AND (CNT-LINE             >   5        )
                ADD     1               TO  CNT-LINE
                MOVE    ALL "-"         TO  HCMSL80-MOJI (CNT-LINE)
           END-IF
           IF      CNT-LINE            >=  MAX-LINE
               PERFORM 400-PRINT-SEC
               MOVE    ZERO            TO  CNT-LINE
               PERFORM 280-HEAD-HENSYU-SEC
           END-IF
           ADD     1                   TO  CNT-LINE
           MOVE    LST-PRT-BODY-01     TO  HCMSL80-MOJI (CNT-LINE)
           IF      WRK-MAE-PTID    NOT =   TBL-PTID
               ADD     1                   TO  CNT-LINE
           END-IF
      *
           MOVE    TBL-PTID            TO  WRK-MAE-PTID
      *
      *    ÌÀºÙÊÔ½¸
           MOVE    SPACE               TO  LST-PRT-BODY-02
           MOVE    TBL-SRYYMD          TO  WRK-SYMD
           PERFORM 800-SEIWA-HEN-SEC
           MOVE    WRK-HENYMDG         TO  PRT-SRYYMD
      *    ¿ÇÎÅ²ÊÊÔ½¸
           PERFORM 200201-SRYKA-HEN-SEC
           MOVE    TBL-HKNCOMBI        TO  PRT-HKNCOMBI
      *
      *    ÌÀºÙÊÔ½¸
           PERFORM VARYING     IDS     FROM    1   BY  1
                   UNTIL      (IDS         >   50  )   OR
                              (TBL-SRY-SRYCD (IDS) =   SPACE )
      *        ¸þÀº¿ÀÌô¤Î¤ß
               IF      TBL-FFECT-KBN(IDS)  NOT =   ZERO
      *
                   MOVE    TBL-NAME (IDS)      TO  PRT-TNS-NAME
      *
                   EVALUATE    TBL-FFECT-KBN(IDS)
                   WHEN    "1"
                       MOVE    "¡ÚÉÔ¡Û"        TO  PRT-KBN
                   WHEN    "2"
                       MOVE    "¡Ú¿ç¡Û"        TO  PRT-KBN
                   WHEN    "3"
                       MOVE    "¡Ú¤¦¡Û"        TO  PRT-KBN
                   WHEN    "4"
                       MOVE    "¡ÚÀº¡Û"        TO  PRT-KBN
                   END-EVALUATE
      *
                   STRING  "("
                           TBL-GROUP-KBN(IDS)
                           ")"                DELIMITED  BY  SIZE
                                              INTO    PRT-YAKKCD
                   END-STRING
      *            Î×»þÅêÌô
                   IF      TBL-RINJI-KBN(IDS)      =   "1"
                       MOVE    "¡ÊÎ×¡Ë"            TO  PRT-SRYKBN
                   END-IF
      *
                   IF      CNT-LINE           >=  MAX-LINE
                       PERFORM 400-PRINT-SEC
                       MOVE    ZERO            TO  CNT-LINE
                       PERFORM 280-HEAD-HENSYU-SEC
                   END-IF
                   ADD     1                   TO  CNT-LINE
                   MOVE    LST-PRT-BODY-02     TO  HCMSL80-MOJI
                                                         (CNT-LINE)
      *
                   IF      WRK-PARA-SYORIKBN   =   "1"
      *                £Ã£Ó£Ö¥Ç¡¼¥¿½ÐÎÏ
                       PERFORM 500-CSV-SYORI-SEC
                   END-IF
      *
                   MOVE    SPACE               TO  LST-PRT-BODY-02
               END-IF
           END-PERFORM
      *
           MOVE    ORCSPSYCHO-COUNT (1)    TO  PRT-CNT1
           MOVE    ORCSPSYCHO-COUNT (2)    TO  PRT-CNT2
           MOVE    ORCSPSYCHO-COUNT (3)    TO  PRT-CNT3
           MOVE    ORCSPSYCHO-COUNT (4)    TO  PRT-CNT4
           IF     (ORCSPSYCHO-TAZAI    =   1 )
               MOVE    "¡ÚÂ¿ºÞÅêÍ¿¡Û"      TO  PRT-ERRMSG
           ELSE
               MOVE    SPACE               TO  PRT-ERRMSG
           END-IF
           IF     (CNT-LINE +  2 )     >   MAX-LINE
               PERFORM 400-PRINT-SEC
               MOVE    ZERO            TO  CNT-LINE
               PERFORM 280-HEAD-HENSYU-SEC
           END-IF
           ADD     2                   TO  CNT-LINE
           MOVE    LST-PRT-BODY-03     TO  HCMSL80-MOJI  (CNT-LINE)
      *    £±¹Ô¶õ¤±¤ë
           ADD     1                   TO  CNT-LINE
      *
      *    ÂÐ¾ÝÍ­ÀßÄê
           MOVE    1                   TO  FLG-CNT-ARI
           IF     (ORCSPSYCHO-TAZAI    =   1 )
               MOVE    1                   TO  FLG-CNT-TAZAI
           END-IF
           IF      ORCSPSYCHO-COUNT (1)    >=  3
               MOVE    1                   TO  FLG-CNT-K1
           END-IF
           IF      ORCSPSYCHO-COUNT (2)    >=  3
               MOVE    1                   TO  FLG-CNT-K2
           END-IF
           IF      ORCSPSYCHO-COUNT (3)    >=  4
               MOVE    1                   TO  FLG-CNT-K3
           END-IF
           IF      ORCSPSYCHO-COUNT (4)    >=  4
               MOVE    1                   TO  FLG-CNT-K4
           END-IF
           .
       20020-PRITN-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    ´µ¼Ô¾ðÊóÊÔ½¸½èÍý
      *****************************************************************
       20020-PTNUM-HEN-SEC        SECTION.
      *
           INITIALIZE                      ORCSPTIDAREA
           MOVE    SPA-HOSPNUM         TO  SPTID-HOSPNUM
           MOVE    TBL-PTID            TO  SPTID-PTID
           CALL    "ORCSPTID"          USING   ORCSPTIDAREA
                                               SPA-AREA
           IF      SPTID-RC            =   ZERO
               MOVE    SPTID-PTNUM         TO  PRT-PTNUM
           END-IF
      *    ´µ¼Ô¥Þ¥¹¥¿¡¼ÆÉ¤ß¹þ¤ß
           MOVE    SPACE               TO  PTINF-REC
           INITIALIZE                      PTINF-REC
           MOVE    SPA-HOSPNUM         TO  PTINF-HOSPNUM
           MOVE    TBL-PTID            TO  PTINF-PTID
           PERFORM 900-PTINF-READ-SEC
           IF      FLG-PTINF           =   1
               MOVE    "¡ö¡ö¡ö¡ö"          TO  PRT-NAME
           ELSE
               MOVE    PTINF-NAME          TO  PRT-NAME
           END-IF
      *
           .
       20020-PTNUM-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ·ï¿ô½¸·×ÊÔ½¸½èÍý
      *****************************************************************
       20029-CNT-SYORI-SEC        SECTION.
      *
           IF      FLG-CNT-ARI         =   1
               ADD     1               TO  CNT-OUT-1
           END-IF
      *    Â¿ºÞÅêÍ¿¤¢¤ê
           IF      FLG-CNT-TAZAI       =   1
               ADD     1               TO  CNT-OUT-2
           END-IF
      *    ¼ïÎà¤¢¤ê
           IF      FLG-CNT-K1          =   1
               ADD     1               TO  CNT-OUT-3-1
           END-IF
           IF      FLG-CNT-K2          =   1
               ADD     1               TO  CNT-OUT-3-2
           END-IF
           IF      FLG-CNT-K3          =   1
               ADD     1               TO  CNT-OUT-3-3
           END-IF
           IF      FLG-CNT-K4          =   1
               ADD     1               TO  CNT-OUT-3-4
           END-IF
      *
           MOVE    ZERO                TO  FLG-CNT-ARI
           MOVE    ZERO                TO  FLG-CNT-TAZAI
           MOVE    ZERO                TO  FLG-CNT-K1
           MOVE    ZERO                TO  FLG-CNT-K2
           MOVE    ZERO                TO  FLG-CNT-K3
           MOVE    ZERO                TO  FLG-CNT-K4
      *
           .
       20029-CNT-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¿ÇÎÅ²ÊÊÔ½¸½èÍý
      *****************************************************************
       200201-SRYKA-HEN-SEC        SECTION.
      *
           PERFORM VARYING     IDZ     FROM    1   BY  1
                   UNTIL      (IDZ         >   40  )   OR
                              (TBL-SRYKA-CD (IDZ)  =   SPACE)
               IF      TBL-SRYKA-CD (IDZ)  =   TBL-SRYKA
                   MOVE    TBL-SRYKA-MEI (IDZ)     TO  PRT-SRYKA
                   MOVE    TBL-SRYKA-MEI (IDZ)     TO  TBL-SRYKAMEI
                   MOVE    40                  TO  IDZ
               END-IF
           END-PERFORM
      *
           .
       200201-SRYKA-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¸«½Ð¤·ÊÔ½¸½èÍý
      *****************************************************************
       280-HEAD-HENSYU-SEC        SECTION.
      *
           MOVE    SPACE               TO  HCMSL80
           ADD     1                   TO  CNT-PAGE
           MOVE    CNT-PAGE            TO  PRT-PAGE
      *
           MOVE    LST-PRT-HEAD1       TO  HCMSL80-MOJI (1)
           MOVE    LST-PRT-HEAD2       TO  HCMSL80-MOJI (3)
           MOVE    LST-PRT-HEAD3       TO  HCMSL80-MOJI (4)
           MOVE    ALL "-"             TO  HCMSL80-MOJI (5)
      *
           MOVE    5                   TO  CNT-LINE
      *
           .
       280-HEAD-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¹ç·×ÊÔ½¸½èÍý
      *****************************************************************
       2004-GOUKEI-HENSYU-SEC          SECTION.
      *
           MOVE    CNT-OUT-1           TO  PRT-F-CNT1
           MOVE    CNT-OUT-2           TO  PRT-F-CNT2
           MOVE    CNT-OUT-3-1         TO  PRT-F-CNT31
           MOVE    CNT-OUT-3-2         TO  PRT-F-CNT32
           MOVE    CNT-OUT-3-3         TO  PRT-F-CNT33
           MOVE    CNT-OUT-3-4         TO  PRT-F-CNT34
           IF     (CNT-LINE +  3 )     >   MAX-LINE
               PERFORM 400-PRINT-SEC
               MOVE    ZERO            TO  CNT-LINE
               PERFORM 280-HEAD-HENSYU-SEC
           END-IF
           ADD     1                   TO  CNT-LINE
           MOVE    ALL "-"             TO  HCMSL80-MOJI (CNT-LINE)
           ADD     2                   TO  CNT-LINE
           MOVE    LST-PRT-FOOT-01     TO  HCMSL80-MOJI (CNT-LINE)
      *
           .
       2004-GOUKEI-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    °õºþ½èÍý
      *****************************************************************
       400-PRINT-SEC          SECTION.
      *
           INITIALIZE                  ORCSPRTAREA
           MOVE    "INS"                TO  SPRT-MODE
           MOVE    LNK-PRTKANRI-RENNUM
                                        TO  SPRT-RENNUM
           MOVE    LNK-PRTKANRI-TBL-KEY
                                        TO  SPRT-TBL-KEY
           MOVE    LNK-PRTKANRI-TBL-GROUP
                                        TO  SPRT-TBL-GROUP
           MOVE    LNK-PRTKANRI-SRYYM
                                        TO  SPRT-SRYYM
           MOVE    LNK-PRTKANRI-SKYYMD
                                        TO  SPRT-SKYYMD
           MOVE    LNK-PRTKANRI-SHELLID 
                                        TO  SPRT-SHELLID
           MOVE    LNK-PRTKANRI-SHORI-RENNUM
                                        TO  SPRT-SHORI-RENNUM
           MOVE    LNK-PRTKANRI-PRIORITY
                                        TO  SPRT-PRIORITY
           MOVE    "HCMSL80.red"        TO  SPRT-PRTID
           MOVE    "¸þÀº¿ÀÌô»ÈÍÑ´µ¼Ô°ìÍ÷É½"
                                        TO  SPRT-TITLE
           MOVE    HCMSL80              TO  SPRT-PRTDATA
           MOVE    LNK-PRTKANRI-TERMID
                                        TO  SPRT-TERMID
           MOVE    LNK-PRTKANRI-OPID
                                        TO  SPRT-OPID
           MOVE    LNK-PRTKANRI-PRTNM
                                        TO  SPRT-PRTNM
           MOVE    "1"                  TO  SPRT-SITEKBN
           CALL    "ORCSPRT"            USING
                                        ORCSPRTAREA
                                        SPA-AREA
           IF      SPRT-RETURN          =   ZERO
               CONTINUE
           ELSE
               MOVE    "°õºþ£Ä£Â¤Ë¹¹¿·¤Ç¤­¤Þ¤»¤ó¤Ç¤·¤¿"
                                        TO  WRK-RECEERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
      *
           .
       400-PRINT-EXIT.
           EXIT.
      *
      *****************************************************************
      *    £Ã£Ó£Ö¥Ç¡¼¥¿½èÍý
      *****************************************************************
       500-CSV-SYORI-SEC             SECTION.
      *
           IF      CNT-CSV             =   ZERO
               PERFORM 5001-CSV-HEAD-SEC
           END-IF
      *
           MOVE    SPACE               TO  WRK-CSV-AREA
           INITIALIZE                      WRK-CSV-AREA
      *    ´µ¼ÔÈÖ¹æ
           MOVE    SPTID-PTNUM         TO  WRK-IN-DATA
           MOVE    20                  TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    ´µ¼Ô»áÌ¾
           MOVE    PTINF-NAME          TO  WRK-IN-DATA
           MOVE    100                 TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    ¿ÇÎÅÆüÉÕ
           MOVE    TBL-SRYYMD          TO  WRK-SYMD
           MOVE    WRK-SYY             TO  WRK-CYY
           MOVE    WRK-SMM             TO  WRK-CMM
           MOVE    WRK-SDD             TO  WRK-CDD
           MOVE    WRK-CSVHENYMD       TO  WRK-IN-DATA
           MOVE    10                  TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    ¿ÇÎÅ²Ê
           MOVE    TBL-SRYKAMEI        TO  WRK-IN-DATA
           MOVE    10                  TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    ÊÝ¸±ÁÈ¹ç¤»ÈÖ¹æ
           MOVE    TBL-HKNCOMBI        TO  WRK-IN-DATA
           MOVE    4                   TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    Ï¢ÈÖ
           MOVE    TBL-JYURRK-RENNUM   TO  WRK-IN-DATA
           MOVE    1                   TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    ¼ïÎà
           EVALUATE    TBL-FFECT-KBN(IDS)
               WHEN    "1"
                   MOVE    "¹³ÉÔ°ÂÌô"            TO  WRK-IN-DATA
               WHEN    "2"
                   MOVE    "¿çÌ²Ìô"              TO  WRK-IN-DATA
               WHEN    "3"
                   MOVE    "¹³¤¦¤ÄÌô"            TO  WRK-IN-DATA
               WHEN    "4"
                   MOVE    "¹³Àº¿ÀÉÂÌô"          TO  WRK-IN-DATA
               WHEN    OTHER
                   MOVE    SPACE                 TO  WRK-IN-DATA
           END-EVALUATE
           MOVE    10                  TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    ¥°¥ë¡¼¥×
           MOVE    TBL-GROUP-KBN(IDS)  TO  WRK-IN-DATA
           MOVE    7                   TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    Î×»þÅêÌô
           IF      TBL-RINJI-KBN(IDS)      =   "1"
               MOVE   "Î×»þÅêÍ¿"           TO  WRK-IN-DATA
           ELSE
               MOVE    SPACE               TO  WRK-IN-DATA
           END-IF
           MOVE    8                   TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    ÌôºÞ¥³¡¼¥É
           MOVE    TBL-SRY-SRYCD (IDS) TO  WRK-IN-DATA
           MOVE    9                   TO  WRK-IN-MAX
           PERFORM 5002-MOJI-HENSYU-SEC
      *    Ì¾¾Î
           MOVE    TBL-NAME  (IDS)     TO  WRK-IN-DATA
           MOVE    80                  TO  WRK-IN-MAX
           MOVE    1                   TO  WRK-END
           PERFORM 5002-MOJI-HENSYU-SEC
      *
           MOVE    SPACE               TO  WRK-OUT-REC
           STRING  WRK-CSV-REC(1:WRK-REC-LENGTH)
                   WRK-KAIGYO              DELIMITED  BY  SIZE
                   INTO        WRK-OUT-REC
           END-STRING
           MOVE    WRK-OUT-REC         TO  WRK-CSV-REC
           PERFORM 600-TOUKEICSV-SEC
      *
           .
       500-CSV-SYORI-EXIT.
           EXIT.
      *
      *
      *****************************************************************
      *    Åý·×£Ã£Ó£Ö½ÐÎÏ½èÍý
      *****************************************************************
       5001-CSV-HEAD-SEC           SECTION.
      *
           MOVE    CSV-HEAD-01         TO  WRK-OUT-REC
           MOVE    SPACE               TO  WRK-CSV-REC
           STRING  WRK-OUT-REC         DELIMITED  BY  SPACE
                   WRK-KAIGYO          DELIMITED  BY  SIZE
                                       INTO  WRK-CSV-REC
           END-STRING
           PERFORM 600-TOUKEICSV-SEC
      *
           .
       5001-CSV-HEAD-EXIT.
           EXIT.
      *
      *****************************************************************
      *    ½ÐÎÏ¥ì¥³¡¼¥É½èÍý¡ÊÊ¸»ú¡Ë
      *****************************************************************
       5002-MOJI-HENSYU-SEC          SECTION.
      *
           IF      WRK-IN-DATA         =   SPACE
               CONTINUE
           ELSE
               PERFORM VARYING IDW     FROM    WRK-IN-MAX  BY  -1
                       UNTIL   IDW     <       1
                   IF      WRK-IN-DATA (IDW:1) NOT =   SPACE
                       IF      WRK-REC-LENGTH      =   ZERO
                           MOVE    WRK-IN-DATA (1:IDW)
                                                   TO  WRK-CSV-REC
                       ELSE
                           MOVE    SPACE           TO  WRK-OUT-REC
                           STRING  WRK-CSV-REC(1:WRK-REC-LENGTH)
                                               DELIMITED   BY  SIZE
                                   WRK-IN-DATA(1:IDW)
                                               DELIMITED   BY  SIZE
                                   INTO        WRK-OUT-REC
                           END-STRING
                           MOVE    WRK-OUT-REC         TO  WRK-CSV-REC
                       END-IF
                       ADD     IDW                 TO  WRK-REC-LENGTH
                       MOVE    1                   TO  IDW
                   END-IF
               END-PERFORM
           END-IF
      *
           IF      WRK-END             =   ZERO
               MOVE    SPACE               TO  WRK-OUT-REC
               STRING  WRK-CSV-REC(1:WRK-REC-LENGTH)
                       WRK-KUGIRI              DELIMITED  BY  SIZE
                       INTO        WRK-OUT-REC
               END-STRING
               MOVE    WRK-OUT-REC         TO  WRK-CSV-REC
               ADD     1                   TO  WRK-REC-LENGTH
           END-IF
      *
           .
       5002-MOJI-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    Åý·×£Ã£Ó£Ö½ÐÎÏ½èÍý
      *****************************************************************
       600-TOUKEICSV-SEC           SECTION.
      *
           INITIALIZE                      ORCSTOUKEICSVAREA
           MOVE    "INS"               TO  STOUKEICSV-MODE
           MOVE    LNK-PRTKANRI-TBL-KEY
                                       TO  STOUKEICSV-TBL-KEY
           MOVE    LNK-PRTKANRI-RENNUM
                                       TO  STOUKEICSV-RENNUM
           MOVE    LNK-PRTKANRI-TBL-GROUP
                                       TO  STOUKEICSV-TBL-GROUP
           MOVE    LNK-PRTKANRI-SHORI-RENNUM
                                       TO  STOUKEICSV-SHORI-RENNUM
           MOVE    LNK-PRTKANRI-SHELLID
                                       TO  STOUKEICSV-SHELLID
           MOVE    WRK-SRY-YYMM        TO  STOUKEICSV-SRYYM
           MOVE    "2"                 TO  STOUKEICSV-NYUGAIKBN
           MOVE    ZERO                TO  STOUKEICSV-PTID
           MOVE    "/var/tmp/"         TO  STOUKEICSV-TO-FOLDER
           MOVE    WRK-TO-DATA         TO  STOUKEICSV-TO-DATA
           MOVE    "2"                 TO  STOUKEICSV-CODE-TYPE
           MOVE    WRK-CSV-REC         TO  STOUKEICSV-CSVDATA
           MOVE    "¸þÀº¿ÀÌôÅêÍ¿´µ¼Ô°ìÍ÷£Ã£Ó£ÖºîÀ®"
                                       TO  STOUKEICSV-TITLE
      *
           CALL    "ORCSTOUKEICSV"     USING
                                       ORCSTOUKEICSVAREA
                                       SPA-AREA
           IF      STOUKEICSV-RETURN   =   ZERO
               ADD     1               TO  CNT-CSV
               CONTINUE
           ELSE
               MOVE    "Åý·×£Ã£Ó£Ö£Ä£Â¤Ë¹¹¿·¤Ç¤­¤Þ¤»¤ó¤Ç¤·¤¿"
                                      TO  WRK-TOUKEIERR
               PERFORM 500-ERR-HENSYU-SEC
               PERFORM 500-COBABORT-SEC
           END-IF
           .
      *
       600-TOUKEICSV-EXT.
           EXIT.
      *****************************************************************
      *    À¾ÎñÏÂÎñÊÔ½¸½èÍý
      *****************************************************************
       800-SEIWA-HEN-SEC             SECTION.
      *
           IF      WRK-SYMD            =   ALL "9"  OR   SPACE
               MOVE    SPACE               TO  WRK-HGO
               MOVE    WRK-SYMD(3:2)       TO  WRK-HYY
               MOVE    WRK-SYMD(5:2)       TO  WRK-HMM
               MOVE    WRK-SYMD(7:2)       TO  WRK-HDD
               MOVE    WRK-HENYMD          TO  WRK-HENYMDG
           ELSE
               INITIALIZE                      STS-AREA-DAY
               INITIALIZE                      LNK-DAY2-AREA
               MOVE    "21"                TO  LNK-DAY2-IRAI
               MOVE    WRK-SYMD            TO  LNK-DAY2-YMD
               CALL    "ORCSDAY"            USING   STS-AREA-DAY
                                                   LNK-DAY2-AREA
               MOVE    LNK-DAY2-EDTYMD1    TO  WRK-HENYMDG
           END-IF
           .
       800-SEIWA-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¥¨¥é¡¼½ÐÎÏ½èÍý
      *****************************************************************
       500-ERR-HENSYU-SEC          SECTION.
      *
           OPEN    INPUT   RECEERR-FILE
           IF      STS-RECEERR         =   ZERO
               CONTINUE
           ELSE
               OPEN    OUTPUT                  RECEERR-FILE
      *
               MOVE    WRK-RECEERR         TO  RECEERR-REC
               WRITE   RECEERR-REC
               CLOSE   RECEERR-FILE
      *
      *        ¥¸¥ç¥Ö´ÉÍý³«»Ï½èÍý
               MOVE    "JBE"               TO  SJOBKANRI-MODE
               INITIALIZE                      JOBKANRI-REC
               MOVE    WRK-PARA-JOBID      TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID    TO  JOB-SHELLID
               MOVE    SPA-HOSPNUM         TO  JOB-HOSPNUM
               MOVE    WRK-RECEERR         TO  JOB-YOBI
               MOVE    "9999"              TO  JOB-ERRCD
               CALL    "ORCSJOB"               USING
                                               ORCSJOBKANRIAREA
                                               JOBKANRI-REC
                                               SPA-AREA
           END-IF
      *
           MOVE    1               TO  FLG-END
                                       FLG-ERR
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    °Û¾ï½ªÎ»½èÍý
      *****************************************************************
       500-COBABORT-SEC          SECTION.
      *
           MOVE    SPACE           TO  WRK-RECEERR
           STRING  "ORCBG022"      DELIMITED  BY  SIZE
                   WRK-TOUKEIERR   DELIMITED  BY  SIZE
                   LOW-VALUE       DELIMITED  BY  SIZE
                                   INTO    WRK-RECEERR
           END-STRING
           CALL    "cobabort"      USING   WRK-RECEERR
           .
       500-COBABORT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ½ªÎ»¡¡¡¡½èÍý
      *****************************************************************
       300-END-SEC                 SECTION.
      *
           IF      CNT-PAGE        =   ZERO 
               MOVE    "½èÍýÂÐ¾Ý¤Î¥Ç¡¼¥¿¤¬¤¢¤ê¤Þ¤»¤ó¤Ç¤·¤¿"
                                       TO  WRK-RECEERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
      *
           DISPLAY "*** ORCBG022 END ***"
      *
      *    ¥¹¥Æ¥Ã¥×´ÉÍý½ªÎ»½èÍý
           MOVE    "STE"               TO  SJOBKANRI-MODE
           INITIALIZE                      JOBKANRI-REC
           MOVE    WRK-PARA-JOBID      TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID    TO  JOB-SHELLID
           MOVE    SPA-HOSPNUM         TO  JOB-HOSPNUM
           MOVE    CNT-PAGE            TO  JOB-UPDCNT
           CALL    "ORCSJOB"               USING
                                           ORCSJOBKANRIAREA
                                           JOBKANRI-REC
                                           SPA-AREA
      *
           PERFORM 900-DBDISCONNECT-SEC
      *
           .
       300-END-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    ¿ÇÎÅ¹Ô°Ù¥Þ¥¹¥¿¡¼ÆÉ¹þ
      *****************************************************************
       900-SYSKANRI-READ-SEC         SECTION.
      *
           PERFORM 920-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  SYSKANRI-REC
               MOVE    ZERO            TO  FLG-SYSKANRI
           ELSE
               INITIALIZE                  SYSKANRI-REC
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           .
       900-SYSKANRI-READ-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    ´µ¼Ô¥Þ¥¹¥¿¡¼ÆÉ¹þ¡Ê¼ç¥­¡¼¡Ë
      *****************************************************************
       900-PTINF-READ-SEC         SECTION.
      *
           MOVE    PTINF-REC           TO  MCPDATA-REC
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_ptinf"         TO  MCP-TABLE
               MOVE    "key"               TO  MCP-PATHNAME
               PERFORM 920-DBFETCH-SEC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  PTINF-REC
                   MOVE    ZERO                TO  FLG-PTINF
               ELSE
                   MOVE    1                   TO  FLG-PTINF
                   INITIALIZE                  PTINF-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PTINF
               INITIALIZE                  PTINF-REC
           END-IF
      *
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       900-PTINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¿ÇÎÅ¹Ô°Ù¥Þ¥¹¥¿¡¼ÆÉ¹þ
      *****************************************************************
       930-SRYACT-READ-SEC         SECTION.
      *
           MOVE    "tbl_sryact"        TO  MCP-TABLE
           PERFORM 920-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  SRYACT-REC
               MOVE    ZERO            TO  FLG-SRYACT
           ELSE
               INITIALIZE                  SRYACT-REC
               MOVE    1               TO  FLG-SRYACT
           END-IF
      *
           .
       930-SRYACT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¿ÇÎÅ²ñ·×¥Þ¥¹¥¿¡¼ÆÉ¹þ¡Ê¼ç¥­¡¼¡Ë
      *****************************************************************
       940-SRYACCT-READ-SEC           SECTION.
      *
           MOVE    SRYACCT-REC         TO  MCPDATA-REC
           MOVE    "tbl_sryacct"       TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_sryacct"       TO  MCP-TABLE
               MOVE    "key"               TO  MCP-PATHNAME
               PERFORM 920-DBFETCH-SEC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  SRYACCT-REC
                   MOVE    ZERO                TO  FLG-SRYACCT
               ELSE
                   MOVE    1                   TO  FLG-SRYACCT
                   INITIALIZE                  SRYACCT-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-SRYACCT
               INITIALIZE                  SRYACCT-REC
           END-IF
      *
           MOVE    "tbl_sryacct"       TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       940-SRYACCT-READ-EXT.
           EXIT.
      *****************************************************************
      *    ¼õ¿ÇÍúÎò¥Þ¥¹¥¿¡¼ÆÉ¹þ
      *****************************************************************
       970-JYURRK-READ-SEC         SECTION.
      *
           MOVE    "tbl_jyurrk"        TO  MCP-TABLE
           PERFORM 920-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  JYURRK-REC
               MOVE    ZERO            TO  FLG-JYURRK
           ELSE
               INITIALIZE                  JYURRK-REC
               MOVE    1               TO  FLG-JYURRK
           END-IF
      *
           .
       970-JYURRK-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    ÅÀ¿ô¥Þ¥¹¥¿¡¼ÆÉ¹þ
      *****************************************************************
       910-TENSU-READ-SEC         SECTION.
      *
           MOVE    SPA-HOSPNUM         TO  TNS-HOSPNUM
           MOVE    TBL-SRYYMD          TO  TNS-YUKOSTYMD
           MOVE    TBL-SRYYMD          TO  TNS-YUKOEDYMD
      *
           MOVE    SPA-HOSPNUM         TO  TNS-HOSPNUM
           MOVE    TNS-TENSU-REC       TO  MCPDATA-REC
           MOVE    "tbl_tensu"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_tensu"         TO  MCP-TABLE
               MOVE    "key"               TO  MCP-PATHNAME
               PERFORM 920-DBFETCH-SEC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  TNS-TENSU-REC
                   MOVE    ZERO                TO  FLG-TENSU
               ELSE
                   INITIALIZE                  TNS-TENSU-REC
                   MOVE    1                   TO  FLG-TENSU
               END-IF
           ELSE
               INITIALIZE                  TNS-TENSU-REC
               MOVE    1                   TO  FLG-TENSU
           END-IF
      *
           MOVE    "tbl_tensu"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       910-TENSU-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    ¥Æ¡¼¥Ö¥ë¸¡º÷½èÍý
      *****************************************************************
       910-DBSELECT-SEC                SECTION.
      *
           MOVE    "DBSELECT"      TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       910-DBSELECT-EXT.
           EXIT.
      *****************************************************************
      *    £Ä£ÂÆÉ¤ß¹þ¤ß½èÍý
      *****************************************************************
       920-DBFETCH-SEC                 SECTION.
      *
           MOVE    "DBFETCH"       TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
      *
       920-DBFETCH-EXT.
           EXIT.
      *
      *****************************************************************
      *    £Ä£Â¥¯¥í¡¼¥¹½èÍý
      *****************************************************************
       990-DBCLOSE-SEC           SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       990-DBCLOSE-EXT.
           EXIT.
      *
      *****************************************************************
      *    £Ä£Â¡¡¥ª¡¼¥×¥ó½èÍý
      *****************************************************************
       100-DBOPEN-SEC              SECTION.
      *
           MOVE    "DBOPEN"        TO  MCP-FUNC.
           MOVE    LOW-VALUE       TO  MCP-TABLE.
           MOVE    LOW-VALUE       TO  MCP-PATHNAME.
           CALL    "ORCDBMAIN"     USING   MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
      *
           MOVE    "DBSTART"       TO  MCP-FUNC.
           MOVE    LOW-VALUE       TO  MCP-TABLE.
           MOVE    LOW-VALUE       TO  MCP-PATHNAME.
           CALL    "ORCDBMAIN"     USING   MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
      *
           .
       100-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    £Ä£Â¡¡¥¯¥í¡¼¥º½èÍý
      *****************************************************************
       900-DBDISCONNECT-SEC        SECTION.
      *
           MOVE    "DBCOMMIT"      TO  MCP-FUNC.
           MOVE    LOW-VALUE       TO  MCP-TABLE.
           MOVE    LOW-VALUE       TO  MCP-PATHNAME.
           CALL    "ORCDBMAIN"     USING   MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
      *
           MOVE    "DBDISCONNECT"  TO  MCP-FUNC.
           MOVE    LOW-VALUE       TO  MCP-TABLE.
           MOVE    LOW-VALUE       TO  MCP-PATHNAME.
           CALL    "ORCDBMAIN"     USING   MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
      *
           .
       900-DBDISCONNECT-EXT.
           EXIT.
