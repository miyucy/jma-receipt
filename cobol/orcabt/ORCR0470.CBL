      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCR0470.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : レセプト（労災・自賠責）
      *  コンポーネント名  : 月次業務　労災自賠責明細書ファイル作成
      *                      （点数摘要欄並び替え）
      *  管理者            : 
      *  作成日付    作業者        記述
      *  00/12/01    MCC−小豆沢   新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *  01.00.01    NACL-藤原    02.09.20  検査のときは表用番号を編集しない
      *                                     検査のときZAITENを編集する
      *  01.00.02    NACL-門脇    02.11.26  検診コードの診療区分編集
      *  01.00.03    NACL-門脇    03.03.03  剤番号桁修正対応
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION           SECTION.
       INPUT-OUTPUT            SECTION.
       FILE-CONTROL.
           SELECT  RECE46-FILE     ASSIGN  RECE46PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE46-KEY
                                   FILE    STATUS  IS  STS-RECE46.
           SELECT  RECE47-FILE    ASSIGN  RECE47PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  RECE47-KEY
                                   FILE    STATUS  IS  STS-RECE47.
           SELECT  RECE47X-FILE   ASSIGN  RECE47PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE47X-KEY
                                   FILE    STATUS  IS  STS-RECE47.
006900*    エラーファイル
           SELECT  RECEERR-FILE    ASSIGN  RECEERR
                                   FILE    STATUS  IS  STS-RECEERR.
      *
       DATA                    DIVISION.
       FILE                    SECTION.
      *
       FD  RECE46-FILE.
       01  RECE46-REC. 
           COPY    "CPRCF046.INC".
      *
       FD  RECE47-FILE.
       01  RECE47-REC. 
           COPY    "CPRCF047.INC".
      *
       FD  RECE47X-FILE.
       01  RECE47X-REC. 
           COPY    "CPRCF047.INC"  REPLACING   //RECE47//
                                   BY          //RECE47X//.
      *    エラーファイル
       FD  RECEERR-FILE.
       01  RECEERR-REC             PIC X(200). 
      *
       WORKING-STORAGE             SECTION.
      *
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01//
                                   BY         //RECE46//.
      *
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01//
                                   BY         //RECE47//.
      *
      *    エラーファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01PARA//
                                   BY         //RECEERR//.
           03  FILLER              PIC X(04)   VALUE   ".dat".
      *
      *    フラグ領域
       01  STS-AREA.
           03  STS-RECE46                              PIC X(02).
           03  STS-RECE47                             PIC X(02).
           03  STS-RECEERR                             PIC X(02).
      *
       01  FLG-AREA.
           03  FLG-END                                 PIC 9(01).
           03  SAISHIN-FLG                             PIC X(01).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-IN                                  PIC 9(06).
           03  CNT-OUT                                 PIC 9(06).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-PARA.
               05  WRK-PARA-SRYYM                      PIC  X(06).
               05  WRK-PARA-TERMID                     PIC  X(16).
               05  WRK-PARA-SYSYMD                     PIC  X(08).
               05  WRK-PARA-JOBID                      PIC  9(07).
               05  WRK-PARA-SHELLID                    PIC  X(08).
      *****03  WRK-IN                                  PIC X(01).
      *
           03  WRK-RECEERR             PIC X(200). 
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *     
      *   ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    ＤＢ検索
           COPY    "MCPAREA".
      *
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
      ****************************************************************
       LINKAGE                 SECTION.
       01  COMMAND-PARAM.
           02  FILLER      PIC X(256).
      ****************************************************************
       PROCEDURE           DIVISION
               USING
           COMMAND-PARAM.
      *
      *****************************************************************
      *    主処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC    UNTIL   FLG-END =   1 
      *
           PERFORM 300-TERM-SEC
      *
           STOP    RUN
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  CNT-AREA
                                       WRK-AREA
      *
           MOVE    ZERO                TO  FLG-END
           MOVE    SPACE               TO  SAISHIN-FLG
      *
           MOVE    COMMAND-PARAM       TO  WRK-PARA
      *
           PERFORM 100-DBOPEN-SEC
      *
      *    ステップ管理開始処理
           MOVE    "STS"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           MOVE    "ORCR0470"      TO  JOB-PGID
           MOVE    "摘要欄並び替え（労災・自賠責）"
                                   TO  JOB-SHELLMSG
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA  
                                   JOBKANRI-REC
      *
           MOVE    "RECE460"           TO  RECE46PARA-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECE46PARA-TERMID
      *
           MOVE    "RECE470"           TO  RECE47PARA-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECE47PARA-TERMID
      *
           MOVE    "RECEERR"           TO  RECEERR-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECEERR-TERMID
      *
           OPEN    OUTPUT              RECE47X-FILE
           CLOSE                       RECE47X-FILE
      *
           OPEN    INPUT               RECE46-FILE
           OPEN    I-O                 RECE47-FILE
      *
           PERFORM    RECE46-READ-SEC
      *
          .
       100-INIT-EXT.
           EXIT.
      * 
      *****************************************************************
      *    主処理
      *****************************************************************
       200-MAIN-SEC                SECTION.
      *
           PERFORM     210-HENSYU-SEC
      *
           PERFORM     RECE46-READ-SEC
           .
       200-MAIN-EXT.
           EXIT.
      * 
      *****************************************************************
      *    編集処理
      *****************************************************************
       210-HENSYU-SEC                   SECTION.
      *
           INITIALIZE                           RECE47-REC
           MOVE    RECE46-HOSPID        TO      RECE47-HOSPID
           MOVE    RECE46-SRYYM         TO      RECE47-SRYYM
           MOVE    RECE46-NYUGAIKBN     TO      RECE47-NYUGAIKBN
           MOVE    RECE46-PTID          TO      RECE47-PTID
           MOVE    RECE46-RECEKA        TO      RECE47-RECEKA
           MOVE    RECE46-HKNID         TO      RECE47-HKNID
           MOVE    RECE46-TEKSTYMD      TO      RECE47-TEKSTYMD
           MOVE    RECE46-ZAINUM        TO      RECE47-ZAINUM
           IF      RECE46-ZAINUM(1:7)  =  9999998
                 MOVE     ZERO          TO      RECE47-ZAINUM
           END-IF
           IF      RECE46-ZAINUM       =  99999999
                 MOVE     ZERO          TO      RECE47-ZAINUM
           END-IF
           IF      RECE46-SRYKBN        =       "60"
              IF   RECE46-KNSJISKBN     =       2
                   MOVE    5            TO      RECE47-KNSJISKBN
              ELSE
                  IF   (RECE46-CDKBN-KBNNUM >=        0)   AND
                       (RECE46-CDKBN-KBNNUM <=      199)
                          MOVE   1      TO      RECE47-KNSJISKBN
                  END-IF
                  IF   (RECE46-CDKBN-KBNNUM >=      400)   AND
                       (RECE46-CDKBN-KBNNUM <=      499)
                          MOVE   2      TO      RECE47-KNSJISKBN
                  END-IF
                  IF   (RECE46-CDKBN-KBNNUM >=      200)   AND
                       (RECE46-CDKBN-KBNNUM <=      399)
                          MOVE   3      TO      RECE47-KNSJISKBN
                  END-IF
                  IF   RECE46-CDKBN-KBN      =      "9"
                          MOVE   4      TO      RECE47-KNSJISKBN
                  END-IF
              END-IF
           END-IF     
      ***  MOVE    RECE46-KNSJISKBN     TO      RECE47-KNSJISKBN
           MOVE    RECE46-LINENUM       TO      RECE47-LINENUM
           MOVE    RECE46-ZAIMEISAISU   TO      RECE47-ZAIMEISAISU
           MOVE    RECE46-PTNUM         TO      RECE47-PTNUM
           MOVE    RECE46-SRYKA         TO      RECE47-SRYKA
           IF   (  RECE46-SRYKBN        =       "99"          )
           OR   (( RECE46-SRYKBN        =       "11" OR "12" )  AND
                 ( RECE46-ZAITEN        =       ZERO         )) 
               MOVE  SPACE              TO      RECE47-SRYKBN
           ELSE
               MOVE  RECE46-SRYKBN      TO      RECE47-SRYKBN
           END-IF
           MOVE    RECE46-ASTER         TO      RECE47-ASTER 
           MOVE    RECE46-TEKIYO        TO      RECE47-TEKIYO
           IF      RECE46-SRYKBN        =       "12"
               IF      RECE46-ASTER         =       "＊"
                   IF      RECE46-TEKIYO(1:24)  =
                                        "再診（診療所）（１回目）"
                       MOVE   "1"           TO      SAISHIN-FLG
                   END-IF
               END-IF
               IF      RECE46-TEKIYO(49:2)  =   SPACE
                   CONTINUE
               ELSE     
                  IF      SAISHIN-FLG          =      "1"
                      MOVE  SPACE         TO   SAISHIN-FLG
                  END-IF
               END-IF
           END-IF     
           IF      RECE46-SRYKBN        NOT =   "21"
               IF   RECE46-SRYKBN       NOT =   "40" AND "60" AND "80"  
      ***      IF   RECE46-SRYKBN       NOT =   "40" AND "80"  
                   MOVE    RECE46-ZAITEN    TO      RECE47-ZAITEN
               END-IF
               MOVE   RECE46-CDKBN-SYO     TO  RECE47-CDKBN-SYO
               MOVE   RECE46-CDKBN-BU      TO  RECE47-CDKBN-BU
               MOVE   RECE46-CDKBN-KBN     TO  RECE47-CDKBN-KBN
               MOVE   RECE46-CDKBN-KBNNUM  TO  RECE47-CDKBN-KBNNUM
               MOVE   RECE46-CDKBN-KBNNUM-EDA
                                           TO  RECE47-CDKBN-KBNNUM-EDA
               MOVE   RECE46-CDKBN-KOUBAN  TO  RECE47-CDKBN-KOUBAN
               MOVE   RECE46-KOUHYOJUNNUM  TO  RECE47-KOUHYOJUNNUM
           END-IF
      ***      IF      RECE46-SRYKBN        =   "60"
      ***          CONTINUE
      ***      ELSE    
      ***          MOVE    RECE46-CDKBN-SYO     TO  RECE47-CDKBN-SYO
      ***          MOVE    RECE46-CDKBN-BU      TO  RECE47-CDKBN-BU
      ***          MOVE    RECE46-CDKBN-KBN     TO  RECE47-CDKBN-KBN
      ***          MOVE    RECE46-CDKBN-KBNNUM  TO  RECE47-CDKBN-KBNNUM
      ***          MOVE    RECE46-CDKBN-KBNNUM-EDA
      ***                                   TO  RECE47-CDKBN-KBNNUM-EDA
      ***          MOVE    RECE46-CDKBN-KOUBAN  TO  RECE47-CDKBN-KOUBAN
      ***          MOVE    RECE46-KOUHYOJUNNUM  TO  RECE47-KOUHYOJUNNUM
      ***      END-IF  
      ***  END-IF
      *****IF     RECE46-SRYKBN  NOT =  "11" AND "95" AND "96"
           IF     RECE46-SRYKBN  NOT =  "95" AND "96"
               PERFORM     RECE47-WRITE-SEC
           END-IF
           .
       210-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    データ更新処理（Ｆ００６１）
      *****************************************************************
       RECE47-WRITE-SEC                SECTION.
      *
           WRITE   RECE47-REC
      *
           ADD     1                   TO  CNT-OUT
      *
           .
       RECE47-WRITE-EXT.
           EXIT.
057400*
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC                SECTION.
      *
           OPEN    INPUT   RECEERR-FILE
           IF      STS-RECEERR         =   ZERO
               CLOSE   RECEERR-FILE
           ELSE
               OPEN    OUTPUT              RECEERR-FILE
      *
               MOVE    WRK-RECEERR         TO  RECEERR-REC
               WRITE   RECEERR-REC
               CLOSE   RECEERR-FILE
      *         
      *        ジョブ管理開始処理
               MOVE    "JBE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               MOVE    WRK-RECEERR     TO  JOB-YOBI
               MOVE    "9999"          TO  JOB-ERRCD
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA  
                                       JOBKANRI-REC
           END-IF
      *                             
           MOVE    1                   TO  FLG-END
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了処理
      *****************************************************************
       300-TERM-SEC                SECTION.
      *
           CLOSE   RECE46-FILE
                   RECE47-FILE
      *
      *    ステップ管理終了処理
           MOVE    "STE"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA  
                                   JOBKANRI-REC
      *
           PERFORM 900-DBDISCONNECT-SEC
      *
           DISPLAY "RECE46-IN   :" CNT-IN   
           DISPLAY "RECE47-OUT :" CNT-OUT   
           DISPLAY "*** ORCR0470 END ***"
      ***     ACCEPT  WRK-IN FROM CONSOLE 
           .
       300-TERM-EXT.
           EXIT.
      *
      *****************************************************************
      *    中間ファイルＲＥＡＤ処理
      *****************************************************************
       RECE46-READ-SEC            SECTION.
      *
           READ    RECE46-FILE   NEXT
               AT  END
                   MOVE    1     TO    FLG-END
               NOT  AT  END
                   ADD     1     TO    CNT-IN
           END-READ
           .
       RECE46-READ-EXT.
           EXIT.
      *
      *      
      *****************************************************************
      *    ＤＢ　オープン処理
      *****************************************************************
       100-DBOPEN-SEC                SECTION.
      *
           MOVE    "DBOPEN"            TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
      *
           MOVE    "DBSTART"           TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
           .
       100-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　クローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC            SECTION.
      *
           MOVE    "DBCOMMIT"          TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
      *
           MOVE    "DBDISCONNECT"      TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
           .
       900-DBCLOSE-EXT.
           EXIT.
      
