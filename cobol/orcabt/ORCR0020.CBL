      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCR0020.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : レセプト
      *  コンポーネント名  : 保険情報編集
      *  管理者            : 
      *  作成日付    作業者        記述
      *  00/12/01    MCC−小豆沢   新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *  01.00.01    MCC-小豆沢   01.12.10  自費保険のレセプト種別対応
      *                                     追加
      *  01.00.02    MCC-藤原     02.02.08  氏名が変更されたときの
      *                                     編集の追加
      *  01.01.03    MCC-藤原     02.02.13  同一保険者で記号番号が変わっ
      *                                     たとき任継・継の変更の時一枚に
      *                                     印刷するように修正
      *                                     保険組合せを３から１０に変更
      *  01.00.04   MCC-藤原      02.02.15  特記事項（老保）の編集の追加
      *  01.00.05   MCC-藤原      02.03.13  同一保険者で記号番号が変わった
      *                                     とき保険の履歴を表示するように修正
      *  01.00.06   MCC-小豆沢    02.03.25  長期特定疾患の場合、レセプトに
      *                                     印字しないように修正
      *  01.00.07   MCC-藤原      02.03.26  老人保険情報の編集の修正
      *  01.00.08   MCC-藤原      02.04.02  県単用項目ファイルのクリア処理追加
      *  01.00.09   MCC-藤原      02.05.28  訂正区分＝１のときレセプト種別は
      *                                     不明分とする
      *  01.00.10   MCC-藤原      02.07.18  生保の受給者番号を月代わり受給者
      *                                     番号マスタから編集する
      *  01.00.11   NACL-藤原     02.08.23  自費を不明分と分ける
      *  01.00.12   NACL-藤原     02.09.05  請求書用ファイルのみ端末ＩＤを固定
      *                                     にする
      *  01.00.13   NACL-小豆沢   02.09.24  （平成１４年１０月改正対応）
      *                                     レセプト種別コード追加
      *  01.00.14   NACL-小豆沢   02.09.27  （平成１４年１０月改正対応）
      *                                     老人保険の場合も給付割合のセットを
      *                                     を行う
      *  01.00.15   NACL-門脇     02.10.07  （平成１４年１０月改正対応）
      *                                     低所得フラグのセットを行う
      *  01.00.16   NACL-小豆沢   02.10.16  （平成１４年１０月改正対応）
      *                                     国保退職者の給付割合セット
      *  01.00.17   NACL-藤原     02.11.06  主保険が自費のとき保険履歴を作成
      *                                     しないようにする 
      *  01.00.18   NACL-藤原     02.11.25  前期高齢者の船員保険のときの
      *                                     レセプト種別の編集を修正 
      *                                     前期高齢者の共済組合の下船３ヶ月の
      *                                     ときのレセプト種別の編集を修正 
      *  01.00.19   NACL-小豆沢   02.12.04  退職者国保の３才未満の給付割合表示
      *                                     を修正
      *  01.00.20   NACL-藤原     03.01.17  特記事項０４老保の判定に年齢判定
      *                                     を追加する
      *  01.00.21   NACL-藤原     03.01.20  月代り受給者番号に入外区分を追加
      *  01.00.22   NACL-藤原     03.01.30  特記事項０４老保の判定にを７０歳
      *                                     を追加
      *  01.00.23    NACL-藤原    03/02/12  テスト患者番号区分・補助区分の追加
      *  01.00.24    NACL-藤原    03/03/07  レセプト記載外公費情報追加
      *                                     診療科数１０から２０へ変更
      *  01.00.25    NACL-門脇    03/03/07  在総診フラグセット
      *  01.00.26    NACL-藤原    03/03/11  種別不明の情報を編集
      *  01.00.27    NACL-門脇    03/03/18  在総診フラグセットの修正
      *  01.00.28    NACL-門脇    03/04/16  船員保険（被保険者）＋通勤災害
      *                                     フラグセット
      *  01.00.29    NACL-藤原    03/04/21  地方公費データのとき主保険が
      *                                     無い場合は不明分とする
      *  01.00.30    NACL-小豆沢  03/05/30  山形県の地方公費は主保険が
      *                                     国保の場合は公費カウントしない
      *  01.00.31    NACL-藤原    03/10/07  福岡県の地方公費は主保険が
      *                                     国保の場合は公費カウントしない
      *                                     （法別番号４１以外のとき）
      *  01.00.32    NACL-藤原    03/10/27  患者情報の編集の修正
      *                                     県番号の追加
      *  01.00.33    NACL-門脇    03/11/27  ”老保”編集条件修正
      *  01.00.34    NACL-門脇    04/01/14  診療行為の包括分は除く
      *                                     SRYACCT5→31
      *  01.00.35    NACL-藤原    04/02/04  月途中に老保の変更があった場合
      *                                     のＫＥＹの編集の修正
      *  01.00.36    NACL-門脇    04/02/12  年齢判定文修正
      *  01.00.37    NACL-藤原    04/03/08  シス管による生保受給者番号の編集
      *  01.00.38    NACL-門脇    04/04/06  公費単独７０歳未満で老人保険入力
      *                                     のある方の在総診フラグ編集修正
      *  01.00.39    NACL-藤原    04/05/27  同一開始日の複数自費レセプトが
      *                                     別々に作成されるように修正
      *  01.00.40    NACL-藤原    04/06/08  適用終了日の追加
      *  01.00.41    NACL-藤原    04/06/15  印刷順の追加（個別作成時のみ）
      *  01.00.42    NACL-藤原    04/07/12  生保の受給者番号を月代わり受給者DBに
      *                                     無い場合は公費DBより編集する
      *                                     保険組合せの公費ＩＤを追加
      *  01.00.43    NACL-藤原    04/07/20  特例退職者＋老人のとき不明分
      *                                     とする
      *  01.00.44    NACL-藤原    04/07/23  補助区分の追加に伴うレセプト
      *                                     種別・給付割合の編集を変更
      *  01.00.45    NACL-藤原    04/10/04  特例退職者＋老人のとき本人の
      *                                     み不明分とする
      *  01.00.46    NACL-門脇    04/10/05  公費単独の在総診フラグセットの修正
      *  中止処理追加
      *  01.01.01    NACL-藤原    04/09/28  中止処理の追加
      *  01.03.02    NACL-藤原    04/11/01  記載なしの公費の処理
      *  01.03.03    NACL-門脇    05/02/25  特記事項”老保”編集修正
      *
      *  02.06.01    NACL-藤原    05/05/11  沖縄県対応
      *                                     精神障害者特別措置(121)の場合
      *                                     主保険なしでも不明分としない
      *  02.06.02    NACL-藤原    05/05/17  神奈川県の障害(180､280)は
      *                                     国保で負担者番号が空白の場合
      *                                     公費カウントしない
      *  02.06.03    NACL-門脇    05/06/09  低所得フラグ編集修正
      *  02.06.04    NACL-竹田    05/05/30  realtime preview 処理追加
      *  02.06.05    NACL-藤原    05/07/05  本人−外来−内容の追加
      *  02.06.06    NACL-藤原    05/07/28  レセプト特記事項編集情報のため
      *                                     のレセプト請求しない地方公費の
      *                                     を追加
      *
      *  02.07.01    NACL-藤原    05/08/20  特別療養費対応
      *  02.07.02    NACL-藤原    05/10/06  公費単独の三歳未満の割合設定
      *                                     をしないようにする
      *  02.07.03    NACL-藤原    06/01/05  山形県のレセプト様式変更対応
      *                                     平成１８年１月分から標準的な
      *                                     レセプト様式へ変更
      *
      *  02.08.01    NACL-藤原    06/02/20  主科対応
      *
      *  02.09.01    NACL-門脇    06/04/13  ２００６年４月改正対応
      *  02.09.02    NACL-門脇    06/05/18  岡山県西粟倉村退職者国保２割対応
      *  3.0.0
      *  03.00.01    NACL-藤原    06/05/15  入退院履歴記載対応
      *
      *  03.01.01    NACL-藤原    06/07/03  旧総合病院の科表示
      *
      *  03.02.01    NACL-藤原    06/07/31  国保主科対応
      *  03.02.02    NACL-藤原    06/09/07  ２００６年１０月改正対応
      *                                     福岡地方公費対応
      *
      *  03.03.01    NACL-門脇    06/09/27  ２００６年１０月改正対応
      *                                     特記事項１６長２対応
      *                                     レセプト種別
      *                                     老齢福祉年金 
      *  03.03.02    NACL-藤原    06/09/19  月途中での氏名変更対応
      *  03.03.03    NACL-藤原    06/12/13  公費分で種別が設定できなかっ
      *                                     たときの対応
      *
      *  03.04.01    NACL-藤原    06/12/05  未請求レセ対応
      *  03.04.02    NACL-藤原    07/02/06  月途中の受給者番号変更に対応
      *  03.04.03    NACL-門脇    07/02/19  本人−外来−回−上限額のセット
      *  03.04.04    NACL-門脇    07/03/23  月途中医療機関コード変更時対応
      *  03.04.05    NACL-門脇    07/04/01  現物給付対応
      *  03.04.06    NACL-門脇    07/07/31  ７０歳未満現物給付保険者毎で計算
      *  03.04.07    NACL-藤原    07/09/04  宮崎県「公費９０」対応
      *                                     ２００７年９月以降のみ対象 
      *  03.04.08    NACL-藤原    08/02/12  宮崎県「公費９０」対応
      *                                     通常の記載方法に戻す 
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION           SECTION.
       INPUT-OUTPUT            SECTION.
       FILE-CONTROL.
           SELECT  RECE01-FILE     ASSIGN  RECE01PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE01-KEY
                                   FILE    STATUS  IS  STS-RECE01.
           SELECT  RECE02-FILE     ASSIGN  RECE02PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  RECE02-KEY
                                   FILE    STATUS  IS  STS-RECE02.
           SELECT  RECE022-FILE    ASSIGN  RECE02PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE022-KEY
                                   FILE    STATUS  IS  STS-RECE02.
           SELECT  RECE09-FILE     ASSIGN  RECE09PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  RECE09-KEY
                                   FILE    STATUS  IS  STS-RECE09.
           SELECT  RECE091-FILE    ASSIGN  RECE09PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE091-KEY
                                   FILE    STATUS  IS  STS-RECE09.
      *    レセプト（県単用項目）データ
           SELECT  RECE11-FILE     ASSIGN  RECE11PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE11-KEY
                                   FILE    STATUS  IS  STS-RECE11.
           SELECT  RECE12-FILE     ASSIGN  RECE12PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  RECE12-KEY
                                   FILE    STATUS  IS  STS-RECE12.
           SELECT  RECE12X-FILE    ASSIGN  RECE12PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE12X-KEY
                                   FILE    STATUS  IS  STS-RECE12.
      *    エラーファイル
           SELECT  RECEERR-FILE    ASSIGN  RECEERR
                                   FILE    STATUS  IS  STS-RECEERR.
      *
       DATA                    DIVISION.
       FILE                    SECTION.
      *
       FD  RECE01-FILE.
       01  RECE01-REC. 
           COPY    "CPRCF001.INC".
      *
       FD  RECE02-FILE.
       01  RECE02-REC. 
           COPY    "CPRCF002.INC".
      *
       FD  RECE022-FILE.
       01  RECE022-REC. 
           COPY    "CPRCF002.INC"  REPLACING  //RECE02//
                                   BY         //RECE022//.
      *
       FD  RECE09-FILE.
       01  RECE09-REC. 
           COPY    "CPRCF009.INC".
      *
       FD  RECE091-FILE.
       01  RECE091-REC. 
           COPY    "CPRCF009.INC"  REPLACING  //RECE09//
                                   BY         //RECE091//.
      *
      *    レセプト（県単用項目）データ
       FD  RECE11-FILE.
       01  RECE11-REC. 
           COPY    "CPRCF011.INC".
      *
       FD  RECE12-FILE.
       01  RECE12-REC. 
           COPY    "CPRCF068.INC"  REPLACING  //RECE68//
                                   BY         //RECE12//.
      *
       FD  RECE12X-FILE.
       01  RECE12X-REC. 
           COPY    "CPRCF068.INC"  REPLACING  //RECE68//
                                   BY         //RECE12X//.
      *     
      *    エラーファイル
       FD  RECEERR-FILE.
       01  RECEERR-REC             PIC X(200). 
      *
       WORKING-STORAGE             SECTION.
      *
      *     レセプト明細ファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC".
      *
      *     レセプト明細ファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01//
                                   BY         //RECE02//.
      *
      *     レセプト明細ファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01//
                                   BY         //RECE09//.
      *
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01//
                                   BY         //RECE11//.
      *
      *     入退院履歴情報ファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01//
                                   BY         //RECE12//.
      *
      *    エラーファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01PARA//
                                   BY         //RECEERR//.
           03  FILLER              PIC X(04)   VALUE   ".dat".
      *    フラグ領域
       01  STS-AREA.
           03  STS-RECE01                              PIC X(02).
           03  STS-RECE02                              PIC X(02).
           03  STS-RECE09                              PIC X(02).
           03  STS-RECE11                              PIC X(02).
           03  STS-RECE12                              PIC X(02).
           03  STS-RECEERR                             PIC X(02).
      *
       01  FLG-AREA.
           03  FLG-END                                 PIC 9(01).
           03  FLG-RECE01                              PIC 9(01). 
           03  FLG-RECE09                              PIC 9(01). 
           03  FLG-RECE12                              PIC 9(01). 
           03  FLG-PTHKNINF                            PIC 9(01).
           03  FLG-PTKOHINF                            PIC 9(01).
           03  FLG-HKNNUM                              PIC 9(01).
           03  FLG-SRYACCT                             PIC 9(01).
           03  FLG-SRYACT                              PIC 9(01).
           03  FLG-SYSKANRI                            PIC 9(01).
           03  FLG-CHK                                 PIC 9(01).
           03  FLG-MONTHLYNUM                          PIC 9(01).
           03  FLG-TSYRRK                              PIC 9(01).
           03  FLG-TNKRRK                              PIC 9(01).
           03  FLG-PTNYUINRRK                          PIC 9(01).
           03  FLG-PTNYUINRRK12                        PIC 9(01).
           03  FLG-PTNYUINRRK22                        PIC 9(01).
           03  FLG-PTNYUINRRK23                        PIC 9(01).
           03  FLG-NYUINACCT                           PIC 9(01).
           03  FLG-HKNCOMBI                            PIC 9(01).
           03  FLG-KYUSEIRRK                           PIC 9(01).
      *
           03  FLG-SET                                 PIC 9(01).
           03  FLG-ROUJINHOKEN                         PIC 9(01).
           03  FLG-GENBUTU70                           PIC 9(01).
           03  FLG-TAISYO                              PIC 9(01).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-RECE12                              PIC 9(06). 
           03  CNT-RECE09                              PIC 9(06). 
           03  CNT-IN-CANCEL                           PIC 9(06).
           03  CNT-TBL-OCC                             OCCURS  2. 
               05  CNT-IN                              PIC 9(06).
               05  CNT-OUT                             PIC 9(06).
      *
       01  KEY-AREA                            VALUE   LOW-VALUE.
           03  KEY-NEW.
               05  KEY-N-HKNCOMBI                      PIC 9(04).
           03  KEY-OLD.
               05  KEY-O-HKNCOMBI                      PIC 9(04).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                                     PIC 9(03).
           03  IDY                                     PIC 9(03).
           03  IDX1                                    PIC 9(03).
           03  IDX2                                    PIC 9(03).
           03  IDX3                                    PIC 9(03).
           03  IDX4                                    PIC 9(03).
           03  IDX5                                    PIC 9(03).
           03  IDY1                                    PIC 9(03).
           03  IDY2                                    PIC 9(03).
           03  IDZ                                     PIC 9(02).
           03  TOKKI-IDX                               PIC 9(02).
           03  SYSKAN-IDX                              PIC 9(03).
      *
      *    パス名領域
       01  WK-PATH                                     PIC X(20).
      *
      *    レセプト種別判定用領域
       01  WK-RECESYUBETU-AREA.
           03  WK-KOKUHO                               PIC X(01).
           03  WK-IHO                                  PIC X(01).
           03  WK-RJN                                  PIC X(01).
           03  WK-KOHI-CNT                             PIC 9(01).
           03  WK-HONKZKKBN                            PIC X(01).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-PARA.
      *********05  WRK-PARA-PREFNUM                    PIC  X(02). 
               05  WRK-PARA-SRYYM                      PIC  X(06).
               05  WRK-PARA-TERMID                     PIC  X(16).
               05  WRK-PARA-SYSYMD                     PIC  X(08).
               05  WRK-PARA-RECEKBN                    PIC  X(01).
               05  WRK-PARA-JOBID                      PIC  9(07).
               05  WRK-PARA-SHELLID                    PIC  X(08).
      *
           03  WRK-SYS-SRYYMD                          PIC  X(08).
      *
           03  WRK-SRYYMD.
               05  WRK-SRYYYMM                         PIC  9(06).
               05  WRK-SRYDD                           PIC  9(02).
      *
           03  WRK-MCP-TABLE       PIC X(64).
           03  WRK-MCP-PATHNAME    PIC X(64).
      *
           03  WRK-RECEERR               PIC X(200).
      *
           03  WRK-HKNNUM                PIC X(03).
      *
           03  WRK-JIHI-FLG              PIC X(01).
      *
           03  WRK-CHOKI                 PIC X(03).
      *
           03  WRK-ZAITAKU               PIC X(01).
      *
           03  RJN-RATE-KBN              PIC X(01).
      *
           03  WRK-RECESKYKBN            PIC  X(01).
      *
           03  WRK-TEISYOTOKU-YMD.
               05  WRK-TEISYOTOKU-YM     PIC 9(06).
               05  WRK-TEISYOTOKU-DD     PIC 9(02).
      *
           03  WK-PTNYUINRRK-TENSTUYMD   PIC  X(08).
           03  WK-PTNYUINRRK-TENSTUYMD2  PIC  X(08).
      *
      *    患者年齢算出用エリア
       01  WRK-NENREI-AREA.
           03  WRK-NENREI                PIC 9(03).
           03  WRK-YMDS1.
               05  WRK-YYS1              PIC 9(04).
               05  WRK-MMS1              PIC 9(02).
               05  WRK-DDS1              PIC 9(02).
           03  WRK-YMDS2.
               05  WRK-YYS2              PIC 9(04).
               05  WRK-MMS2              PIC 9(02).
               05  WRK-DDS2              PIC 9(02).
           03  WRK-YMDS2R    REDEFINES   WRK-YMDS2.
               05  WRK-YYMMS2R           PIC 9(06).
               05  WRK-DDS2R             PIC 9(02).
      *              
      *    診療回数テーブル（入院会計用　１：基本　２：組合せ）
           03  WRK-DAY-AREA.
               05  WRK-DAY-TBL         OCCURS  2.
                   07  WRK-DAY         PIC 9(03)
                                           OCCURS   31.
      *    保険組合せ番号情報
           03  WRK-HKNCOMBIINF-AREA.
               05  WRK-HKNCOMBIINF     OCCURS  10.
      *            保険組合せ番号
                   07  WRK-HKNCOMBI    PIC 9(04).
      *            適用開始年月日
                   07  WRK-TEKSTYMDX   PIC X(08). 
      *            適用終了年月日
                   07  WRK-TEKEDYMDX   PIC X(08).
      *        適用開始年月日
               05  WRK-TEKSTYMD-X      PIC X(08). 
      *        適用終了年月日
               05  WRK-TEKEDYMD-X      PIC X(08). 
      *     
      *    ワーク保険組合せ（入院会計用） 
       01  WRK-HKNCOMBI-TABLE.
           02  WRK-HKNCOMBI-REC        OCCURS  10. 
           COPY    "CPHKNCOMBI.INC"    REPLACING  //COMB-//
                                       BY         //WRK-COMB-//.
           03  WRK-HKNCOMBI-SYOKBN     PIC X(01).   
      *
      *    ワーク入退院履歴
       01  WRK-RECE12-REC. 
           COPY    "CPRCF068.INC"  REPLACING  //RECE12//
                                   BY         //WRK-RECE12//.
      *
      *    ワーク入院履歴
       01  WRK-PTNYUINRRK-REC.
           COPY    "CPPTNYUINRRK.INC"  REPLACING  //PTNYUINRRK-//
                                       BY         //WRK-PTNYUINRRK-//.
      * 
      *    保険番号マスタ退避ワーク
       01  WRK-HKNNUM-AREA.
           03  WRK-HKNNUM-IDX                  PIC 9(05).
           03  WRK-HKNNUM-MAX                  PIC 9(05).
           03  WRK-CHK-HKNNUM                  PIC X(03).
      *
       01  TABLE-HKNNUM-AREA.
         02  TBL-HKNNUM-REC                    OCCURS  2000.
           COPY    "CPHKNNUM.INC"  REPLACING   //HKN-//
                                   BY          //TBL-HKN-//.        
       01  TBL-HKNNUM-CONST-MAX                PIC 9(05)
                                               VALUE   2000.
      *
      *    主保険用保険番号情報
       01  WRK-HKNNUM-REC.
           COPY    "CPHKNNUM.INC"  REPLACING  //HKN-//
                                   BY         //WRK-HKN-//.
      *
       01  WRK-CONS-AREA.
      *    
      *    生活保護保険番号 
           03  WRK-CONS-SEIHO-HKNNUM
                                   PIC X(03)   VALUE   "012". 
      *    
      *    自費保険番号 
           03  WRK-CONS-JIHI-HKNNUM
                                   PIC X(03)   VALUE   "980". 
      *
      *    労災保険番号 
           03  WRK-CONS-ROUSAI-HKNNUM
                                   PIC X(03)   VALUE   "971". 
      *    自賠責保険番号 
           03  WRK-CONS-JIBAI-HKNNUM
                                    PIC X(03)   VALUE   "973".       
      *    
      *    固定端末ＩＤ
           03  WRK-CONS-TERMID     PIC X(32)   VALUE
                                   "0100007FZZZZZZZZZZZZZZZZZZZZZZZZ". 
      *
           03  WRK-CONS-SRYYM-200609
                                   PIC 9(06)   VALUE   200609.      
      *
      *    在総診テーブル
       01  TBL-ZAITAKU-AREA.
      *    寝たきり老人在宅総合診療料（院内処方せんを交付する）
           03  FILLER          PIC X(09)   VALUE   "114701210".
      *    寝たきり老人在宅総合診療料（院内処方せんを交付しない）
           03  FILLER          PIC X(09)   VALUE   "114701710".
      *    在宅末期医療総合診療料（院外）
           03  FILLER          PIC X(09)   VALUE   "114007610".
      *    在宅末期医療総合診療料（院内）
           03  FILLER          PIC X(09)   VALUE   "114007710".
      *
       01  TBL-ZAITAKU-R       REDEFINES   TBL-ZAITAKU-AREA.
            03  TBL-ZAITAKU    PIC X(09)   OCCURS   4.       
      *
       01  TBL-ZAITAKU-MAX     PIC 9(02)   VALUE    4.  
      *
      *    在総診テーブル（２００６年４月診療分〜）
       01  TBL-ZAITAKU2-AREA.
      *    在宅末期医療総合診療料（院外）
           03  FILLER          PIC X(09)   VALUE   "114007610".
      *    在宅末期医療総合診療料（院内）
           03  FILLER          PIC X(09)   VALUE   "114007710".
      *    在宅時医学総合管理料１（院外処方せん）
           03  FILLER          PIC X(09)   VALUE   "114012210".
      *    在宅時医学総合管理料１（院内処方せん）
           03  FILLER          PIC X(09)   VALUE   "114012310".
      *    在宅時医学総合管理料２（院内処方せん）
           03  FILLER          PIC X(09)   VALUE   "114012410".
      *    在宅時医学総合管理料２（院外処方せん）
           03  FILLER          PIC X(09)   VALUE   "114007510".
      *    在医総・在医総管（ＤＵＭＭＹ）
           03  FILLER          PIC X(09)   VALUE   "099140003".
      *
       01  TBL-ZAITAKU2-R      REDEFINES   TBL-ZAITAKU2-AREA.
            03  TBL-ZAITAKU2   PIC X(09)   OCCURS   7.
      *
       01  TBL-ZAITAKU2-MAX    PIC 9(02)   VALUE    7.
      *
           COPY    "MCPAREA".
      *
           COPY    "COMMON-SPA".
      *
      *    レセ電診療科コードテーブル
           COPY    "CMRECEDENSRYKACD.INC".
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *    患者保険情報
       01  PTHKNINF-REC.
           COPY    "CPPTHKNINF.INC".
      *
      *    患者公費情報
       01  PTKOHINF-REC.
           COPY    "CPPTKOHINF.INC".
      *
      *    保険番号
       01  HKNNUM-REC.
           COPY    "CPHKNNUM.INC".
      *
      *    システム管理マスタ
           COPY    "CPSYSKANRI.INC".
      *
      *    医療機関情報
           COPY    "CPSK1001.INC".
      *    
           COPY    "CPSK1005.INC".
      *
           COPY    "CPSK2005.INC".
      *
           COPY    "CPSK2009.INC".
      *
      *    月代わり受給者番号
       01  MONTHLYNUM-REC.
           COPY    "CPMONTHLYNUM.INC".
      *
      *    低所得
       01  TSYRRK-REC.
           COPY    "CPTSYRRK.INC".
      *
      *    年金
       01  TNKRRK-REC.
           COPY    "CPTNKRRK.INC".
      *
      *    診療会計
       01  SRYACCT-REC.
           COPY    "CPSRYACCT.INC".
      *
      *    診療行為
       01  SRYACT-REC.
           COPY    "CPSRYACT.INC".
      *
      *    入院履歴
       01  PTNYUINRRK-REC.
           COPY    "CPPTNYUINRRK.INC".
      *
      *    入院会計
       01  NYUINACCT-REC.
           COPY    "CPNYUINACCT.INC".
      *
      *    保険組み合わせ
       01  HKNCOMBI-REC.
           COPY    "CPHKNCOMBI.INC".
      *
      *    旧姓履歴情報
       01  KYUSEIRRK-REC.
           COPY    "CPKYUSEIRRK.INC".
      *
      *****************************************************************
      *    サブプロ用領域
      *****************************************************************
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *    数字変換領域
           COPY    "CPORCSNUM.INC".
      *    患者番号変換サブ
           COPY    "CPORCSPTID.INC".
      *   ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
      *    主科取得サブ
       01  ORCSSYUACCAREA.
           COPY    "CPORCSSYUACC.INC".
      *
      *    レセプト種別返却サブ
           COPY    "CPORCSSYUBETUGET.INC".
      *
      ****************************************************************
       LINKAGE                 SECTION.
       01  COMMAND-PARAM.
           02  FILLER      PIC X(256).
      ****************************************************************
       PROCEDURE           DIVISION
               USING
           COMMAND-PARAM.
      *
      *
      *****************************************************************
      *    主処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC    UNTIL   FLG-END =   1 
      *
           PERFORM 300-TERM-SEC
      *
           IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
               STOP    RUN
           ELSE
               EXIT    PROGRAM
           END-IF 
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  CNT-AREA
                                       SPA-AREA
                                       WRK-AREA
                                       WRK-HKNNUM-AREA
                                       TABLE-HKNNUM-AREA  
      *
           MOVE    ZERO                TO  FLG-END
      *
           MOVE    COMMAND-PARAM       TO  WRK-PARA
      *
           MOVE    "RECEERR"           TO  RECEERR-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECEERR-TERMID
      *
           IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
      *    ＤＢオープン処理
               PERFORM 100-DBOPEN-SEC
      *
      *    ステップ管理開始処理
               MOVE    "STS"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               MOVE    "ORCR0020"      TO  JOB-PGID
               MOVE    "保険情報編集"  TO  JOB-SHELLMSG
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA  
                                       JOBKANRI-REC
      *    処理中止設定処理
               PERFORM 500-CANCEL-HENSYU-SEC
           END-IF
      *         
      *    レセプト編集区分情報
           IF      FLG-END             =   ZERO
               MOVE    SPACE           TO  SYSKANRI-REC
               INITIALIZE                  SYSKANRI-REC
               MOVE    "2009"          TO  SYS-KANRICD
               MOVE    "*"             TO  SYS-KBNCD
               MOVE    WRK-PARA-SRYYM  TO  SYS-STYUKYMD (1:6)
               MOVE    "01"            TO  SYS-STYUKYMD (7:2)
               MOVE    SYS-STYUKYMD    TO  SYS-EDYUKYMD
               MOVE    "tbl_syskanri"  TO  WRK-MCP-TABLE
               MOVE    "key10"         TO  WRK-MCP-PATHNAME
               PERFORM 960-KANRIMST-READ-SEC
               IF      FLG-SYSKANRI    =   ZERO
                   MOVE    MCPDATA-REC     TO  SYS-2009-REC
               ELSE    
                   INITIALIZE                  SYS-2009-REC
               END-IF
               IF      SYS-2009-SYUKA-KBN
                                       =   SPACE
                   MOVE    "0"             TO  SYS-2009-SYUKA-KBN
               END-IF
               IF    ( SYS-2009-SYUKA-KBN
                                       =   "1"   )
               AND   ( SYS-2009-SYUKA-SYAHO
                                       =   SPACE )
                   MOVE    "1"             TO  SYS-2009-SYUKA-SYAHO
               END-IF      
               IF    ( SYS-2009-SYUKA-KBN
                                       =   "1"   )
               AND   ( SYS-2009-SYUKA-KOKUHO
                                       =   SPACE )
                   MOVE    "0"             TO  SYS-2009-SYUKA-KOKUHO
               END-IF
      *         
      *        レセプト編集情報
               MOVE    SPACE           TO  SYSKANRI-REC
               INITIALIZE                  SYSKANRI-REC
               MOVE    "2005"          TO  SYS-KANRICD
               MOVE    "*"             TO  SYS-KBNCD
               MOVE    WRK-PARA-SRYYM  TO  SYS-STYUKYMD (1:6)
               MOVE    "01"            TO  SYS-STYUKYMD (7:2)
               MOVE    SYS-STYUKYMD    TO  SYS-EDYUKYMD
               MOVE    "tbl_syskanri"  TO  WRK-MCP-TABLE
               MOVE    "key10"         TO  WRK-MCP-PATHNAME
               PERFORM 960-KANRIMST-READ-SEC
               IF      FLG-SYSKANRI    =   ZERO
                   MOVE    MCPDATA-REC     TO  SYS-2005-REC
               ELSE    
                   INITIALIZE                  SYS-2005-REC
               END-IF
               IF      SYS-2005-NYUTAIINREKI-KSIKBN
                                       =   SPACE
                   MOVE    "0"         TO  SYS-2005-NYUTAIINREKI-KSIKBN
               END-IF
          END-IF
      *
          .
       100-INIT-EXT.
           EXIT.
      * 
      *****************************************************************
      *    主処理
      *****************************************************************
       200-MAIN-SEC                SECTION.
      *
           PERFORM VARYING IDZ FROM    1   BY  1
                   UNTIL   IDZ >       2
                   OR      FLG-END     =   1
      *
               IF      IDZ             =   1               
      *    レセプト処理
                   MOVE    "RECE010"           TO  RECE01PARA-FILE-ID
                   MOVE    "RECE020"           TO  RECE02PARA-FILE-ID
                   MOVE    "RECE090"           TO  RECE09PARA-FILE-ID
                   MOVE    "RECE110"           TO  RECE11PARA-FILE-ID
                   MOVE    "RECE120"           TO  RECE12PARA-FILE-ID
                   MOVE    WRK-PARA-TERMID     TO  RECE02PARA-TERMID
               ELSE
      *    請求書処理
                   MOVE    "RECE01X"           TO  RECE01PARA-FILE-ID
                   MOVE    "RECE02X"           TO  RECE02PARA-FILE-ID
      *-->         MOVE    WRK-CONS-TERMID     TO  RECE02PARA-TERMID
                   IF      WRK-PARA-SHELLID    =   "RECEPTX"
                     MOVE    WRK-PARA-TERMID   TO  RECE02PARA-TERMID
                   ELSE
                     MOVE    WRK-CONS-TERMID   TO  RECE02PARA-TERMID
                   END-IF
               END-IF
               MOVE    WRK-PARA-TERMID     TO  RECE01PARA-TERMID
               MOVE    WRK-PARA-TERMID     TO  RECE09PARA-TERMID
               MOVE    WRK-PARA-TERMID     TO  RECE11PARA-TERMID
               MOVE    WRK-PARA-TERMID     TO  RECE12PARA-TERMID
      *
               PERFORM 200-HENSYU-SEC
           END-PERFORM 
      *
           MOVE    1                   TO  FLG-END
           .
       200-MAIN-EXT.
           EXIT.
      * 
      *****************************************************************
      *    主処理
      *****************************************************************
       200-HENSYU-SEC                SECTION.
      *
           MOVE    ZERO                TO  FLG-RECE01
      *
           OPEN    OUTPUT              RECE022-FILE
           CLOSE                       RECE022-FILE
      *
           IF      IDZ             =   1               
               OPEN    OUTPUT              RECE091-FILE
               CLOSE                       RECE091-FILE
      *
               OPEN    I-O                 RECE09-FILE
      *
               OPEN    OUTPUT              RECE11-FILE
               CLOSE                       RECE11-FILE
      *
               OPEN    OUTPUT              RECE12X-FILE
               CLOSE                       RECE12X-FILE
               OPEN    I-O                 RECE12-FILE
      *
           END-IF    
      *
           OPEN    INPUT               RECE01-FILE
           IF      STS-RECE01      NOT =   ZERO
               DISPLAY "*** ORCR0020 RECE01 OPEN ERR *** " STS-RECE01 
               MOVE    1                   TO  FLG-END
               GO  TO  200-HENSYU-EXT
           END-IF
      *
           OPEN    I-O                 RECE02-FILE
      *
           PERFORM     RECE01-READ-SEC
      *
           PERFORM                     UNTIL   FLG-RECE01  =   1
                                       OR      FLG-END     =   1
      *
      *        保険履歴情報編集処理
               IF      IDZ             =   1               
                   PERFORM 230-RECE09-HENSYU-SEC
               END-IF
      *            
      *        編集処理
      *        処理区分（社保・国保）により読み飛ばす
               EVALUATE    WRK-PARA-RECEKBN
                   WHEN    "0"
                       PERFORM 210-HENSYU-SEC
                   WHEN    "1"
                       IF      RECE01-HKNNUM   NOT =   "060" AND "067"
                                                   AND "068" AND "069"    
                           PERFORM 210-HENSYU-SEC
                       END-IF
                   WHEN    "2"
                       IF      RECE01-HKNNUM       =   "060" OR  "067"
                                                   OR  "068" OR  "069"
                           PERFORM 210-HENSYU-SEC
                       END-IF
               END-EVALUATE
      *
               PERFORM     RECE01-READ-SEC
      *
      *        処理中止設定処理
               IF      CNT-IN-CANCEL       =   50
                   MOVE    ZERO                TO  CNT-IN-CANCEL
                   PERFORM 500-CANCEL-HENSYU-SEC
               END-IF
           END-PERFORM
      *
           CLOSE   RECE01-FILE
                   RECE02-FILE
      *
           IF      IDZ             =   1               
               CLOSE   RECE09-FILE
                       RECE12-FILE
           END-IF    
           .
       200-HENSYU-EXT.
           EXIT.
      * 
      *****************************************************************
      *    編集処理
      *****************************************************************
       210-HENSYU-SEC                   SECTION.
      *
           MOVE    SPACE                TO  WK-RECESYUBETU-AREA
           INITIALIZE                       WK-RECESYUBETU-AREA
      *     
           MOVE    ZERO                 TO  TOKKI-IDX
      *
           MOVE    SPACE                TO  RECE02-REC
           INITIALIZE                       RECE02-REC
      *
           MOVE    SPACE                TO  RJN-RATE-KBN
      *
           MOVE    RECE01-HOSPID        TO  RECE02-HOSPID
           MOVE    RECE01-SRYYM         TO  RECE02-SRYYM
           MOVE    RECE01-NYUGAIKBN     TO  RECE02-NYUGAIKBN
           MOVE    RECE01-PTID          TO  RECE02-PTID
           MOVE    RECE01-RECEKA        TO  RECE02-RECEKA
           IF      RECE01-HKNNUM (1:2)  =   "98"
               MOVE    RECE01-HKNNUM        TO  RECE02-HKNJANUM-KEY
           ELSE  
               MOVE    RECE01-HKNJANUM      TO  RECE02-HKNJANUM-KEY
           END-IF
           MOVE    RECE01-HOJOKBN       TO  RECE02-HOJOKBN-KEY
      *****MOVE    RECE01-TEKSTYMD      TO  RECE02-TEKSTYMD
      *****MOVE    RECE01-TEKSTYMDX     TO  RECE02-TEKSTYMD
           PERFORM VARYING IDX FROM    1   BY  1
                   UNTIL   IDX >       10
                   OR      RECE01-TEKSTYMDX (IDX)  =   SPACE
               IF      RECE02-TEKSTYMD     =   SPACE 
                   MOVE    RECE01-TEKSTYMDX (IDX)  TO  RECE02-TEKSTYMD
               ELSE
                   IF      RECE02-TEKSTYMD <   RECE01-TEKSTYMDX (IDX)
                       MOVE    RECE01-TEKSTYMDX (IDX)  
                                               TO  RECE02-TEKSTYMD
                   END-IF
               END-IF
               IF      RECE02-TEKSTYMD-X   =   SPACE 
                   MOVE    RECE01-TEKSTYMDX (IDX)  TO  RECE02-TEKSTYMD-X
                   MOVE    RECE01-TEKEDYMDX (IDX)  TO  RECE02-TEKEDYMD-X
               ELSE
                   IF      RECE02-TEKSTYMD-X >   RECE01-TEKSTYMDX (IDX)
                       MOVE    RECE01-TEKSTYMDX (IDX)  
                                               TO  RECE02-TEKSTYMD-X
                   END-IF
                   IF      RECE02-TEKEDYMD-X <   RECE01-TEKEDYMDX (IDX)
                       MOVE    RECE01-TEKEDYMDX (IDX)  
                                               TO  RECE02-TEKEDYMD-X
                   END-IF
               END-IF
           END-PERFORM
      *    患者番号
           MOVE    RECE01-PTNUM        TO  RECE02-PTNUM
      *    診療科情報のセット
           PERFORM   VARYING   IDX   FROM   1   BY   1
                     UNTIL    (IDX      >   20)    OR
                              (RECE01-SRYKA(IDX)  =   SPACE)
               MOVE  RECE01-SRYKA(IDX)    TO   RECE02-SRYKA(IDX)
           END-PERFORM
      *    審査支払機関
           IF      RECE01-HKNNUM        =   "060"   OR    "067"   OR
                                            "068"   OR    "069"
               MOVE    2                TO  RECE02-TEISYUTUSAKI
           ELSE
               MOVE    1                TO  RECE02-TEISYUTUSAKI
           END-IF
      *    患者情報の取得
           MOVE    RECE01-KANANAME     TO  RECE02-KANANAME
           MOVE    RECE01-NAME         TO  RECE02-NAME
           MOVE    RECE01-SEX          TO  RECE02-SEX
           MOVE    RECE01-BIRTHDAY     TO  RECE02-BIRTHDAY
           MOVE    RECE01-DEATHKBN     TO  RECE02-DEATHKBN
           MOVE    RECE01-TSTPTNUMKBN  TO  RECE02-TSTPTNUMKBN
           MOVE    RECE01-AGE          TO  WRK-NENREI
                                           RECE02-AGE
      *    旧姓履歴の取得（月途中で保険変更があるとき）
           IF    ( RECE01-TEISEIKBN    NOT =   "1"   )
           AND   ( RECE02-TEKSTYMD-X   NOT =   SPACE )
               INITIALIZE                      STS-AREA-DAY
               INITIALIZE                      LNK-DAY7-AREA
               MOVE    "71"                TO  LNK-DAY7-IRAI
               MOVE    RECE02-SRYYM        TO  LNK-DAY7-YM
               CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                                   LNK-DAY7-AREA
               IF    ( RECE02-TEKEDYMD-X   <   LNK-DAY7-KOYOMI )
                   INITIALIZE                      KYUSEIRRK-REC
                   MOVE    RECE02-HOSPID       TO  KYUSEI-HOSPID
                   MOVE    RECE02-PTID         TO  KYUSEI-PTID
                   MOVE    RECE02-TEKEDYMD-X   TO  KYUSEI-CHGYMD
                   MOVE    KYUSEIRRK-REC       TO  MCPDATA-REC
                   PERFORM 900-KYUSEIRRK-READ-SEC
                   IF       FLG-KYUSEIRRK      =   ZERO
                       MOVE    KYUSEI-NAME         TO  RECE02-NAME
                       MOVE    KYUSEI-KANANAME     TO  RECE02-KANANAME
                   END-IF
               END-IF
           END-IF
      *    県番号
           MOVE    RECE01-PREFNUM      TO  RECE02-PREFNUM
      *    患者保険情報テーブルから主保険情報の取得
           INITIALIZE                      WRK-HKNNUM-REC
      *
           IF      RECE01-HKNID        NOT =   ZERO
               MOVE    RECE01-HKNNUM   TO  RECE02-HKNNUM
               MOVE    RECE01-HKNID    TO  RECE02-HKNID
      *        法別番号のセットと給付割合情報の取得
      *        （退職特別療養費の場合は退職国保から取得する）
               INITIALIZE                  HKNNUM-REC
               MOVE   RECE02-HOSPID    TO  HKN-HOSPID
               IF     RECE02-HKNNUM    =   "069"
                   MOVE    "067"           TO  HKN-HKNNUM
               ELSE
                   MOVE   RECE02-HKNNUM    TO  HKN-HKNNUM
               END-IF
               PERFORM 900-HKNNUM-CHK-SEC
               IF      FLG-HKNNUM      =   ZERO
                   MOVE    HKNNUM-REC      TO  WRK-HKNNUM-REC
                   MOVE    HKN-HBTNUM      TO  RECE02-HKNHBTNUM
                   MOVE    SPACE           TO  WRK-JIHI-FLG
                   IF      HKN-HKNKOHSBTKBN    =   "8"
                       MOVE   "1"              TO  WRK-JIHI-FLG
                   END-IF
               END-IF
               MOVE    RECE01-HKN-HKNJANUM  TO  RECE02-HKNJANUM
               MOVE    RECE01-HKN-HONKZKKBN TO  RECE02-HONKZKKBN
                                                WK-HONKZKKBN
               MOVE    RECE01-HKN-HOJOKBN   TO  RECE02-HOJOKBN
               MOVE    RECE01-CONTKBN       TO  RECE02-CONTKBN
               MOVE    RECE01-KIGO          TO  RECE02-KIGO
               MOVE    RECE01-NUM           TO  RECE02-NUM
               MOVE    RECE01-CERTNUM       TO  RECE02-CERTNUM
           END-IF
      *    患者公費情報テーブルから老人保険情報の取得
           IF    ( RECE01-KOHHKNNUM-O (1)  =   "027" )
           OR    ( RECE01-KOHID            >   ZERO  )
               INITIALIZE                      PTKOHINF-REC
               MOVE    RECE01-HOSPID       TO  PTKOH-HOSPID
               MOVE    RECE01-PTID         TO  PTKOH-PTID
               MOVE    RECE01-KOHID        TO  PTKOH-KOHID
               MOVE    PTKOHINF-REC        TO  MCPDATA-REC
               MOVE    "DBSELECT"          TO  MCP-FUNC
               MOVE    "PTKOHINF-KEY"      TO  ORC-DBPATH
               CALL    "ORCMCPSUB"         USING   MCPAREA
                                                   ORCMCPAREA
                                                   MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    "PTKOHINF-KEY"      TO  ORC-DBPATH
                   PERFORM PTKOHINF-READ-SEC
                   IF      FLG-PTKOHINF        =   ZERO
                       MOVE    PTKOH-KOHID         TO  RECE02-RJNHKNID
                       MOVE    PTKOH-KOHNUM        TO  RECE02-RJNHKNNUM
                       IF    ( PTKOH-TEKSTYMD (1:6)
                                                   =   RECE01-SRYYM    )
                       AND   ( PTKOH-TEKSTYMD      >   RECE02-TEKSTYMD )
                           MOVE    PTKOH-TEKSTYMD  TO  RECE02-TEKSTYMD
                       END-IF
      *                法別番号のセット
                       INITIALIZE                  HKNNUM-REC
                       MOVE   RECE01-HOSPID    TO  HKN-HOSPID
                       MOVE   PTKOH-KOHNUM     TO  HKN-HKNNUM
                       PERFORM 900-HKNNUM-CHK-SEC
                       IF       FLG-HKNNUM     =   ZERO
                           MOVE   HKN-HBTNUM TO RECE02-RJNHBTNUM
                       END-IF
                       MOVE    PTKOH-FTNJANUM   TO  RECE02-RJNFTNJANUM
                       MOVE    PTKOH-JKYSNUM    TO  RECE02-RJNJKYSNUM
                       MOVE    PTKOH-PAYKBN     TO  RECE02-RJNPAYKBN
      *----(01.00.14) LINE ADD START --------------------------
      *                平成１４年１０月対応
      *                老人保険の患者について負担割合を退避
                       IF   RECE02-SRYYM   >=   "200210"
                            EVALUATE    PTKOH-PAYKBN
                                WHEN    "01"
                                    MOVE      "1"   TO  RJN-RATE-KBN
                                WHEN    "02"
                                    MOVE      "2"   TO  RJN-RATE-KBN
                                WHEN    "03"
                                    MOVE      "3"   TO  RJN-RATE-KBN
                            END-EVALUATE
                       END-IF
      *----(01.00.14) LINE ADD END   --------------------------
                   ELSE
                        DISPLAY  "PTKOHINF FETCH ERR" 
                   END-IF
               ELSE
                   DISPLAY   "PTKOHINF SELECT ERR" 
               END-IF
               MOVE   "PTKOHINF-KEY"      TO  WK-PATH
               PERFORM   CSR-CLOSE-SEC
           END-IF
      *
      *    長期高額療養費の編集
           MOVE    SPACE              TO   WRK-CHOKI
           PERFORM   VARYING   IDX   FROM    1   BY   1
                     UNTIL    (IDX     >   4)    
                     OR       (RECE01-KOHHKNNUM-O(IDX)  =   SPACE)
               IF    RECE01-KOHHKNNUM-O(IDX)     =    "027"
                   CONTINUE
               ELSE
                 INITIALIZE                        HKNNUM-REC
                 MOVE    RECE01-HOSPID         TO  HKN-HOSPID
                 MOVE    RECE01-KOHHKNNUM-O(IDX) TO  HKN-HKNNUM
                 MOVE    "00"                  TO  HKN-PAYKBN
                 MOVE    RECE01-SRYYM          TO  HKN-TEKSTYMD(1:6)
                 MOVE    "01"                  TO  HKN-TEKSTYMD(7:2)
                 MOVE    RECE01-SRYYM          TO  HKN-TEKEDYMD(1:6)
                 MOVE    "01"                  TO  HKN-TEKEDYMD(7:2)
                 MOVE    HKNNUM-REC            TO  MCPDATA-REC
                 MOVE    "DBSELECT"            TO  MCP-FUNC
                 MOVE    "HKNNUM-KEY"          TO  ORC-DBPATH
                 CALL    "ORCMCPSUB"            USING   MCPAREA
                                                        ORCMCPAREA
                                                        MCPDATA-REC
                 IF      MCP-RC                =   ZERO
                   MOVE    "HKNNUM-KEY"        TO  ORC-DBPATH
                   PERFORM  HKNNUM-READ-SEC
                   IF       FLG-HKNNUM    =   0
                       IF   HKN-HKNKOHSBTKBN   =   "2"
                           EVALUATE  HKN-HKNNUM
                            WHEN     "972"
                              ADD      1           TO  TOKKI-IDX
                              MOVE    "02"         TO  RECE02-TOKKI
                                                       (TOKKI-IDX)
                            WHEN     "974"
                              ADD      1           TO  TOKKI-IDX
                              MOVE    "16"         TO  RECE02-TOKKI
                                                       (TOKKI-IDX)
                           END-EVALUATE
                           MOVE    RECE01-KOHHKNNUM-O(IDX)  TO
                                                    WRK-CHOKI 
                           MOVE    1            TO  FLG-HKNNUM
                           MOVE    4            TO  IDX  
                       END-IF     
                   END-IF
                 END-IF
                 MOVE    "HKNNUM-KEY"      TO  WK-PATH
                 PERFORM CSR-CLOSE-SEC
               END-IF
           END-PERFORM
      *
      *    公費情報のセット
           MOVE    ZERO        TO  IDX2
           PERFORM   VARYING   IDX    FROM   1   BY   1
                     UNTIL    (IDX          >     4)
                     OR       (RECE01-KOHID-O(IDX)  =   ZERO)  
      ******** IF  (RECE01-KOHHKNNUM-O (IDX)  =       "027")  OR
      ********     (RECE01-KOHHKNNUM-O (IDX)  =   WRK-CHOKI)
               IF  (RECE01-KOHHKNNUM-O (IDX)  =       "027")
                     CONTINUE
               ELSE
                   IF      RECE01-KOHHKNNUM-O (IDX)  =   WRK-CHOKI
                       MOVE   "4"             TO  WRK-RECESKYKBN
                   ELSE
                       MOVE   RECE01-RECESKYKBN-O (IDX)
                                             TO  WRK-RECESKYKBN
                   END-IF
                   PERFORM   KOHI-SET-SEC
               END-IF
           END-PERFORM
      *
      *    レセプト種別
           PERFORM   RECESYUBETU-SET-SEC
      *
      *    主保険給付割合のセット
           PERFORM   KYURATE-SET-SEC
      *
      *    保険組合せ番号情報
           PERFORM VARYING IDX    FROM   1   BY   1
                   UNTIL   IDX          >     10
                   OR          RECE01-HKNCOMBI-O(IDX)  =   ZERO
               MOVE    RECE01-HKNCOMBI-O(IDX)
                                       TO  RECE02-HKNCOMBI(IDX)
               MOVE    RECE01-HKNNUMHC (IDX)
                                       TO  RECE02-HKNNUMHC(IDX)
               PERFORM VARYING IDY2    FROM    1   BY  1
                       UNTIL   IDY2    >       4
                   IF   ( RECE01-KOHNUMHC (IDX IDY2)
                                       NOT =   "027"       )
                   AND  ( RECE01-KOHNUMHC (IDX IDY2) (1:1)
                                           =   "0" OR  "9" )
                   AND  ( RECE01-KOHNUMHC (IDX IDY2) (1:2)
                                       NOT =   "99"        )
                       MOVE    1           TO  RECE02-HKNCOMBIFLG(IDX)
                       MOVE    4           TO  IDY2
                   END-IF
               END-PERFORM
      *
               MOVE    ZERO     TO   FLG-TAISYO
               PERFORM VARYING IDY2    FROM    1   BY  1
                       UNTIL   IDY2    >       4
                   IF   RECE01-KOHNUMHC (IDX IDY2) NOT = SPACE
                     IF RECE01-KOHNUMHC (IDX IDY2) = "972" OR "974"
                        IF   FLG-TAISYO  =  ZERO
                           MOVE   1     TO   FLG-TAISYO
                        END-IF
                     ELSE
                        MOVE      9     TO   FLG-TAISYO
                     END-IF
                   END-IF
               END-PERFORM
               IF   FLG-TAISYO  =  1
                   IF  RECE02-HKNCOMBIFLG(IDX)  =  1
                        MOVE      2     TO   RECE02-HKNCOMBIFLG(IDX)
                   ELSE
                        MOVE      3     TO   RECE02-HKNCOMBIFLG(IDX)
                   END-IF
               END-IF
      *        組合せ公費保険番号情報のセット
               PERFORM   KOHNUMHC-SET-SEC
           END-PERFORM
      *
      *
      *    特記事項のセット
      *    老保の編集
           MOVE    ZERO       TO     FLG-ROUJINHOKEN
           IF      RECE02-HKNNUM        =   SPACE
      ***  AND     RECE02-KOHHBTNUM (1)   NOT =  SPACE
      ***  AND     RECE02-KOHHBTNUM (2) =   SPACE
      ***  AND     RECE02-KOHHBTNUM (3) =   SPACE
      ***  AND     RECE02-KOHHBTNUM (4) =   SPACE
               IF  (   RECE01-SRYYM         >=  "200210"            )
               AND ((  RECE02-AGE           >=  75             ) OR 
                    (( RECE02-BIRTHDAY      <=  19320930 ) AND
                     ( RECE02-AGE           >=  70       )     )    )
                   ADD      1           TO  TOKKI-IDX
                   MOVE    "04"         TO  RECE02-TOKKI (TOKKI-IDX)
               ELSE
                   INITIALIZE                       PTKOHINF-REC
                   MOVE    RECE01-HOSPID        TO  PTKOH-HOSPID
                   MOVE    RECE01-PTID          TO  PTKOH-PTID
                   MOVE    "027"                TO  PTKOH-KOHNUM
                   MOVE    PTKOHINF-REC         TO  MCPDATA-REC
                   MOVE    "DBSELECT"           TO  MCP-FUNC
                   MOVE    "PTKOHINF-KEY4"      TO  ORC-DBPATH
                   CALL    "ORCMCPSUB"           USING   MCPAREA
                                                         ORCMCPAREA
                                                         MCPDATA-REC
                   IF      MCP-RC               =       ZERO
                       MOVE    "PTKOHINF-KEY4"  TO      ORC-DBPATH
                       PERFORM  PTKOHINF-READ-SEC
                       PERFORM  UNTIL   FLG-PTKOHINF    =   1
                           IF      PTKOH-TEKSTYMD(1:6)  <=
                                                    RECE01-SRYYM
                           AND     RECE01-SRYYM         <=
                                                    PTKOH-TEKEDYMD(1:6)
                           AND     PTKOH-SAKJOKBN   =  SPACE
                               ADD      1           TO  TOKKI-IDX
                               MOVE    "04"         TO  RECE02-TOKKI
                                                            (TOKKI-IDX)
                               MOVE    1            TO  FLG-ROUJINHOKEN
                               MOVE    1            TO  FLG-PTKOHINF     
                           ELSE
                              MOVE    "PTKOHINF-KEY4"  TO  ORC-DBPATH
                              PERFORM  PTKOHINF-READ-SEC
                           END-IF   
                       END-PERFORM
                   END-IF
                   MOVE    "PTKOHINF-KEY4"   TO  WK-PATH
                   PERFORM CSR-CLOSE-SEC
               END-IF
           END-IF              
      *
           IF   RECE01-SRYYM    >=  "200210"
      *
             MOVE    RECE01-SRYYM        TO  WRK-TEISYOTOKU-YM
             MOVE    01                  TO  WRK-TEISYOTOKU-DD
      *
      *      年金履歴より
             INITIALIZE                      TNKRRK-REC
             MOVE    RECE01-HOSPID       TO  TNKRRK-HOSPID
             MOVE    RECE01-PTID         TO  TNKRRK-PTID
             MOVE    TNKRRK-REC          TO  MCPDATA-REC
             MOVE    "DBSELECT"          TO  MCP-FUNC
             MOVE    "TNKRRK-KEY3"       TO  ORC-DBPATH
             CALL    "ORCMCPSUB"          USING     MCPAREA
                                                    ORCMCPAREA
                                                    MCPDATA-REC
             IF      MCP-RC              =   ZERO
                 MOVE    "TNKRRK-KEY3"      TO  ORC-DBPATH
                 PERFORM TNKRRK-READ-SEC
                 PERFORM  UNTIL  
                       ( FLG-TNKRRK          =   1                ) OR
                      (( TNKRRK-STYMD       <=  WRK-TEISYOTOKU-YMD  AND
                         WRK-TEISYOTOKU-YMD <=  TNKRRK-EDYMD      ))  
      *
                        MOVE    "TNKRRK-KEY3"      TO  ORC-DBPATH
                        PERFORM TNKRRK-READ-SEC
      *
                 END-PERFORM
             END-IF
      *
             MOVE   "TNKRRK-KEY3"       TO  WK-PATH
             PERFORM   CSR-CLOSE-SEC
      *
      *      低所得より
             INITIALIZE                      TSYRRK-REC
             MOVE    RECE01-HOSPID       TO  TSYRRK-HOSPID
             MOVE    RECE01-PTID         TO  TSYRRK-PTID
             MOVE    TSYRRK-REC          TO  MCPDATA-REC
             MOVE    "DBSELECT"          TO  MCP-FUNC
             MOVE    "TSYRRK-KEY3"       TO  ORC-DBPATH
             CALL    "ORCMCPSUB"          USING     MCPAREA
                                                    ORCMCPAREA
                                                    MCPDATA-REC
             IF      MCP-RC              =   ZERO
                 MOVE    "TSYRRK-KEY3"      TO  ORC-DBPATH
                 PERFORM TSYRRK-READ-SEC
                 PERFORM  UNTIL  
                     ( FLG-TSYRRK            =   1                ) OR
                    (( TSYRRK-NINSTYMD      <= WRK-TEISYOTOKU-YMD   AND
                       WRK-TEISYOTOKU-YMD   <= TSYRRK-NINEDYMD    ))  
      *
                        MOVE    "TSYRRK-KEY3"      TO  ORC-DBPATH
                        PERFORM TSYRRK-READ-SEC
      *
                 END-PERFORM
             END-IF
      *
             MOVE   "TSYRRK-KEY3"       TO  WK-PATH
             PERFORM   CSR-CLOSE-SEC
      *
             EVALUATE    FLG-TSYRRK    ALSO     FLG-TNKRRK
                 WHEN    1             ALSO     1
                       MOVE     SPACE   TO       RECE02-TEISYOTOKU
                 WHEN    0             ALSO     1
                       MOVE     "2"     TO       RECE02-TEISYOTOKU
                 WHEN    0             ALSO     0
                 WHEN    1             ALSO     0
                       MOVE     "1"     TO       RECE02-TEISYOTOKU
                       IF  (RECE02-SRYYM     >   WRK-CONS-SRYYM-200609 )
                       AND (TNKRRK-RRI-FUKUSHI-KBN
                                             =   "1"                   )
                           MOVE  "4"         TO       RECE02-TEISYOTOKU
                       END-IF
             END-EVALUATE
      *
           END-IF
      *
      *    在総診フラグセット
           PERFORM    ZAITAKU-SET-SEC
      *
      *    レセプト種別不明エラー情報
           IF      RECE01-TEISEIKBN      =   "1"
               MOVE    RECE01-ERR-ERRKBN     TO  RECE02-ERR-ERRKBN
               MOVE    RECE01-ERR-DAY        TO  RECE02-ERR-DAY
               MOVE    RECE01-ERR-HKNCOMBINUM
                                             TO  RECE02-ERR-HKNCOMBINUM
               MOVE    RECE01-ERR-TEKSTYMD   TO  RECE02-ERR-TEKSTYMD
               MOVE    RECE01-ERR-TEKEDYMD   TO  RECE02-ERR-TEKEDYMD
           END-IF 
      *
      *    印刷順
           IF      IDZ                 =   1
               MOVE    RECE01-PRTSEQ       TO  RECE02-PRTSEQ
           END-IF
      *
      *    レセプト請求しない地方公費情報
           IF      IDZ                 =   1
               PERFORM VARYING IDX     FROM    1   BY  1
                       UNTIL   IDX     >       4
                   MOVE    RECE01-KOHSKY-KOHID-O     (IDX)
                                       TO  RECE02-KOHSKY-KOHID  (IDX)
                   MOVE    RECE01-KOHSKY-KOHHKNNUM-O (IDX)
                                       TO  RECE02-KOHSKY-KOHNUM (IDX)
      *
                   INITIALIZE                          PTKOHINF-REC
                   MOVE    RECE02-HOSPID           TO  PTKOH-HOSPID
                   MOVE    RECE02-PTID             TO  PTKOH-PTID
                   MOVE    RECE02-KOHSKY-KOHID  (IDX)
                                                   TO  PTKOH-KOHID
                   MOVE    PTKOHINF-REC            TO  MCPDATA-REC
                   MOVE    "DBSELECT"              TO  MCP-FUNC
                   MOVE    "PTKOHINF-KEY"          TO  ORC-DBPATH
                   CALL    "ORCMCPSUB"             USING   MCPAREA
                                                           ORCMCPAREA
                                                           MCPDATA-REC
                   IF      MCP-RC                  =   ZERO
                       MOVE    "PTKOHINF-KEY"      TO  ORC-DBPATH
                       PERFORM PTKOHINF-READ-SEC
                       IF      FLG-PTKOHINF           =   ZERO
                           MOVE    PTKOH-FTNJANUM       TO
                                       RECE02-KOHSKY-KOHFTNJANUM (IDX)
                       END-IF
                   END-IF    
      *
                   MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
                   MOVE    "PTKOHINF-KEY"      TO  ORC-DBPATH
                   CALL    "ORCMCPSUB"         USING
                                               MCPAREA
                                               ORCMCPAREA
                                               MCPDATA-REC
               END-PERFORM
           END-IF
      *
      *    主科設定
           IF    ( IDZ                 =   1      )
           AND   ( SYS-2009-SYUKA-KBN  =   "1"    )
      *****AND   ( RECE02-TEISYUTUSAKI =   "1"    )
           AND   ( RECE02-RECESYUBETU  
                                   NOT =   "8888" )
             IF  (     ( RECE02-TEISYUTUSAKI   =   "1" )
                   AND ( SYS-2009-SYUKA-SYAHO
                                           NOT =   "0" ) )
             OR  (     ( RECE02-TEISYUTUSAKI   =   "2" )
                   AND ( SYS-2009-SYUKA-KOKUHO
                                           NOT =   "0" ) )
               INITIALIZE                      ORCSSYUACCAREA
               MOVE    RECE02-HOSPID   TO  SPA-HOSPID
               MOVE    RECE02-SRYYM    TO  LNK-SYUACC-I-SRYYM
               MOVE    RECE02-PTID     TO  SPA-PTID
               MOVE    RECE02-NYUGAIKBN
                                       TO  SPA-NYUGAIKBN
               MOVE    SYS-2009-SYUKA-KBN
                                       TO  SPA-SYUKA-KBN
               MOVE    SYS-2009-SYUKA-NYUIN
                                       TO  SPA-SYUKA-NYUIN
               MOVE    SYS-2009-SYUKA-GAIRAI
                                       TO  SPA-SYUKA-GAIRAI
               MOVE    SYS-2009-SYUKA-MODE
                                       TO  SPA-SYUKA-MODE
               MOVE    SYS-2009-SYUKA-SELECT
                                       TO  SPA-SYUKA-SELECT
               MOVE    RECE02-RECESYUBETU
                                       TO  LNK-SYUACC-IO-RECESYUBETU (1)
               MOVE    RECE02-HKNJANUM-KEY
                                       TO  LNK-SYUACC-IO-HKNJANUM    (1)
               MOVE    RECE02-HOJOKBN-KEY
                                       TO  LNK-SYUACC-IO-HOJOKBN     (1)
               MOVE    RECE02-TEISYUTUSAKI
                                       TO  LNK-SYUACC-IO-TEISYUTUSAKI(1)
               MOVE    "06"            TO  LNK-SYUACC-I-KBN
               CALL    "ORCSSYUKAMAIN" USING   SPA-AREA
                                               ORCSSYUACCAREA
               IF      LNK-SYUACC-IO-H-SRYKA
                                       NOT =   SPACE
                   MOVE    LNK-SYUACC-IO-H-SRYKA   TO  RECE02-SYUKA
               ELSE
                   IF      LNK-SYUACC-IO-SYUKA-INPUT (1)
                                           NOT =   SPACE
                       MOVE    LNK-SYUACC-IO-SYUKA-INPUT (1)
                                                       TO  RECE02-SYUKA
                   ELSE
                       IF      LNK-SYUACC-IO-SYUKA-AUTO (1)
                                               NOT =   SPACE
                           MOVE    LNK-SYUACC-IO-SYUKA-AUTO (1) 
                                                       TO  RECE02-SYUKA
                       ELSE
                           MOVE    "XX"                TO  RECE02-SYUKA
                       END-IF
                   END-IF
               END-IF
      *
      *        主科設定時のみ診療科名称を設定    
               IF  (     ( RECE02-TEISYUTUSAKI   =   "1" )
                     AND ( SYS-2009-SYUKA-SYAHO  =   "1" ) )
               OR  (     ( RECE02-TEISYUTUSAKI   =   "2" )
                     AND ( SYS-2009-SYUKA-KOKUHO =   "1" ) )
                   MOVE    SPACE                TO  SYSKANRI-REC
                   INITIALIZE                       SYSKANRI-REC
                   MOVE    "1005"               TO  SYS-KANRICD
                   MOVE    RECE02-SYUKA         TO  SYS-KBNCD
                   MOVE    RECE02-SRYYM         TO  SYS-STYUKYMD (1:6)
                   MOVE    "01"                 TO  SYS-STYUKYMD (7:2)
                   MOVE    SYS-STYUKYMD         TO  SYS-EDYUKYMD
                   MOVE    "tbl_syskanri"       TO  WRK-MCP-TABLE
                   MOVE    "key10"              TO  WRK-MCP-PATHNAME
                   PERFORM 960-KANRIMST-READ-SEC
                   IF      FLG-SYSKANRI         =   ZERO
                       MOVE    MCPDATA-REC         TO  SYS-1005-REC
                   ELSE
                       INITIALIZE                      SYS-1005-REC
                   END-IF
                   MOVE    SYS-1005-RECESRYKA  TO  RECE02-RECESYUKA
                   MOVE    SYS-1005-SRYKANAME  TO  RECE02-SYUKANAME
               END-IF   
             END-IF
           END-IF    
      *
      *    旧総合病院の場合の診療科名称
           IF    ( IDZ                 =   1     )
           AND   ( RECE02-RECEKA   NOT =   SPACE )
      *        レセ電診療科コード検索
               SET     TBL-RECEDENSRYKACD-IDX  TO  1
               SEARCH  TBL-RECEDENSRYKACD-OCC  
                                       VARYING TBL-RECEDENSRYKACD-IDX
                   AT  END
                       MOVE    SPACE           TO  RECE02-SYUKANAME
                   WHEN    RECE02-RECEKA   =   TBL-RECEDEN-SRYKACD
                                               (TBL-RECEDENSRYKACD-IDX)
                       MOVE    TBL-RECEDEN-SRYKANAME
                                       (TBL-RECEDENSRYKACD-IDX)
                                              TO  RECE02-SYUKANAME
               END-SEARCH
           END-IF
      *
      *    入退院履歴出力処理
           IF    ( IDZ             =   1   )
           AND   ( SYS-2005-NYUTAIINREKI-KSIKBN
                                   =   "1" )               
               PERFORM 240-PTNYUINRRK-SYORI-SEC
           END-IF
      *未請求フラグ
           IF    ( IDZ             =   1   )
               MOVE    RECE01-MISEIKYU-FLG     TO  RECE02-MISEIKYU-FLG
           END-IF
      *
           PERFORM     220-DATA-WRITE-SEC
           .
       210-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費情報のセット
      *****************************************************************
       KOHI-SET-SEC                SECTION.
      *
           INITIALIZE                           PTKOHINF-REC
           MOVE    RECE01-HOSPID        TO      PTKOH-HOSPID
           MOVE    RECE01-PTID          TO      PTKOH-PTID
           MOVE    RECE01-KOHID-O(IDX)  TO      PTKOH-KOHID
           MOVE    PTKOHINF-REC         TO      MCPDATA-REC
           MOVE    "DBSELECT"           TO      MCP-FUNC
           MOVE    "PTKOHINF-KEY"       TO      ORC-DBPATH
           CALL    "ORCMCPSUB"           USING   MCPAREA
                                                 ORCMCPAREA
                                                 MCPDATA-REC
           IF      MCP-RC               =       ZERO
               MOVE    "PTKOHINF-KEY"   TO      ORC-DBPATH
               PERFORM  PTKOHINF-READ-SEC
               IF       FLG-PTKOHINF       =   ZERO
                   ADD     1               TO  IDX2
      *
                   MOVE   PTKOH-KOHID      TO  RECE02-KOHID       (IDX2)
                   MOVE   PTKOH-KOHNUM     TO  RECE02-KOHNUM      (IDX2)
                   MOVE   WRK-RECESKYKBN   TO  RECE02-RECESKYKBN  (IDX2)
                   MOVE   PTKOH-FTNJANUM   TO  RECE02-KOHFTNJANUM (IDX2)
                   MOVE   SPACE            TO
                                           RECE02-KOHNINIKYUKBN (IDX2)
      *            法別番号・本人−外来−内容のセット
                   INITIALIZE                  HKNNUM-REC
                   MOVE   RECE01-HOSPID    TO  HKN-HOSPID
                   MOVE   PTKOH-KOHNUM     TO  HKN-HKNNUM
                   PERFORM 900-HKNNUM-CHK-SEC
                   IF       FLG-HKNNUM     =   ZERO
                       MOVE   HKN-HBTNUM          TO
                                       RECE02-KOHHBTNUM        (IDX2)
                       MOVE   HKN-HON-GAIKFTNRATE TO
                                       RECE02-HON-GAIKFTNRATE  (IDX2)
                       MOVE   HKN-HON-GAIKJGNGAK  TO
                                       RECE02-HON-GAIKJGNGAK   (IDX2)
                       MOVE   HKN-HON-GAIDJGNGAK  TO
                                       RECE02-HON-GAIDJGNGAK   (IDX2)
                       MOVE   HKN-HON-GAIDJGNCNT  TO
                                       RECE02-HON-GAIDJGNCNT   (IDX2)
                       MOVE   HKN-HON-GAIMNAIJGNGAK
                                                  TO
                                       RECE02-HON-GAIMNAIJGNGAK(IDX2)
                       MOVE   HKN-HON-GAIMGAIJGNGAK
                                                  TO
                                       RECE02-HON-GAIMGAIJGNGAK(IDX2)
                       MOVE   HKN-HON-GAIMJGNCNT  TO
                                       RECE02-HON-GAIMJGNCNT   (IDX2)
                   END-IF
      *             
      *            生保のとき月代わり受給者番号マスタより受給者番号をセット 
                   IF      PTKOH-KOHNUM    =   WRK-CONS-SEIHO-HKNNUM
                       MOVE    RECE02-HOSPID   TO  MONTHLYNUM-HOSPID
                       MOVE    RECE02-PTID     TO  MONTHLYNUM-PTID
                       MOVE    PTKOH-KOHID     TO  MONTHLYNUM-KOHID
                       MOVE    RECE02-SRYYM    TO  MONTHLYNUM-SRYYM
                       MOVE    RECE02-NYUGAIKBN
                                               TO  MONTHLYNUM-NYUGAIKBN
                       MOVE    MONTHLYNUM-REC  TO  MCPDATA-REC
                       MOVE    "tbl_monthlynum"
                                               TO  WRK-MCP-TABLE
                       MOVE    "key4"          TO  WRK-MCP-PATHNAME
                       PERFORM 910-MONTHLYNUM-READ-SEC
                       IF    ( FLG-MONTHLYNUM          =   ZERO  )
                           MOVE    MONTHLYNUM-JKYSNUM 
                                           TO  RECE02-KOHJKYSNUM (IDX2)
                       ELSE                       
                           MOVE    RECE02-HOSPID   TO  MONTHLYNUM-HOSPID
                           MOVE    RECE02-PTID     TO  MONTHLYNUM-PTID
                           MOVE    ZERO            TO  MONTHLYNUM-KOHID
                           MOVE    RECE02-SRYYM    TO  MONTHLYNUM-SRYYM
                           MOVE    RECE02-NYUGAIKBN
                                                   TO
                                                   MONTHLYNUM-NYUGAIKBN
                           MOVE    MONTHLYNUM-REC  TO  MCPDATA-REC
                           MOVE    "tbl_monthlynum"
                                                   TO  WRK-MCP-TABLE
                           MOVE    "key4"          TO  WRK-MCP-PATHNAME
                           PERFORM 910-MONTHLYNUM-READ-SEC
                           IF    ( FLG-MONTHLYNUM          =   ZERO  )
                               MOVE    MONTHLYNUM-JKYSNUM 
                                           TO  RECE02-KOHJKYSNUM (IDX2)
                           ELSE                       
                               MOVE   PTKOH-JKYSNUM
                                           TO  RECE02-KOHJKYSNUM (IDX2)
                           END-IF
                       END-IF    
                   ELSE                       
                       MOVE   PTKOH-JKYSNUM
                                           TO  RECE02-KOHJKYSNUM (IDX2)
                   END-IF    
               ELSE
                   DISPLAY  "PTKOHINF FETCH ERR"  
               END-IF
           ELSE
               DISPLAY   "PTKOHINF SELECT ERR" 
           END-IF
           MOVE    "PTKOHINF-KEY"      TO  WK-PATH
           PERFORM CSR-CLOSE-SEC
      *
      *    神奈川地方公費対応
           IF    ( RECE02-PREFNUM  =   "14"           )
           AND   ( RECE02-TEISYUTUSAKI
                                   =   "2"            )
           AND   ( RECE02-KOHNUM      (IDX2)
                                   =   "180" OR "280" )
           AND   ( RECE02-KOHFTNJANUM (IDX2)
                                   =   SPACE          )
               MOVE   "4"          TO  RECE02-RECESKYKBN(IDX2)
           END-IF
      *
           .
       KOHI-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    レセプト種別セット処理
      *****************************************************************
       RECESYUBETU-SET-SEC              SECTION.
      *
      *    保険変更後、訂正していないとき
      *    保険組合せがないとき
           IF      RECE01-TEISEIKBN     =   "1"
               GO  TO  RECESYUBETU-SET-EXT
           END-IF
      *
      *    地方公費データのとき主保険が無かったら不明分とする
           IF      IDZ                  =   2
           AND     RECE02-HKNNUM        =   SPACE
      *
      *        沖縄県で精神障害者特別措置(121)の場合は不明分としない
               IF    ( RECE02-PREFNUM       =   "47"  )
               AND   ( RECE02-KOHNUM (1)    =   "121" )
                   CONTINUE
               ELSE
                   MOVE    6                    TO  RECE02-ERR-ERRKBN
                   GO  TO  RECESYUBETU-SET-EXT
               END-IF
           END-IF
      *     
      *    自費のときのレセプト種別を編集
           IF     (RECE02-HKNNUM        NOT =   SPACE)   AND
                  (WRK-JIHI-FLG         =   "1"      ) 
               MOVE    8888             TO  RECE02-RECESYUBETU    
               GO  TO  RECESYUBETU-SET-EXT
           END-IF
      *         
           IF     (RECE02-HKNNUM        NOT =   SPACE)   AND
                  (WRK-JIHI-FLG         =   SPACE)
               IF      RECE02-HKNNUM    =   "060"   OR   "067"   OR
                                            "068"   OR   "069"
                   IF   RECE02-HKNNUM   =   "060"   OR   "068"
                        MOVE   "1"      TO  WK-KOKUHO
                   ELSE
                        MOVE   "2"      TO  WK-KOKUHO
                   END-IF
               ELSE
                   MOVE    "1"          TO  WK-IHO
               END-IF
           END-IF
      *
           IF      RECE02-SRYYM     <      "200210" 
             IF      RECE02-RJNHKNNUM     =   "027"
                 MOVE        "1"          TO  WK-RJN
             END-IF
           END-IF
      *
           MOVE    ZERO            TO    WK-KOHI-CNT
           PERFORM VARYING IDX     FROM    1   BY  1
                   UNTIL   IDX     >       4
               IF    ( RECE02-KOHNUM     (IDX) NOT =   SPACE )
               AND   ( RECE02-RECESKYKBN (IDX)     =   SPACE )
                   ADD     1               TO    WK-KOHI-CNT
               END-IF
           END-PERFORM    
      *
      *    山形地方公費対応
           IF    ( RECE02-PREFNUM      =   "06"     )
           AND   ( RECE02-SRYYM        <=  "200512" )
               IF  WK-KOKUHO               =     "1"  OR  "2"
                   IF   (RECE02-KOHNUM(1) (1:1)  =   "1")   OR
                        (RECE02-KOHNUM(2) (1:1)  =   "1")   OR
                        (RECE02-KOHNUM(3) (1:1)  =   "1")   OR
                        (RECE02-KOHNUM(4) (1:1)  =   "1")   OR
                        (RECE02-KOHNUM(1) (1:1)  =   "2")   OR
                        (RECE02-KOHNUM(2) (1:1)  =   "2")   OR
                        (RECE02-KOHNUM(3) (1:1)  =   "2")   OR
                        (RECE02-KOHNUM(4) (1:1)  =   "2")
                       COMPUTE  WK-KOHI-CNT  =  WK-KOHI-CNT  -  1
                   END-IF
               END-IF
           END-IF
      *
      *    福岡地方公費対応
           IF    ( RECE02-PREFNUM      =   "40"                  )
           AND   ( RECE02-SRYYM        <=  WRK-CONS-SRYYM-200609 )
               IF  WK-KOKUHO               =   "1" OR  "2"
                   IF   (RECE02-KOHNUM   (1)(1:1)  =   "1" OR  "2")
                   AND  (RECE02-KOHHBTNUM(1)   NOT =   "41"       )     
                       COMPUTE  WK-KOHI-CNT  =  WK-KOHI-CNT  -  1
                   END-IF
                   IF   (RECE02-KOHNUM   (2)(1:1)  =   "1" OR  "2")
                   AND  (RECE02-KOHHBTNUM(2)   NOT =   "41"       )     
                       COMPUTE  WK-KOHI-CNT  =  WK-KOHI-CNT  -  1
                   END-IF
                   IF   (RECE02-KOHNUM   (3)(1:1)  =   "1" OR  "2")
                   AND  (RECE02-KOHHBTNUM(3)   NOT =   "41"       )     
                       COMPUTE  WK-KOHI-CNT  =  WK-KOHI-CNT  -  1
                   END-IF
                   IF   (RECE02-KOHNUM   (4)(1:1)  =   "1" OR  "2")
                   AND  (RECE02-KOHHBTNUM(4)   NOT =   "41"       )     
                       COMPUTE  WK-KOHI-CNT  =  WK-KOHI-CNT  -  1
                   END-IF
               END-IF
           END-IF
      *
      *----(01.00.13) LINE ADD START --------------------------
      *   平成１４年１０月対応
      *    ３歳未満の場合、ＷＫ−ＨＯＮＫＺＫＫＢＮに"３"をセット
           IF  RECE01-HKNID             NOT =   ZERO
               IF      RECE02-SRYYM             >=      "200210"
      **             MOVE   PTINF-BIRTHDAY   TO  WRK-YMDS1
      **             MOVE   RECE02-SRYYM     TO  WRK-YYMMS2R
      **             MOVE   01               TO  WRK-DDS2R
      **             MOVE   ZERO             TO  WRK-NENREI
      **             COMPUTE  WRK-NENREI     =   WRK-YYS2
      **                                     -   WRK-YYS1
      **             IF   WRK-YMDS1(5:4)     >   WRK-YMDS2(5:4)
      **                  ADD   -1           TO  WRK-NENREI
      **             END-IF
      **             IF   WRK-NENREI         =   3
      **                 IF    WRK-YMDS1(5:2)  = WRK-YMDS2(5:2)
      **                       IF   WRK-YMDS1(7:2)  NOT = "01"
      **                            ADD   -1     TO  WRK-NENREI
      **                       END-IF
      **                 END-IF
      **             END-IF
      *
                   IF      WRK-NENREI         <     3
                               MOVE   "3"     TO   WK-HONKZKKBN
                   END-IF
               END-IF
           END-IF
      *
      *    １割、２割負担の高齢者の場合ＷＫ−ＨＯＮＫＺＫＫＢＮに
      *    "９"または"８"をセット
      *    老人保険の場合はＷＫ−ＲＪＮにセットする
           IF  RECE02-HKNID             NOT =   ZERO
               EVALUATE    TRUE
                   WHEN    RECE02-SRYYM     >   WRK-CONS-SRYYM-200609
                     EVALUATE    RECE02-HKNNUM
                       WHEN    "002" 
                           EVALUATE   RECE02-HOJOKBN
                               WHEN   "A"
                               WHEN   "B"
                               WHEN   "C"
                               WHEN   "9"
                                   MOVE   "9"   TO   WK-HONKZKKBN
                               WHEN   "D"
                               WHEN   "E"
                               WHEN   "F"
                               WHEN   "8"
                                   MOVE   "?"   TO   WK-HONKZKKBN
                               WHEN   "G"
                               WHEN   "H"
                               WHEN   "I"
                               WHEN   "7"
                                   MOVE   "7"   TO   WK-HONKZKKBN
                           END-EVALUATE
                       WHEN    "031"  THRU    "034"  
                           EVALUATE   RECE02-HOJOKBN
                               WHEN   "B"
                               WHEN   "9"   
                                   MOVE   "9"   TO   WK-HONKZKKBN
                               WHEN   "E"
                               WHEN   "8"
                                   MOVE   "?"   TO   WK-HONKZKKBN
                               WHEN   "H"
                               WHEN   "7"
                                   MOVE   "7"   TO   WK-HONKZKKBN
                           END-EVALUATE
                       WHEN    "060" 
                       WHEN    "068" 
                           IF     (WRK-NENREI  >=   70)  AND
                                  (WRK-NENREI  <=   74)
                               EVALUATE   RECE02-HOJOKBN
                                   WHEN   "1"
                                   WHEN   "4"
                                       MOVE   "9"   TO   WK-HONKZKKBN
                                   WHEN   "2"
                                   WHEN   "5"
                                       MOVE   "8"   TO   WK-HONKZKKBN
                                   WHEN   "3"
                                   WHEN   "6"
                                       MOVE   "7"   TO   WK-HONKZKKBN
                               END-EVALUATE
                           END-IF     
                       WHEN    OTHER 
                           EVALUATE   RECE02-HOJOKBN
                               WHEN   "9"
                                   MOVE   "9"   TO   WK-HONKZKKBN
                               WHEN   "8"
                                   MOVE   "?"   TO   WK-HONKZKKBN
                               WHEN   "7"
                                   MOVE   "7"   TO   WK-HONKZKKBN
                           END-EVALUATE
                     END-EVALUATE
                     IF      RECE02-RJNHKNID >   ZERO
                       EVALUATE   RECE02-RJNPAYKBN
                           WHEN   "01"
                               MOVE   "9"   TO   WK-RJN
                           WHEN   "02"
                               MOVE   "?"   TO   WK-RJN
                           WHEN   "03"
                               MOVE   "7"   TO   WK-RJN
                       END-EVALUATE
                     END-IF
                   WHEN    RECE02-SRYYM     >=      "200210"
                     EVALUATE    RECE02-HKNNUM
                       WHEN    "002" 
                           EVALUATE   RECE02-HOJOKBN
                               WHEN   "A"
                               WHEN   "B"
                               WHEN   "C"
                               WHEN   "9"
                                   MOVE   "9"   TO   WK-HONKZKKBN
                               WHEN   "D"
                               WHEN   "E"
                               WHEN   "F"
                               WHEN   "8"
                                   MOVE   "8"   TO   WK-HONKZKKBN
                           END-EVALUATE
                       WHEN    "031"  THRU    "034"  
                           EVALUATE   RECE02-HOJOKBN
                               WHEN   "B"
                               WHEN   "9"   
                                   MOVE   "9"   TO   WK-HONKZKKBN
                               WHEN   "E"
                               WHEN   "8"
                                   MOVE   "8"   TO   WK-HONKZKKBN
                           END-EVALUATE
                       WHEN    "060" 
                       WHEN    "068" 
                           IF     (WRK-NENREI  >=   70)  AND
                                  (WRK-NENREI  <=   74)
                               EVALUATE   RECE02-HOJOKBN
                                   WHEN   "1"
                                   WHEN   "4"
                                       MOVE   "9"   TO   WK-HONKZKKBN
                                   WHEN   "2"
                                   WHEN   "5"
                                       MOVE   "8"   TO   WK-HONKZKKBN
                               END-EVALUATE
                           END-IF     
                       WHEN    OTHER 
                           EVALUATE   RECE02-HOJOKBN
                               WHEN   "9"
                                   MOVE   "9"   TO   WK-HONKZKKBN
                               WHEN   "8"
                                   MOVE   "8"   TO   WK-HONKZKKBN
                           END-EVALUATE
                     END-EVALUATE
                     IF      RECE02-RJNHKNID >   ZERO
                       EVALUATE   RECE02-RJNPAYKBN
                           WHEN   "01"
                               MOVE   "9"   TO   WK-RJN
                           WHEN   "02"
                               MOVE   "8"   TO   WK-RJN
                       END-EVALUATE
                     END-IF
               END-EVALUATE
           END-IF
      *
           IF    ( WK-RJN          NOT =   SPACE )
           AND   ( RECE02-HONKZKKBN    =   "1"   )     
               IF      RECE02-HKNNUM   =   "063"   OR  "072"   OR
                                           "073"   OR  "074"   OR
                                           "075"
                   MOVE    "2"          TO  WK-IHO
               END-IF
           END-IF
      *
           IF      RECE02-SRYYM        >  WRK-CONS-SRYYM-200609
               IF    ( WK-HONKZKKBN        =   "?" )
               OR    ( WK-RJN              =   "?" )
                   MOVE    7                    TO  RECE02-ERR-ERRKBN
               END-IF
           END-IF
      *          
      *----(01.00.13) LINE ADD END   --------------------------
      *
      *      DISPLAY "WK-HONKZKKBN: " WK-HONKZKKBN 
      *      DISPLAY "RECE01-NYUGAIKBN: " RECE01-NYUGAIKBN 
      *      DISPLAY "WK-IHO: " WK-IHO 
      *      DISPLAY "WK-KOKUHO: " WK-KOKUHO 
      *      DISPLAY "WK-RJN: " WK-RJN 
      *      DISPLAY "WK-KOHI-CNT: " WK-KOHI-CNT 
      *
           INITIALIZE                  ORCSSYUBETUGETAREA  
           MOVE    WK-KOKUHO           TO  LNK-SYUBETUGET-KOKUHO
           MOVE    WK-IHO              TO  LNK-SYUBETUGET-IHO
           MOVE    WK-RJN              TO  LNK-SYUBETUGET-RJN
           MOVE    WK-KOHI-CNT         TO  LNK-SYUBETUGET-KOHI-CNT
           MOVE    WK-HONKZKKBN        TO  LNK-SYUBETUGET-HONKZKKBN
           MOVE    RECE01-NYUGAIKBN    TO  LNK-SYUBETUGET-NYUGAIKBN
           MOVE    RECE02-SRYYM        TO  LNK-SYUBETUGET-SRYYM
           MOVE    RECE02-HKNNUM       TO  LNK-SYUBETUGET-HKNNUM
           CALL    "ORCSSYUBETUGET"    USING   ORCSSYUBETUGETAREA
      *
           MOVE    LNK-SYUBETUGET-RECESYUBETU
                                       TO  RECE02-RECESYUBETU
           MOVE    LNK-SYUBETUGET-RECESYUBETU-X
                                       TO  RECE02-RECESYUBETU-X
      *
      *    公費分で種別が設定できなかったとき不明分にしない
           IF    ( IDZ                 =   2    )
           AND   ( RECE02-RECESYUBETU  =   ZERO ) 
               MOVE    5555                TO  RECE02-RECESYUBETU
           END-IF                 
      *
      *     DISPLAY   "RECE02-RECESYUBETU: " RECE02-RECESYUBETU
           .
       RECESYUBETU-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    主保険−給付割合のセット
      *****************************************************************
       KYURATE-SET-SEC                SECTION.
      *
      *----(01.00.14) LINE MOD START --------------------------
      *平成１４年１０月改正対応
      *平成１４年９月まで有効
      *    補助区分から患者の負担割合を再セット
      *   （国保のみ）
           IF      RECE02-SRYYM     <=      "200209"
             IF    RECE02-HKNNUM           =    "060"
               EVALUATE   RECE02-HOJOKBN
                   WHEN   "0"
                          MOVE   ZERO      TO   RECE02-KYURATE
                   WHEN   "1"
                          MOVE   10        TO   RECE02-KYURATE
                   WHEN   "2"
                          MOVE   20        TO   RECE02-KYURATE
                   WHEN   "3"
                          MOVE   30        TO   RECE02-KYURATE
                   WHEN   "4"
                          MOVE   10        TO   RECE02-KYURATE
                   WHEN   "5"
                          MOVE   20        TO   RECE02-KYURATE
                   WHEN   "6"
                          MOVE   30        TO   RECE02-KYURATE
               END-EVALUATE
             ELSE
               IF    RECE02-HONKZKKBN        =    "1"
                     MOVE   WRK-HKN-HON-GAIKFTNRATE  TO  RECE02-KYURATE
               ELSE
                     MOVE   WRK-HKN-KZK-GAIKFTNRATE  TO  RECE02-KYURATE
               END-IF
             END-IF
           END-IF
      *----(01.00.14) LINE MOD START --------------------------
      *
      *----(01.00.14) LINE ADD START --------------------------
      *平成１４年１０月改正対応
      *平成１４年１０月以降有効
      *    主保険情報の補助区分から患者の負担割合を再セット
           IF    ( RECE02-SRYYM     >=      "200210" )
           AND   ( RECE02-SRYYM     <=      WRK-CONS-SRYYM-200609 ) 
               EVALUATE    RECE02-HKNNUM
                   WHEN    "060"
                   WHEN    "068"
                       IF       WRK-NENREI           <     3
                           IF    RECE02-HOJOKBN =    "0"  OR   "1" OR
                                                     "4"
                               EVALUATE   RECE02-HOJOKBN
                                  WHEN   "0"
                                      MOVE   ZERO    TO   RECE02-KYURATE
                                  WHEN   "1"
                                      MOVE   10      TO   RECE02-KYURATE
                                  WHEN   "4"
                                      MOVE   10      TO   RECE02-KYURATE
                               END-EVALUATE
                           ELSE
                               MOVE  20      TO    RECE02-KYURATE
                           END-IF
                       ELSE
                           EVALUATE   RECE02-HOJOKBN
                              WHEN   "0"
                                  MOVE   ZERO      TO   RECE02-KYURATE
                              WHEN   "1"
                                  MOVE   10        TO   RECE02-KYURATE
                              WHEN   "2"
                                  MOVE   20        TO   RECE02-KYURATE
                              WHEN   "3"
                                  MOVE   30        TO   RECE02-KYURATE
                              WHEN   "4"
                                  MOVE   10        TO   RECE02-KYURATE
                              WHEN   "5"
                                  MOVE   20        TO   RECE02-KYURATE
                              WHEN   "6"
                                  MOVE   30        TO   RECE02-KYURATE
                              WHEN   "8"
                                  MOVE   20        TO   RECE02-KYURATE
                              WHEN   "9"
                                  MOVE   10        TO   RECE02-KYURATE
                           END-EVALUATE
                       END-IF
                   WHEN    "067"
                   WHEN    "069"
                       IF       WRK-NENREI           <     3
                           MOVE  20      TO    RECE02-KYURATE
                       ELSE
                           IF      RECE02-HOJOKBN      NOT =  SPACE
                               EVALUATE   RECE02-HOJOKBN
                                   WHEN   "8"
                                       MOVE    20  TO  RECE02-KYURATE
                                   WHEN   "9"
                                       MOVE    10  TO  RECE02-KYURATE
                               END-EVALUATE
                           ELSE
                               IF   RECE02-HKNJANUM = "67330753"
                                 MOVE   20         TO  RECE02-KYURATE
                               ELSE
                                 IF RECE02-HONKZKKBN      =    "1"
                                   MOVE   WRK-HKN-HON-GAIKFTNRATE
                                                   TO  RECE02-KYURATE
                                 ELSE
                                   MOVE   WRK-HKN-KZK-GAIKFTNRATE
                                                   TO  RECE02-KYURATE
                                 END-IF
                               END-IF
                           END-IF
                       END-IF
                   WHEN    SPACE
                       MOVE    ZERO            TO  RECE02-KYURATE
                   WHEN    OTHER
                       IF      WRK-NENREI           <     3
                           MOVE    20          TO  RECE02-KYURATE
                       ELSE
                           IF      RECE02-HOJOKBN      =     SPACE
                               IF      RECE02-HONKZKKBN    =     "1"
                                   MOVE   WRK-HKN-HON-GAIKFTNRATE
                                                   TO  RECE02-KYURATE
                               ELSE
                                   MOVE   WRK-HKN-KZK-GAIKFTNRATE
                                                   TO  RECE02-KYURATE
                               END-IF
                           ELSE
                               EVALUATE   RECE02-HOJOKBN
                                   WHEN   "8"
                                       MOVE    20  TO  RECE02-KYURATE
                                   WHEN   "9"
                                       MOVE    10  TO  RECE02-KYURATE
                              END-EVALUATE
                          END-IF
                      END-IF
               END-EVALUATE
      *
               IF      RJN-RATE-KBN      NOT =    SPACE
                   EVALUATE   RJN-RATE-KBN
                       WHEN   "1"
                           MOVE   10        TO  RECE02-KYURATE
                       WHEN   "2"
                           MOVE   20        TO  RECE02-KYURATE
                   END-EVALUATE
               END-IF
           END-IF
      *
      *平成１８年１０月改正対応
      *    主保険情報の補助区分から患者の負担割合を再セット
           IF    ( RECE02-SRYYM    >   WRK-CONS-SRYYM-200609 )
               EVALUATE    RECE02-HKNNUM
                   WHEN    "060"
                   WHEN    "068"
                       IF       WRK-NENREI           <     3
                           IF    RECE02-HOJOKBN =    "0"  OR   "1" OR
                                                     "4"
                               EVALUATE   RECE02-HOJOKBN
                                  WHEN   "0"
                                      MOVE   ZERO    TO   RECE02-KYURATE
                                  WHEN   "1"
                                      MOVE   10      TO   RECE02-KYURATE
                                  WHEN   "4"
                                      MOVE   10      TO   RECE02-KYURATE
                               END-EVALUATE
                           ELSE
                               MOVE  20      TO    RECE02-KYURATE
                           END-IF
                       ELSE
                           EVALUATE   RECE02-HOJOKBN
                              WHEN   "0"
                                  MOVE   ZERO      TO   RECE02-KYURATE
                              WHEN   "1"
                                  MOVE   10        TO   RECE02-KYURATE
                              WHEN   "2"
                                  MOVE   20        TO   RECE02-KYURATE
                              WHEN   "3"
                                  MOVE   30        TO   RECE02-KYURATE
                              WHEN   "4"
                                  MOVE   10        TO   RECE02-KYURATE
                              WHEN   "5"
                                  MOVE   20        TO   RECE02-KYURATE
                              WHEN   "6"
                                  MOVE   30        TO   RECE02-KYURATE
                              WHEN   "8"
                                  MOVE   20        TO   RECE02-KYURATE
                              WHEN   "9"
                                  MOVE   10        TO   RECE02-KYURATE
                           END-EVALUATE
                       END-IF
                   WHEN    "067"
                   WHEN    "069"
                       IF       WRK-NENREI           <     3
                           MOVE  20      TO    RECE02-KYURATE
                       ELSE
                           IF      RECE02-HOJOKBN      NOT =  SPACE
                               EVALUATE   RECE02-HOJOKBN
                                   WHEN   "7"
                                       MOVE    30  TO  RECE02-KYURATE
                                   WHEN   "8"
                                       MOVE    20  TO  RECE02-KYURATE
                                   WHEN   "9"
                                       MOVE    10  TO  RECE02-KYURATE
                               END-EVALUATE
                           ELSE
                               IF   RECE02-HKNJANUM = "67330753"
                                 MOVE   20         TO  RECE02-KYURATE
                               ELSE
                                 IF RECE02-HONKZKKBN      =    "1"
                                   MOVE   WRK-HKN-HON-GAIKFTNRATE
                                                   TO  RECE02-KYURATE
                                 ELSE
                                   MOVE   WRK-HKN-KZK-GAIKFTNRATE
                                                   TO  RECE02-KYURATE
                                 END-IF
                               END-IF
                           END-IF
                       END-IF
                   WHEN    SPACE
                       MOVE    ZERO            TO  RECE02-KYURATE
                   WHEN    OTHER
                       IF      WRK-NENREI           <     3
                           MOVE    20          TO  RECE02-KYURATE
                       ELSE
                           IF      RECE02-HOJOKBN      =     SPACE
                               IF      RECE02-HONKZKKBN    =     "1"
                                   MOVE   WRK-HKN-HON-GAIKFTNRATE
                                                   TO  RECE02-KYURATE
                               ELSE
                                   MOVE   WRK-HKN-KZK-GAIKFTNRATE
                                                   TO  RECE02-KYURATE
                               END-IF
                           ELSE
                               EVALUATE   RECE02-HOJOKBN
                                   WHEN   "7"
                                       MOVE    30  TO  RECE02-KYURATE
                                   WHEN   "9"
                                       MOVE    10  TO  RECE02-KYURATE
                              END-EVALUATE
                          END-IF
                      END-IF
               END-EVALUATE
      *
               IF      RJN-RATE-KBN      NOT =    SPACE
                   EVALUATE   RJN-RATE-KBN
                       WHEN   "1"
                           MOVE   10        TO  RECE02-KYURATE
                       WHEN   "3"
                           MOVE   30        TO  RECE02-KYURATE
                   END-EVALUATE
               END-IF
           END-IF
      *----(01.00.14) LINE ADD START --------------------------
      *
      *    船員保険（被保険者）＋通勤災害フラグセット
           IF    RECE02-HKNNUM           =    "002"
               EVALUATE   RECE02-HOJOKBN
                    WHEN   "3"
                    WHEN   "C"
                    WHEN   "F"
                    WHEN   "I"
                        MOVE     "1"     TO     RECE02-SENIN-TUUSAI
               END-EVALUATE
           END-IF
      *
           .
       KYURATE-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    組合せ公費保険番号情報のセット
      *****************************************************************
       KOHNUMHC-SET-SEC                SECTION.
      *
           IF      RECE01-KOHNUMHC (IDX 1) =   "027"
               MOVE    2                   TO  IDY1
           ELSE
               MOVE    1                   TO  IDY1
           END-IF
           MOVE    ZERO            TO  IDX2
           PERFORM VARYING IDY2    FROM    IDY1    BY  1
                   UNTIL   IDY2    >       4
               ADD     1                   TO  IDX2  
               MOVE    RECE01-KOHNUMHC (IDX IDY2)
                                           TO  WRK-HKNNUM
               PERFORM KOHNUM-CHECK-SEC
               IF      FLG-CHK         =   1
                   MOVE RECE01-KOHNUMHC (IDX IDY2) TO
                                           RECE02-KOHNUMHC (IDX IDX2)
                   MOVE RECE01-KOHIDHC  (IDX IDY2) TO
                                           RECE02-KOHIDHC  (IDX IDX2)
               END-IF
           END-PERFORM
           .
       KOHNUMHC-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    データ出力処理
      *****************************************************************
       KOHNUM-CHECK-SEC                SECTION.
      *
           MOVE    ZERO                TO  FLG-CHK
      *
           IF      WRK-HKNNUM          NOT =   SPACE
               PERFORM VARYING IDY FROM    1   BY  1
                       UNTIL   IDY >       4
                       OR      RECE01-KOHHKNNUM-O (IDY)  =   SPACE
                       OR      FLG-CHK     =  1
                   IF      WRK-HKNNUM      =   RECE01-KOHHKNNUM-O (IDY)
                       MOVE    1               TO  FLG-CHK
                   END-IF
               END-PERFORM
           END-IF             
      *
           .
       KOHNUM-CHECK-EXT.
           EXIT.
      *
      *****************************************************************
      *    在総診フラグセット
      *****************************************************************
       ZAITAKU-SET-SEC                SECTION.
      *
      *    自費または特別療養費、公費単独の場合は
      *    在宅総合・在宅末期フラグをクリアする
           IF      RECE02-RECESYUBETU  =   "8888"  OR  "6666"
               MOVE    SPACE           TO  RECE02-ZAITAKU
               GO  TO  ZAITAKU-SET-EXT
           END-IF
           IF      RECE02-HKNNUM        =   SPACE 
               MOVE    SPACE           TO  RECE02-ZAITAKU
               GO  TO  ZAITAKU-SET-EXT
           END-IF
      *
      *    対象でない患者のとき在宅総合・在宅末期フラグをクリアする
           MOVE    ZERO    TO   FLG-GENBUTU70
           MOVE    SPACE   TO   WRK-ZAITAKU
           IF    ( RECE02-AGE          >=  70      )   OR
                 ( RECE02-AGE          >=  65  AND                    
                   RECE01-KOHID    NOT  =   ZERO   )   OR
                 ( RECE02-AGE          >=  65  AND                    
                   FLG-ROUJINHOKEN      =   1      )
               MOVE   1    TO   FLG-GENBUTU70
           ELSE
             IF  RECE01-SRYYM    >=  "200704"
               MOVE   2    TO   FLG-GENBUTU70
             ELSE
               MOVE    SPACE           TO  RECE02-ZAITAKU
               GO  TO  ZAITAKU-SET-EXT
             END-IF
           END-IF
      *
           IF   RECE01-SRYYM    >=  "200210"
      *
            IF  RECE01-SRYYM    >=  "200604"
             PERFORM   VARYING   IDX3   FROM    1   BY  1
                         UNTIL   ( IDX3   >   TBL-ZAITAKU2-MAX )
                          OR     ( WRK-ZAITAKU    =    "1"     )
      *
               INITIALIZE                           SRYACT-REC
               MOVE    RECE01-HOSPID        TO      SRY-HOSPID
               MOVE    RECE01-NYUGAIKBN     TO      SRY-NYUGAIKBN
               MOVE    RECE01-PTID          TO      SRY-PTID
               MOVE    RECE01-SRYYM         TO      SRY-SRYYM
               MOVE    TBL-ZAITAKU2(IDX3)   TO      SRY-SRYCD(1)
               MOVE    TBL-ZAITAKU2(IDX3)   TO      SRY-SRYCD(2)
               MOVE    TBL-ZAITAKU2(IDX3)   TO      SRY-SRYCD(3)
               MOVE    TBL-ZAITAKU2(IDX3)   TO      SRY-SRYCD(4)
               MOVE    TBL-ZAITAKU2(IDX3)   TO      SRY-SRYCD(5)
               MOVE    SRYACT-REC           TO      MCPDATA-REC
               MOVE    "DBSELECT"           TO      MCP-FUNC
               MOVE    "SRYACT-KEY7"        TO      ORC-DBPATH
               CALL    "ORCMCPSUB"           USING   MCPAREA
                                                     ORCMCPAREA
                                                     MCPDATA-REC
               IF      MCP-RC               =       ZERO
                   MOVE    "SRYACT-KEY7"    TO      ORC-DBPATH 
                   PERFORM   SRYACT-READ-SEC
                   IF      FLG-SRYACT       =       ZERO
                       INITIALIZE                   SRYACCT-REC
                       MOVE    SRY-HOSPID       TO   ACCT-HOSPID
                       MOVE    SRY-NYUGAIKBN    TO   ACCT-NYUGAIKBN
                       MOVE    SRY-SRYKA        TO   ACCT-SRYKA
                       MOVE    SRY-PTID         TO   ACCT-PTID
                       MOVE    SRY-SRYYM        TO   ACCT-SRYYM
                       MOVE    SRY-ZAINUM       TO   ACCT-ZAINUM
                       MOVE    SRYACCT-REC      TO   MCPDATA-REC
                       MOVE    "DBSELECT"       TO   MCP-FUNC
                       MOVE    "SRYACCT-KEY31"  TO   ORC-DBPATH
                       CALL    "ORCMCPSUB"           USING
                                                     MCPAREA
                                                     ORCMCPAREA
                                                     MCPDATA-REC
                       IF      MCP-RC           =       ZERO
                           MOVE    "SRYACCT-KEY31"  TO  ORC-DBPATH 
                           PERFORM   SRYACCT-READ-SEC
                           IF      FLG-SRYACCT  =       ZERO
                             IF TBL-ZAITAKU2(IDX3)  = "099140003"
                               IF  (ACCT-ZAIKAISU NOT =  ZERO)
                                   MOVE    "1" TO   WRK-ZAITAKU
                               END-IF
                             ELSE
                               IF  (ACCT-ZAITEN   NOT =  ZERO)  AND
                                   (ACCT-ZAIKAISU NOT =  ZERO)
                                   MOVE    "1" TO   WRK-ZAITAKU
                               END-IF
                             END-IF
                           END-IF
                       END-IF
      *
                       MOVE    "SRYACCT-KEY31" TO    WK-PATH
                       PERFORM   CSR-CLOSE-SEC
      *
                   END-IF
               END-IF
      *
               MOVE    "SRYACT-KEY7"    TO    WK-PATH
               PERFORM   CSR-CLOSE-SEC
      *
             END-PERFORM
            ELSE
             PERFORM   VARYING   IDX3   FROM    1   BY  1
                         UNTIL   ( IDX3   >   TBL-ZAITAKU-MAX )
                          OR     ( WRK-ZAITAKU    =    "1"    )
      *
               INITIALIZE                           SRYACT-REC
               MOVE    RECE01-HOSPID        TO      SRY-HOSPID
               MOVE    RECE01-NYUGAIKBN     TO      SRY-NYUGAIKBN
               MOVE    RECE01-PTID          TO      SRY-PTID
               MOVE    RECE01-SRYYM         TO      SRY-SRYYM
               MOVE    TBL-ZAITAKU(IDX3)    TO      SRY-SRYCD(1)
               MOVE    TBL-ZAITAKU(IDX3)    TO      SRY-SRYCD(2)
               MOVE    TBL-ZAITAKU(IDX3)    TO      SRY-SRYCD(3)
               MOVE    TBL-ZAITAKU(IDX3)    TO      SRY-SRYCD(4)
               MOVE    TBL-ZAITAKU(IDX3)    TO      SRY-SRYCD(5)
               MOVE    SRYACT-REC           TO      MCPDATA-REC
               MOVE    "DBSELECT"           TO      MCP-FUNC
               MOVE    "SRYACT-KEY7"        TO      ORC-DBPATH
               CALL    "ORCMCPSUB"           USING   MCPAREA
                                                     ORCMCPAREA
                                                     MCPDATA-REC
               IF      MCP-RC               =       ZERO
                   MOVE    "SRYACT-KEY7"    TO      ORC-DBPATH 
                   PERFORM   SRYACT-READ-SEC
                   IF      FLG-SRYACT       =       ZERO
                       INITIALIZE                   SRYACCT-REC
                       MOVE    SRY-HOSPID       TO   ACCT-HOSPID
                       MOVE    SRY-NYUGAIKBN    TO   ACCT-NYUGAIKBN
                       MOVE    SRY-SRYKA        TO   ACCT-SRYKA
                       MOVE    SRY-PTID         TO   ACCT-PTID
                       MOVE    SRY-SRYYM        TO   ACCT-SRYYM
                       MOVE    SRY-ZAINUM       TO   ACCT-ZAINUM
                       MOVE    SRYACCT-REC      TO   MCPDATA-REC
                       MOVE    "DBSELECT"       TO   MCP-FUNC
                       MOVE    "SRYACCT-KEY31"  TO   ORC-DBPATH
                       CALL    "ORCMCPSUB"           USING
                                                     MCPAREA
                                                     ORCMCPAREA
                                                     MCPDATA-REC
                       IF      MCP-RC           =       ZERO
                           MOVE    "SRYACCT-KEY31"  TO  ORC-DBPATH 
                           PERFORM   SRYACCT-READ-SEC
                           IF      FLG-SRYACCT  =       ZERO
                               IF  (ACCT-ZAITEN   NOT =  ZERO)  AND
                                   (ACCT-ZAIKAISU NOT =  ZERO)
                                   MOVE    "1" TO   WRK-ZAITAKU
                               END-IF
                           END-IF
                       END-IF
      *
                       MOVE    "SRYACCT-KEY31" TO    WK-PATH
                       PERFORM   CSR-CLOSE-SEC
      *
                   END-IF
               END-IF
      *
               MOVE    "SRYACT-KEY7"    TO    WK-PATH
               PERFORM   CSR-CLOSE-SEC
      *
             END-PERFORM
            END-IF
      *
            IF    WRK-ZAITAKU   =   "1"
              EVALUATE   FLG-GENBUTU70
                WHEN        1
                   MOVE   "1"   TO  RECE02-ZAITAKU
                WHEN        2
                   MOVE   "2"   TO  RECE02-ZAITAKU
              END-EVALUATE
            END-IF
      *
           END-IF
           .
       ZAITAKU-SET-EXT.
           EXIT.
      * 
      *****************************************************************
      *    データ出力処理
      *****************************************************************
       220-DATA-WRITE-SEC                SECTION.
      *
           WRITE   RECE02-REC
      *
           ADD     1                   TO  CNT-OUT (IDZ)
      *
           .
       220-DATA-WRITE-EXT.
           EXIT.
      * 
      *****************************************************************
      *    保険履歴情報編集処理
      *****************************************************************
       230-RECE09-HENSYU-SEC                   SECTION.
      *
           IF      RECE01-HKNID             NOT =   ZERO
      ***  AND     RECE01-HKNNUM            NOT =   WRK-CONS-JIHI-HKNNUM
           AND     RECE01-HKNNUM(1:2)       NOT =   "98"
           AND     RECE01-TEISEIKBN             =   SPACE
               PERFORM VARYING IDX FROM    1   BY  1
                       UNTIL   IDX >       10
                       OR      RECE01-TEKSTYMDX (IDX)  =   SPACE
                 MOVE    SPACE                TO  RECE09-REC
                 INITIALIZE                       RECE09-REC
                 MOVE    RECE01-HOSPID        TO  RECE09-HOSPID
                 MOVE    RECE01-SRYYM         TO  RECE09-SRYYM
                 MOVE    RECE01-NYUGAIKBN     TO  RECE09-NYUGAIKBN
                 MOVE    RECE01-PTID          TO  RECE09-PTID
                 MOVE    RECE01-RECEKA        TO  RECE09-RECEKA
                 MOVE    RECE01-TEKSTYMDX (IDX)
                                              TO  RECE09-TEKSTYMD
                 PERFORM 900-RECE09-READ-SEC
                 IF      FLG-RECE09           =   1
                   MOVE    SPACE                TO  RECE09-REC
                   INITIALIZE                       RECE09-REC
                   MOVE    RECE01-HOSPID        TO  RECE09-HOSPID
                   MOVE    RECE01-SRYYM         TO  RECE09-SRYYM
                   MOVE    RECE01-NYUGAIKBN     TO  RECE09-NYUGAIKBN
                   MOVE    RECE01-PTID          TO  RECE09-PTID
                   MOVE    RECE01-RECEKA        TO  RECE09-RECEKA
                   MOVE    RECE01-TEKSTYMDX (IDX)
                                              TO  RECE09-TEKSTYMD
                   MOVE    RECE01-TEKSTYMDX (IDX)
                                                TO  RECE09-TEKSTYMDX
                   MOVE    RECE01-TEKEDYMDX (IDX)
                                                TO  RECE09-TEKEDYMDX
      * 
                   IF      RECE01-HKNNUM       =   "060"   OR  "067"
                                               OR  "068"   OR  "069"
                       MOVE    2               TO  RECE09-TEISYUTUSAKI
                   ELSE
                       MOVE    1               TO  RECE09-TEISYUTUSAKI
                   END-IF
      *                                          
                   INITIALIZE                       PTHKNINF-REC
                   MOVE    RECE01-HOSPID        TO  PTHKN-HOSPID
                   MOVE    RECE01-PTID          TO  PTHKN-PTID
                   MOVE    RECE01-HKNNUM        TO  PTHKN-HKNNUM
                   MOVE    RECE01-TEKSTYMDX(IDX)
                                                TO  PTHKN-TEKSTYMD
                   MOVE    RECE01-TEKEDYMDX(IDX)
                                                TO  PTHKN-TEKEDYMD
                   MOVE    PTHKNINF-REC         TO  MCPDATA-REC
                   MOVE    "DBSELECT"           TO  MCP-FUNC
                   MOVE    "PTHKNINF-KEY4"      TO  ORC-DBPATH
                   CALL    "ORCMCPSUB"          USING   MCPAREA
                                                        ORCMCPAREA
                                                        MCPDATA-REC
                   IF      MCP-RC               =   ZERO
                       MOVE    "PTHKNINF-KEY4"  TO  ORC-DBPATH
                       PERFORM PTHKNINF-READ-SEC
                   ELSE
                       MOVE    1                TO  FLG-PTHKNINF  
                   END-IF
                   PERFORM              UNTIL   FLG-PTHKNINF     =   1
                       IF      PTHKN-SAKJOKBN   =   SPACE
                           MOVE   PTHKN-HKNID      TO  RECE09-HKNID
                           MOVE   PTHKN-HKNNUM     TO  RECE09-HKNNUM
                           MOVE   PTHKN-HKNJANUM   TO  RECE09-HKNJANUM
                           MOVE   PTHKN-HONKZKKBN  TO  RECE09-HONKZKKBN
                           MOVE   PTHKN-HOJOKBN    TO  RECE09-HOJOKBN
                           MOVE   PTHKN-CONTKBN    TO  RECE09-CONTKBN
                           MOVE   PTHKN-KIGO       TO  RECE09-KIGO
                           MOVE   PTHKN-NUM        TO  RECE09-NUM
                           MOVE   SPACE            TO  RECE09-CERTNUM
      *         
                           INITIALIZE                  HKNNUM-REC
                           MOVE   RECE01-HOSPID    TO  HKN-HOSPID
                           MOVE   PTHKN-HKNNUM     TO  HKN-HKNNUM
                           MOVE   RECE01-TEKSTYMDX (IDX)
                                                   TO  HKN-TEKSTYMD
                           MOVE   RECE01-TEKEDYMDX (IDX)
                                                   TO  HKN-TEKEDYMD
                           MOVE   HKNNUM-REC       TO  MCPDATA-REC
                           MOVE   "DBSELECT"       TO  MCP-FUNC
                           MOVE   "HKNNUM-KEY5"    TO  ORC-DBPATH
                           CALL   "ORCMCPSUB"      USING
                                                   MCPAREA
                                                   ORCMCPAREA
                                                   MCPDATA-REC
                           IF     MCP-RC           =   ZERO
                               MOVE    "HKNNUM-KEY5"   TO  ORC-DBPATH
                               PERFORM HKNNUM-READ-SEC
                               IF      FLG-HKNNUM      =   ZERO
                                   MOVE   HKN-TANSEIDONAME
                                               TO  RECE09-TANSEIDONAME
                                   MOVE   HKN-HBTNUM
                                               TO RECE09-HKNHBTNUM
                               END-IF
                           END-IF
                           MOVE   "HKNNUM-KEY5"       TO  WK-PATH
                           PERFORM   CSR-CLOSE-SEC
                           MOVE    1                TO  FLG-PTHKNINF
                       ELSE
                           MOVE    "PTHKNINF-KEY4"  TO  ORC-DBPATH
                           PERFORM PTHKNINF-READ-SEC
                       END-IF
                   END-PERFORM
                   MOVE   "PTHKNINF-KEY4"     TO  WK-PATH
                   PERFORM   CSR-CLOSE-SEC
      *
                   WRITE   RECE09-REC
                   ADD     1                  TO  CNT-RECE09
                 END-IF  
               END-PERFORM
           END-IF           
      *
           .
       230-RECE09-HENSYU-EXT.
           EXIT.
      * 
      *****************************************************************
      *    入退院履歴より情報取得処理
      *****************************************************************
       240-PTNYUINRRK-SYORI-SEC                   SECTION.
      *
      *    保険組合せ取得
           INITIALIZE                      WRK-DAY-AREA
                                           WRK-HKNCOMBIINF-AREA
                                           WRK-HKNCOMBI-TABLE
           MOVE    LOW-VALUE           TO  KEY-AREA
      *
      *    入院会計読み込み
           INITIALIZE                      NYUINACCT-REC
           MOVE    RECE02-HOSPID       TO  NACCT-HOSPID
           MOVE    RECE02-PTID         TO  NACCT-PTID
           MOVE    RECE02-SRYYM        TO  NACCT-SRYYM
           MOVE    NYUINACCT-REC       TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_nyuinacct"     TO  MCP-TABLE
           MOVE    "key14"             TO  MCP-PATHNAME
           CALL    "MONFUNC"           USING
                                       MCPAREA
                                       MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_nyuinacct"     TO  MCP-TABLE
               MOVE    "key14"             TO  MCP-PATHNAME
               PERFORM 900-NYUINACCT-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-NYUINACCT
           END-IF
      *
      *    入院会計がないとき記載なし 
           IF      FLG-NYUINACCT       =   1
               MOVE    "tbl_nyuinacct"     TO  MCP-TABLE
               MOVE    "key14"             TO  MCP-PATHNAME
               PERFORM 900-CLOSE-SEC
               GO  TO  240-PTNYUINRRK-SYORI-EXT
           END-IF
      *   
      *    保険組合せ編集
           PERFORM UNTIL       FLG-NYUINACCT     =   1
               EVALUATE    NACCT-ZAISKBKBN
                   WHEN    "1"
                       MOVE    1               TO  IDX
                   WHEN    "5"
                       MOVE    2               TO  IDX
               END-EVALUATE
               PERFORM VARYING IDX1    FROM    1   BY  1
                       UNTIL   IDX1    >       31
                   IF      NACCT-DAY  (IDX1)       >   ZERO
                       MOVE    NACCT-DAY (IDX1)    TO
                                                   WRK-DAY (IDX IDX1)
                   END-IF
               END-PERFORM            
               MOVE    "tbl_nyuinacct"     TO  MCP-TABLE
               MOVE    "key14"             TO  MCP-PATHNAME
               PERFORM 900-NYUINACCT-READ-SEC
           END-PERFORM
      *    カーソルクロース
           MOVE    "tbl_nyuinacct"     TO  MCP-TABLE
           MOVE    "key14"             TO  MCP-PATHNAME
           PERFORM 900-CLOSE-SEC
      *
      *    入院会計より保険組合せ読込     
           PERFORM VARYING IDX FROM    1   BY  1
                   UNTIL   IDX >       31
               IF      WRK-DAY (1 IDX)     >   ZERO    AND
                       WRK-DAY (2 IDX)     >   ZERO
      *
                   PERFORM VARYING IDX1    FROM    1   BY  1
                           UNTIL   IDX1    >       10
      *
      *                保険組合せ追加
                       IF      WRK-COMB-HKNCOMBINUM (IDX1) =   ZERO
                           INITIALIZE                  HKNCOMBI-REC
                           MOVE    RECE02-HOSPID   TO  COMB-HOSPID
                           MOVE    RECE02-PTID     TO  COMB-PTID
                           MOVE    WRK-DAY (2 IDX) TO  COMB-HKNCOMBINUM 
                           MOVE    HKNCOMBI-REC    TO  MCPDATA-REC
                           PERFORM 910-HKNCOMBI-INV-SEC
      *                   （対象外の組合せは１をセット）
                           IF      FLG-HKNCOMBI    =   ZERO
                               MOVE    HKNCOMBI-REC    TO
                                           WRK-HKNCOMBI-REC  (IDX1)
                               IF      COMB-HKNNUM     =
                                           WRK-CONS-ROUSAI-HKNNUM OR
                                           WRK-CONS-JIBAI-HKNNUM 
                                   MOVE    "1"         TO
                                           WRK-HKNCOMBI-SYOKBN (IDX1)
                               END-IF
                           ELSE
                               MOVE    WRK-DAY (2 IDX) TO
                                           WRK-COMB-HKNCOMBINUM (IDX1)
                               MOVE    "1"             TO
                                           WRK-HKNCOMBI-SYOKBN  (IDX1)
                           END-IF
                       END-IF    
                       IF      WRK-DAY (2 IDX)     =
                                           WRK-COMB-HKNCOMBINUM (IDX1)
                           MOVE    10              TO  IDX1
                       END-IF            
                   END-PERFORM
               END-IF
           END-PERFORM    
      *
      *    入院会計の保険組合せを追加
           PERFORM VARYING IDX2    FROM    1   BY  1
                   UNTIL   IDX2    >       10
                   OR      WRK-COMB-HKNCOMBINUM (IDX2) =   ZERO
               IF      WRK-HKNCOMBI-SYOKBN (IDX2)  =   SPACE
                   PERFORM VARYING IDX FROM    1   BY  1
                           UNTIL   IDX >       10
                       IF      WRK-HKNCOMBI (IDX)  =   ZERO
                           MOVE    COMB-HKNCOMBINUM    TO
                                                   WRK-HKNCOMBI (IDX)
                           MOVE    10                   TO  IDX
                       ELSE
                           IF      COMB-HKNCOMBINUM    =
                                                   WRK-HKNCOMBI (IDX)
                               MOVE    10                   TO  IDX
                           END-IF
                       END-IF
                   END-PERFORM
               END-IF 
           END-PERFORM
      *
      *    診療会計読み込み
           INITIALIZE                      SRYACCT-REC
           MOVE    RECE02-HOSPID       TO  ACCT-HOSPID
           MOVE    RECE02-PTID         TO  ACCT-PTID
           MOVE    RECE02-SRYYM        TO  ACCT-SRYYM
           MOVE    "1"                 TO  ACCT-NYUGAIKBN
           MOVE    SRYACCT-REC         TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SRYACCT-KEY37"     TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SRYACCT-KEY37"     TO  ORC-DBPATH
               PERFORM 900-SRYACCT-READ-SEC
           ELSE
               INITIALIZE                  SRYACCT-REC
               MOVE    1                   TO  FLG-SRYACCT
           END-IF
      *
           PERFORM UNTIL       FLG-SRYACCT     =   1
      *        保険組合せ読込み
               INITIALIZE                  HKNCOMBI-REC
               MOVE    RECE02-HOSPID   TO  COMB-HOSPID
               MOVE    RECE02-PTID     TO  COMB-PTID
               MOVE    ACCT-HKNCOMBI   TO  COMB-HKNCOMBINUM 
               MOVE    HKNCOMBI-REC    TO  MCPDATA-REC
               PERFORM  910-HKNCOMBI-INV-SEC
      *
               IF      FLG-HKNCOMBI    =   ZERO
                   IF      COMB-HKNNUM     =   WRK-CONS-ROUSAI-HKNNUM OR
                                               WRK-CONS-JIBAI-HKNNUM
                       CONTINUE
                   ELSE    
                       PERFORM VARYING IDX FROM    1   BY  1
                               UNTIL   IDX >       10
                           IF      WRK-HKNCOMBI (IDX)      =   ZERO
                               MOVE    COMB-HKNCOMBINUM    TO
                                                   WRK-HKNCOMBI (IDX)
                               MOVE    10                  TO  IDX
                           ELSE
                               IF      COMB-HKNCOMBINUM    =
                                                   WRK-HKNCOMBI (IDX)
                                   MOVE    10              TO  IDX
                               END-IF
                           END-IF
                       END-PERFORM
                   END-IF
               END-IF    
      *
               MOVE    KEY-NEW             TO  KEY-OLD
               PERFORM     UNTIL   FLG-SRYACCT     =   1 
                           OR      KEY-NEW     NOT =   KEY-OLD
                   MOVE    "SRYACCT-KEY37"     TO  ORC-DBPATH
                   PERFORM 900-SRYACCT-READ-SEC
               END-PERFORM
           END-PERFORM
      *
           MOVE    "SRYACCT-KEY37"     TO  WK-PATH
           PERFORM CSR-CLOSE-SEC
      *
           PERFORM VARYING IDX FROM    1   BY  1
                   UNTIL   IDX >       10
                   OR      WRK-COMB-HKNCOMBINUM (IDX) =   ZERO
      *       主保険情報読み出し
               IF      WRK-COMB-HKNID (IDX)    NOT =   ZERO
                   INITIALIZE                      PTHKNINF-REC
                   MOVE    RECE02-HOSPID       TO  PTHKN-HOSPID
                   MOVE    RECE02-PTID         TO  PTHKN-PTID
                   MOVE    WRK-COMB-HKNID (IDX)
                                               TO  PTHKN-HKNID
                   MOVE    PTHKNINF-REC        TO  MCPDATA-REC
                   MOVE    WRK-COMB-TEKSTYMD (IDX)
                                               TO  ORC-DBYMD
                   PERFORM 910-PTHKNINF-INV-SEC
                   MOVE    PTHKN-TEKSTYMD      TO  WRK-TEKSTYMDX (IDX)
                   MOVE    PTHKN-TEKEDYMD      TO  WRK-TEKEDYMDX (IDX)
               END-IF
           END-PERFORM
           PERFORM VARYING IDX FROM    1   BY  1
                   UNTIL   IDX >       10
                   OR      WRK-COMB-HKNCOMBINUM (IDX)  =   ZERO
               IF      WRK-TEKSTYMD-X  =   SPACE 
                   MOVE    WRK-TEKSTYMDX (IDX) TO  WRK-TEKSTYMD-X
                   MOVE    WRK-TEKEDYMDX (IDX) TO  WRK-TEKEDYMD-X
               ELSE
                   IF      WRK-TEKSTYMD-X >   WRK-TEKSTYMDX (IDX)
                       MOVE    WRK-TEKSTYMDX (IDX)
                                               TO  WRK-TEKSTYMD-X
                   END-IF
                   IF      WRK-TEKEDYMD-X <   WRK-TEKEDYMDX (IDX)
                       MOVE    WRK-TEKEDYMDX (IDX)
                                               TO  WRK-TEKEDYMD-X
                   END-IF
               END-IF
           END-PERFORM
      *
      *    診療月内の入退院情報
           INITIALIZE                      PTNYUINRRK-REC
           MOVE    RECE02-HOSPID       TO  PTNYUINRRK-HOSPID
           MOVE    RECE02-PTID         TO  PTNYUINRRK-PTID
           MOVE    RECE02-SRYYM        TO  PTNYUINRRK-NYUINYMD (1:6)
           MOVE    "31"                TO  PTNYUINRRK-NYUINYMD (7:2)
           MOVE    RECE02-SRYYM        TO  PTNYUINRRK-TAIINYMD (1:6)
           MOVE    "01"                TO  PTNYUINRRK-TAIINYMD (7:2)
           MOVE    PTNYUINRRK-REC      TO  MCPDATA-REC
           MOVE    "PTNYUINRRK-KEY23"  TO  ORC-DBPATH
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING   MCPAREA
                                               ORCMCPAREA
                                               MCPDATA-REC
           IF      MCP-RC              =   ZERO
               PERFORM 900-PTNYUINRRK-KEY23-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-PTNYUINRRK23
           END-IF
           PERFORM                 UNTIL   FLG-PTNYUINRRK23    =   1
             IF    ( PTNYUINRRK-TENNYUYMD  >   WRK-TEKEDYMD-X )
             AND   ( WRK-TEKEDYMD-X    NOT =   SPACE          )
                 CONTINUE
             ELSE
               IF      PTNYUINRRK-NYUINYMD (1:6)
                                               <   RECE02-SRYYM
                   MOVE    1                   TO  IDX4
               ELSE
                   MOVE    PTNYUINRRK-NYUINYMD TO  WRK-SRYYMD
                   MOVE    WRK-SRYDD           TO  IDX4
               END-IF 
               IF      PTNYUINRRK-TAIINYMD (1:6)
                                               >   RECE02-SRYYM
                   MOVE    31                  TO  IDX5
               ELSE
                   MOVE    PTNYUINRRK-TAIINYMD TO  WRK-SRYYMD
                   MOVE    WRK-SRYDD           TO  IDX5
               END-IF
      *        保険組合せのチェック（会計に対象の組合せがないときは全て）
               IF      WRK-DAY-AREA        =   ALL "0"
                   PERFORM 2401-PTNYUINRRK-HENSYU-SEC
               ELSE
                   PERFORM VARYING IDX FROM    IDX4    BY  1
                           UNTIL   IDX >       IDX5
                       PERFORM VARYING IDY FROM    1   BY  1
                               UNTIL   IDY >       10
                               OR  WRK-HKNCOMBI (IDY)   =   ZERO
                           IF      WRK-DAY (2 IDX)
                                           =   WRK-HKNCOMBI (IDY)
                               PERFORM 2401-PTNYUINRRK-HENSYU-SEC
                               MOVE    10                  TO  IDY
                               MOVE   IDX5                 TO  IDX
                           END-IF
                       END-PERFORM
                   END-PERFORM
               END-IF
             END-IF          
      *                 
             PERFORM 900-PTNYUINRRK-KEY23-READ-SEC
           END-PERFORM        
      *                 
           MOVE    "PTNYUINRRK-KEY23"  TO  ORC-DBPATH
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING   MCPAREA
                                               ORCMCPAREA
                                               MCPDATA-REC
      *                                                                
           .
       240-PTNYUINRRK-SYORI-EXT.
           EXIT.
      * 
      *****************************************************************
      *    入退院履歴情報編集処理
      *****************************************************************
       2401-PTNYUINRRK-HENSYU-SEC           SECTION.
      *
      *    入退院履歴情報
           PERFORM 24012-RECE12-WRITE-SEC
      *
      *    診療月内の入退院情報の履歴
           IF      FLG-RECE12          =   1                            
               MOVE    PTNYUINRRK-REC  TO  WRK-PTNYUINRRK-REC
               INITIALIZE                  PTNYUINRRK-REC
               MOVE    RECE01-HOSPID   TO  PTNYUINRRK-HOSPID
               MOVE    RECE01-PTID     TO  PTNYUINRRK-PTID
               MOVE    WRK-PTNYUINRRK-SHONUM
                                       TO  PTNYUINRRK-SHONUM
               MOVE    RECE01-SRYYM    TO  PTNYUINRRK-NYUINYMD (1:6)
               MOVE    "31"            TO  PTNYUINRRK-NYUINYMD (7:2)
               MOVE    PTNYUINRRK-REC  TO  MCPDATA-REC
               MOVE    "PTNYUINRRK-KEY22"
                                       TO  ORC-DBPATH
               MOVE    "DBSELECT"      TO  MCP-FUNC
               CALL    "ORCMCPSUB"     USING   MCPAREA
                                               ORCMCPAREA
                                               MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   PERFORM 900-PTNYUINRRK-KEY22-READ-SEC
               ELSE
                   MOVE    1               TO  FLG-PTNYUINRRK22 
               END-IF
      *
               PERFORM             UNTIL   FLG-PTNYUINRRK22 =  1
                   PERFORM 24012-RECE12-WRITE-SEC
                   PERFORM 900-PTNYUINRRK-KEY22-READ-SEC
               END-PERFORM
      *                 
               MOVE    "PTNYUINRRK-KEY22"  TO  ORC-DBPATH
               MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
               CALL    "ORCMCPSUB"         USING   MCPAREA
                                                   ORCMCPAREA
                                                   MCPDATA-REC
           END-IF
           .
       2401-PTNYUINRRK-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    入退院履歴出力処理
      *****************************************************************
       24012-RECE12-WRITE-SEC         SECTION.
      *
           MOVE    RECE02-HOSPID       TO  RECE12-HOSPID
           MOVE    RECE02-SRYYM        TO  RECE12-SRYYM
           MOVE    RECE02-NYUGAIKBN    TO  RECE12-NYUGAIKBN
           MOVE    RECE02-PTID         TO  RECE12-PTID
           MOVE    RECE02-RECEKA       TO  RECE12-RECEKA
           MOVE    RECE02-TEISYUTUSAKI TO  RECE12-TEISYUTUSAKI
           MOVE    RECE02-RECESYUBETU  TO  RECE12-RECESYUBETU
           MOVE    RECE02-HKNJANUM-KEY TO  RECE12-HKNJANUM
           MOVE    RECE02-HOJOKBN-KEY  TO  RECE12-HOJOKBN
           MOVE    RECE02-TEKSTYMD     TO  RECE12-TEKSTYMD
           MOVE    PTNYUINRRK-NYUINYMD TO  RECE12-NYUINYMD
           MOVE    RECE12-REC          TO  WRK-RECE12-REC          
           PERFORM 900-RECE12-READ-SEC
           IF      FLG-RECE12          =   1
               MOVE    WRK-RECE12-REC      TO  RECE12-REC 
               MOVE    PTNYUINRRK-TAIINYMD TO  RECE12-TAIINYMD
               MOVE    PTNYUINRRK-NYUINCHUKBN
                                           TO  RECE12-NYUINCHUKBN
               WRITE   RECE12-REC
               ADD     1                   TO  CNT-RECE12
               MOVE    "1"                 TO  RECE02-NYUINRRK-FLG
           END-IF    
           .
       24012-RECE12-WRITE-EXT.
           EXIT.
      *
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC                SECTION.
      *
           OPEN    INPUT   RECEERR-FILE
           IF      STS-RECEERR         =   ZERO
               CLOSE   RECEERR-FILE
           ELSE
               OPEN    OUTPUT              RECEERR-FILE
      *
               MOVE    WRK-RECEERR         TO  RECEERR-REC
               WRITE   RECEERR-REC
               CLOSE   RECEERR-FILE
      *         
             IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
      *        ジョブ管理開始処理
               MOVE    "JBE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               MOVE    WRK-RECEERR     TO  JOB-YOBI
               MOVE    "9999"          TO  JOB-ERRCD
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA  
                                       JOBKANRI-REC
             END-IF
           END-IF
      *                             
           MOVE    1                   TO  FLG-END
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    処理中止処理
      *****************************************************************
       500-CANCEL-HENSYU-SEC                SECTION.
      *
           IF      WRK-PARA-SHELLID    =   "RECEPTX"
               CONTINUE
           ELSE
               MOVE    "CHK"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA  
                                       JOBKANRI-REC
               IF    ( SJOBKANRI-RETURN
                                       =   ZERO )
               AND   ( JOB-STOPFLG     =   1    )
                   MOVE    "処理が中止されました"
                                           TO  WRK-RECEERR
      *
                   OPEN    INPUT   RECEERR-FILE
                   IF      STS-RECEERR         =   ZERO
                       CLOSE   RECEERR-FILE
                   ELSE
                       OPEN    OUTPUT              RECEERR-FILE
      *    
                       MOVE    WRK-RECEERR         TO  RECEERR-REC
                       WRITE   RECEERR-REC
                       CLOSE   RECEERR-FILE
      *         
      *                ジョブ管理終了処理
                       MOVE    "CAN"           TO  SJOBKANRI-MODE
                       INITIALIZE                  JOBKANRI-REC
                       MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
                       MOVE    WRK-PARA-SHELLID
                                               TO  JOB-SHELLID
                       MOVE    WRK-RECEERR     TO  JOB-YOBI
                       MOVE    "8888"          TO  JOB-ERRCD
                       MOVE    2               TO  JOB-STOPFLG
                       CALL    "ORCSJOB"       USING
                                               ORCSJOBKANRIAREA  
                                               JOBKANRI-REC
                   END-IF
      *                             
                   MOVE    1                   TO  FLG-END
               END-IF
           END-IF
           .
       500-CANCEL-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了処理
      *****************************************************************
       300-TERM-SEC                SECTION.
      *
           IF      CNT-OUT (1)          =   ZERO
           AND     CNT-OUT (2)          =   ZERO
               MOVE    "処理対象のデータがありませんでした"
                                           TO  WRK-RECEERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
      *
           IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
      *    ステップ管理終了処理
               MOVE    "STE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA  
                                       JOBKANRI-REC
      *
               PERFORM 900-DBDISCONNECT-SEC
           END-IF
      *
           DISPLAY "RECE01  CNT-IN  :" CNT-IN  (1) 
           DISPLAY "RECE02  CNT-OUT :" CNT-OUT (1) 
           DISPLAY "RECE09  CNT-OUT :" CNT-RECE09  
           DISPLAY "RECE12  CNT-OUT :" CNT-RECE12  
           DISPLAY "RECE01X CNT-IN  :" CNT-IN  (2) 
           DISPLAY "RECE02X CNT-OUT :" CNT-OUT (2) 
           DISPLAY "*** ORCR0020 END ***"
           .
       300-TERM-EXT.
           EXIT.
      *****************************************************************
      *    患者保険情報読込
      *****************************************************************
       PTHKNINF-READ-SEC           SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  PTHKNINF-REC
               MOVE    ZERO            TO  FLG-PTHKNINF
           ELSE
               INITIALIZE                  PTHKNINF-REC
               MOVE    1               TO  FLG-PTHKNINF
           END-IF
      *
           .
       PTHKNINF-READ-EXT.
           EXIT.
      *****************************************************************
      *    患者公費情報読込
      *****************************************************************
       PTKOHINF-READ-SEC           SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  PTKOHINF-REC
               MOVE    ZERO            TO  FLG-PTKOHINF
           ELSE
               INITIALIZE                  PTKOHINF-REC
               MOVE    1               TO  FLG-PTKOHINF
           END-IF
      *
           .
       PTKOHINF-READ-EXT.
           EXIT.
      *****************************************************************
      *    保険番号マスタ検索処理
      *****************************************************************
       900-HKNNUM-CHK-SEC              SECTION.
      *
           MOVE    ZERO                TO  FLG-HKNNUM
           MOVE    HKN-HKNNUM          TO  WRK-CHK-HKNNUM
      *
           INITIALIZE                      HKNNUM-REC
      *
      *    保険番号マスタ退避ワークチェック
      *
           PERFORM VARYING WRK-HKNNUM-IDX    FROM    1   BY  1
                   UNTIL   WRK-HKNNUM-IDX    >   WRK-HKNNUM-MAX
                   OR      HKN-HKNNUM        NOT =   SPACE
               IF    ( WRK-CHK-HKNNUM  =
                                       TBL-HKN-HKNNUM (WRK-HKNNUM-IDX) )
               AND   ( TBL-HKN-TEKSTYMD (WRK-HKNNUM-IDX) (1:6)
                                       <=  RECE02-SRYYM                )
               AND   ( RECE02-SRYYM    <=
                               TBL-HKN-TEKEDYMD(WRK-HKNNUM-IDX)(1:6)   )
                   MOVE    TBL-HKNNUM-REC (WRK-HKNNUM-IDX)
                                       TO  HKNNUM-REC
               END-IF
           END-PERFORM
      *
      *    退避ワークに存在しない場合はＤＢを検索
           IF      HKN-HKNNUM          =   SPACE
               INITIALIZE                      HKNNUM-REC
               MOVE   RECE02-HOSPID        TO  HKN-HOSPID
               MOVE   WRK-CHK-HKNNUM       TO  HKN-HKNNUM
               MOVE   HKNNUM-REC           TO  MCPDATA-REC
               MOVE   "DBSELECT"           TO  MCP-FUNC
               MOVE   "HKNNUM-KEY2"        TO  ORC-DBPATH
               CALL   "ORCMCPSUB"          USING   MCPAREA
                                                   ORCMCPAREA
                                                   MCPDATA-REC
               IF     MCP-RC               =   ZERO
                   MOVE    "HKNNUM-KEY2"       TO  ORC-DBPATH
                   PERFORM HKNNUM-READ-SEC
                   PERFORM UNTIL ( FLG-HKNNUM  =    1                 )
                           OR    ((HKN-TEKSTYMD (1:6)
                                               <=  RECE02-SRYYM     )
                                    AND
                                  (RECE02-SRYYM
                                               <=  HKN-TEKEDYMD(1:6)) )
                       MOVE    "HKNNUM-KEY2"       TO  ORC-DBPATH
                       PERFORM HKNNUM-READ-SEC
                   END-PERFORM
      *
                   IF      FLG-HKNNUM          =   ZERO
      *                退避ワークに編集
                       IF      TBL-HKNNUM-CONST-MAX
                                                   >   WRK-HKNNUM-MAX
                           COMPUTE WRK-HKNNUM-MAX
                                           =   WRK-HKNNUM-MAX  +   1
                           MOVE    HKNNUM-REC      TO
                                       TBL-HKNNUM-REC (WRK-HKNNUM-MAX)
                       END-IF
                   ELSE
                       INITIALIZE                  HKNNUM-REC
                       MOVE    1               TO  FLG-HKNNUM
                   END-IF
               ELSE
                   INITIALIZE                  HKNNUM-REC
                   MOVE    1               TO  FLG-HKNNUM
               END-IF
      *
               MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
               MOVE   "HKNNUM-KEY2"        TO  ORC-DBPATH
               CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
           END-IF
      *
           .
      *
       900-HKNNUM-CHK-EXT.
           EXIT.
      *****************************************************************
      *    保険番号読込
      *****************************************************************
       HKNNUM-READ-SEC           SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  HKNNUM-REC
               MOVE    ZERO            TO  FLG-HKNNUM
           ELSE
               INITIALIZE                  HKNNUM-REC
               MOVE    1               TO  FLG-HKNNUM
           END-IF
      *
           .
       HKNNUM-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    低所得マスタ読み込み
      *****************************************************************
       TSYRRK-READ-SEC        SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  TSYRRK-REC
               MOVE    ZERO            TO  FLG-TSYRRK
           ELSE
               INITIALIZE                  TSYRRK-REC
               MOVE    1               TO  FLG-TSYRRK
           END-IF
      *
           .
       TSYRRK-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    年金マスタ読み込み
      *****************************************************************
       TNKRRK-READ-SEC        SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  TNKRRK-REC
               MOVE    ZERO            TO  FLG-TNKRRK
           ELSE
               INITIALIZE                  TNKRRK-REC
               MOVE    1               TO  FLG-TNKRRK
           END-IF
      *
           .
       TNKRRK-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療会計マスター読込
      *****************************************************************
       SRYACCT-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  SRYACCT-REC
               MOVE    ZERO            TO  FLG-SRYACCT
           ELSE
               INITIALIZE                  SRYACCT-REC
               MOVE    1               TO  FLG-SRYACCT
           END-IF
      *
           .
       SRYACCT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療行為マスター読込
      *****************************************************************
       SRYACT-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  SRYACT-REC
               MOVE    ZERO            TO  FLG-SRYACT
           ELSE
               INITIALIZE                  SRYACT-REC
               MOVE    1               TO  FLG-SRYACT
           END-IF
      *
           .
       SRYACT-READ-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    システム管理マスタ読込
      *****************************************************************
       960-KANRIMST-READ-SEC         SECTION.
      *
           MOVE    SYSKANRI-REC        TO  MCPDATA-REC
           MOVE    WRK-MCP-TABLE       TO  MCP-TABLE
           MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "MONFUNC"       USING
                                       MCPAREA
                                       MCPDATA-REC
           IF      MCP-RC          =   ZERO
               MOVE    WRK-MCP-TABLE       TO  MCP-TABLE
               MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
               PERFORM 900-KANRIMST-NEXT-SEC
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    WRK-MCP-TABLE       TO  MCP-TABLE
           MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
           PERFORM 900-CLOSE-SEC
           .
       960-KANRIMST-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理マスタ読み込み処理
      *****************************************************************
       900-KANRIMST-NEXT-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "MONFUNC"       USING
                                       MCPAREA
                                       MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO            TO  FLG-SYSKANRI
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           .
       900-KANRIMST-NEXT-EXT.
           EXIT.
      *
      *****************************************************************
      *    月代わり受給者番号マスタ読込
      *****************************************************************
       910-MONTHLYNUM-READ-SEC         SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    WRK-MCP-TABLE       TO  MCP-TABLE
           MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
           CALL    "MONFUNC"           USING
                                           MCPAREA
                                           MCPDATA-REC
           IF      MCP-RC               =   ZERO
               MOVE    "DBFETCH"            TO  MCP-FUNC
               MOVE    WRK-MCP-TABLE       TO  MCP-TABLE
               MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
               CALL    "MONFUNC"           USING
                                               MCPAREA
                                               MCPDATA-REC
               IF      MCP-RC               =   ZERO
                   MOVE    ZERO                 TO  FLG-MONTHLYNUM
                   MOVE    MCPDATA-REC          TO  MONTHLYNUM-REC
               ELSE
                   MOVE    1                   TO  FLG-MONTHLYNUM
               END-IF
           ELSE
               MOVE    1                   TO  FLG-MONTHLYNUM
           END-IF
      *
           MOVE    WRK-MCP-TABLE       TO  MCP-TABLE
           MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
           PERFORM 900-CLOSE-SEC
      *
           .
       910-MONTHLYNUM-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療行為マスタ　ＲＥＡＤ
      *****************************************************************
       900-SRYACCT-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO                TO  FLG-SRYACCT
               MOVE    MCPDATA-REC         TO  SRYACCT-REC
           ELSE
               MOVE    1                   TO  FLG-SRYACCT
           END-IF
      *
           IF      FLG-SRYACCT         =   ZERO
               MOVE    ACCT-HKNCOMBI       TO  KEY-N-HKNCOMBI
           END-IF
           .
       900-SRYACCT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院会計マスタ　ＲＥＡＤ
      *****************************************************************
       900-NYUINACCT-READ-SEC          SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "MONFUNC"           USING
                                       MCPAREA
                                       MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO                TO  FLG-NYUINACCT
               MOVE    MCPDATA-REC         TO  NYUINACCT-REC
           ELSE
               MOVE    1               TO  FLG-NYUINACCT
           END-IF
           .
       900-NYUINACCT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険組合せマスタ読込
      *****************************************************************
       910-HKNCOMBI-INV-SEC         SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "HKNCOMBI-KEY"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "HKNCOMBI-KEY"      TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO                TO  FLG-HKNCOMBI
                   MOVE    MCPDATA-REC         TO  HKNCOMBI-REC
                   IF      COMB-DELKBN         =   "1"
                       MOVE    1                   TO  FLG-HKNCOMBI
                   END-IF
               ELSE
                   MOVE    1                   TO  FLG-HKNCOMBI
                   INITIALIZE                      HKNCOMBI-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-HKNCOMBI
               INITIALIZE                      HKNCOMBI-REC
           END-IF
      *
           MOVE    "HKNCOMBI-KEY"      TO  WK-PATH
           PERFORM CSR-CLOSE-SEC
      *
           .
       910-HKNCOMBI-INV-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    入院履歴読込（ＫＥＹ２２）
      *****************************************************************
       900-PTNYUINRRK-KEY22-READ-SEC    SECTION.
      *    
           MOVE    "DBFETCH"            TO  MCP-FUNC
           MOVE    "PTNYUINRRK-KEY22"   TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC               =   ZERO
               MOVE    ZERO                 TO  FLG-PTNYUINRRK22
               MOVE    MCPDATA-REC          TO  PTNYUINRRK-REC
           ELSE
               MOVE    1                    TO  FLG-PTNYUINRRK22
           END-IF
           .
       900-PTNYUINRRK-KEY22-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院履歴読込（ＫＥＹ１３）
      *****************************************************************
       900-PTNYUINRRK-KEY23-READ-SEC    SECTION.
      *    
           MOVE    "DBFETCH"            TO  MCP-FUNC
           MOVE    "PTNYUINRRK-KEY23"   TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC               =   ZERO
               MOVE    ZERO                 TO  FLG-PTNYUINRRK23
               MOVE    MCPDATA-REC          TO  PTNYUINRRK-REC
           ELSE
               MOVE    1                    TO  FLG-PTNYUINRRK23
           END-IF
           .
       900-PTNYUINRRK-KEY23-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者保険情報マスタ読込
      *****************************************************************
       910-PTHKNINF-INV-SEC         SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "PTHKNINF-KEY3"     TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "PTHKNINF-KEY3"     TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO                TO  FLG-PTHKNINF
                   MOVE    MCPDATA-REC         TO  PTHKNINF-REC
                   IF      PTHKN-SAKJOKBN      =   "1"
                       MOVE    1                   TO  FLG-PTHKNINF
                   END-IF
               ELSE
                   MOVE    1                   TO  FLG-PTHKNINF
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PTHKNINF
           END-IF
      *
           MOVE    "PTHKNINF-KEY3"     TO  WK-PATH
           PERFORM CSR-CLOSE-SEC
      *
           .
       910-PTHKNINF-INV-EXT.
           EXIT.
      *
      *****************************************************************
      *    旧姓履歴マスタ読み込み
      *****************************************************************
       900-KYUSEIRRK-READ-SEC          SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_kyuseirrk"     TO  MCP-TABLE
           MOVE    "key3"              TO  MCP-PATHNAME
           CALL    "MONFUNC"           USING
                                           MCPAREA
                                           MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "tbl_kyuseirrk"     TO  MCP-TABLE
               MOVE    "key3"              TO  MCP-PATHNAME
               CALL    "MONFUNC"           USING
                                               MCPAREA
                                               MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  KYUSEIRRK-REC
                   MOVE    ZERO                TO  FLG-KYUSEIRRK
               ELSE
                   MOVE    1                   TO  FLG-KYUSEIRRK
               END-IF
           ELSE
               MOVE    1               TO  FLG-KYUSEIRRK
           END-IF
      *
           MOVE    "tbl_kyuseirrk"     TO  MCP-TABLE
           MOVE    "key3"              TO  MCP-PATHNAME
           PERFORM 900-CLOSE-SEC
      *
           .
       900-KYUSEIRRK-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    カーソルクローズ処理
      *****************************************************************
       CSR-CLOSE-SEC            SECTION.
      *
           INITIALIZE                  MCPAREA
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    WK-PATH             TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           .
       CSR-CLOSE-EXT.
           EXIT.
      *
      *****************************************************************
      *    中間ファイルＲＥＡＤ処理
      *****************************************************************
       RECE01-READ-SEC            SECTION.
      *
           READ    RECE01-FILE   NEXT
               AT  END
                   MOVE    1     TO    FLG-RECE01
               NOT  AT  END
                   ADD     1     TO    CNT-IN (IDZ)
                                       CNT-IN-CANCEL
      *
                   DISPLAY "RECE01 " IDZ " PTNUM=" RECE01-PTNUM     
           END-READ
           .
       RECE01-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険履歴情報読込
      *****************************************************************
       900-RECE09-READ-SEC             SECTION.
      *
           READ    RECE09-FILE
               INVALID
                   MOVE    1           TO  FLG-RECE09
               NOT INVALID
                   MOVE    ZERO        TO  FLG-RECE09
           END-READ
           .
       900-RECE09-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    入退院履歴読込
      *****************************************************************
       900-RECE12-READ-SEC            SECTION.
      *
           READ    RECE12-FILE
               INVALID
                   MOVE    1           TO  FLG-RECE12
               NOT INVALID
                   MOVE    ZERO        TO  FLG-RECE12
           END-READ
           .
       900-RECE12-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    テーブルクローズ処理
      *****************************************************************
       900-CLOSE-SEC               SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           CALL    "MONFUNC"           USING
                                           MCPAREA
                                           MCPDATA-REC
      *
           .
       900-CLOSE-EXT.
           EXIT.
      *      
      *****************************************************************
      *    ＤＢオープン処理
      *****************************************************************
       100-DBOPEN-SEC                SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBOPEN"            TO  MCP-FUNC
           CALL    "MONFUNC"           USING
                                       MCPAREA
                                       MCPDATA-REC
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBSTART"           TO  MCP-FUNC
           CALL    "MONFUNC"           USING
                                       MCPAREA
                                       MCPDATA-REC
           .
       100-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢクローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC            SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBCOMMIT"          TO  MCP-FUNC
           CALL    "MONFUNC"           USING
                                       MCPAREA
                                       MCPDATA-REC
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBDISCONNECT"      TO  MCP-FUNC
           CALL    "MONFUNC"           USING
                                       MCPAREA
                                       MCPDATA-REC
           .
       900-DBDISCONNECT-EXT.
           EXIT.
